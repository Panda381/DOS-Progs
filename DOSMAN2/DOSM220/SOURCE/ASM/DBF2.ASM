
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                          D A T A B  Z E - ‡ st 2
;
;                Zobrazen¡ datab ze, obsluha p©¡stupu k souboru
;
; =============================================================================
;
; Ve©ejn‚ procedury:
;
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

CodeDbf  SEGMENT   BYTE PUBLIC
         ASSUME    cs:CodeDbf,ds:Data

; *****************************************************************************
;                              InitBMnu
;                     inicializace menu datab ze
; -----------------------------------------------------------------------------
; VSTUP: DS:SI=adresa definice menu
; *****************************************************************************

InitTBMn LABEL     FAR

InitBMnu PROC      FAR

         push      ax
         mov       al,ds:[Pozic]            ; po‡et pozic na © dek
         mov       ds:[si+MnuSir],al        ; ¨¡©ka okna
         mov       al,ds:[Radku]            ; po‡et © dk–
         dec       ax
;         sub       al,3
         mov       ds:[si+MnuVys],al        ; v˜¨ka okna
         mov       word ptr ds:[si+MnuPoz],0 ; po‡ te‡n¡ © dek a pozice
;         call      far ptr CentMenu         ; vyst©edˆn¡ okna
         pop       ax
         ret

InitBMnu ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ kurzoru editoru datab ze DS:SI
; -----------------------------------------------------------------------------

DispDBFK PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      dx
         push      es

; ------ vypnut¡ kurzoru, nen¡-li editace

         test      byte ptr ds:[DBFEPar2],bit0 ; je nastaven¡ ¨¡©ky pole ?
         jnz       DspDBFK1                 ; je nastaven¡ ¨¡©ky pole
         test      byte ptr ds:[DBFEParm],bit0 ; je editace ?
         jnz       DspDBFK1                 ; je editace
         call      far ptr KurzOff          ; vypnut¡ kurzoru
         jmp       DspDBFK9

; ------ adresa segmentu okna

DspDBFK1:call      DBFAdrES                 ; aktualizace adresy okna -> ES

; ------ test, zda je str nkov˜ re‘im

         test      byte ptr es:[DBFParm],bit0 ; je str nkov˜ re‘im ?
         jz        DspDBFK6                 ; nen¡ str nkov˜ re‘im

; ------ stanoven¡ © dku kurzoru v str nkov‚m re‘imu

         call      GetDBRK                  ; stanoven¡ relativn¡ho © dku
         sub       dx,es:[DBFTopL]          ; offset od prvn¡ho © dku
         mov       dh,dl                    ; relativn¡ © dek v oknˆ
         add       dh,2                     ; p©i‡ten¡ po‡ tku okna
         add       dh,ds:[si+MnuRad]        ; © dek

; ------ ukazatel popisova‡e polo‘ky

         mov       bx,es:[DBFAktP]          ; aktivn¡ polo‘ka
         shl       bx,1
         shl       bx,1
         shl       bx,1
         add       bx,es:[DBFAdrP]          ; adresa popisova‡e

; ------ pozice na © dku pro MEMO pole

         mov       dl,3                     ; pozice pro MEMO pole
         test      byte ptr es:[bx+DBFParX],bit5 ; je MEMO pole ?
         jnz       DspDBFK5                 ; je MEMO pole

; ------ pozice na © dku pro datum

         test      byte ptr es:[bx+DBFParX],bit3 ; je datum ?
         jz        DspDBFK3
         mov       bh,0
         mov       bl,ds:[FormDat]          ; form t data (0=MDR, 1=DMR, 2=RMD)
         shl       bx,1
         shl       bx,1
         shl       bx,1
         add       bx,es:[DBFKurz]          ; pozice kurzoru v polo‘ce
         mov       dl,cs:[bx+EditPPT3]      ; pozice kurzoru
         jmp       short DspDBFK5

; ------ pozice na © dku pro ‡as

DspDBFK3:test      byte ptr es:[bx+DBFParX],bit6 ; je to ‡as ?
         jz        DspDBFK4                 ; nen¡ to ‡as
         mov       dl,es:[DBFKurz]          ; kurzor
         cmp       dl,2                     ; je oddˆlova‡ ?
         jb        DspDBFK5                 ; nen¡ oddˆlova‡
         inc       dx                       ; p©i‡ten¡ oddˆlova‡e
         cmp       dl,5                     ; je oddˆlova‡ ?
         jb        DspDBFK5                 ; nen¡ oddˆlova‡
         inc       dx                       ; p©esko‡en¡ oddˆlova‡e
         jmp       short DspDBFK5

; ------ pozice na © dku pro ostatn¡ polo‘ky

DspDBFK4:mov       bl,ds:[si+MnuSir]        ; ¨¡©ka okna
         sub       bl,15                    ; maxim ln¡ ¨¡©ka polo‘ky
         mov       ax,es:[DBFKurz]          ; kurzor na © dku
         div       bl                       ; relativn¡ pozice na © dku
         mov       dl,ah                    ; pozice na © dku

; ------ korekce pozice

DspDBFK5:add       dl,13                    ; p©i‡ten¡ po‡ tku okna
         add       dl,ds:[si+MnuPoz]        ; pozice
         jmp       short DspDBFK7           ; zobrazen¡ kurzoru

; ------ zobrazen¡ kurzoru v © dkov‚m re‘imu

DspDBFK6:call      GetDBFK                  ; stanoven¡ kurzoru na © dku
         add       dl,ds:[si+MnuPoz]
         inc       dx
         sub       dx,es:[DBFTopP]          ; ode‡ten¡ po‡ te‡n¡ pozice © dku

         mov       dh,-1
         test      byte ptr ds:[DBFEPar2],bit0 ; je nastaven¡ ¨¡©ky pole ?
         jnz       DsDBFK62                 ; je nastaven¡ ¨¡©ky pole
         mov       dh,es:[DBFAktZ]
         sub       dh,es:[DBFTopZ]
DsDBFK62:add       dh,ds:[si+MnuRad]
         add       dh,3

DspDBFK7:call      far ptr SetKurz          ; nastaven¡ pozice kurzoru

; ------ nastaven¡ velikosti kurzoru

         test      byte ptr ds:[DBFEParm],bit2 ; je INSERT ?
         jz        DspDBFK8                 ; nen¡ INSERT
         call      far ptr SizeLKur         ; nastaven¡ n¡zk‚ho kurzoru
         jmp       short DspDBFK9

DspDBFK8:call      far ptr SizeHKur         ; nastaven¡ vysok‚ho kurzoru

; ------ n vrat registr–

DspDBFK9:pop       es
         pop       dx
         pop       bx
         pop       ax
         ret

DispDBFK ENDP

; -----------------------------------------------------------------------------
;     v˜po‡et relativn¡ho © dku kurzoru ve str nkov‚m re‘imu (okno ES -> DX)
; -----------------------------------------------------------------------------

GetDBRK  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      si

; ------ maxim ln¡ ¨¡©ka textov‚ polo‘ky

         mov       dl,ds:[si+MnuSir]        ; ¨¡©ka okna
         sub       dl,15                    ; zbytek na polo‘ku
         mov       dh,0

; ------ nalezen¡ aktivn¡ polo‘ky (u kr tk˜ch polo‘ek nep©esnost nevad¡)

         xor       si,si                    ; ukazatel relativn¡ho © dku
         mov       bx,es:[DBFAdrP]          ; ukazatel popisova‡–
         mov       cx,es:[DBFAktP]          ; aktivn¡ polo‘ka
         jcxz      GetDBRK3                 ; je prvn¡ polo‘ka
GetDBRK1:mov       ax,es:[bx+DBFDelX]       ; d‚lka polo‘ky (bajt–)
         dec       dx                       ; ¨¡©ka © dku - 1
         add       ax,dx                    ; zarovn n¡ nahoru
         inc       dx                       ; n vrat ¨¡©ky © dku
         div       dl                       ; v˜po‡et po‡tu © dk–
         mov       ah,0
         add       si,ax                    ; zv˜¨en¡ ‡¡ta‡e © dk–
         add       bx,DBFX                  ; adresa dal¨¡ polo‘ky
         loop      GetDBRK1                 ; dal¨¡ polo‘ka

; ------ korekce © dku pro posledn¡ polo‘ku

GetDBRK3:mov       ax,es:[DBFKurz]          ; pozice kurzoru v polo‘ce
         div       dl                       ; v˜po‡et cel˜ch © dk–
         mov       ah,0
         add       si,ax                    ; zv˜¨en¡ ‡¡ta‡e © dk–

; ------ n vrat registr–

         mov       dx,si                    ; DX <- relativn¡ © dek

         pop       si
         pop       cx
         pop       bx
         pop       ax
         ret

GetDBRK  ENDP

; -----------------------------------------------------------------------------
;  v˜po‡et relativn¡ pozice kurzoru na © dku v © dkov‚m re‘imu (okno ES -> DX)
; -----------------------------------------------------------------------------

GetDBFK  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx

; ------ nalezen¡ pozice kurzoru na © dku v © dkov‚m re‘imu

         mov       bx,es:[DBFAdrP]          ; ukazatel popisova‡–
         xor       dx,dx                    ; ukazatel pozice na © dku
         xor       ax,ax                    ; AX <- 0
         mov       cx,es:[DBFAktP]          ; aktivn¡ polo‘ka
         jcxz      GetDBFK2                 ; je prvn¡ polo‘ka
GetDBFK1:add       dx,es:[bx+DBFSirX]       ; zv˜¨en¡ ukazatele pozice
         inc       dx                       ; oddˆlovac¡ mezera
         add       bx,DBFX                  ; dal¨¡ popisova‡
         loop      GetDBFK1

; ------ p©¡prava d‚lky polo‘ky -> CX

GetDBFK2:mov       al,es:[bx+DBFParX]       ; parametry polo‘ky
         mov       cl,10                    ; datum
         test      al,bit3                  ; je to datum ?
         jnz       GetDBFK3                 ; je to datum
         mov       cl,3                     ; logick  polo‘ka
         test      al,bit4                  ; logick  polo‘ka ?
         jnz       GetDBFK3                 ; logick  polo‘ka
         mov       cl,6                     ; MEMO pole
         test      al,bit5                  ; MEMO pole ?
         jnz       GetDBFK3                 ; MEMO pole
         mov       cl,8                     ; ‡as
         test      al,bit6                  ; ‡as ?
         jnz       GetDBFK3                 ; je to ‡as
         mov       cl,es:[bx+DBFDelX]       ; d‚lka pro text a ‡¡slo

; ------ omezen¡ d‚lky polo‘ky podle zobrazen‚ ¨¡©ky

GetDBFK3:cmp       cx,es:[bx+DBFSirX]       ; je polo‘ka dlouh  ?
         jbe       GetDBFK4                 ; polo‘ka je OK
         mov       cx,es:[bx+DBFSirX]       ; omezen¡ d‚lky polo‘ky

; ------ pozice p©i nastaven¡ ¨¡©ky pole - je za‡ tek polo‘ky

GetDBFK4:test      byte ptr ds:[DBFEPar2],bit0 ; je nastaven¡ ¨¡©ky pole ?
         jnz       GetDBFK9                 ; je nastaven¡ ¨¡©ky pole

; ------ odsun polo‘ek o mezeru (kromˆ textu)

         test      al,bit0                  ; je to text ?
         jnz       GetDBFK5                 ; text se neodsouv 
         mov       ax,es:[bx+DBFSirX]       ; ¨¡©ka pole
         sub       ax,cx                    ; zbytek na mezeru
         add       dx,ax                    ; odsun o mezeru

; ------ pozice pro logickou a MEMO polo‘ku

GetDBFK5:test      byte ptr es:[bx+DBFParX],bit4+bit5 ; je logick /MEMO polo‘ka?
         jz        GetDBFK6                 ; nen¡
         shr       cx,1                     ; polovina ¨¡©ky polo‘ky
         add       dx,cx                    ; posun pozice
         jmp       short GetDBFK9

; ------ pozice v datu

GetDBFK6:test      byte ptr es:[bx+DBFParX],bit3 ; je datum ?
         jz        GetDBFK7                 ; nen¡ datum
         mov       bh,0
         mov       bl,ds:[FormDat]          ; form t data (0=MDR, 1=DMR, 2=RMD)
         shl       bx,1
         shl       bx,1
         shl       bx,1                     ; form t data * 8
         add       bx,es:[DBFKurz]          ; pozice kurzoru v polo‘ce
         add       dl,cs:[bx+EditPPT3]      ; pozice kurzoru v datu
         adc       dh,0
         jmp       short GetDBFK9

; ------ pozice v ‡asu

GetDBFK7:add       dx,es:[DBFKurz]          ; p©i‡ten¡ pozice kurzoru

         test      byte ptr es:[bx+DBFParX],bit6 ; je ‡as ?
         jz        GetDBFK8                 ; nen¡ ‡as
         cmp       byte ptr es:[DBFKurz],1  ; je hodina ?
         jbe       GetDBFK9                 ; je hodina
         inc       dx                       ; p©esko‡en¡ oddˆlova‡e ‡asu
         cmp       byte ptr es:[DBFKurz],3  ; je minuta ?
         jbe       GetDBFK9                 ; je hodina
         inc       dx                       ; p©esko‡en¡ oddˆlova‡e hodiny
         jmp       short GetDBFK9

; ------ pozice textu a ‡¡sla

GetDBFK8:sub       dx,es:[DBFOffT]          ; ode‡ten¡ offsetu po‡ tku polo‘ky

; ------ n vrat registr–

GetDBFK9:pop       cx
         pop       bx
         pop       ax
         ret

GetDBFK  ENDP

; *****************************************************************************
;                              DispBMnu
;                  zobrazen¡ okna editoru datab ze
; -----------------------------------------------------------------------------
; VSTUP: DS:SI=definice okna
;        DS=datov˜ segment
; -----------------------------------------------------------------------------
; lok ln¡ promˆnn‚: SS:[BP-2] (2) adresa definice okna
;                   SS:[BP-4] (2) ‡¡slo segmentu okna
;                   SS:[BP-6] (2) adresa segmentu okna
;                   SS:[BP-7] (1) ukazatel © dku na displeji
;                   SS:[BP-8] (1) pozice na displeji
;                   SS:[BP-9] (1) ‡¡ta‡ © dk– okna
;                   SS:[BP-10] (1) ¨¡©ka okna - 2
;                   SS:[BP-14] (4) ukazatel polo‘ky k zobrazen¡
;                   SS:[BP-16] (2) ‡¡ta‡ aktivn¡ polo‘ky
;                   SS:[BP-17] (1) barva editovan‚ polo‘ky
;                   SS:[BP-18] (1) parametry DBFEParm (bit 0: 1=je editace)
;                                                  (bit1: 1=k¢dov n¡ pro DEMO)
;                   SS:[BP-20] (2) ‡¡ta‡ aktivn¡ polo‘ky v aktivn¡m © dku
;                   SS:[BP-22] (2) hladina k zobrazen¡
;                   SS:[BP-23] (1) oddˆlovac¡ znak data
;                   SS:[BP-24] (1) form t data (0=MDR, 1=DMR, 2=RMD)
;               -------------- str nkov˜ re‘im
;                   SS:[BP-26] (2) ukazatel definice polo‘ek
;                   SS:[BP-28] (2) ukazatel dat v z znamu
;                   SS:[BP-30] (2) ‡¡ta‡ zbyl˜ch pozic v polo‘ce
;                   SS:[BP-32] (2) ‡¡ta‡ polo‘ek v z znamu k zobrazen¡
;                   SS:[BP-34] (2) maxim ln¡ d‚lka polo‘ky na © dku
;               --------------
;                   SS:[BP-35] (1) oddˆlovac¡ znak ‡asu
;                   SS:[BP-36] (1) barva polo‘ky z hlav¡ s nastavovanou ¨¡©kou
; *****************************************************************************
;þ
DispBMnu PROC      FAR

; ------ korekce kurzoru, zobrazen¡ r mu a nadpisu datab ze

         call      KorBMnu                  ; korekce menu
         call      DispBNad                 ; zobrazen¡ nadpisu datab ze
         call      far ptr DispRamM         ; zobrazen¡ r mu

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         push      es
         mov       bp,sp

; ------ vytvo©en¡ bufferu

         sub       sp,36
         mov       cl,ds:[si+MnuSir]        ; ¨¡©ka okna
         mov       ch,0
         shl       cx,1
         sub       sp,cx                    ; m¡sto pro buffer

; ------ inicializace lok ln¡ch promˆnn˜ch

         mov       ax,ds:[si+DBMnuSeg]      ; datov˜ segment okna
         mov       ss:[bp-2],si             ; adresa definice okna
         mov       ss:[bp-4],ax             ; ‡¡slo segmentu okna
         call      far ptr GetDat           ; poskytnut¡ adresy segmentu okna
         mov       ss:[bp-6],es             ; adresa segmentu okna
         mov       dx,word ptr ds:[si+MnuPoz] ; pozice a © dek
         add       dh,2
         mov       ss:[bp-8],dx             ; pozice a © dek na displeji
         mov       cl,ds:[si+MnuSir]        ; ¨¡©ka okna
         mov       ch,es:[DBFVysL]          ; v˜¨ka © dk– okna
         sub       cx,102h
         mov       ss:[bp-10],cx            ; ¨¡©ka a v˜¨ka okna
         mov       ax,es:[DBFTopZ]          ; prvn¡ zobrazen˜ z znam
         mov       ss:[bp-14],ax            ; ukazatel polo‘ky k zobrazen¡ LOW
         mov       ax,es:[DBFTopZ+2]
         mov       ss:[bp-14+2],ax
         mov       ax,es:[DBFAktZ]          ; aktivn¡ z znam
         mov       dx,es:[DBFAktZ+2]
         sub       ax,ss:[bp-14]
         sbb       dx,ss:[bp-12]
         jz        DispBMn1
         xor       ax,ax
DispBMn1:inc       ax                       ; p©ednastaven¡ + 1
         mov       ss:[bp-16],ax            ; ‡¡ta‡ aktivn¡ polo‘ky
         mov       al,ds:[si+MnuLev]        ; hladina
         mov       ah,0
         mov       ss:[bp-22],ax            ; hladina k zobrazen¡

         mov       al,ds:[DBFEParm]         ; p©¡znak editace
         mov       ah,ds:[ColDBF42]         ; barva editovan‚ polo‘ky
         and       al,not bit1              ; nen¡ k¢dov n¡ pro DEMO
         mov       ss:[bp-18],ax

         mov       ah,ds:[ZnakDat]          ; oddˆlovac¡ znak data
         mov       al,ds:[FormDat]          ; form t data
         mov       ss:[bp-24],ax            ; parametry data

         mov       ah,ds:[ZnakTim]          ; oddˆlovac¡ znak ‡asu
         mov       al,ds:[ColDBF22]         ; polo‘ka s nastavovanou ¨¡©kou
         test      byte ptr ds:[DBFEPar2],bit0 ; je nastaven¡ ¨¡©ky ?
         jnz       DispBM12                 ; je nastaven¡ ¨¡©ky
         mov       al,ds:[ColDBF2]          ; bˆ‘n  barva z hlav¡ polo‘ek
DispBM12:mov       ss:[bp-36],ax

         mov       ah,0
         mov       al,ds:[si+MnuSir]        ; ¨¡©ka okna
         sub       al,15                    ; zbytek na polo‘ku
         mov       ss:[bp-34],ax            ; maxim ln¡ d‚lka polo‘ky

         test      byte ptr es:[DBFParm],bit0 ; je str nkov˜ re‘im ?
         jz        DispBM27                 ; je © dkov˜ re‘im

         jmp       DispBMn6                 ; je str nkov˜ re‘im

; ------ vymaz n¡ © dku z hlav¡ (v © dkov‚m re‘imu)

DispBM27:mov       di,sp                    ; buffer v z sobn¡ku
         push      ss
         pop       es                       ; ES <- segment bufferu
         cld
         mov       ch,0
         mov       cl,ss:[bp-10]            ; ¨¡©ka okna - 2
         mov       ah,ds:[ColMnu1R]         ; bˆ‘n  barva okna
         mov       al,"³"                   ; "³"
         stosw                              ; lev˜ okraj
         push      di
         mov       al," "
         mov       ah,ds:[ColDBF2]          ; barva z hlav¡ polo‘ek
         rep       stosw
         mov       ah,ds:[ColMnu1L]         ; bˆ‘n  barva okna
         mov       al,"³"                   ; "³" prav˜ okraj
         stosw
         pop       di

; ------ p©¡prava registr–

         mov       ah,ds:[ColDBF2]          ; barva z hlav¡ polo‘ek
         push      ds
         mov       ds,ss:[bp-6]             ; adresa segmentu okna
         mov       dx,ds:[DBFTopP]          ; po‡ te‡n¡ pozice © dku
         neg       dx                       ; ukazatel pozice na © dku
         mov       si,ds:[DBFAdrP]          ; tabulka popisova‡–
         mov       bx,ds:[DBFNumP]          ; po‡et popisova‡–
         mov       ch,0
         cld
         or        bx,bx                    ; je nˆjak  polo‘ka ?
         jz        DispBM3A                 ; nen¡ ‘ dn  polo‘ka

         mov       ax,ds:[DBFAktP]          ; ‡¡slo aktivn¡ polo‘ky
         inc       ax                       ; p©ednastaven¡ + 1
         mov       ss:[bp-20],ax            ; ‡¡ta‡ aktivn¡ polo‘ky

; ------ dek¢dov n¡ jm‚na polo‘ky

DispBM31:push      si

         mov       ah,es:[di+1]             ; bˆ‘n  barva z hlav¡
         dec       word ptr ss:[bp-20]      ; ‡¡ta‡ aktivn¡ polo‘ky
         jnz       DspBM312                 ; nen¡ aktivn¡ polo‘ka
         mov       ah,ss:[bp-36]            ; barva aktivn¡ polo‘ky

DspBM312:mov       cl,ds:[si+DBFNumX]       ; d‚lka jm‚na popisova‡e
         cmp       cl,ds:[si+DBFSirX]       ; je men¨¡ zobrazen  ¨¡©ka ?
         jbe       DispBM32                 ; d‚lka jm‚na je OK
         mov       cl,ds:[si+DBFSirX]       ; omezen¡ d‚lky jm‚na
DispBM32:mov       si,ds:[si+DBFNamX]       ; adresa jm‚na popisova‡e
         jcxz      DispBM37                 ; nen¡ ‘ dn˜ znak

DispBM33:lodsb                              ; na‡ten¡ prvn¡ho znaku
         call      DispBNC                  ; ulo‘en¡ znaku
         cmp       al,"_"                   ; je oddˆlova‡ jm‚na ?
DispBM34:loope     DispBM33                 ; dal¨¡ znak (pokud je oddˆlova‡ _)
         jcxz      DispBM37                 ; konec jm‚na

DispBM35:lodsb                              ; znak k zobrazen¡
         call      far ptr DownCase         ; konverze na mal‚ p¡smeno
         call      DispBNC                  ; ulo‘en¡ znaku
         cmp       al,"_"                   ; je oddˆlova‡ jm‚na ?
         je        DispBM34                 ; je oddˆlova‡ jm‚na
         loop      DispBM35                 ; jinak dal¨¡ znak

DispBM37:pop       si

         mov       cl,ds:[si+DBFSirX]       ; ¨¡©ka pole
         sub       cl,ds:[si+DBFNumX]       ; po‡et zbyl˜ch pozic
         jbe       DispBM39                 ; nezbyla ‘ dn  pozice
         mov       al," "                   ; znak mezery
DispBM38:call      DispBNC                  ; ulo‘en¡ znaku mezery
         loop      DispBM38                 ; vymaz n¡ zbytku polo‘ky

DispBM39:mov       al,"³"                   ; oddˆlovac¡ ‡ ra
         call      DispBNC                  ; oddˆlovac¡ ‡ ra

         add       si,DBFX                  ; zv˜¨en¡ ukazatele index–
         dec       bx                       ; ‡¡ta‡ polo‘ek
         jnz       DispBM31                 ; dal¨¡ polo‘ka

DispBM3A:pop       ds

; ------ zobrazen¡ © dku z hlav¡

         call      DispBMDs                 ; zobrazen¡ © dku

; ------ p©¡prava k¢dov n¡ pro DEMO

DispBM5: test      byte ptr ds:[LicDPar0],bit4 ; je DEMO verze ?
         jz        DspBM500                 ; nen¡ DEMO verze
         or        byte ptr ss:[bp-18],bit1 ; je k¢dov n¡ pro DEMO
         cmp       word ptr ss:[bp-14+2],0
         jne       DispBM50                 ; polo‘ka bude k¢dovan 
         cmp       word ptr ss:[bp-14],30
         jae       DispBM50
DspBM500:and       byte ptr ss:[bp-18],not bit1 ; nebude k¢dov n¡

; ------ p©¡prava barvy © dku polo‘ky -> BL, BH

DispBM50:mov       bl,ds:[ColDBF3]          ; bˆ‘n  barva
         mov       bh,ds:[ColDBF5]          ; ozna‡en˜ z znam
         dec       word ptr ss:[bp-16]      ; ‡¡ta‡ kurzoru
         jnz       DispBM51                 ; nen¡ kurzor
         test      byte ptr ss:[bp-18],bit0 ; je editace ?
         jnz       DispBM51                 ; je editace
         mov       bl,ds:[ColDBF4]          ; bˆ‘n˜ kurzor
         mov       bh,ds:[ColDBF6]          ; ozna‡en˜ kurzor

; ------ p©¡prava z znamu -> ES:SI

DispBM51:mov       si,ss:[bp-2]             ; adresa definice okna
         mov       ax,ss:[bp-14]            ; polo‘ka k zobrazen¡ LOW
         mov       dx,ss:[bp-14+2]          ; polo‘ka k zobrazen¡ HIGH
         call      far ptr GetDBF           ; poskytnut¡ polo‘ky DBF
         mov       si,di                    ; SI <- adresa polo‘ky

; ------ p©¡prava ‡¡ta‡e aktivn¡ polo‘ky na editovan‚m © dku

         pushf
         mov       word ptr ss:[bp-20],-1   ; nen¡ aktivn¡ polo‘ka
         cmp       word ptr ss:[bp-16],0    ; je kurzor ?
         jne       DspBM512                 ; nen¡ kurzor
         test      byte ptr ss:[bp-18],bit0 ; je editace ?
         jz        DspBM512                 ; nen¡ editace
         mov       ax,es:[DBFAktP]          ; ‡¡slo aktivn¡ polo‘ky
         inc       ax                       ; p©ednastaven¡ + 1
         mov       ss:[bp-20],ax            ; ‡¡ta‡ aktivn¡ polo‘ky
         mov       si,es:[DBFAdrE]          ; adresa edita‡n¡ho bufferu
DspBM512:popf

; ------ vymaz n¡ © dku polo‘ky (CY=polo‘ka neplatn )

         cld
         mov       di,sp                    ; DI <- buffer v z sobn¡ku
         mov       al,"³"                   ; "³"
         mov       ah,ds:[ColMnu1R]         ; barva lev‚ho okraje

         push      ds

         push      es
         push      ss
         pop       es                       ; ES <- segment bufferu v z sobn¡ku
         stosw                              ; ulo‘en¡ lev‚ho okraje
         mov       ah,ds:[ColMnu1L]         ; barva prav‚ho okraje
         pop       ds                       ; DS <- segment okna

         pushf

         push      di
         push      ax                       ; AX = znak prav‚ho okraje

         mov       ah,bl                    ; AH <- bˆ‘n  barva
         mov       al," "                   ; mezera k ulo‘en¡
         jc        DispB520                 ; polo‘ka neplatn 
         cmp       byte ptr ds:[si]," "     ; ozna‡en  polo‘ka ?
         je        DispB520                 ; polo‘ka nen¡ ozna‡en 
         mov       ah,bh                    ; zv˜raznˆn  barva-ozna‡en  polo‘ka
DispB520:mov       cl,ss:[bp-10]            ; ¨¡©ka © dku - 2
         rep       stosw                    ; vymaz n¡ © dku
         mov       bh,ah                    ; BH <- £schova aktivn¡ barvy

         pop       ax
         stosw                              ; prav˜ okraj
         pop       di

         popf
         jnc       DispB524                 ; polo‘ka OK

; ------ polo‘ka neplatn  - p©i editaci je jako platn 

DispB522:test      byte ptr ss:[bp-18],bit0 ; je editace ?
         jz        DispB523                 ; nen¡ editace
         cmp       word ptr ss:[bp-16],0    ; je kurzor ?
         je        DispB524                 ; je kurzor
DispB523:jmp       DispBM5B                 ; jinak konec

; ------ p©¡prava k dek¢dov n¡ z znamu

DispB524:inc       si                       ; prvn¡ polo‘ka
         mov       ah,bh                    ; barva textu
         mov       dx,ds:[DBFTopP]          ; po‡ te‡n¡ pozice © dku
         mov       bx,ds:[DBFAdrP]          ; adresa indexu popisova‡–
         mov       cl,ss:[bp-10]            ; ¨¡©ka © dku
         mov       ch,0

; ------ oprava barvy polo‘ky, je-li editovan˜ © dek

DispB525:push      ax
         cmp       word ptr ss:[bp-16],0    ; je kurzor ?
         jne       DispB528                 ; nen¡ kurzor
         test      byte ptr ss:[bp-18],bit0 ; je editace ?
         jz        DispB528                 ; nen¡ editace

         mov       ah,ss:[bp-17]            ; barva editovan‚ polo‘ky

; ------ dek¢dov n¡ polo‘ky

DispB528:call      DekDBPol                 ; dek¢dov n¡ polo‘ky
         pop       ax
         jcxz      DispBM5B                 ; je konec © dku

         push      bx
         mov       bl,1                     ; 1 znak
         mov       al,"³"                   ; znak oddˆlovac¡ ‡ ry
         call      DispBCh                  ; ulo‘en¡ znaku ‡ ry
         pop       bx

         push      ax
         mov       ax,ds:[DBFAdrE]          ; edit. buffer = konec polo‘ek
         sub       ax,DBFX                  ; posledn¡ index v tabulce
         cmp       bx,ax                    ; jsou ji‘ v¨echny polo‘ky ?
         pop       ax
         jbe       DispB525                 ; je je¨tˆ dal¨¡ polo‘ka

; ------ zobrazen¡ © dku polo‘ky

DispBM5B:pop       ds

         call      DispBMDs                 ; zobrazen¡ © dku

; ------ p©¡prava pro dal¨¡ © dek s polo‘kou

         add       word ptr ss:[bp-14],1    ; zv˜¨en¡ ‡¡sla polo‘ky
         adc       word ptr ss:[bp-14+2],0

         dec       byte ptr ss:[bp-9]       ; ‡¡ta‡ © dk–
         jz        DispBM5c                 ; konec
         jmp       DispBM5                  ; dal¨¡ polo‘ka

DispBM5c:jmp       DispBMn9                 ; konec

; -----------------------------------------------------------------------------
;        zobrazen¡ str nkov‚ho m¢du
; -----------------------------------------------------------------------------
; lok ln¡ promˆnn‚: SS:[BP-2] (2) adresa definice okna
;                   SS:[BP-4] (2) ‡¡slo segmentu okna
;                   SS:[BP-6] (2) adresa segmentu okna
;                   SS:[BP-7] (1) ukazatel © dku na displeji
;                   SS:[BP-8] (1) pozice na displeji
;                   SS:[BP-9] (1) ‡¡ta‡ © dk– okna
;                   SS:[BP-10] (1) ¨¡©ka okna - 2
;                   SS:[BP-14] (4) ukazatel polo‘ky k zobrazen¡
;                   SS:[BP-16] (2) ‡¡ta‡ aktivn¡ polo‘ky
;                   SS:[BP-17] (1) barva editovan‚ polo‘ky
;                   SS:[BP-18] (1) parametry DBFEParm (bit 0: 1=je editace)
;                                        (bit1: 1=k¢dov n¡ pro DEMO)
;                   SS:[BP-20] (2) ‡¡ta‡ aktivn¡ polo‘ky v aktivn¡m © dku
;                   SS:[BP-22] (2) hladina k zobrazen¡
;                   SS:[BP-23] (1) oddˆlovac¡ znak data
;                   SS:[BP-24] (1) form t data (0=MDR, 1=DMR, 2=RMD)
;               -------------- str nkov˜ re‘im
;                   SS:[BP-26] (2) ukazatel definice polo‘ek
;                   SS:[BP-28] (2) ukazatel dat v z znamu
;                   SS:[BP-30] (2) ‡¡ta‡ zbyl˜ch pozic v polo‘ce
;                   SS:[BP-32] (2) ‡¡ta‡ polo‘ek v z znamu k zobrazen¡
;                   SS:[BP-34] (2) maxim ln¡ d‚lka polo‘ky na © dku
;               --------------
;                   SS:[BP-35] (1) oddˆlovac¡ znak ‡asu
;                   SS:[BP-36] (1) barva polo‘ky z hlav¡ s nastavovanou ¨¡©kou
; -----------------------------------------------------------------------------

DispBMn6:inc       byte ptr ss:[bp-9]       ; po‡et © dk– k zobrazen¡

         test      byte ptr ds:[LicDPar0],bit4 ; je DEMO verze ?
         jz        DspBM600                 ; nen¡ DEMO verze

         mov       es,ss:[bp-6]             ; adresa segmentu okna
         or        byte ptr ss:[bp-18],bit1 ; je k¢dov n¡ pro DEMO
         cmp       word ptr es:[DBFAktZ+2],0
         jne       DspBM601                 ; polo‘ka bude k¢dovan 
         cmp       word ptr es:[DBFAktZ],30
         jae       DspBM601
DspBM600:and       byte ptr ss:[bp-18],not bit1 ; nebude k¢dov n¡

; ------ p©¡prava parametr–

DspBM601:mov       word ptr ss:[bp-30],0    ; nezbyly ‘ dn‚ pozice
         mov       word ptr ss:[bp-32],0    ; nejsou ‘ dn‚ polo‘ky

; ------ p©¡prava ‡¡ta‡e aktivn¡ polo‘ky

         mov       es,ss:[bp-6]             ; adresa segmentu okna
         mov       ax,es:[DBFAktP]          ; ‡¡slo aktivn¡ polo‘ky
         mov       ss:[bp-20],ax            ; ‡¡ta‡ aktivn¡ polo‘ky

; ------ p©¡prava z znamu, pokud je editace

         mov       di,es:[DBFAdrE]          ; adresa edita‡n¡ho bufferu
         test      byte ptr ss:[bp-18],bit0 ; je editace ?
         jnz       DispBM60                 ; je editace
         mov       word ptr ss:[bp-20],-1   ; nen¡ aktivn¡ polo‘ka

; ------ p©¡prava z znamu, nen¡-li editace

         call      far ptr GetADBF          ; aktivn¡ polo‘ka DBF
         jc        DispBMn7                 ; chyba - nen¡ ‘ dn  polo‘ka
DispBM60:inc       di                       ; p©esko‡en¡ znaku ozna‡en¡ polo‘ky
         mov       ss:[bp-28],di            ; ukazatel dat v z znamu
         mov       bx,es:[DBFAdrP]          ; adresa popisova‡–
         mov       ss:[bp-26],bx            ; ukazatel definice polo‘ek
         mov       ax,es:[DBFNumP]          ; po‡et polo‘ek
         mov       ss:[bp-32],ax            ; ‡¡ta‡ polo‘ek v z znamu
         mov       cx,es:[DBFTopL]          ; prvn¡ zobrazen˜ © dek

; ------ nalezen¡ po‡ te‡n¡ho © dku

         jcxz      DispBMn7                 ; je prvn¡ © dek
DispBM61:cmp       word ptr ss:[bp-30],0    ; zbyly pozice ?
         jne       DispBM62                 ; zbyly pozice
         mov       ax,es:[bx+DBFDelX]       ; d‚lka polo‘ky
         mov       ss:[bp-30],ax            ; zbyl‚ pozice

DispBM62:mov       ax,ss:[bp-30]            ; zbyl˜ po‡et bajt– polo‘ky
         cmp       ax,ss:[bp-34]            ; je del¨¡ ne‘ © dek ?
         jbe       DispB622                 ; nen¡ del¨¡
         mov       ax,ss:[bp-34]            ; omezen¡ na d‚lku © dku
DispB622:add       di,ax                    ; posun adresy polo‘ky
         mov       ss:[bp-28],di            ; nov˜ ukazatel dat v z znamu

         sub       word ptr ss:[bp-30],ax   ; sn¡‘en¡ ‡¡ta‡e pozice
         jnz       DispBM63                 ; bude je¨tˆ dal¨¡ © dek
         add       bx,DBFX                  ; adresa dal¨¡ho popisova‡e
         mov       ss:[bp-26],bx            ; nov˜ ukazatel definice polo‘ek
         dec       word ptr ss:[bp-20]      ; sn¡‘en¡ ‡¡ta‡e aktivn¡ polo‘ky
         dec       word ptr ss:[bp-32]      ; sn¡‘en¡ ‡¡ta‡e polo‘ek (ZY=konec)

DispBM63:loopnz    DispBM61                 ; dal¨¡ © dek

; ------ inicializa‡n¡ vymaz n¡ © dku

DispBMn7:mov       di,sp                    ; buffer v z sobn¡ku
         push      ss
         pop       es                       ; ES <- segment bufferu v z sobn¡ku
         cld
         mov       ch,0
         mov       cl,ss:[bp-10]            ; ¨¡©ka okna - 2
         mov       ah,ds:[ColMnu1R]         ; bˆ‘n  barva okna
         mov       al,"³"                   ; "³"
         stosw                              ; lev˜ okraj
         push      di
         mov       al," "                   ; znak mezery k vymaz n¡
         mov       ah,ds:[ColDBF12]         ; barva podkladu
         rep       stosw                    ; vymaz n¡ © dku
         mov       ah,ds:[ColMnu1L]         ; barva pro prav˜ okraj
         mov       al,"³"                   ; "³" prav˜ okraj
         stosw
         pop       di

; ------ test, zda m  b˜t nadpis polo‘ky

         add       di,2*12                  ; za‡ tek k dek¢dov n¡ textu
         cmp       word ptr ss:[bp-30],0    ; zbyly nˆjak‚ pozice ?
         jne       DispBM76                 ; zbyly nˆjak‚ pozice
         cmp       word ptr ss:[bp-32],0    ; zbyly nˆjak‚ polo‘ky ?
         je        DispBM76                 ; nezbyly ‘ dn‚ polo‘ky

         mov       ah,ds:[ColDBF15]         ; zv˜raznˆn  barva
         push      di
         push      ds

         dec       di
         dec       di                       ; oddˆlovac¡ mezera

         mov       ds,ss:[bp-6]             ; adresa segmentu okna
         mov       si,ss:[bp-26]            ; ukazatel index– polo‘ek
         mov       bx,ds:[si+DBFDelX]       ; d‚lka polo‘ky (bajt–)
         mov       ss:[bp-30],bx            ; ‡¡ta‡ zbyl˜ch pozic

         mov       cl,ds:[si+DBFNumX]       ; d‚lka jm‚na popisova‡e
         mov       si,ds:[si+DBFNamX]       ; adresa jm‚na popisova‡e
         jcxz      DispBM75                 ; nen¡ ‘ dn˜ znak

         sub       di,cx
         sub       di,cx                    ; adresa k dek¢dov n¡ jm‚na

DispBM71:lodsb                              ; na‡ten¡ znaku
         stosw                              ; ulo‘en¡ znaku
         cmp       al,"_"                   ; je oddˆlovac¡ mezera ?
DispBM72:loope     DispBM71                 ; dal¨¡ znak
         jcxz      DispBM75                 ; konec

DispBM73:lodsb                              ; znak k zobrazen¡
         call      far ptr DownCase         ; konverze na mal‚ p¡smeno
         stosw                              ; ulo‘en¡ znaku
         cmp       al,"_"                   ; byla oddˆlovac¡ mezera ?
         je        DispBM72                 ; byla oddˆlovac¡ mezera
         loop      DispBM73                 ; dal¨¡ znak

DispBM75:pop       ds
         pop       di

; ------ ur‡en¡ barvy textu polo‘ky

DispBM76:mov       ah,ds:[ColDBF13]         ; bˆ‘n  polo‘ka
         cmp       word ptr ss:[bp-20],0    ; je to aktivn¡ polo‘ka ?
         jne       DispBM77                 ; nen¡ to aktivn¡ polo‘ka
         mov       ah,ds:[ColDBF14]         ; barva aktivn¡ polo‘ky

; ------ dek¢dov n¡ textu polo‘ky

DispBM77:push      ds
         cmp       word ptr ss:[bp-30],0    ; je nˆjak  polo‘ka ?
         je        DispBM78                 ; nen¡ ‘ dn  polo‘ka
         mov       ds,ss:[bp-6]             ; adresa segmentu okna
         mov       bx,ss:[bp-26]            ; ukazatel definice polo‘ky
         mov       cx,ss:[bp-34]            ; d‚lka © dku
         xor       dx,dx                    ; pozice - je za‡ tek © dku
         mov       si,ss:[bp-28]            ; ukazatel dat v z znamu
         call      DekDBPol                 ; dek¢dov n¡ polo‘ky
         mov       ss:[bp-28],si            ; nov˜ ukazatel dat
DispBM78:pop       ds

; ------ p©¡prava pro dal¨¡ © dek

         mov       ax,ss:[bp-34]            ; maxim ln¡ d‚lka polo‘ky
         sub       ss:[bp-30],ax            ; sn¡‘en¡ ‡¡ta‡e zbyl˜ch pozic
         ja        DispBMn8                 ; zbyly nˆjak‚ pozice
         mov       word ptr ss:[bp-30],0    ; nezbyla dal¨¡ pozice
         cmp       word ptr ss:[bp-32],0    ; zbyly nˆjak‚ polo‘ky ?
         je        DispBMn8                 ; nezbyly ‘ dn‚ polo‘ky
         add       word ptr ss:[bp-26],DBFX ; nov˜ ukazatele index– polo‘ek
         dec       word ptr ss:[bp-20]      ; sn¡‘en¡ ‡¡ta‡e aktivn¡ polo‘ky
         dec       word ptr ss:[bp-32]      ; sn¡‘en¡ ‡¡ta‡e polo‘ek

; ------ zobrazen¡ © dku

DispBMn8:call      DispBMDs                 ; zobrazen¡ © dku

; ------ p©¡prava pro dal¨¡ © dek

         dec       byte ptr ss:[bp-9]       ; ‡¡ta‡ © dk–
         jz        DispBMn9                 ; konec
         jmp       DispBMn7

; ------ test, zda bude zobrazen¡ MEMO pole

DispBMn9:mov       es,ss:[bp-6]             ; adresa segmentu okna
         mov       ax,es:[DBFVysQ]          ; v˜¨ka rychl‚ho prohl¡‘en¡
         or        ax,ax                    ; je rychl‚ prohl¡‘en¡ ?
         jnz       DispBM91                 ; je rychl‚ prohl¡‘en¡
         jmp       DispBM99                 ; nen¡ rychl‚ prohl¡‘en¡
DispBM91:mov       ss:[bp-9],al             ; ‡¡ta‡ © dk– k zobrazen¡

; ------ zobrazen¡ oddˆlovac¡ ‡ ry

         mov       di,sp                    ; buffer v z sobn¡ku
         push      ss
         pop       es                       ; ES <- segment bufferu v z sobn¡ku
         cld
         mov       ch,0
         mov       cl,ss:[bp-10]            ; ¨¡©ka okna - 2
         mov       ah,ds:[ColMnu1R]         ; bˆ‘n  barva okna
         mov       al,"Ã"                   ; "Ã"
         stosw                              ; lev˜ okraj
         mov       al,"Ä"                   ; znak mezery k vymaz n¡
         mov       ah,ds:[ColMnu1]          ; bˆ‘n  barva r mu
         rep       stosw                    ; vymaz n¡ © dku
         mov       ah,ds:[ColMnu1L]         ; barva pro prav˜ okraj
         mov       al,"´"                   ; "´" prav˜ okraj
         stosw
         call      DispBMDs                 ; zobrazen¡ © dku

         mov       ax,ds:[DBZobSeg]         ; segment bufferu
         call      far ptr GetDat           ; adresa bufferu -> ES
         mov       word ptr ss:[bp-28],TextBBeg ; ukazatel dat
         mov       word ptr ss:[bp-30],0    ; ‡¡ta‡ zbyl˜ch dat - nen¡ nic
         jc        DispBM92                 ; nen¡ segment MEMO
         mov       ax,es:[TextBNum]         ; po‡et bajt– v bufferu
         mov       ss:[bp-30],ax            ; ‡¡ta‡ zbyl˜ch dat

; ------ vymaz n¡ © dku pro zobrazen¡ textu

DispBM92:mov       di,sp                    ; buffer v z sobn¡ku
         push      ss
         pop       es                       ; ES <- segment bufferu v z sobn¡ku
         cld
         mov       ch,0
         mov       cl,ss:[bp-10]            ; ¨¡©ka okna - 2
         mov       bx,cx                    ; BX <- ¨¡©ka © dku
         mov       ah,ds:[ColMnu1R]         ; bˆ‘n  barva okna
         mov       al,"³"                   ; "³"
         stosw                              ; lev˜ okraj
         push      di
         mov       al," "                   ; znak mezery k vymaz n¡
         mov       ah,ds:[ColDBF7]          ; bˆ‘n  barva textu
         rep       stosw                    ; vymaz n¡ © dku
         mov       ah,ds:[ColMnu1L]         ; barva pro prav˜ okraj
         mov       al,"³"                   ; "³" prav˜ okraj
         stosw
         pop       di

; ------ dek¢dov n¡ textu © dku

         test      byte ptr ds:[DBZobPar],bit4 ; je text MEMO ?
         jnz       DspBM982                 ; nen¡ text MEMO pole

         push      ds
         mov       ax,ds:[DBZobSeg]         ; segment bufferu
         call      far ptr GetSgAdr         ; adresa bufferu -> DX
         mov       ds,dx                    ; adresa bufferu
         mov       si,ss:[bp-28]            ; SI <- ukazatel dat
         mov       cx,ss:[bp-30]            ; CX <- ‡¡ta‡ zbyl˜ch dat

DispBM94:jcxz      DispBM98                 ; nen¡ dal¨¡ znak
         lodsb                              ; na‡ten¡ znaku
         dec       cx                       ; sn¡‘en¡ ‡¡ta‡e zbyl˜ch dat
         cmp       al,CR
         je        DispBM94                 ; znak CR se ignoruje
         cmp       al,EOF
         je        DispBM94                 ; znak EOF se ignoruje
         cmp       al,LF
         je        DispBM98                 ; je konec © dku
         or        bx,bx                    ; zbyla dal¨¡ pozice ?
         jz        DispBM94                 ; nezbyla dal¨¡ pozice
         stosb                              ; ulo‘en¡ znaku
         inc       di                       ; p©esko‡en¡ barvy
         dec       bx                       ; sn¡‘en¡ ‡¡ta‡e voln‚ho m¡sta
         jmp       short DispBM94           ; dal¨¡ znak

DispBM98:mov       ss:[bp-30],cx            ; nov˜ ‡¡ta‡ zbyl˜ch dat
         mov       ss:[bp-28],si            ; nov˜ ukazatel textu
         pop       ds

; ------ zobrazen¡ © dku

DspBM982:call      DispBMDs                 ; zobrazen¡ © dku

; ------ p©¡prava pro dal¨¡ © dek

         dec       byte ptr ss:[bp-9]       ; ‡¡ta‡ © dk–
         jnz       DispBM92                 ; dal¨¡ © dek

; ------ n vrat registr–

DispBM99:mov       sp,bp
         pop       es
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

DispBMnu ENDP

; ------ zobrazen¡ © dku

DispBMDs:mov       si,sp                    ; SI <- buffer v z sobn¡ku
         inc       si
         inc       si                       ; p©esko‡en¡ n vratov‚ adresy
         mov       dx,ss:[bp-8]             ; © dek a pozice
         mov       cl,ss:[bp-10]            ; ¨¡©ka © dku - 2
         mov       ax,ss:[bp-22]            ; hladina k zobrazen¡
         inc       cx
         inc       cx                       ; ¨¡©ka © dku
         call      far ptr DispMStr         ; zobrazen¡ © dku
         inc       byte ptr ss:[bp-7]       ; zv˜¨en¡ ukazatele © dku
         ret

; -----------------------------------------------------------------------------
;        dek¢dov n¡ jedn‚ polo‘ky v z znamu
; -----------------------------------------------------------------------------
; VSTUP: AH=barva textu
;        DS:BX=ukazatel popisova‡e polo‘ky
;        CX=‡¡ta‡ zbyl˜ch pozic do konce © dku (mus¡ b˜t kladn‚ ‡¡slo)
;        DX=po‡et pozic do za‡ tku © dku (kladn‚ ‡¡slo = je p©ed za‡ tkem © dku)
;        DS:SI=ukazatel za‡ tku polo‘ky
;        ES:DI=ukl dac¡ adresa
;        CLD
; VSTUP: DS:BX=ukazatel popisova‡e dal¨¡ polo‘ky
;         CX=nov˜ ‡¡ta‡ zbyl˜ch pozic do konce © dku
;         DX=nov˜ po‡et pozic do za‡ tku
;         DS:SI=dal¨¡ polo‘ka
;         ES:DI=nov  ukl dac¡ adresa
; -----------------------------------------------------------------------------
;                   SS:[BP-20] (2) ‡¡ta‡ aktivn¡ polo‘ky v aktivn¡m © dku
;                   SS:[BP-23] (1) oddˆlovac¡ znak data
;                   SS:[BP-24] (1) form t data (0=MDR, 1=DMR, 2=RMD)
;                   SS:[BP-30] (2) ‡¡ta‡ zbyl˜ch pozic v polo‘ce
;                   SS:[BP-34] (2) maxim ln¡ d‚lka polo‘ky na © dku
;               --------------
;                   SS:[BP-35] (1) oddˆlovac¡ znak ‡asu
;                   SS:[BP-36] (1) barva polo‘ky z hlav¡ s nastavovanou ¨¡©kou
; -----------------------------------------------------------------------------

DekDBPol PROC      NEAR

         push      bx
         push      dx
         push      si

; ------ posun po‡ tku polo‘ky pro textovou a ‡¡selnou polo‘ku

         test      byte ptr ds:[DBFParm],bit0 ; je str nkov˜ re‘im ?
         jnz       DekDBPl0                 ; je str nkov˜ re‘im
         dec       word ptr ss:[bp-20]      ; ‡¡ta‡ aktivn¡ polo‘ky
         jnz       DekDBPl0                 ; nen¡ aktivn¡ polo‘ka
         test      byte ptr ds:[bx+DBFParX],bit0+bit1+bit2+bit7 ; je text/‡¡slo ?
         jz        DekDBPl0                 ; nen¡ text/‡¡slo
         add       si,ds:[DBFOffT]          ; po‡ tek polo‘ky

; ------ test, zda je polo‘ka cel  p©ed za‡ tkem © dku

DekDBPl0:cmp       dx,ds:[bx+DBFSirX]       ; je polo‘ka p©ed za‡ tkem © dku ?
         jl        DekDBPl1                 ; polo‘ka je z‡ sti zobrazen 
         jmp       DekDBPl9                 ; polo‘ka je p©ed za‡ tkem © dku

; ------ p©¡prava k dek¢dov n¡ polo‘ky

DekDBPl1:push      dx
         mov       dh,ds:[bx+DBFSirX]       ; DH (BH) <- ¨¡©ka polo‘ky (pozic)
         mov       dl,ds:[bx+DBFDelX]       ; DL (BL) <- d‚lka polo‘ky (bajt–)
         mov       al,ds:[bx+DBFParX]       ; AL <- parametry polo‘ky
         mov       bx,dx                    ; BX <- ¨¡©ka a d‚lka polo‘ky
         pop       dx

; ------ oprava d‚lky polo‘ky -> BL

         test      al,bit3                  ; datum ?
         jz        DkDBP112                 ; nen¡ datum
         mov       bl,10                    ; d‚lka polo‘ky "datum"

DkDBP112:test      al,bit4                  ; logick  polo‘ka ?
         jz        DkDBP113                 ; nen¡ logick  polo‘ka
         mov       bl,3                     ; d‚lka logick‚ polo‘ky

DkDBP113:test      al,bit5                  ; je to MEMO pole ?
         jz        DkDBP114                 ; nen¡ to MEMO pole
         mov       bl,6                     ; d‚lka pro MEMO pole

DkDBP114:test      al,bit6                  ; je to ‡as ?
         jz        DekDBP12                 ; nen¡ to ‡as
         mov       bl,8                     ; d‚lka pro ‡as

; ------ omezen¡ d‚lky polo‘ky -> BL, zbyl‚ mezery -> BH

DekDBP12:test      byte ptr ds:[DBFParm],bit0 ; je str nkov˜ re‘im ?
         jz        DekDBP16                 ; nen¡ str nkov˜ re‘im

         mov       bh,0                     ; nezb˜v  ‘ dn  mezera
         test      al,bit3+bit4+bit5+bit6   ; implicitn¡ d‚lka ?
         jnz       DekDBP14                 ; je implicitn¡ d‚lka
         mov       bl,ss:[bp-30]            ; po‡et zbyl˜ch pozic v polo‘ce
DekDBP14:cmp       bl,ss:[bp-34]            ; p©esahuje d‚lku © dku ?
         jbe       DekDBPl2                 ; d‚lka je OK
         mov       bl,ss:[bp-34]            ; omezen¡ d‚lky © dku
         jmp       short DekDBPl2

DekDBP16:cmp       bl,bh                    ; p©esahuje d‚lka ¨¡©ku polo‘ky ?
         jb        DekDBP18                 ; d‚lka je OK
         mov       bl,bh                    ; omezen¡ d‚lky polo‘ky
DekDBP18:sub       bh,bl                    ; BH <- zbytek na mezery

; ------ £vodn¡ mezera (p©ed polo‘kou) - kromˆ textu

DekDBPl2:test      al,bit1+bit2+bit3+bit4+bit5+bit6 ; m  b˜t mezera ?
         jz        DekDBP22                 ; nem  b˜t mezera
         call      DispBSpc                 ; dek¢dov n¡ mezer

; ------ datum

DekDBP22:test      al,bit3                  ; je to datum ?
         jz        DekDBPl6                 ; nen¡ datum

         cmp       byte ptr ss:[bp-24],1    ; evropsk˜ form t data 1 ?
         jne       DekDBPl3                 ; nen¡ evropsk˜ form t

         add       si,6                     ; 2 znaky dne
         call      DispBTx0                 ; dek¢dov n¡ dne
         call      DispBChD                 ; ulo‘en¡ oddˆlova‡e data
         sub       si,4                     ; mˆs¡c
         call      DispBTx0                 ; dek¢dov n¡ mˆs¡ce
         call      DispBChD                 ; ulo‘en¡ oddˆlova‡e data
         sub       si,6                     ; rok
         jmp       short DekDBPl5           ; dek¢dov n¡ roku

DekDBPl3:jb        DekDBPl4                 ; je americk˜ form t
                                           ;* je japonsk˜ form t
         mov       al,4                     ; 4 znaky roku
         call      DispBTxt                 ; dek¢dov n¡ roku
         call      DispBChD                 ; oddˆlovac¡ znak data
         call      DispBTx0                 ; dek¢dov n¡ mˆs¡ce
         call      DispBChD                 ; oddˆlovac¡ znak data
         call      DispBTx0                 ; dek¢dov n¡ dne
         jmp       short DekDBP52

DekDBPl4:add       si,4                     ; 2 znaky mˆs¡ce
         call      DispBTx0                 ; dek¢dov n¡ mˆs¡ce
         call      DispBChD                 ; ulo‘en¡ oddˆlova‡e data
         call      DispBTx0                 ; dek¢dov n¡ dne
         call      DispBChD                 ; ulo‘en¡ oddˆlova‡e data
         sub       si,8                     ; adresa roku
DekDBPl5:mov       al,4                     ; 4 znaky roku
         call      DispBTxt                 ; dek¢dov n¡ roku
DekDBP52:jmp       DekDBPl9

; ------ dek¢dov n¡ MEMO polo‘ky

DekDBPl6:test      al,bit5                  ; je MEMO polo‘ka ?
         jz        DekDBP61                 ; nen¡ MEMO polo‘ka

         cmp       byte ptr ds:[si+9]," "   ; je MEMO pole platn‚ ?
         mov       si,offset DekDBMT2       ; text pro "nen¡ MEMO"
         je        DekDBP64                 ; nen¡ MEMO pole
         mov       si,offset DekDBMmT       ; text pro "je MEMO"
         jmp       short DekDBP64           ; dek¢dov n¡ textu

; ------ dek¢dov n¡ logick‚ polo‘ky

DekDBP61:test      al,bit4                  ; je to logick  polo‘ka ?
         jz        DekDBPl7                 ; nen¡ to logick  polo‘ka

         lodsb                              ; znak k zobrazen¡
         call      far ptr UpCase           ; konverze na velk‚ p¡smeno

         mov       si,offset DekDBLT1       ; text pro nedefinovanou polo‘ku
         cmp       al,"?"
         je        DekDBP64                 ; nedefinovan  hodnota
         cmp       al," "
         je        DekDBP64                 ; nedefinovan  hodnota

         mov       si,offset DekDBLT2       ; text pro NE
         cmp       bl,1                     ; je kr tk˜ text ?
         ja        DekDBP62                 ; je dlouh˜ text
         mov       si,offset DekDBLT4       ; text pro NE pro kr tk˜ text

DekDBP62:cmp       al,"N"
         je        DekDBP64                 ; je NE
         cmp       al,"F"
         je        DekDBP64                 ; je NE

         mov       si,offset DekDBLT3       ; jina ANO
DekDBP64:call      DekDBTxC                 ; dek¢dov n¡ textu CS:SI
         jmp       short DekDBPl9

; ------ dek¢dov n¡ ‡asu

DekDBPl7:test      al,bit6                  ; je ‡as ?
         jz        DekDBPl8                 ; nen¡ ‡as

         call      DispBTx0                 ; dek¢dov n¡ hodiny
         call      DispBChT                 ; ulo‘en¡ oddˆlova‡e ‡asu
         call      DispBTx0                 ; dek¢dov n¡ minuty
         call      DispBChT                 ; ulo‘en¡ oddˆlova‡e ‡asu
         call      DispBTx0                 ; dek¢dov n¡ sekundy
         jmp       short DekDBPl9

; ------ dek¢dov n¡ ‡¡seln‚ a textov‚ polo‘ky

DekDBPl8:test      al,bit1+bit2             ; je to ‡¡slo ?
         mov       al,bl                    ; AL <- po‡et znak– polo‘ky
         jz        DekDBP86                 ; nen¡ to ‡¡slo
         call      DispBTxN                 ; dek¢dov n¡ ‡¡sla
         jmp       short DekDBP88

DekDBP86:call      DispBTxt                 ; dek¢dov n¡ textu
DekDBP88:call      DispBSpc                 ; dek¢dov n¡ zbytku mezer

; ------ adresa dal¨¡ polo‘ky v z znamu

DekDBPl9:pop       si
         pop       dx
         pop       bx

         test      byte ptr ds:[DBFParm],bit0 ; je str nkov‚ zobrazen¡ ?
         jz        DekDBP96                 ; nen¡ str nkov‚ zobrazen¡

         push      cx
         mov       cx,ss:[bp-30]            ; po‡et zbyl˜ch pozic v polo‘ce
         cmp       cl,ss:[bp-34]            ; p©esahuje d‚lku © dku ?
         jbe       DekDBP94                 ; d‚lka je OK
         mov       cl,ss:[bp-34]            ; omezen¡ d‚lky © dku
DekDBP94:add       si,cx                    ; zv˜¨en¡ adresy polo‘ky
         pop       cx
         jmp       short DekDBP98

DekDBP96:add       si,ds:[bx+DBFDelX]       ; adresa dal¨¡ polo‘ky
         sub       dx,ds:[bx+DBFSirX]       ; sn¡‘en¡ po‡ te‡n¡ pozice
DekDBP98:add       bx,DBFX                  ; dal¨¡ popisova‡
         ret

DekDBPol ENDP

DekDBMmT db        '<TEXT>'                 ; "je MEMO"
DekDBMT2 db        '-text-'                 ; "nen¡ MEMO"

DekDBLT1 db        '   '                    ; logick  polo‘ka nedefinovan 
DekDBLT2 db        ' ne'                    ; logick  polo‘ka NE
DekDBLT3 db        'ANO'                    ; logick  polo‘ka YES
DekDBLT4 db        '-'                      ; logick  polo‘ka NE pro 1 znak

; -----------------------------------------------------------------------------
;        ulo‘en¡ textu CS:SI (BL znak–)
; -----------------------------------------------------------------------------

DekDBTxC PROC      NEAR

DekDBTC2:mov       al,cs:[si]
         inc       si
         call      DispBCh
         or        bl,bl                    ; je ji‘ cel˜ text ?
         jnz       DekDBTC2                 ; dal¨¡ znak
         ret

DekDBTxC ENDP

; -----------------------------------------------------------------------------
;        ulo‘en¡ textu do bufferu
; -----------------------------------------------------------------------------
; VSTUP: AH=barva textu
;        AL=d‚lka textu (m–‘e b˜t = 0)
;        BL=po‡et zb˜vaj¡c¡ch znak– do konce polo‘ky
;        CX=‡¡ta‡ zbyl˜ch pozic do konce © dku (mus¡ b˜t kladn‚ ‡¡slo)
;        DX=po‡et pozic do za‡ tku © dku (kladn‚ ‡¡slo = je p©ed za‡ tkem © dku)
;        DS:SI=text k ulo‘en¡
;        ES:DI=ukl dac¡ adresa
;        CLD
; VSTUP: BL=nov˜ po‡et zb˜vaj¡c¡ch znak– do konce polo‘ky
;         CX=nov˜ ‡¡ta‡ zbyl˜ch pozic do konce © dku
;         DX=nov˜ po‡et pozic do za‡ tku
;         DS:SI=nov  adresa textu
;         ES:DI=nov  ukl dac¡ adresa
; zni‡en‚ registry: AL
; -----------------------------------------------------------------------------

DispBTx0:mov       al,2                     ; 2 znaky

DispBTxt PROC      NEAR

         cmp       al,0                     ; je nˆjak˜ text ?
         je        DispBTx2                 ; nen¡ text

DispBTx1:push      ax
         lodsb                              ; znak textu k ulo‘en¡
         call      DispBCh                  ; ulo‘en¡ znaku AX
         pop       ax
         dec       al
         jnz       DispBTx1
DispBTx2:ret

DispBTxt ENDP

; ------ dek¢dov n¡ textu ‡¡sla

DispBTxN PROC      NEAR

         cmp       al,0                     ; je nˆjak˜ text ?
         je        DispBTN4                 ; nen¡ text

DispBTN1:push      ax
         lodsb                              ; znak textu k ulo‘en¡
         cmp       al,"."
         jne       DispBTN2
         mov       al,cs:[DBFOdDes]         ; oddˆlova‡ desetin
DispBTN2:call      DispBCh                  ; ulo‘en¡ znaku AX
         pop       ax
         dec       al
         jnz       DispBTN1
DispBTN4:ret

DispBTxN ENDP

; -----------------------------------------------------------------------------
;        ulo‘en¡ mezer do bufferu (p©esko‡en¡m £seku © dku)
; -----------------------------------------------------------------------------
; VSTUP: BH=po‡et mezer k ulo‘en¡ (m–‘e b˜t = 0)
;        CX=‡¡ta‡ zbyl˜ch pozic do konce © dku (mus¡ b˜t kladn‚ ‡¡slo)
;        DX=po‡et pozic do za‡ tku © dku (kladn‚ ‡¡slo = je p©ed za‡ tkem © dku)
;        ES:DI=ukl dac¡ adresa
;        CLD
; VSTUP: CX=nov˜ ‡¡ta‡ zbyl˜ch pozic do konce © dku
;         DX=nov˜ po‡et pozic do za‡ tku
;         ES:DI=nov  ukl dac¡ adresa
; zni‡en‚ registry: BH
; -----------------------------------------------------------------------------

DispBSpc PROC      NEAR

         or        bh,bh                    ; jsou nˆjak‚ mezery k ulo‘en¡ ?
         jz        DispBSp3                 ; nejsou ‘ dn‚ mezery
DispBSp1:dec       dx                       ; sn¡‘en¡ ‡¡ta‡e zbyl˜ch pozic
         jns       DispBSp2                 ; je p©ed za‡ tkem okna
         jcxz      DispBSp2                 ; je za koncem © dku
         inc       di
         inc       di                       ; p©esko‡en¡ znaku mezery
         dec       cx                       ; sn¡‘en¡ ‡¡ta‡e zbyl˜ch pozic
DispBSp2:dec       bh                       ; ‡¡ta‡ mezer k ulo‘en¡
         jnz       DispBSp1                 ; je dal¨¡ mezera
DispBSp3:ret

DispBSpc ENDP

; -----------------------------------------------------------------------------
;        ulo‘en¡ jednoho znaku do bufferu
; -----------------------------------------------------------------------------
; VSTUP: AX=znak k ulo‘en¡
;        BL=po‡et zb˜vaj¡c¡ch pozic do konce pole polo‘ky
;        CX=‡¡ta‡ zbyl˜ch pozic do konce © dku (mus¡ b˜t kladn‚ ‡¡slo)
;        DX=po‡et pozic do za‡ tku © dku (kladn‚ ‡¡slo = je p©ed za‡ tkem © dku)
;        ES:DI=ukl dac¡ adresa
;        CLD
; VSTUP: BL=nov˜ po‡et zb˜vaj¡c¡ch pozic do konce pole polo‘ky
;         CX=nov˜ ‡¡ta‡ zbyl˜ch pozic do konce © dku
;         DX=nov˜ po‡et pozic do za‡ tku
;         ES:DI=nov  ukl dac¡ adresa
; -----------------------------------------------------------------------------

DispBChT:mov       al,ss:[bp-35]            ; oddˆlovac¡ znak ‡asu
         jmp       short DispBCh

DispBChD:mov       al,ss:[bp-23]            ; oddˆlovac¡ znak data

DispBCh  PROC      NEAR

         or        bl,bl                    ; zb˜v  dal¨¡ znak ?
         jz        DispBCh2                 ; nezb˜v  dal¨¡ znak
         dec       bl                       ; sn¡‘en¡ ‡¡ta‡e zbyl˜ch znak–
         dec       dx                       ; sn¡‘en¡ ‡¡ta‡e zbyl˜ch pozic
         jns       DispBCh2                 ; je p©ed za‡ tkem okna
         jcxz      DispBCh2                 ; je za koncem © dku

         test      byte ptr ss:[bp-18],bit1 ; je k¢dov n¡ DEMO ?
         jz        DispBCh1                 ; nen¡ k¢dov n¡ DEMO
         cmp       al," "
         je        DispBCh1
         cmp       al,"³"
         je        DispBCh1
         cmp       al,ss:[bp-35]            ; je oddˆlova‡ ‡asu ?
         je        DispBCh1
         cmp       al,ss:[bp-23]            ; je oddˆlovac¡ znak data ?
         je        DispBCh1
         cmp       al,cs:[DBFOdDes]         ; je oddˆlova‡ desetin ?
         je        DispBCh1

         push      ax
         mov       al,"*"
         stosw
         pop       ax
         dec       cx
         ret

DispBCh1:stosw                              ; ulo‘en¡ znaku do bufferu
         dec       cx                       ; sn¡‘en¡ ‡¡ta‡e zbyl˜ch pozic
DispBCh2:ret

DispBCh  ENDP

; -----------------------------------------------------------------------------
;        dek¢dov n¡ znaku z hlav¡
; -----------------------------------------------------------------------------
; VSTUP: AX=znak k ulo‘en¡
;        ES:DI=ukl dac¡ adresa
;        DX=ukazatel pozice na © dku
;        SS:[BP-10] (1) po‡et pozic na © dek (¨¡©ka okna - 2)
;        CLD
; -----------------------------------------------------------------------------

DispBNC  PROC      NEAR

         or        dh,dh
         jnz       DispBNC2                 ; je pozice za © dkem
         cmp       dl,ss:[bp-10]            ; p©ekro‡en konec © dku ?
         jae       DispBNC2                 ; je pozice za © dkem
         stosw                              ; ulo‘en¡ znaku
DispBNC2:inc       dx                       ; zv˜¨en¡ ukazatele pozice
         ret

DispBNC  ENDP

; -----------------------------------------------------------------------------
;        Zobrazen¡ © dku nadpisu datab ze DS:SI
; -----------------------------------------------------------------------------
; lok ln¡ promˆnn‚: SS:[BP-2] (2) © dek a pozice na displeji
;                   SS:[BP-4] (2) adresa definice menu
;                   SS:[BP-6] (2) ‡¡slo segmentu okna
;                   SS:[BP-7] (1) hladina k zobrazen¡ © dku
;                   SS:[BP-8] (1) ¨¡©ka © dku
;                   SS:[BP-10] (2) adresa okna
; -----------------------------------------------------------------------------

DispBNad PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         push      es
         mov       bp,sp

; ------ p©¡prava lok ln¡ch promˆnn˜ch

         sub       sp,10                    ; lok ln¡ promˆnn‚
         mov       ax,word ptr ds:[si+MnuPoz] ; © dek a pozice po‡ tku okna
         add       ah,1                     ; © dek a pozice k zobrazen¡
         mov       ss:[bp-2],ax             ; © dek a pozice na displeji
         mov       ss:[bp-4],si             ; adresa definice menu
         mov       ax,ds:[si+DBMnuSeg]      ; segment okna
         mov       ss:[bp-6],ax             ; segment okna
         call      far ptr GetSgAdr         ; adresa okna
         mov       ss:[bp-10],dx            ; adresa okna
         mov       al,ds:[si+MnuSir]        ; ¨¡©ka okna
         mov       ah,0
         sub       sp,ax
         sub       sp,ax                    ; buffer pro © dek
         mov       ah,ds:[si+MnuLev]        ; hladina k zobrazen¡
         mov       ss:[bp-8],ax             ; ¨¡©ka © dku a hladina

; ------ vymaz n¡ © dku

         cld
         push      ss
         pop       es                       ; ES <- segment z sobn¡ku
         mov       di,sp                    ; buffer v z sobn¡ku
         mov       al,"³"                   ; "³"
         mov       ah,ds:[ColMnu1R]         ; bˆ‘n  barva menu
         stosw                              ; lev˜ okraj

         mov       al," "
         mov       ah,ds:[ColDBF1]          ; barva stavov‚ho © dku
         mov       cl,ss:[bp-8]             ; ¨¡©ka © dku
         mov       ch,0
         dec       cx
         dec       cx                       ; bez okraj–
         rep       stosw                    ; vymaz n¡ © dku

         mov       al,"³"                   ; "³"
         mov       ah,ds:[ColMnu1L]         ; bˆ‘n  barva menu
         stosw                              ; prav˜ okraj

; ------ dek¢dov n¡ jm‚na souboru (CH=0)

         mov       ah,ds:[ColDBF1]          ; bˆ‘n  barva © dku
         mov       di,sp
         add       di,2*2                   ; za‡ tek k ulo‘en¡ jm‚na souboru
         mov       cl,8                     ; d‚lka jm‚na souboru
         push      ds

         test      byte ptr ds:[DBFEParm],bit1 ; je buffer modifikov n ?
         jz        DispBNd0                 ; buffer nen¡ modifikov n
         mov       byte ptr es:[di-2],"!"   ; p©¡znak modifikace bufferu

DispBNd0:mov       ds,ss:[bp-10]            ; segment okna
         test      byte ptr ds:[DBFParm],bit1 ; je z kaz z pisu ?
         jz        DispBNd1                 ; nen¡ z kaz z pisu
         mov       byte ptr es:[di-2],"*"   ; ozna‡en¡ z kazu z pisu

DispBNd1:mov       si,DBFName               ; jm‚no souboru
DispBNd2:lodsb
         stosw
         loop      DispBNd2

         mov       bh,ah                    ; bˆ‘n  barva
         mov       ax,ds:[DBFAktZ]          ; aktivn¡ z znam LOW
         mov       dx,ds:[DBFAktZ+2]        ; aktivn¡ z znam HIGH
         pop       ds

; ------ dek¢dov n¡ ‡¡sla polo‘ky (CH=0)

         add       ax,1                     ; korekce ‡¡sla aktivn¡ polo‘ky
         adc       dx,0
         mov       bl,ds:[OddelRad]         ; oddˆlova‡ © d–
         mov       cl,ss:[bp-8]             ; ¨¡©ka © dku
         mov       di,sp
         add       di,cx
         add       di,cx                    ; konec © dku
         sub       di,4                     ; konec k ulo‘en¡ ‡¡sla
         call      far ptr Dek10Num         ; dek¢dov n¡ ‡¡sla polo‘ky

; ------ dek¢dov n¡ nadpisu (CH=0)

         mov       di,sp                    ; buffer v z sobn¡ku
         mov       ah,ds:[ColDBF11]         ; barva nadpisu
         push      ds
         mov       ds,ss:[bp-10]            ; segment okna
         mov       si,DBFKom                ; koment ©
         mov       cl,ds:[DBFNKom]          ; d‚lka koment ©e
         mov       bl,ss:[bp-8]             ; ¨¡©ka © dku
         cmp       cl,bl
         jb        DspBNd24
         mov       cl,bl
DspBNd24:mov       bh,0
         sub       bx,cx                    ; zbytek na okraje
         and       bl,not bit0              ; zarovn n¡
         add       di,bx                    ; adresa k ulo‘en¡ textu
         jcxz      DispBNd4
DispBNd3:lodsb
         stosw
         loop      DispBNd3
DispBNd4:pop       ds

; ------ p©¡znak ozna‡en¡ souboru

         mov       cl,ss:[bp-8]             ; ¨¡©ka © dku
         mov       di,sp
         add       di,cx
         add       di,cx                    ; konec © dku
         push      es
         push      di
         mov       si,ss:[bp-4]             ; adresa definice menu
         call      far ptr GetADBF          ; poskytnut¡ aktivn¡ polo‘ky
         mov       al,es:[di]               ; p©¡znak polo‘ky
         pop       di
         pop       es
         jc        DispBNd5
         mov       es:[di-4],al             ; p©¡znak zru¨en¡ polo‘ky

; ------ zobrazen¡ © dku nadpisu

DispBNd5:mov       si,sp
         mov       dx,ss:[bp-2]             ; © dek a pozice na displeji
         mov       cl,ss:[bp-8]             ; ¨¡©ka © dku
         mov       al,ss:[bp-7]             ; hladina k zobrazen¡
         mov       ah,0
         call      far ptr DispMStr         ; zobrazen¡ © dku

; ------ n vrat registr–

         mov       sp,bp
         pop       es
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

DispBNad ENDP

; -----------------------------------------------------------------------------
;        korekce menu DS:SI
; -----------------------------------------------------------------------------

KorBMnu  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      es

; ------ adresa okna -> ES

         call      DBFAdrES                 ; aktualizace adresy okna -> ES

; ------ p©¡prava v˜¨ky okna

         mov       ah,0
         mov       al,ds:[si+MnuVys]        ; v˜¨ka okna
         sub       al,3                     ; bez okraj–
         mov       es:[DBFVysL],al          ; v˜¨ka datov˜ch © dk–
         mov       es:[DBFVysQ],ah          ; v˜¨ka prohl¡‘e‡e = 0
         test      byte ptr ds:[DBZobPar],bit0+bit1+bit2 ; povolena funkce ?
         jnz       KorBMn0H                 ; funkce nen¡ povolena
;         test      byte ptr ds:[DBZobPar],bit4 ; je text na‡ten ?
;         jnz       KorBMn0H                 ; text nen¡ na‡ten
         mov       cl,3
         div       cl                       ; v˜¨ka okna / 3
         mov       es:[DBFVysQ],al          ; v˜¨ka prohl¡‘e‡e
         inc       ax                       ; v‡etnˆ oddˆlovac¡ ‡ ry
         sub       es:[DBFVysL],al          ; 2/3 v˜¨ky okna

; ------ korekce © dk– ve str nkov‚m re‘imu

KorBMn0H:test      byte ptr es:[DBFParm],bit0 ; je str nkov˜ re‘im ?
         jz        KorBMn0B                 ; nen¡ str nkov˜ re‘im

; ------ omezen¡ po‡ te‡n¡ho © dku ve str nkov‚m re‘imu

         test      byte ptr ds:[DBFEParm],bit0 ; je editace ?
         jz        KorBMn0F                 ; nen¡ editace

         call      GetDBRK                  ; poskytnut¡ © dku s kurzorem -> DX
         mov       ax,es:[DBFTopL]          ; po‡ te‡n¡ © dek
         cmp       ax,dx                    ; je podte‡en¡ po‡ tku ?
         jb        KorBMn0C                 ; nen¡ podte‡en¡ po‡ tku
         mov       ax,dx                    ; © dek s kurzorem
         or        ax,ax                    ; je © dek 0 ?
         jz        KorBMn0D                 ; je © dek 0
         dec       ax                       ; nov˜ po‡ te‡n¡ © dek
KorBMn0D:mov       es:[DBFTopL],ax          ; nov˜ po‡ te‡n¡ © dek

; ------ omezen¡ koncov‚ho © dku ve str nkov‚m re‘imu

KorBMn0C:mov       cx,es:[DBFVysL]          ; v˜¨ka okna © dk–
         dec       cx
         dec       cx
;         mov       cl,ds:[si+MnuVys]        ; v˜¨ka okna
;         mov       ch,0
;         sub       cl,5                     ; po‡et vnit©n¡ch © dk– - 2
         sub       dx,cx                    ; minim ln¡ po‡ te‡n¡ © dek
         jnc       KorBMn0E
         xor       dx,dx                    ; omezen¡ p©i podte‡en¡
KorBMn0E:cmp       dx,es:[DBFTopL]          ; po‡ te‡n¡ © dek je OK ?
         jbe       KorBMn0F                 ; po‡ te‡n¡ © dek je OK
         mov       es:[DBFTopL],dx          ; omezen¡ po‡ te‡n¡ho © dku

; ------ omezen¡ na maxim ln¡ po‡ te‡n¡ © dek

KorBMn0F:mov       ax,es:[DBFNumL]          ; po‡et © dk– ve str nkov‚m re‘imu
;         mov       cl,ds:[si+MnuVys]        ; v˜¨ka okna
;         mov       ch,0
;         sub       cl,3                     ; po‡et vnit©n¡ch © dk–
         mov       cx,es:[DBFVysL]          ; vnit©n¡ v˜¨ka okna
         sub       ax,cx                    ; maxim ln¡ po‡ te‡n¡ © dek
         jnc       KorBMn0A                 ; OK
         xor       ax,ax                    ; m lo © dku - omezen¡ na 0
KorBMn0A:cmp       ax,es:[DBFTopL]          ; p©ekro‡en po‡ te‡n¡ © dek ?
         jae       KorBMn0B                 ; po‡ te‡n¡ © dek nep©ekro‡en
         mov       es:[DBFTopL],ax          ; omezen¡ po‡ te‡n¡ho © dku

; ------ p©¡prava n povˆdy

KorBMn0B:mov       word ptr ds:[si+MnuHlp],offset DBFMnHlP ; je nastaven¡ ¨¡©ky
         test      byte ptr ds:[DBFEPar2],bit0 ; nastaven¡ ¨¡©ky ?
         jnz       KorBMn0X                 ; je nastaven¡ ¨¡©ky
         mov       word ptr ds:[si+MnuHlp],offset DBFMnHl1 ; © dkov˜ re‘im
         test      byte ptr es:[DBFParm],bit0 ; je str nkov˜ re‘im ?
         jz        KorBMn01                 ; nen¡ str nkov˜ re‘im
         mov       word ptr ds:[si+MnuHlp],offset DBFMnHl2 ; str nkov˜ re‘im
KorBMn01:test      byte ptr ds:[DBFEParm],bit0 ; je editace ?
         jnz       KorBMn00                 ; je editace
         jmp       KorBMn08                 ; nen¡ editace

; ------ p©¡prava n povˆdy v re‘imu editace

KorBMn00:mov       bx,es:[DBFAktP]          ; aktivn¡ polo‘ka
         shl       bx,1
         shl       bx,1
         shl       bx,1                     ; offset v tabulce index– popisova‡–
         add       bx,es:[DBFAdrP]          ; adresa popisova‡e polo‘ky
         mov       al,es:[bx+DBFParX]       ; parametry polo‘ky

         mov       bx,offset DBFMnHl3       ; znakov‚ pole
         test      al,bit0+bit7             ; je znakov‚ pole ?
         jnz       KorBMn02                 ; je znakov‚ pole

         mov       bx,offset DBFMnHl4       ; ‡¡slo
         test      al,bit1+bit2             ; je ‡¡slo ?
         jnz       KorBMn02                 ; je ‡¡slo

         mov       bx,offset DBFMnHl8       ; logick‚ pole
         test      al,bit4                  ; je logick‚ pole ?
         jnz       KorBMn02                 ; logick‚ pole

         mov       bx,offset DBFMnHl9       ; textov‚ pole
         test      al,bit5                  ; je textov‚ pole ?
         jnz       KorBMn02                 ; textov‚ pole

         mov       bx,offset DBFMnHlA       ; ‡as
         test      al,bit6                  ; je ‡as ?
         jnz       KorBMn02                 ; ‡as

         mov       bx,offset DBFMnHl5       ; datum DMR
         cmp       byte ptr ds:[FormDat],1  ; evropsk˜ form t data ?
         je        KorBMn02                 ; form t data DMR
         mov       bx,offset DBFMnHl6       ; datum MDR
         jb        KorBMn02                 ; form t data MDR
         mov       bx,offset DBFMnHl7       ; datum RMD
KorBMn02:mov       ds:[si+MnuHlp],bx        ; adresa n povˆdy

; ------ korekce pozice kurzoru na © dku (je-li editace v © dkov‚m re‘imu)

KorBMn0X:test      byte ptr es:[DBFParm],bit0 ; je str nkov˜ re‘im ?
         jnz       KorBMn08                 ; je str nkov˜ re‘im

         call      GetDBFK                  ; stanoven¡ pozice kurzoru na © dku
         xchg      ax,dx                    ; AX <- pozice kurzoru na © dku
         sub       ax,10                    ; rezerva vlevo
         jnc       KorBMn03
         xor       ax,ax                    ; omezen¡ p©i podte‡en¡
KorBMn03:cmp       ax,es:[DBFTopP]          ; podte‡en¡ po‡ tku ?
         jae       KorBMn04                 ; nen¡ podte‡en¡ po‡ tku
         mov       es:[DBFTopP],ax          ; omezen¡ po‡ tku
KorBMn04:add       ax,20+2                  ; rezerva vpravo + korekce
         mov       dl,ds:[si+MnuSir]        ; ¨¡©ka okna
         mov       dh,0
         sub       ax,dx                    ; minim ln¡ po‡ tek
         jbe       KorBMn08
         cmp       ax,es:[DBFTopP]          ; p©ete‡en¡ konce ?
         jbe       KorBMn08                 ; nen¡ p©ete‡en¡ konce
         mov       es:[DBFTopP],ax          ; omezen¡ po‡ tku

; ------ omezen¡ maxim ln¡ho po‡ tku © dku okna

KorBMn08:mov       ax,es:[DBFSirR]          ; ¨¡©ka © dku
         mov       bl,ds:[si+MnuSir]        ; ¨¡©ka okna
         sub       bl,2                     ; vnit©n¡ ¨¡©ka okna
         mov       bh,0
         sub       ax,bx                    ; maxim ln¡ po‡ tek
         jnc       KorBMn09
         xor       ax,ax                    ; omezen¡ po‡ tku na 0
KorBMn09:cmp       ax,es:[DBFTopP]          ; p©ekro‡en maxim ln¡ po‡ tek ?
         jae       KorBMnu0                 ; je to OK
         mov       es:[DBFTopP],ax          ; omezen¡ po‡ tku

; ------ p©¡prava maxim ln¡ho z znamu (p©i editaci m–‘e b˜t 1 nav¡c)

KorBMnu0:mov       di,es:[DBFMaxZ]          ; po‡et z znam– LOW
         mov       bx,es:[DBFMaxZ+2]        ; po‡et z znam– HIGH
         test      byte ptr ds:[DBFEParm],bit0 ; je editace ?
         jz        KorBMn12                 ; nen¡ editace
         cmp       bx,es:[DBFAktZ+2]        ; je kurzor na konci ?
         jne       KorBMn11
         cmp       di,es:[DBFAktZ]
KorBMn11:jbe       KorBMnu1                 ; kurzor je na konci
KorBMn12:sub       di,1                     ; ‡¡slo posledn¡ho z znamu
         sbb       bx,0
         jnc       KorBMnu1
         xor       di,di                    ; omezen¡ p©i p©ete‡en¡
         xor       bx,bx

; ------ omezen¡ © dku kurzoru na posledn¡ z znam

KorBMnu1:mov       dx,es:[DBFAktZ+2]        ; aktivn¡ z znam HIGH
         mov       ax,es:[DBFAktZ]          ; aktivn¡ z znam LOW
         cmp       dx,bx
         jne       KorBMnu2
         cmp       ax,di
KorBMnu2:jbe       KorBMnu3                 ; kurzor je OK
         mov       ax,di
         mov       dx,bx                    ; omezen¡ kurzoru
         mov       es:[DBFAktZ],ax
         mov       es:[DBFAktZ+2],dx        ; omezen˜ maxim ln¡ z znam

; ------ omezen¡ p©i podte‡en¡ kurzoru pod po‡ tek okna

KorBMnu3:cmp       dx,es:[DBFTopZ+2]        ; je podte‡en prvn¡ © dek ?
         jne       KorBMnu4
         cmp       ax,es:[DBFTopZ]
KorBMnu4:ja        KorBMnu5                 ; nen¡ podte‡en¡ pod po‡ tek
         mov       es:[DBFTopZ],ax          ; omezen¡ po‡ tku okna
         mov       es:[DBFTopZ+2],dx
         mov       cx,ax
         or        cx,dx                    ; je prvn¡ z znam ?
         jz        KorBMnu5                 ; je prvn¡ z znam
         sub       word ptr es:[DBFTopZ],1  ; 1 © dek bude rezerva
         sbb       word ptr es:[DBFTopZ+2],0

; ------ stanoven¡ maxim ln¡ho po‡ tku -> BX:DI

KorBMnu5:mov       cx,es:[DBFVysL]          ; vnit©n¡ v˜¨ka okna
         dec       cx
         dec       cx
;         mov       ch,0
;         mov       cl,ds:[si+MnuVys]        ; celkov  v˜¨ka okna
;         sub       cl,5                     ; vnit©n¡ v˜¨ka okna - 1
         sub       di,cx                    ; maxim ln¡ po‡ tek
         sbb       bx,0
         jnc       KorBMnu6                 ; nen¡ p©ete‡en¡
         xor       di,di                    ; omezen¡ p©i p©ete‡en¡
         xor       bx,bx

; ------ stanoven¡ po‡ tku podle kurzoru

KorBMnu6:dec       cx                       ; vnit©n¡ v˜¨ka okna
         sub       ax,cx                    ; maxim ln¡ po‡ tek okna
         sbb       dx,0
         jnc       KorBMnu7
         xor       ax,ax                    ; omezen¡ p©i podte‡en¡
         xor       dx,dx

; ------ kontrola p©ete‡en¡ konce okna podle kurzoru

KorBMnu7:cmp       dx,es:[DBFTopZ+2]
         jne       KorBMnu8
         cmp       ax,es:[DBFTopZ]
KorBMnu8:jbe       KorBMn82                 ; nen¡ p©ete‡en¡ po‡ tku
         mov       es:[DBFTopZ+2],dx        ; omezen¡ po‡ tku
         mov       es:[DBFTopZ],ax

; ------ omezen¡ po‡ tku podle maxim ln¡ho po‡ tku

KorBMn82:cmp       bx,es:[DBFTopZ+2]
         jne       KorBMn84
         cmp       di,es:[DBFTopZ]
KorBMn84:jae       KorBMnu9
         mov       es:[DBFTopZ+2],bx
         mov       es:[DBFTopZ],di

; ------ n vrat registr–

KorBMnu9:pop       es
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

KorBMnu  ENDP

; *****************************************************************************
;                             HlavDBF
;                 z pis z hlav¡ do souboru DBF
; -----------------------------------------------------------------------------
; VSTUP: SI=definice okna
;        DS=datov˜ segment
; VSTUP:CY=chyba operace
; *****************************************************************************

HlavDBF  PROC      FAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx
         push      si
         push      es

; ------ adresa definice okna

         call      DBFAdrES                 ; aktualizace adresy okna -> ES

; ------ oprava po‡tu z znam–

         mov       ax,es:[DBFMaxZ]
         mov       es:[DBFNumZ],ax
         mov       ax,es:[DBFMaxZ+2]
         mov       es:[DBFNumZ+2],ax

; ------ korekce popisova‡e typu souboru

         mov       al,3                     ; bˆ‘n˜ soubor bez MEMO
         test      byte ptr es:[DBFParm],bit4 ; je MEMO soubor ?
         jz        HlavDBF3                 ; nen¡ MEMO soubor
         mov       al,83h                   ; soubor DBASE MEMO
         test      byte ptr es:[DBFParm],bit3 ; je soubor DBASE MEMO ?
         jnz       HlavDBF3                 ; je soubor DBASE MEMO
         mov       al,0f5h                  ; jinak FOXPRO MEMO
HlavDBF3:mov       es:[DBFVer],al           ; identifik tor souboru

; ------ aktualizace data

         test      byte ptr ds:[DBFEParm],bit7 ; mˆn¡ se datum ?
         jnz       HlavDBF5                 ; datum se nemˆn¡
         mov       ah,2ah
         int       21h                      ; poskytnut¡ data
         sub       cx,1900
         cmp       cx,100
         jb        HlavDBF4
         sub       cx,100                   ; korekce roku
HlavDBF4:mov       es:[DBFRok],cl           ; rok
         mov       es:[DBFMes],dh           ; mˆs¡c
         mov       es:[DBFDen],dl           ; den
HlavDBF5:and       byte ptr ds:[DBFEParm],not bit7 ; zru¨en¡ p©¡znaku

; ------ inicializace intern¡ch p©ep¡na‡–

         test      byte ptr es:[DBFParm2],bit0 ; jsou intern¡ p©ep¡na‡e ?
         jz        HlavDBF6                 ; nejsou intern¡ p©ep¡na‡e
         mov       word ptr es:[DBFSwIdn],"MD" ; identifik tor intern¡ch p©ep¡na‡–
         mov       word ptr es:[DBFSwKod],"00" ; k¢d neur‡en˜
         cmp       byte ptr ds:[CodePagK],0 ; je k¢d ur‡en˜ ?
         je        HlavDBF6                 ; k¢d nen¡ ur‡en˜
         mov       word ptr es:[DBFSwKod],"AK" ; je k¢d Kamenick˜ch
         cmp       byte ptr ds:[CodePagK],1 ; je Kamenick˜ch ?
         je        HlavDBF6                 ; je Kamenick˜ch
         mov       word ptr es:[DBFSwKod],"2L" ; k¢d Latin 2

; ------ z pis z hlav¡ do souboru

HlavDBF6:mov       ax,es:[DBFIdent]
         call      far ptr ResFile          ; ukazatel v souboru na za‡ tek
         jc        HlavDBF9                 ; chyba z pisu
         mov       si,DBFHlava              ; z hlav¡ souboru
         mov       cx,es:[DBFBytH]          ; d‚lka z hlav¡ souboru
         call      far ptr WritFile         ; z pis z hlav¡ do souboru
         jc        HlavDBF9                 ; chyba

; ------ vypr zdnˆn¡ zmˆn

         call      far ptr FlshFile         ; ulo‘en¡ zmˆn na disk

; ------ n vrat registr–

HlavDBF9:pop       es
         pop       si
         pop       dx
         pop       cx
         pop       ax
         ret

HlavDBF  ENDP

; *****************************************************************************
;                           SetADBF
;               ulo‘en¡ aktivn¡ho z znamu na disk
; -----------------------------------------------------------------------------
; VSTUP: SI=definice okna
;        DS=datov˜ segment
; VSTUP:CY=chyba
; *****************************************************************************

SetADBF  PROC      FAR

; ------ £schova registr–

         push      ax
         push      dx
         push      di
         push      es

; ------ poskytnut¡ adresy z znamu

         call      far ptr GetADBF          ; poskytnut¡ aktivn¡ polo‘ky
         jc        SetADBF2                 ; chyba

; ------ ulo‘en¡ aktivn¡ho z znamu

         mov       dx,es:[DBFAktZ+2]        ; ‡¡slo z znamu HIGH
         mov       ax,es:[DBFAktZ]          ; ‡¡slo z znamu LOW
         call      far ptr SetDBF           ; ulo‘en¡ z znamu na disk

; ------ n vrat registr–

SetADBF2:pop       es
         pop       di
         pop       dx
         pop       ax
         ret

SetADBF  ENDP

; *****************************************************************************
;                              SetDBF
;          ulo‘en¡ z znamu na disk (nezaji¨Ÿuje aktualizaci bufferu)
; -----------------------------------------------------------------------------
; VSTUP: SI=adresa definice okna
;        DX:AX=‡¡slo z znamu (nekontroluj¡ se meze)
;        ES:DI=adresa z znamu
;        DS=datov˜ segment
; VSTUP:CY=chyba
; *****************************************************************************

SetDBF   PROC      FAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx
         push      si

; ------ adresa okna

         push      es

         push      ax                       ; ‡¡slo z znamu LOW
         call      DBFAdrES                 ; aktualizace adresy okna -> ES

; ------ v˜po‡et offsetu v souboru

         mov       cx,es:[DBFbytZ]          ; po‡et bajt– na z znam
         xchg      ax,dx                    ; AX <- ‡¡slo z znam HIGH
         mul       cx                       ; v˜po‡et offsetu HIGH
         xchg      ax,cx                    ; CX <- offset HIGH
         pop       dx                       ; ‡¡slo z znamu LOW
         mul       dx                       ; v˜po‡et offsetu LOW
         add       dx,cx                    ; uschovan˜ offset HIGH
         add       ax,es:[DBFBytH]          ; p©i‡ten¡ bajt– na z hlav¡
         adc       dx,0

; ------ nastaven¡ ukazatele v souboru

         xchg      ax,cx                    ; ukazatel v souboru LOW
         mov       ax,es:[DBFIdent]         ; identifik tor souboru
         call      far ptr SetUFile         ; nastaven¡ ukazatele v souboru
         mov       cx,es:[DBFBytZ]          ; po‡et bajt– na z znam

         pop       es                       ; adresa z znamu
         jc        SetDBF2                  ; nˆjak  chyba

; ------ ulo‘en¡ z znamu na disk

         or        byte ptr ds:[DBFEPar2],bit5 ; byl z pis z znamu na disk
         mov       si,di                    ; SI <- offset z znamu
         call      far ptr WritFile         ; ulo‘en¡ z znamu na disk

; ------ n vrat registr–

SetDBF2: pop       si
         pop       dx
         pop       cx
         pop       ax
         ret

SetDBF   ENDP

; *****************************************************************************
;                             GetADBF
;                 poskytnut¡ aktivn¡ho z znamu datab ze
; -----------------------------------------------------------------------------
; VSTUP: SI=definice menu okna editoru datab ze
;        DS=datov˜ segment
; VSTUP:ES:DI=adresa datab zov‚ polo‘ky
;        CY=chyba
; *****************************************************************************

GetADBF  PROC      FAR

         push      ax
         push      dx

         call      DBFAdrES                 ; aktualizace adresy okna -> ES

         mov       ax,es:[DBFAktZ]
         mov       dx,es:[DBFAktZ+2]
         call      far ptr GetDBF

GetADBF3:pop       dx
         pop       ax
         ret

GetADBF  ENDP

; *****************************************************************************
;                              GetDBF
;                  poskytnut¡ polo‘ky datab ze
; -----------------------------------------------------------------------------
; VSTUP: DX:AX=‡¡slo po‘adovan‚ polo‘ky (0...MAX-1)
;        SI=definice menu okna editoru datab ze
;        DS=datov˜ segment
; VSTUP:ES:DI=adresa datab zov‚ polo‘ky
;        CY=chyba
; *****************************************************************************

GetDBF   PROC      FAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si

; ------ adresa definice okna

         call      DBFAdrES                 ; aktualizace adresy okna -> ES

; ------ kontrola, zda je platn  polo‘ka

         cmp       dx,es:[DBFMaxZ+2]        ; je ‡¡slo z znamu OK ?
         jne       GetDBF1
         cmp       ax,es:[DBFMaxZ]
GetDBF1: cmc
         jc        GetDBF3                  ; chybn‚ ‡¡slo z znamu

; ------ kontrola, zda je polo‘ka v bufferu

GetDBF2: mov       cx,ax                    ; CX <- ‡¡slo polo‘ky LOW
         mov       bx,dx                    ; BX <- ‡¡slo polo‘ky HIGH
         sub       cx,es:[DBFBegD]          ; relativn¡ ‡¡slo polo‘ky v bufferu
         sbb       bx,es:[DBFBegD+2]
         jnz       GetDBF4                  ; polo‘ka je pod bufferem nebo za
         cmp       cx,es:[DBFNumD]          ; kontrola konce bufferu
         jae       GetDBF4                  ; polo‘ka nen¡ v bufferu

; ------ polo‘ka je v bufferu - v˜po‡et jej¡ adresy

         mov       ax,es:[DBFBytZ]          ; po‡et bajt– na z znam
         mul       cx                       ; v˜po‡et offsetu v bufferu
         add       ax,es:[DBFAdrD]          ; adresa polo‘ky v bufferu
         xchg      ax,di                    ; DI <- adresa polo‘ky
         clc                                ; p©¡znak polo‘ky OK
GetDBF3: jmp       short GetDBF9

; ------ v˜po‡et nov‚ po‡ te‡n¡ polo‘ky v bufferu

GetDBF4: mov       cx,ax                    ; £schova polo‘ky LOW
         mov       si,es:[DBFMaxD]          ; po‡et z znam– v bufferu
         shr       si,1                     ; po‡et z znam– v polovinˆ bufferu
         sub       ax,si                    ; po‡ te‡n¡ z znam v bufferu
         sbb       dx,0
         jnc       GetDBF5
         xor       ax,ax                    ; omezen¡ p©i podte‡en¡
         xor       dx,dx
GetDBF5: mov       es:[DBFBegD],ax          ; nov˜ po‡ te‡n¡ z znam
         mov       es:[DBFBegD+2],dx
         sub       cx,ax                    ; ‡¡slo z znamu relativnˆ v bufferu
         mov       word ptr es:[DBFNumD],0  ; nejsou z znamy v bufferu

; ------ v˜po‡et adresy polo‘ky -> DI

         push      ax
         push      dx

         xchg      ax,cx                    ; AX <- z znam relativnˆ
         mov       cx,es:[DBFBytZ]          ; po‡et bajt– na z znam
         mul       cx                       ; v˜po‡et offsetu v bufferu
         add       ax,es:[DBFAdrD]          ; adresa z znamu v bufferu
         xchg      ax,di                    ; DI <- adresa z znamu

         pop       dx
         pop       ax

; ------ v˜po‡et offsetu po‡ tku v souboru -> DX:AX

         push      ax                       ; ‡¡slo z znamu LOW
         xchg      ax,dx                    ; AX <- ‡¡slo z znam HIGH
         mul       cx                       ; v˜po‡et offsetu HIGH
         xchg      ax,cx                    ; CX <- offset HIGH
         pop       dx                       ; ‡¡slo z znamu LOW
         mul       dx                       ; v˜po‡et offsetu LOW
         add       dx,cx                    ; uschovan˜ offset HIGH
         add       ax,es:[DBFBytH]          ; p©i‡ten¡ bajt– na z hlav¡
         adc       dx,0

; ------ nastaven¡ ukazatele po‡ tku bufferu

         xchg      ax,cx                    ; CX <- offset za‡ tku LOW
         mov       ax,es:[DBFIdent]         ; identifik tor souboru
         call      far ptr SetUFile         ; nastaven¡ ukazatele souboru
         jc        GetDBF9                  ; chyba operace

; ------ na‡ten¡ dat do bufferu

         push      di
         mov       ax,es:[DBFBytZ]          ; po‡et bajt– na z znam
         mul       word ptr es:[DBFMaxD]    ; po‡et bajt– k na‡ten¡
         xchg      ax,cx                    ; CX <- po‡et bajt– k na‡ten¡
         mov       di,es:[DBFAdrD]          ; adresa bufferu
         mov       ax,es:[DBFIdent]         ; identifik tor souboru
         call      far ptr ReadFile         ; na‡ten¡ dat do bufferu
         pop       di
         jc        GetDBF9                  ; chyba - nen¡ nic na‡teno

; ------ v˜po‡et nov‚ho po‡tu z znam– v bufferu

         xchg      ax,cx                    ; AX <- po‡et na‡ten˜ch bajt–
         xor       dx,dx                    ; DX <- 0
         div       word ptr es:[DBFBytZ]    ; v˜po‡et nov‚ho po‡tu z znam–
         mov       es:[DBFNumD],ax          ; nov˜ po‡et z znam– v bufferu
         clc                                ; p©¡znak operace OK

; ------ n vrat registr–

GetDBF9: pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

GetDBF   ENDP

; *****************************************************************************
;                               MiniDBF
;     minimalizace velikosti bloku datab ze (uvolnˆn¡ bufferu) - na 1 z znam
; -----------------------------------------------------------------------------
; VSTUP: DS=datov˜ segment
;        SI=definice okna editoru datab ze
; *****************************************************************************

MiniDBF  PROC      FAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx
         push      di
         push      es

; ------ adresa bloku -> ES

         call      DBFAdrES                 ; aktualizace adresy okna -> ES

; ------ v˜po‡et po‡tu bajt– k vypu¨tˆn¡

         mov       word ptr es:[DBFNumD],0  ; v bufferu nen¡ ‘ dn˜ z znam
         mov       ax,es:[DBFBytZ]          ; po‡et bajt– na z znam
         mov       cx,es:[DBFMaxD]          ; maxim lnˆ z znam– v bufferu
         jcxz      MiniDBF9                 ; nˆjak  chyba ?
         dec       cx                       ; 1 z znam se ponech 
         jcxz      MiniDBF9                 ; buffer je ji‘ minim ln¡
         mul       cx                       ; v˜po‡et velikosti bufferu
         xchg      ax,cx                    ; CX <- po‡et bajt– ke zru¨en¡
         mov       word ptr es:[DBFMaxD],1  ; velikost bufferu 1 z znam

; ------ zru¨en¡ nadbyte‡n‚ ‡ sti bufferu

         mov       di,es:[DBFAdrD]          ; adresa bufferu
         mov       ax,ds:[si+DBMnuSeg]      ; segment datab ze
         call      far ptr DelDat           ; zru¨en¡ dat z bufferu

; ------ n vrat registr–

MiniDBF9:pop       es
         pop       di
         pop       dx
         pop       cx
         pop       ax
         ret

MiniDBF  ENDP

; *****************************************************************************
;                               MaxiDBF
;        maximalizace velikosti bloku datab ze (na maxim ln¡ volnou pamˆŸ)
; -----------------------------------------------------------------------------
; VSTUP: DS=datov˜ segment
;        SI=definice okna editoru datab ze
; *****************************************************************************

MaxiDBF  PROC      FAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      di
         push      es

; ------ adresa bloku -> ES

         call      DBFAdrES                 ; aktualizace adresy okna -> ES

; ------ stanoven¡ voln‚ho m¡sta v pamˆti

         mov       word ptr es:[DBFNumD],0  ; v bufferu nen¡ ‘ dn˜ z znam
         call      far ptr GetFree          ; poskytnut¡ voln‚ pamˆti
         sub       bx,100h                  ; rezerva 4 KB
         jbe       MaxiDBF9                 ; m lo pamˆti
         cmp       bx,0fffh                 ; maximum 64 KB
         jbe       MaxiDBF2                 ; OK
         mov       bx,0fffh                 ; omezen¡ na 64 KB
MaxiDBF2:mov       cl,4
         shl       bx,cl                    ; voln  pamˆŸ v bajtech

; ------ v˜po‡et konce bufferu

         add       bx,es:[0]                ; nov˜ mo‘n˜ konec bufferu
         jnc       MaxiDBF3                 ; nep©es hne 64 KB
         mov       bx,0fff0h                ; omezen˜ konec bufferu

; ------ v˜po‡et nov‚ho po‡tu z znam– v bufferu

MaxiDBF3:sub       bx,es:[0]                ; voln‚ m¡sto v bufferu
         jbe       MaxiDBF9                 ; nelze ji‘ zvˆt¨it
         xchg      ax,bx                    ; AX <- voln‚ m¡sto
         xor       dx,dx                    ; DX <- 0
         div       word ptr es:[DBFBytZ]    ; v˜po‡et po‡tu z znam–
         mov       bx,ax                    ; BX <- nov˜ po‡et z znam–
         mul       word ptr es:[DBFBytZ]    ; nov˜ po‡et bajt– k vlo‘en¡
         xchg      ax,cx                    ; CX <- nov˜ po‡et bajt–

; ------ vlo‘en¡ nov˜ch dat

         mov       di,es:[DBFAdrD]          ; adresa datov‚ho bufferu
         mov       ax,ds:[si+DBMnuSeg]      ; segment datab ze
         call      far ptr InsDat           ; vytvo©en¡ m¡sta pro buffer
         jc        MaxiDBF9                 ; chyba ?
         add       es:[DBFMaxD],bx          ; zv˜¨en¡ po‡tu z znam– v bufferu

; ------ n vrat registr–

MaxiDBF9:pop       es
         pop       di
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

MaxiDBF  ENDP

; *****************************************************************************
;                               OpenDBF
;                    otev©en¡ okna editoru datab ze
; -----------------------------------------------------------------------------
; VSTUP: DS=datov˜ segment
;        SI=definice okna editoru datab ze
;        ES:DI=jm‚no souboru DBF (pozor - nesm¡ b˜t ovlivnˆno posouv n¡m blok–)
;        CX=d‚lka jm‚na souboru DBF (max. 86 znak– !)
; VSTUP: CY=nedostatek pamˆti, soubor nenalezen nebo m  chybnou strukturu
; -----------------------------------------------------------------------------
; lok ln¡ promˆnn‚: SS:[BP-2] (2) segment jm‚na souboru
;                   SS:[BP-4] (2) offset jm‚na souboru
;                   SS:[BP-6] (2) ‡¡slo datov‚ho bloku
;                   SS:[BP-7] (1) ¨¡©ka textov‚ polo‘ky v str nkov‚m re‘imu
;                   SS:[BP-8] (1)
;                   SS:[BP-10] (2) adresa definice menu
; *****************************************************************************
;þ
OpenDBF  PROC      FAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         push      es
         mov       bp,sp

; ------ p©¡prava lok ln¡ch promˆnn˜ch

         sub       sp,10
         mov       ss:[bp-2],es             ; segment jm‚na souboru
         mov       ss:[bp-4],di             ; offset jm‚na souboru
         mov       al,ds:[si+MnuSir]        ; ¨¡©ka okna
         sub       al,15                    ; zbytek na textov˜ © dek
         mov       ss:[bp-7],al             ; maxim ln¡ ¨¡©ka textov‚ polo‘ky
         mov       ss:[bp-10],si            ; adresa definice menu

; ------ vytvo©en¡ datov‚ho bloku

         call      far ptr CreatSeg         ; vytvo©en¡ datov‚ho bloku
         jnc       OpenDBF1
         jmp       OpenDBF9                 ; chyba (CY=chyba)

OpenDBF1:mov       ss:[bp-6],ax             ; ‡¡slo datov‚ho bloku
         mov       ds:[si+DBMnuSeg],ax      ; ‡¡slo datov‚ho bloku

; ------ adresa datov‚ho bloku -> ES

         call      DBFAdrES                 ; aktualizace adresy okna -> ES

; ------ nastaven¡ inicializa‡n¡ velikosti datov‚ho bloku

         mov       bx,DBFPopis              ; inicializa‡n¡ velikost bloku
         call      far ptr ModiSegS         ; nastaven¡ velikosti bloku
         jnc       OpenDBF3                 ; operace OK
OpenDBF2:jmp       OpenDBF8                 ; chyba - nedostatek pamˆti

; ------ nulov n¡ bufferu

OpenDBF3:push      cx                       ; d‚lka jm‚na souboru
         cld
         xor       di,di
         mov       ax,bx                    ; AX <- inicializa‡n¡ velikost
         stosw                              ; velikost datov‚ho bloku
         mov       cx,(DBFPopis+1-2)/2      ; velikost datov‚ho bloku ve slovech
         xor       ax,ax                    ; AX <- 0
         rep       stosw                    ; vynulov n¡ bloku
         pop       cx                       ; d‚lka jm‚na souboru

; ------ p©¡prava jm‚na souboru DBF (CX=d‚lka jm‚na souboru)

         push      ds

         lds       si,ss:[bp-4]             ; adresa jm‚na souboru
         mov       di,DBFSoub               ; jm‚no souboru DBF
         mov       es:[DBFSoubN],cx         ; d‚lka jm‚na souboru
         cld
         push      si
         push      cx
         rep       movsb                    ; p©enos jm‚na souboru
         pop       cx
         pop       si
         mov       al,0
         stosb                              ; koncov  0

; ------ p©¡prava jm‚na souboru FPT

         mov       di,DBFSouT               ; jm‚no souboru DBT
         rep       movsb                    ; p©enos jm‚na souboru
         sub       di,4                     ; n vrat na oddˆlovac¡ te‡ku
         mov       cl,4                     ; po‡et pozic k hled n¡
         mov       al,"."                   ; oddˆlovac¡ te‡ka
         repne     scasb                    ; nalezen¡ oddˆlovac¡ te‡ky
         je        OpenDB32                 ; te‡ka nalezena
         stosb                              ; oddˆlovac¡ te‡ka
OpenDB32:mov       ax,"PF"
         stosw                              ; "FP"
         mov       ax,"T"
         stosw                              ; "T" a koncov  0

         sub       di,DBFSouT+1             ; d‚lka jm‚na souboru MEMO
         mov       es:[DBFSouTN],di         ; d‚lka jm‚na souboru MEMO

         pop       ds

; ------ otev©en¡ souboru datab ze DBF

         mov       si,DBFSoub               ; jm‚no souboru DBF
         call      far ptr OpenRW           ; otev©en¡ pro ‡ten¡ i z pis
         jnc       OpenDB33                 ; soubor otev©en OK
         call      far ptr OpenR            ; otev©en¡ alespo¤ pro ‡ten¡
         jc        OpenDBF2                 ; chyba - soubor nenalezen
         or        byte ptr es:[DBFParm],bit1 ; p©¡znak z kazu z pisu
OpenDB33:mov       es:[DBFIdent],ax         ; identifik tor souboru

; ------ p©¡prava implicitn¡ho z hlav¡ souboru (je-li nov˜ soubor)

         mov       di,DBFHlava              ; za‡ tek z hlav¡ souboru
         mov       byte ptr es:[di],3       ; verze datab ze
         mov       byte ptr es:[di+DBFBytH-DBFHlava],33 ; d‚lka z hlav¡
         mov       word ptr es:[di+DBFSwIdn-DBFHlava],"MD" ; identifik tor
         mov       word ptr es:[di+DBFSwKod-DBFHlava],"00" ; k¢d neur‡en˜
         cmp       byte ptr ds:[CodePagK],0 ; je k¢d ur‡en˜ ?
         je        OpnDB332                 ; k¢d nen¡ ur‡en˜
         mov       word ptr es:[di+DBFSwKod-DBFHlava],"AK" ; k¢d Kamenick˜ch
         cmp       byte ptr ds:[CodePagK],1 ; je Kamenick˜ch ?
         je        OpnDB332                 ; je Kamenick˜ch
         mov       word ptr es:[di+DBFSwKod-DBFHlava],"2L" ; k¢d Latin 2

; ------ na‡ten¡ popisova‡e z hlav¡ souboru do pamˆti

OpnDB332:mov       cx,DBFPopis-DBFHlava     ; d‚lka popisova‡e z hlav¡ souboru
         call      far ptr ReadFile         ; na‡ten¡ popisova‡e z hlav¡
         jnc       OpnDB333
         jmp       OpenDBF7                 ; chyba ‡ten¡

OpnDB333:cmp       word ptr es:[di+DBFBytH-DBFHlava],33 ; minim ln¡ d‚lka z hlav¡
         jae       OpnDB334                 ; d‚lka z hlav¡ je OK
         mov       word ptr es:[di+DBFBytH-DBFHlava],33 ; minim ln¡ d‚lka

OpnDB334:cmp       word ptr es:[di+DBFBytZ-DBFHlava],0 ; je spr vn  d‚lka z znamu ?
         jne       OpenDB34                 ; d‚lka z znamu je OK
         inc       byte ptr es:[di+DBFBytZ-DBFHlava]; oprava d‚lky z znamu na 1

; ------ rozli¨en¡, zda je form t DBASE nebo FOXPRO

OpenDB34:cmp       byte ptr es:[di],83h     ; je MEMO DBASE ?
         jne       OpenDB35                 ; nen¡ MEMO DBASE (tak je FOXPRO)
         or        byte ptr es:[DBFParm],bit3 ; je MEMO DBASE
         mov       di,es:[DBFSouTN]         ; d‚lka jm‚na souboru MEMO
         mov       word ptr es:[di+DBFSouT-3],"BD" ; je soubor "DBT"

; ------ test, zda jsou intern¡ p©ep¡na‡e

OpenDB35:cmp       word ptr es:[DBFSwIdn],"MD" ; jsou intern¡ p©ep¡na‡e ?
         jne       OpenDB38                 ; nejsou intern¡ p©ep¡na‡e
         mov       dl,bit2                  ; k¢d pro Kamenick˜ch
         cmp       word ptr es:[DBFSwKod],"AK" ; je k¢d Kamenick˜ch ?
         je        OpenDB36                 ; je k¢d Kamenick˜ch
         mov       dl,bit3                  ; k¢d pro Latin 2
         cmp       word ptr es:[DBFSwKod],"2L" ; je k¢d Latin 2 ?
         jne       OpenDB38                 ; nejsou intern¡ p©ep¡na‡e
OpenDB36:or        byte ptr es:[DBFParm2],bit0 ; jsou intern¡ p©ep¡na‡e
         mov       dh,bit1                  ; p©¡prava v˜stupn¡ho k¢du
         mov       cl,ds:[CodePagK]         ; aktu ln¡ k¢d
         cmp       cl,0                     ; je k¢d ur‡en˜ ?
         je        OpenDB38                 ; k¢d nen¡ ur‡en˜
         shl       dh,cl                    ; p©¡prava k¢du ke konverzi
         cmp       dh,dl                    ; je pot©eba zmˆnit k¢d ?
         je        OpenDB38                 ; k¢d nen¡ pot©eba mˆnit
         mov       cx,es:[DBFBytH]          ; d‚lka z hlav¡ v bajtech
         mov       bx,ss:[bp-6]             ; ‡¡slo datov‚ho bloku
         call      OpenKonv                 ; konverze k¢du -> soubor AX
         jc        OpenDB75                 ; chyba nebo p©eru¨en¡
;þ
OpenDB38:

; ------ vytvo©en¡ m¡sta pro na‡ten¡ popisova‡– polo‘ek

         mov       cx,es:[DBFBytH]          ; d‚lka z hlav¡ v bajtech
         sub       cx,DBFPopis-DBFHlava     ; ode‡ten¡ ji‘ na‡ten‚ ‡ sti
;         jbe       OpenDB75                 ; mal  d‚lka z hlav¡
         push      ax
         mov       ax,ss:[bp-6]             ; ‡¡slo datov‚ho bloku
         mov       di,DBFPopis              ; adresa k na‡ten¡ popisova‡–
         call      far ptr InsDat           ; vytvo©en¡ m¡sta
         pop       ax
         jnc       OpenDB76                 ; operace OK
OpenDB75:jmp       OpenDBF7                 ; chyba - nedostatek pamˆti

; ------ vynulov n¡ popisova‡– polo‘ek

OpenDB76:push      ax
         push      cx
         push      di
         mov       al,CR                    ; p©¡znak konce z hlav¡
         cld
         rep       stosb                    ; vynulov n¡ popisova‡–
         pop       di
         pop       cx
         pop       ax

; ------ na‡ten¡ popisova‡– polo‘ek

         push      cx
         call      far ptr ReadFile         ; na‡ten¡ zbytku z hlav¡
         pop       cx
         jc        OpenDB75                 ; byla chyba operace

; ------ p©¡prava k vytvo©en¡ indexov‚ tabulky popisova‡–

         mov       word ptr ds:[DBZobOld+2],-1 ; pot©eba na‡ten¡ MEMO
         or        byte ptr ds:[DBZobPar],bit1+bit4 ; nen¡ MEMO pole
         and       byte ptr ds:[DBFEParm],not bit6 ; nen¡ nov˜ soubor
         mov       si,di                    ; adresa za‡ tku popisova‡–
         add       di,cx                    ; adresa konce popisova‡–
         mov       bx,di                    ; £schova konce popisova‡–
         mov       cx,DBFX                  ; d‚lka popisova‡e polo‘ky
         mov       ax,ss:[bp-6]             ; ‡¡slo datov‚ho bloku
         mov       es:[DBFAdrP],di          ; adresa indexu popisova‡–
         sub       bx,32                    ; rezerva pro celou polo‘ku

; ------ kontrola, zda je dal¨¡ polo‘ka popisova‡e

OpenDBF4:cmp       si,bx                    ; je je¨tˆ dal¨¡ popisova‡ ?
         jbe       OpenDB4B                 ; je je¨tˆ dal¨¡ popisova‡
         jmp       OpenDBF5                 ; nen¡ dal¨¡ popisova‡

; ------ kontrola, zda je platn˜ popisova‡

OpenDB4B:cmp       byte ptr es:[si]," "     ; je platn˜ popisova‡ ?
         jbe       OpenDB4D                 ; nen¡ platn˜ popisova‡
         mov       dl,es:[si+16]            ; d‚lka polo‘ky (bajt–)
         or        dl,dl                    ; je platn  d‚lka polo‘ky ?
         jnz       OpenDB4C                 ; polo‘ka je OK

; ------ vytvo©en¡ m¡sta pro index

OpenDB4D:jmp       OpenDB48                 ; nen¡ platn  polo‘ka

OpenDB4C:call      far ptr InsDat           ; vytvo©en¡ m¡sta pro index
         jnc       OpenDB4x                 ; pamˆŸ je OK
OpenDB74:jmp       OpenDBF7

; ------ inicializace indexu

OpenDB4x:inc       word ptr es:[DBFNumP]    ; zv˜¨en¡ po‡tu popisova‡–
         mov       es:[di+DBFNamX],si       ; adresa jm‚na polo‘ky
         mov       dh,0
         mov       es:[di+DBFDelX],dx       ; d‚lka polo‘ky (bajt–)

; ------ po‡et © dk– ve str nkov‚m re‘imu

OpenDB4M:inc       word ptr es:[DBFNumL]    ; zv˜¨en¡ po‡tu © dk– str nk. re‘imu
         sub       dl,ss:[bp-7]             ; ode‡ten¡ ¨¡©ky jednoho © dku
         ja        OpenDB4M                 ; je dal¨¡ © dek

; ------ typ polo‘ky

         mov       dl,es:[si+11]            ; typ polo‘ky

         mov       dh,bit0                  ; textov  polo‘ka "C"
         cmp       dl,"C"
         je        OpenDB40                 ; je textov  polo‘ka "C"

         mov       dh,bit1                  ; ‡¡seln  polo‘ka "N"
         cmp       dl,"N"
         je        OpenDB40                 ; je ‡¡seln  polo‘ka "N"

         mov       dh,bit2                  ; ‡¡seln  polo‘ka "F"
         cmp       dl,"F"
         je        OpenDB40                 ; ‡¡seln  polo‘ka "F"

         cmp       byte ptr es:[si+16],8    ; d‚lka polo‘ky pro datum ?
         jne       OpnDB4M2                 ; nen¡ datum
         mov       dh,bit3                  ; datum
         cmp       dl,"D"                   ; je to datum ?
         je        OpenDB40                 ; je polo‘ka "datum"

OpnDB4M2:cmp       byte ptr es:[si+16],1    ; d‚lka polo‘ky pro logickou polo‘ku ?
         jne       OpnDB4M4                 ; nen¡ datum
         mov       dh,bit4                  ; logick  polo‘ka
         cmp       dl,"L"
         je        OpenDB40                 ; logick  polo‘ka "L"

OpnDB4M4:cmp       byte ptr es:[si+16],10   ; d‚lka polo‘ky pro MEMO pole
         jne       OpenDB4y                 ; nen¡ MEMO pole
         mov       dh,bit5                  ; memo polo‘ka
         cmp       dl,"M"
         jne       OpenDB4y
         or        byte ptr es:[DBFParm],bit4 ; p©¡znak MEMO souboru
         and       byte ptr ds:[DBZobPar],not bit1 ; je MEMO pole
         jmp       short OpenDB40           ; memo polo‘ka

OpenDB4y:cmp       byte ptr es:[si+16],6    ; d‚lka polo‘ky pro ‡as
         jne       OpenDB4z                 ; nen¡ ‡as
         mov       dh,bit6                  ; ‡as
         cmp       dl,"T"
         je        OpenDB40                 ; je polo‘ka "‡as"

OpenDB4z:mov       dh,bit7                  ; jinak nezn m˜ typ polo‘ky

OpenDB40:mov       es:[di+DBFParX],dh       ; nastaven¡ typu polo‘ky

         test      dh,bit7                  ; je nezn m˜ typ polo‘ky ?
         jnz       OpenDB4E                 ; je nezn m˜ typ polo‘ky

         mov       dl,es:[si+16]            ; d‚lka polo‘ky
         sub       dl,2                     ; po‡et desetinn˜ch m¡st
         jnc       OpenDB4F                 ; nen¡ podte‡en¡
         mov       dl,0                     ; omezen¡
OpenDB4F:cmp       dl,es:[si+17]            ; je po‡et desetinn˜ch m¡st OK ?
         ja        OpenDB4E                 ; po‡et desetinn˜ch m¡st je OK
         mov       es:[si+17],dl            ; omezen¡ po‡tu desetinn˜ch m¡st

; ------ stanoven¡ d‚lky jm‚na

OpenDB4E:mov       byte ptr es:[di+DBFNumX],0 ; inicializace d‚lky jm‚na
         push      si
OpenDB41:cmp       byte ptr es:[di+DBFNumX],10 ; maxim ln¡ d‚lka jm‚na
         jae       OpenDB42                 ; je ji‘ maxim ln¡ d‚lka
         inc       byte ptr es:[di+DBFNumX] ; zv˜¨en¡ ‡¡ta‡e d‚lky jm‚na
         inc       si                       ; zv˜¨en¡ adresy
         cmp       byte ptr es:[si]," "     ; je ji‘ konec jm‚na ?
         jae       OpenDB41                 ; nen¡ konec - dal¨¡ znak
OpenDB42:pop       si

; ------ stanoven¡ ¨¡©ky pole polo‘ky

         mov       dx,es:[si+DBFPSirk]      ; polo‘ka ¨¡©ky pole
         sub       dh,dl                    ; teƒ by mˆlo b˜t "D"
         sub       dh,DBFPSirC              ; ode‡ten¡ kontroln¡ho znaku
         jz        OpenDB47                 ; ¨¡©ka je platn  OK

OpnDB422:mov       dx,es:[di+DBFDelX]       ; d‚lka polo‘ky
         test      byte ptr es:[di+DBFParX],bit5 ; je MEMO pole ?
         jz        OpenDB43
         mov       dl,6                     ; ¨¡©ka MEMO pole

OpenDB43:test      byte ptr es:[di+DBFParX],bit4 ; je to logick  polo‘ka ?
         jz        OpenDB44                 ; nen¡ to logick  polo‘ka
         mov       dl,3                     ; ¨¡©ka logick‚ polo‘ky 3 znaky

OpenDB44:test      byte ptr es:[di+DBFParX],bit6 ; je to ‡as ?
         jnz       OpenDB45                 ; je to ‡as
         test      byte ptr es:[di+DBFParX],bit3 ; je to datum ?
         jz        OpenDB46                 ; nen¡ to datum
OpenDB45:inc       dx
         inc       dx                       ; p©i‡ten¡ pozic pro oddˆlova‡e

OpenDB46:cmp       dl,es:[di+DBFNumX]       ; je polo‘ka del¨¡ ne‘ jm‚no ?
         jae       OpenDB47                 ; polo‘ka del¨¡ ne‘ jm‚no - OK
         mov       dl,es:[di+DBFNumX]       ; bude platit d‚lka jm‚na

OpenDB47:mov       es:[di+DBFSirX],dx       ; zobrazen  ¨¡©ka polo‘ky bez okraj–
         add       es:[DBFSirR],dx          ; ‡¡ta‡ celkov‚ ¨¡©ky © dku

; ------ p©¡prava dal¨¡ho popisova‡e

         add       di,DBFX                  ; adresa dal¨¡ho popisova‡e
OpenDB48:add       si,32                    ; adresa dal¨¡ho popisova‡e
         jmp       OpenDBF4                 ; dal¨¡ popisova‡

; ------ kontrola, zda je alespo¤ 1 polo‘ka

OpenDBF5:mov       ax,es:[DBFNumP]          ; po‡et polo‘ek
         dec       ax                       ; po‡et polo‘ek - 1
         jns       OpnDBF52                 ; po‡et polo‘ek je OK
         xor       ax,ax                    ; omezen¡
         or        byte ptr ds:[DBFEParm],bit6 ; je nov˜ soubor
OpnDBF52:add       es:[DBFSirR],ax          ; korekce celkov‚ ¨¡©ky © dku

; ------ vytvo©en¡ m¡sta pro edita‡n¡ buffer z znamu

         mov       di,es:[0]                ; konec datov‚ho bloku
         mov       cx,es:[DBFBytZ]          ; po‡et bajt– na z znam
;         or        cx,cx                    ; je nˆco ?
;         jnz       OpnDBF54                 ; je nˆco OK
;         inc       word ptr es:[DBFBytZ]    ; minim lnˆ 1 bajt
OpnDBF54:inc       cx
         inc       cx                       ; 2 bajty rezerva pro CR/LF
         mov       ax,ss:[bp-6]             ; ‡¡slo datov‚ho bloku
         call      far ptr InsDat           ; vytvo©en¡ m¡sta pro buffer
         jc        OpnDBF62                 ; chyba - nedostatek pamˆti
         mov       es:[DBFAdrE],di          ; adresa po‡ tku bufferu z znamu

; ------ vytvo©en¡ m¡sta pro datov˜ buffer

         call      far ptr GetFree          ; poskytnut¡ voln‚ pamˆti -> BX
         mov       di,es:[0]                ; konec datov‚ho bloku
         mov       ax,0ffa0h                ; asi tak maxim ln¡ konec bloku
         sub       ax,di                    ; po‡et zbyl˜ch bajt–
         jnc       OpnDBF56
         xor       ax,ax                    ; AX <- 0
OpnDBF56:mov       cl,4                     ; po‡et rotac¡
         shr       ax,cl                    ; zbyl‚ m¡sto v odstavc¡ch
         sub       bx,100h                  ; rezerva 4 KB
         jbe       OpenDBF7                 ; m lo pamˆti
         cmp       bx,ax                    ; je v¡ce ne‘ voln‚ m¡sto ?
         jb        OpenDBF6                 ; pamˆŸ je OK
         xchg      ax,bx                    ; BX <- omezen¡ velikosti bloku
OpenDBF6:shl       bx,cl                    ; voln  pamˆŸ v bajtech
         mov       cx,bx                    ; CX <- voln  pamˆŸ v bajtech
         mov       ax,ss:[bp-6]             ; ‡¡slo datov‚ho bloku
         call      far ptr InsDat           ; vytvo©en¡ m¡sta pro buffer
OpnDBF62:jc        OpenDBF7                 ; chyba - nedostatek pamˆti
         mov       es:[DBFAdrD],di          ; adresa po‡ tku bufferu
         xchg      ax,cx                    ; AX <- velikost bufferu v bajtech
         xor       dx,dx                    ; DX <- 0
         div       word ptr es:[DBFBytZ]    ; v˜po‡et max. po‡tu polo‘ek
         mov       es:[DBFMaxD],ax          ; maxim ln¡ po‡et z znam– v bufferu
         or        ax,ax                    ; alespo¤ 1 z znam ?
         jz        OpenDBF7                 ; chyba - m lo pamˆti
         mul       word ptr es:[DBFBytZ]    ; p©epo‡et zpˆt na bajty
         mov       es:[DBFSizD],ax          ; velikost bufferu v bajtech

; ------ stanoven¡ velikosti souboru

         mov       ax,es:[DBFIdent]         ; identifik tor souboru
         call      far ptr SizeFile         ; stanoven¡ velikosti souboru
         jc        OpenDBF7                 ; nˆjak  chyba
         sub       cx,es:[DBFBytH]          ; ode‡ten¡ z hlav¡ LOW
         sbb       bx,0                     ; ode‡ten¡ z hlav¡ HIGH
         jnc       OpnDBF66                 ; d‚lka souboru OK
         xor       cx,cx
         xor       bx,bx                    ; omezen¡ p©i mal‚ d‚lce souboru

; ------ stanoven¡ skute‡n‚ho po‡tu z znam–

OpnDBF66:xchg      ax,bx                    ; AX <- velikost HIGH
         xor       dx,dx                    ; DX <- 0
         div       word ptr es:[DBFBytZ]    ; vydˆlen¡ vy¨¨¡ho slova
         mov       es:[DBFMaxZ+2],ax        ; vy¨¨¡ bajt po‡tu z znam–
         xchg      ax,cx                    ; AX <- velikost LOW
         div       word ptr es:[DBFBytZ]    ; vydˆlen¡ ni‘¨¡ho slova
         mov       es:[DBFMaxZ],ax          ; ni‘¨¡ bajt po‡tu z znam–

; ------ v¨e OK - konec

         clc                                ; p©¡znak operace OK
         jmp       short OpenDBF9           ; v¨e OK - konec

; ------ p©i chybˆ uzav©en¡ souboru datab ze (ES=adresa definice bloku)

OpenDBF7:call      DBFClosF                 ; uzav©en¡ souboru v oknˆ

; ------ p©i chybˆ zru¨en¡ datov‚ho bloku

OpenDBF8:mov       ax,ss:[bp-6]             ; ‡¡slo datov‚ho bloku
         call      far ptr DelSeg           ; zru¨en¡ segmentu datov‚ho bloku
         mov       si,ss:[bp-10]            ; adresa definice menu
         mov       word ptr ds:[si+DBMnuSeg],0 ; zru¨en¡ ‡¡sla datov‚ho bloku
         stc                                ; p©¡znak chyby operace

; ------ n vrat registr–

OpenDBF9:mov       sp,bp
         pop       es
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

OpenDBF  ENDP

; -----------------------------------------------------------------------------
;        konverze n rodn¡ho k¢du souboru DBF (p©i otev©en¡ souboru)
; -----------------------------------------------------------------------------
; VSTUP: AX=identifik tor souboru DBF (t‚‘ v DBFIdent)
;        BX=‡¡slo datov‚ho bloku DBF editoru
;        CX=d‚lka z hlav¡ souboru
;        DL=vstupn¡ k¢d souboru (bit2=Kam,bit3=Lat)
;        DH=v˜stupn¡ k¢d souboru (bit2=Kam,bit3=Lat)
;        DS=datov˜ segment
;        ES=segment bloku DBF editoru
;        ES:DBFSoub=jm‚no souboru DBF
; VSTUP: AX=identifik tor nov‚ho souboru DBF (t‚‘ do DBFIdent)
;         - ukazatel souboru nastaven na za‡ tek popisova‡–
;         CY=p©eru¨en¡ operace (AX=star˜ soubor nebo 0)
; -----------------------------------------------------------------------------
; lok ln¡ promˆnn‚: SS:[BP-2] (2) identifik tor vstupn¡ho souboru DBF
;                   SS:[BP-4] (2) identifik tor v˜stupn¡ho souboru DBF
;                   SS:[BP-6] (2) segment bloku DBF
;                   SS:[BP-8] (2) adresa bloku DBF
;                   SS:[BP-10] (2) d‚lka z hlav¡ souboru
;                        (SS:[BP-12] (2) vstupn¡ a v˜stupn¡ k¢d ke konverzi)
;                   SS:[BP-14] (2) segment bloku bufferu (0=nen¡)
;                        (SS:[BP-16] (2) adresa bloku bufferu)
;                   SS:[BP-18] (2) velikost bloku bufferu
;                   SS:[BP-20] (2) datum souboru
;                   SS:[BP-22] (2) ‡as souboru
;                   SS:[BP-24] (2) atributy souboru
;                   DX=vstupn¡ a v˜stupn¡ k¢d ke konverzi
; -----------------------------------------------------------------------------
; ni‡¡ registry: BX, CX, DX, SI, DI
; -----------------------------------------------------------------------------
;þ
OpenKonv PROC      NEAR

; ------ £schova registr–

         push      bp
         push      es
         mov       bp,sp
         sub       sp,24+FileMax

; ------ p©¡prava lok ln¡ch promˆnn˜ch

         mov       ss:[bp-2],ax             ; identifik tor vstupn¡ho souboru
         mov       ss:[bp-6],bx             ; segment bloku DBF
         mov       ss:[bp-8],es             ; adresa bloku DBF
         mov       ss:[bp-10],cx            ; d‚lka z hlav¡ souboru
;         mov       ss:[bp-12],dx            ; vstupn¡ a v˜stupn¡ k¢d konverze
         mov       word ptr ss:[bp-14],0    ; datov˜ buffer nen¡

; ------ hl ¨en¡ o prov dˆn¡ konverze

         mov       si,offset DBPMKon        ; hl ¨en¡
         push      dx
         call      far ptr Lin0MenF         ; zobrazen¡ hl ¨en¡
         pop       dx
         jc        OpnKnv10                 ; p©eru¨en¡ operace ESC
         dec       bx                       ; je potvrzen¡ konverze ?
         jz        OpnKnv04                 ; je konverze
         jmp       OpenKnv9                 ; konverze se neprovede

; ------ hl ¨en¡ o konverzi

OpnKnv04:mov       si,offset OpnKnvHl       ; text hl ¨en¡
         call      far ptr InfDisp0         ; zobrazen¡ hl ¨en¡

; ------ na‡ten¡ data, ‡asu a atribut– souboru AX/ES:SI

         push      dx
         push      cx

         call      far ptr GetDFile         ; poskytnut¡ data a ‡asu
         mov       ss:[bp-20],dx            ; datum souboru
         mov       ss:[bp-22],cx            ; ‡as souboru
         mov       si,DBFSoub               ; jm‚no souboru
         call      far ptr GetAFile         ; poskytnut¡ atribut– souboru
         mov       ss:[bp-24],cx            ; atributy souboru

         pop       cx
         pop       dx

; ------ £schova jm‚na souboru do bufferu v z sobn¡ku

         mov       di,sp                    ; DI <- buffer v z sobn¡ku
         push      cx
         push      ds

         mov       si,DBFSoub               ; jm‚no souboru DBF
         mov       cx,FileMax               ; velikost dat
         push      es
         pop       ds                       ; DS <- segment bufferu DBF
         push      ss
         pop       es                       ; ES <- segment z sobn¡ku
         cld
         rep       movsb                    ; £schova jm‚na souboru

         pop       ds
         pop       cx

; ------ vytvo©en¡ v˜stupn¡ho souboru (ES=SS)

         mov       si,sp                    ; jm‚no souboru DBF
         call      far ptr CreatPF          ; vytvo©en¡ p©echodn‚ho souboru
         jnc       OpnKnv11
OpnKnv10:jmp       OpenKnv8                 ; chyba

OpnKnv11:mov       ss:[bp-4],ax             ; identifik tor souboru

; ------ vytvo©en¡ datov‚ho bufferu

         mov       bx,cx                    ; BX <- minim ln¡ velikost-z hlav¡
         call      far ptr CreatDat         ; vytvo©en¡ bufferu
         jnc       OpnKnv14
OpnKnv12:jmp       OpenKnv7                 ; chyba pamˆti

OpnKnv14:mov       ss:[bp-14],ax            ; segment bufferu
         mov       ss:[bp-18],bx            ; velikost datov‚ho bufferu

; ------ adresa datov‚ho bufferu -> ES

         call      far ptr GetDat           ; adresa datov‚ho bufferu -> ES
;         mov       ss:[bp-16],es            ; adresa datov‚ho bufferu

; ------ na‡ten¡ z hlav¡ souboru do bufferu

         mov       ax,ss:[bp-2]             ; vstupn¡ soubor
         call      far ptr ResFile          ; resetov n¡ ukazatele
         xor       di,di                    ; ukl dac¡ adresa
         call      far ptr ReadFile         ; na‡ten¡ z hlav¡ souboru
         jc        OpnKnv12                 ; chyba

; ------ p©¡prava intern¡ch promˆnn˜ch

         mov       word ptr es:[di+DBFSwIdn-DBFHlava],"MD" ; identifik tor
         mov       word ptr es:[di+DBFSwKod-DBFHlava],"00" ; k¢d neur‡en˜
         cmp       byte ptr ds:[CodePagK],0 ; je neur‡en˜ k¢d ?
         je        OpenKnv1                 ; k¢d neur‡en˜
         mov       word ptr es:[di+DBFSwKod-DBFHlava],"AK" ; k¢d Kamenick˜ch
         cmp       byte ptr ds:[CodePagK],1 ; je Kamenick˜ch ?
         je        OpenKnv1                 ; je Kamenick˜ch
         mov       word ptr es:[di+DBFSwKod-DBFHlava],"2L" ; k¢d Latin 2

; ------ p©ekonvertov n¡ z hlav¡ souboru (DX=k¢d)

OpenKnv1:push      cx
         mov       si,DBFPopis-DBFHlava     ; ukazatel dat
         mov       bx,cx                    ; BX <- d‚lka z hlav¡
         sub       bx,DBFPopis-DBFHlava     ; d‚lka z hlav¡
         jbe       OpenKnv3
OpenKnv2:sub       bx,32                    ; je dal¨¡ popisova‡ ?
         jb        OpenKnv3                 ; nen¡ dal¨¡ popisova‡
         mov       cx,11                    ; d‚lka jm‚na polo‘ky
         call      far ptr KonvFnt          ; konverze font–
         add       si,32                    ; zv˜¨en¡ ukazatele dat
         jmp       short OpenKnv2           ; dal¨¡ polo‘ka
OpenKnv3:pop       cx

; ------ z pis dat do v˜stupn¡ho souboru (CX bajt–)

OpenKnv4:xor       si,si                    ; za‡ tek z hlav¡
         mov       ax,ss:[bp-4]             ; v˜stupn¡ soubor
         call      far ptr WritFile         ; z pis z hlav¡ souboru
         jc        OpnKnv12                 ; chyba z pisu

; ------ na‡ten¡ dal¨¡ho bloku dat

         xor       di,di
         mov       ax,ss:[bp-2]             ; vstupn¡ soubor
         mov       cx,ss:[bp-18]            ; velikost bufferu
         call      far ptr ReadFile         ; na‡ten¡ dal¨¡ch dat
         jc        OpenKnv7                 ; chyba ‡ten¡
         jcxz      OpenKnv5                 ; konec souboru

; ------ p©ekonvertov n¡ bloku dat (DX=k¢d)

         call      far ptr KonvFnt          ; p©ekonvertov n¡ dat
         jmp       short OpenKnv4           ; dal¨¡ blok dat

; ------ uzav©en¡ vstupn¡ho a v˜stupn¡ho souboru

OpenKnv5:call      far ptr ClosFile         ; uzav©en¡ vstupn¡ho souboru
         mov       ss:[bp-2],ax             ; p©¡znak uzav©en¡ souboru
         mov       ax,ss:[bp-4]             ; v˜stupn¡ soubor
         call      far ptr ClosFile         ; uzav©en¡ v˜stupn¡ho souboru

; ------ zru¨en¡ vstupn¡ho souboru

         mov       es,ss:[bp-8]             ; adresa bloku DBF
         mov       si,DBFSoub               ; jm‚no vstupn¡ho souboru
         xor       cx,cx                    ; atributy se vynuluj¡
         call      far ptr SetAFile         ; vynulov n¡ atribut– souboru
         call      far ptr DelFile          ; zru¨en¡ vstupn¡ho souboru
         jc        OpenKn72                 ; chyba zru¨en¡ souboru

; ------ p©ejmenov n¡ soubor–

         mov       dx,es                    ; DX <- segment c¡lov‚ho jm‚na
         mov       di,si                    ; DI <- c¡lov‚ jm‚no
         push      ss
         pop       es                       ; ES <- segment zdrojov‚ho jm‚na
         mov       si,sp                    ; SI <- offset zdrojov‚ho jm‚na
         call      far ptr RenFile          ; p©ejmenov n¡ soubor–
         jc        OpenKnv9                 ; chyba

; ------ nastaven¡ atribut– souboru

         mov       es,dx                    ; ES <- segment bufferu DBF
         mov       si,di                    ; SI <- jm‚no souboru
         mov       cx,ss:[bp-24]            ; atributy souboru
         call      far ptr SetAFile         ; nastaven¡ atribut– souboru

; ------ nov‚ otev©en¡ souboru

         and       byte ptr es:[DBFParm],not bit1 ; nen¡ z kaz z pisu
         call      far ptr OpenRW           ; otev©en¡ pro ‡ten¡ i z pis
         jnc       OpenKnv6                 ; soubor otev©en OK
         call      far ptr OpenR            ; otev©en¡ alespo¤ pro ‡ten¡
         jc        OpenKnv9                 ; chyba - soubor nelze otev©¡t
         or        byte ptr es:[DBFParm],bit1 ; p©¡znak z kazu z pisu
OpenKnv6:mov       es:[DBFIdent],ax         ; identifik tor souboru
         mov       ss:[bp-2],ax             ; identifik tor souboru

; ------ nastaven¡ data a ‡asu souboru

         mov       dx,ss:[bp-20]            ; datum souboru
         mov       cx,ss:[bp-22]            ; ‡as souboru
         call      far ptr SetDFile         ; nastaven¡ data a ‡asu souboru
         jmp       short OpenKnv9           ; (CY=chyba)

; ------ chyba - uzav©en¡ a zru¨en¡ v˜stupn¡ho souboru

OpenKnv7:mov       ax,ss:[bp-4]             ; identifik tor v˜stup. souboru
         call      far ptr ClosFile         ; uzav©en¡ v˜stupn¡ho souboru
OpenKn72:push      ss
         pop       es                       ; ES <- segment z sobn¡ku
         mov       si,sp                    ; SI <- v˜stupn¡ soubor
         call      far ptr DelFile          ; zru¨en¡ v˜stupn¡ho souboru
OpenKnv8:stc                                ; p©¡znak chyby

; ------ zru¨en¡ datov‚ho bufferu

OpenKnv9:pushf
         mov       ax,ss:[bp-14]            ; datov˜ buffer
         call      far ptr DelSeg           ; zru¨en¡ bufferu

; ------ nastaven¡ ukazatele v souboru

         mov       ax,ss:[bp-2]             ; identifik tor souboru
         mov       cx,DBFPopis-DBFHlava     ; d‚lka z hlav¡
         xor       dx,dx                    ; DX <- 0
         call      far ptr SetUFile         ; nastaven¡ ukazatele v souboru
         popf

; ------ n vrat registr–

         mov       sp,bp
         pop       es
         pop       bp
         call      far ptr InfoClos         ; uzav©en¡ informa‡n¡ho © dku
         ret

OpenKonv ENDP

; *****************************************************************************
;                               ClosDBF
;                     uzav©en¡ okna editoru datab ze
; -----------------------------------------------------------------------------
; VSTUP: DS=datov˜ segment
;        SI=definice okna editoru datab ze
; *****************************************************************************

ClosDBF  PROC      FAR

; ------ £schova registr–

         push      ax
         push      es

; ------ zru¨en¡ segmentu bufferu MEMO

         call      EditBQVD                 ; zru¨en¡ bufferu MEMO

; ------ adresa datov‚ho bloku

         mov       ax,ds:[si+DBMnuSeg]      ; segment okna
         call      far ptr GetDat           ; adresa segmentu -> ES
         jc        ClosDBF9

; ------ uzav©en¡ souboru datab ze a souboru MEMO

         push      ax
         call      DBFClosF                 ; uzav©en¡ souboru v oknˆ
         mov       ax,es:[DBFIdntT]         ; identifik tor souboru MEMO
         call      far ptr ClosFile         ; uzav©en¡ souboru
         pop       ax

; ------ zru¨en¡ datov‚ho bloku

         call      far ptr DelSeg           ; zru¨en¡ segmentu AX
         mov       ds:[si+DBMnuSeg],ax      ; ozna‡en¡ segmentu za zru¨en˜

; ------ n vrat registr–

ClosDBF9:pop       es
         pop       ax
         ret

ClosDBF  ENDP

CodeDbf  ENDS

; *****************************************************************************
;
;                          Datov˜ segment
;
; *****************************************************************************
;þ
Data     SEGMENT

; -----------------------------------------------------------------------------
;        potvrzen¡ konverze souboru
; -----------------------------------------------------------------------------

DBPMKon  label     byte
         db        3                        ; typ menu - © dkov‚ menu

         db        0                        ; hladina k zobrazen¡ menu
         dw        1                        ; aktivn¡ polo‘ka menu
         dw        3                        ; po‡et platn˜ch polo‘ek menu
         dw        9                        ; celkov˜ po‡et polo‘ek menu

         dw        DBPMKPol                 ; adresa definice polo‘ek
         dw        DBPMKHlp                 ; adresa n povˆd
         dw        Hlp@DatabazoveMenu       ; ‡¡slo str nky velk‚ n povˆdy
         dw        DBPMKBlk                 ; adresa blokovac¡ tabulky
         dw        0                        ; adresa tabulky p©ep¡na‡–
         dw        DBPMKTit                 ; adresa titulu okna
         dw        LinMenTE                 ; adresa obsluh voleb
         dw        LinMenTM                 ; adresy podmenu
         dd        0                        ; n vratov  adresa

         db        19                       ; po‡ te‡n¡ pozice
         db        7                        ; po‡ te‡n¡ © dek
         db        53                       ; ¨¡©ka
         db        11                       ; v˜¨ka

         dw        0                        ; maxim ln¡ d‚lka © dku
         dw        0                        ; prvn¡ zobrazen˜ © dek
         dw        0                        ; aktivn¡ zobrazen˜ © dek
         dw        0                        ; celkov˜ po‡et © dk–
         dw        0                        ; po‡et zobrazen˜ch © dk–

         db        0                        ; maska znak–
         db        0,'      '               ; zak zan‚ znaky

         db        0                        ; identifik tor historie
         db        0                        ; po‡et p©edvoleb

DBPMKPol db        bit6,0
         db        bit6,9,0,'VAROVN‹'
         db        bit6,47,'Soubor je v jin‚m n rodn¡m k¢du ne‘ je nastaven'
         db        bit6,45,'jako aktu ln¡. Mohu prov‚st konverzi souboru?'
         db        bit6,0
         db        bit6+bit7,0
         db        1,11,'Konvertovat'
         db        1,13,'Nekonvertovat'
         db        1,8,'P©eru¨it'

DBPMKBlk db        0,0,0
DBPMKTit db        23,'konverze n rodn¡ho k¢du'

HelpS    SEGMENT   BYTE PUBLIC
DBPMKHlp db        41,'Proveden¡ konverze n rodn¡ho k¢du souboru'
         db        74,'N rodn¡ k¢d souboru bude ponech n nezmˆnˆn a bude pova‘ov n za platn˜ k¢d.'
         db        3,1
         dw        MenuHlP2
HelpS    ENDS


Data     ENDS
