
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                            D A T A B  Z E - ‡ st 3
;
;                 Obsluha editace polo‘ek, ozna‡en¡, export a import dat
;
; =============================================================================
;
; Ve©ejn‚ procedury:
;
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

INCLUDE  ASM\DEF.ASM

CodeDbf  SEGMENT   BYTE PUBLIC
         ASSUME    cs:CodeDbf,ds:Data

; -----------------------------------------------------------------------------
;                            UlozDBF
;              ulo‘en¡ editovan‚ho z znamu z bufferu na disk
; -----------------------------------------------------------------------------
; VSTUP: ES=segment definice okna
;        SI=adresa definice okna
;        DS=datov˜ segment
; VSTUP: CY=chyba
; -----------------------------------------------------------------------------

UlozDBF  PROC      NEAR

; ------ test, zda byl buffer modifikov n

         test      byte ptr ds:[DBFEParm],bit1 ; byl buffer modifikov n ?
         jnz       UlozDBF1                 ; buffer byl modifikov n
         ret

UlozDBF1:and       byte ptr ds:[DBFEParm],not bit1 ; zru¨en¡ p©¡znaku modifikace

; ------ £schova registr–

         push      ax
         push      cx
         push      dx
         push      di

; ------ ukon‡en¡ editace ‡¡seln‚ polo‘ky

         call      EditNmEn                 ; ukon‡en¡ editace ‡¡seln‚ polo‘ky

; ------ aktualizace adres ©e

         push      si
         mov       si,offset DBFSoub        ; jm‚no souboru
         call      far ptr ModiWDir         ; aktualizace adres ©e
         pop       si
         call      DBFAdrES                 ; aktualizace adresy okna -> ES

; ------ ulo‘en¡ z znamu na disk

         mov       di,es:[DBFAdrE]          ; edita‡n¡ buffer
         mov       dx,es:[DBFAktZ+2]        ; ‡¡slo z znamu HIGH
         mov       ax,es:[DBFAktZ]          ; ‡¡slo z znamu LOW
         call      far ptr SetDBF           ; ulo‘en¡ z znamu na disk
         jc        UlozDBF9                 ; chyba z pisu

; ------ test, zda je to platn˜ z znam

         cmp       dx,es:[DBFMaxZ+2]        ; je to platn˜ z znam ?
         jne       UlozDBF4
         cmp       ax,es:[DBFMaxZ]
UlozDBF4:jb        UlozDBF7                 ; je to platn˜ z znam

; ------ nov˜ po‡et z znam– v souboru

         add       ax,1
         adc       dx,0
         mov       es:[DBFMaxZ],ax          ; nov˜ po‡et z znam–
         mov       es:[DBFMaxZ+2],dx

; ------ z pis koncov‚ho znaku EOF

         call      UlozDEOF                 ; z pis koncov‚ho EOF

; ------ z pis z hlav¡ souboru

UlozDBF7:call      far ptr HlavDBF          ; z pis z hlav¡ souboru
         mov       word ptr es:[DBFNumD],0  ; datov˜ buffer je neplatn˜
         clc                                ; p©¡znak operace OK

; ------ n vrat registr–

UlozDBF9:pop       di
         pop       dx
         pop       cx
         pop       ax
         ret

UlozDBF  ENDP

; ------ z pis koncov‚ho znaku EOF (okno ES)

UlozDEOF PROC      NEAR

         push      ax
         push      cx
         push      si
         push      es

         mov       ax,es:[DBFIdent]         ; identifik tor souboru
         push      cs
         pop       es                       ; ES <- segment znaku EOF
         mov       si,offset UlozDBFE       ; znak EOF
         mov       cx,1                     ; zap¡¨e se 1 znak
         call      far ptr WritFile         ; z pis koncov‚ho znaku EOF
         xor       cx,cx                    ; CX <- 0
         call      far ptr WritFile         ; useknut¡ konce souboru

         pop       es
         pop       si
         pop       cx
         pop       ax
         ret

UlozDEOF ENDP

UlozDBFE db        EOF                      ; koncov˜ znak EOF

; -----------------------------------------------------------------------------
;        BS: zru¨en¡ znaku p©ed kurzorem
; -----------------------------------------------------------------------------

; ------ test, zda je operace povolena

EditBBS: test      byte ptr ds:[DBFEParm],bit0 ; je editace ?
         jz        EditBBS8                 ; nen¡ editace
         test      byte ptr es:[DBFParm],bit1 ; je z kaz z pisu ?
         jnz       EditBBS8                 ; je z kaz z pisu

; ------ posun kurzoru vlevo

         call      EditPLft                 ; posun kurzoru vlevo
         jc        EditBBS8                 ; nen¡ dal¨¡ pozice

; ------ adresa popisova‡e polo‘ky -> ES:BP a adresa kurzoru -> ES:DI

         mov       cx,es:[DBFAktP]          ; CX <- aktivn¡ polo‘ka
         mov       bp,es:[DBFAdrP]          ; BP <- adresa popisova‡e
         mov       di,es:[DBFAdrE]          ; adresa bufferu
         inc       di                       ; adresa prvn¡ polo‘ky
         jcxz      EditBBS4                 ; je prvn¡ polo‘ka
EditBBS2:add       di,es:[bp+DBFDelX]       ; adresa dal¨¡ polo‘ky
         add       bp,DBFX                  ; adresa dal¨¡ho popisova‡e
         loop      EditBBS2                 ; nalezen¡ polo‘ky
EditBBS4:add       di,es:[DBFKurz]          ; p©i‡ten¡ offsetu kurzoru

; ------ pro MEMO pole se funkce ignoruje (needituje se)

         test      byte ptr es:[bp+DBFParX],bit5 ; je MEMO pole ?
         jnz       EditBBS8                 ; je MEMO pole

; ------ je-li INSERT zapnut, bude n sledovat funkce DELETE

         test      byte ptr ds:[DBFEParm],bit2 ; je zapnut INSERT ?
         jnz       EditBDlt                 ; je zapnut INSERT - bude DELETE

; ------ ulo‘en¡ znaku mezery

         mov       byte ptr es:[di]," "     ; ulo‘en¡ znaku mezery
         or        byte ptr ds:[DBFEParm],bit1 ; p©¡znak modifikace bufferu

; ------ p©¡znak modifikace ‡¡seln‚ho pole

         test      byte ptr es:[bp+DBFParX],bit1+bit2+bit3+bit6 ; ‡¡slo/datum/‡as ?
         jz        EditBBS8                 ; nen¡ ‡¡slo/datum/‡as
         or        byte ptr ds:[DBFEParm],bit3 ; modifikace ‡¡seln‚ho pole
EditBBS8:ret                                ; (sem se sk ‡e i z EditBDlt !)

; -----------------------------------------------------------------------------
;        Delete: zru¨en¡ znaku za kurzorem
; -----------------------------------------------------------------------------

; ------ test, zda je operace Delete povolena

EditBDlt:test      byte ptr ds:[DBFEParm],bit0 ; je editace ?
         jz        EditBBS8                 ; nen¡ editace
         test      byte ptr es:[DBFParm],bit1 ; je z kaz z pisu ?
         jnz       EditBBS8                 ; je z kaz z pisu

; ------ adresa popisova‡e polo‘ky -> ES:BP a adresa kurzoru -> ES:DI

         mov       cx,es:[DBFAktP]          ; CX <- aktivn¡ polo‘ka
         mov       bp,es:[DBFAdrP]          ; adresa popisova‡e
         mov       di,es:[DBFAdrE]          ; adresa bufferu
         inc       di                       ; adresa prvn¡ polo‘ky
         jcxz      EditBDl2                 ; je prvn¡ polo‘ka
EditBDl1:add       di,es:[bp+DBFDelX]       ; adresa dal¨¡ polo‘ky
         add       bp,DBFX                  ; adresa dal¨¡ho popisova‡e
         loop      EditBDl1                 ; nalezen¡ polo‘ky
EditBDl2:add       di,es:[DBFKurz]          ; p©i‡ten¡ offsetu kurzoru

; ------ stanoven¡ parametr– pro ‡as

         test      byte ptr es:[bp+DBFParX],bit6 ; je ‡as ?
         jz        EditBDl3                 ; nen¡ ‡as
         mov       cl,es:[DBFKurz]          ; CL <- kurzor
         and       cl,bit0                  ; p©¡znak lichosti
         xor       cl,bit0                  ; po‡et zbyl˜ch bit–
         jmp       short EditBD56           ; zru¨en¡ znaku

; ------ stanoven¡ parametr– pro datum

EditBDl3:test      byte ptr es:[bp+DBFParX],bit3 ; je datum ?
         jz        EditBDl4                 ; nen¡ datum

         push      si
         mov       si,es:[DBFKurz]          ; kurzor
         mov       cl,cs:[si+EditBIIT]      ; po‡et zbyl˜ch pozic
         pop       si
         jmp       short EditBDl6

; ------ pro MEMO pole se funkce ignoruje (needituje se)

EditBDl4:test      byte ptr es:[bp+DBFParX],bit5 ; je to MEMO pole ?
         jnz       EditBDl9                 ; je to MEMO pole

; ------ stanoven¡ parametr– pro ‡¡slo

         test      byte ptr es:[bp+DBFParX],bit1+bit2 ; je to ‡¡slo ?
         jz        EditBDl7                 ; nen¡ to ‡¡slo

         push      di
         mov       di,es:[bp+DBFNamX]       ; popisova‡ polo‘ky
         mov       cx,es:[di+16]            ; CL <- d‚lka pol., CH <- desetiny
         or        ch,ch                    ; jsou desetiny ?
         jz        EditBDl5                 ; nejsou desetiny
         sub       cl,ch                    ; ode‡ten¡ desetin
         dec       cx                       ; oddˆlovac¡ te‡ka -> CL=cel  ‡ st
EditBDl5:pop       di
         sub       cl,es:[DBFKurz]          ; je celo‡¡seln  ‡ st ?
         ja        EditBDl6                 ; je celo‡¡seln  ‡ st

         add       ch,cl                    ; zbytek do konce polo‘ky
         mov       cl,ch                    ; zbytek do konce polo‘ky
EditBD56:inc       cx                       ; bez oddˆlovac¡ te‡ky

EditBDl6:or        byte ptr ds:[DBFEParm],bit3 ; modifikace ‡¡seln‚ho pole
         jmp       short EditBDl8

; ------ stanoven¡ parametr– pro ostatn¡ polo‘ky

EditBDl7:mov       cl,es:[bp+DBFDelX]       ; d‚lka polo‘ky (bajt–)
         sub       cl,es:[DBFKurz]          ; zbytek do konce polo‘ky

; ------ vymaz n¡ znaku ES:DI, d‚lka pole CL

EditBDl8:call      EditBDlC                 ; vymaz n¡ znaku
         or        byte ptr ds:[DBFEParm],bit1 ; p©¡znak modifkace bufferu
EditBDl9:ret

; -----------------------------------------------------------------------------
;        zru¨en¡ znaku z pozice ES:DI, d‚lka pole CL
; -----------------------------------------------------------------------------

EditBDlC PROC      NEAR

         push      si
         push      di
         push      cx

         mov       ch,0
         cld
         mov       si,di
         inc       si
         dec       cx
         rep       movs byte ptr es:[di],es:[si] ; p©¡sun zbytku textu
         mov       byte ptr es:[di]," "     ; vymaz n¡ posledn¡ho znaku

         pop       cx
         pop       di
         pop       si
         ret

EditBDlC ENDP

; -----------------------------------------------------------------------------
;        vlo‘en¡ znaku
; -----------------------------------------------------------------------------
;þ
EditBICh PROC      NEAR

; ------ test, zda je operace povolena

         test      byte ptr ds:[DBFEParm],bit0 ; je editace ?
         jnz       EditBIC1                 ; je editace
         cmp       bx,1c0dh                 ; je ENTER ?
         je        EditBI11                 ; je ENTER -> dal¨¡ polo‘ka
EditBIC0:jmp       EditBMn8                 ; n vrat

; ------ test, zda je platn˜ znak

EditBIC1:or        bx,bx                    ; platn  kl vesa ?
         jz        EditBIC0                 ; nen¡ platn  kl vesa
         cmp       bx,1c0dh                 ; ENTER ?
         jne       EditBI12                 ; nen¡ ENTER
         test      byte ptr ds:[DBFEParm],bit3 ; modifikace ‡¡sla ?
         jnz       EditBI14                 ; je modifikace ‡¡sla
EditBI11:jmp       EditBEnt                 ; ENTER

EditBI12:test      byte ptr es:[DBFParm],bit1 ; je z kaz z pisu ?
         jnz       EditBIC0                 ; je z kaz z pisu
         cmp       bh,0
         je        EditBI14                 ; znak s Alt-
         cmp       bl,EOF
         je        EditBIC0                 ; zak zan˜ znak EOF
         cmp       bl,CR
         je        EditBIC0                 ; zak zan˜ znak CR
         cmp       bl,LF
         je        EditBIC0                 ; zak zan˜ znak LF
         cmp       bx,300h                  ; Ctrl-@
         je        EditBI14                 ; znak Ctrl-@ je povolen
         cmp       bl," "
         jb        EditBIC0                 ; ©¡dic¡ kl vesa s Ctrl-

; ------ adresa popisova‡e polo‘ky -> ES:BP a adresa kurzoru -> ES:DI

EditBI14:mov       cx,es:[DBFAktP]          ; CX <- aktivn¡ polo‘ka
         mov       bp,es:[DBFAdrP]          ; adresa popisova‡e
         mov       di,es:[DBFAdrE]          ; adresa bufferu
         inc       di                       ; adresa prvn¡ polo‘ky
         jcxz      EditBI16                 ; je prvn¡ polo‘ka
EditBI15:add       di,es:[bp+DBFDelX]       ; adresa dal¨¡ polo‘ky
         add       bp,DBFX                  ; adresa dal¨¡ho popisova‡e
         loop      EditBI15                 ; nalezen¡ polo‘ky
EditBI16:add       di,es:[DBFKurz]          ; p©i‡ten¡ offsetu kurzoru

; ------ MEMO pole - ignorov n¡ znaku

         test      byte ptr es:[bp+DBFParX],bit5 ; je to MEMO pole ?
         jz        EditBIC2                 ; nen¡ MEMO pole
EditBI19:jmp       EditBIC9                 ; pro MEMO pole zak z no

; ------ logick‚ pole

EditBIC2:test      byte ptr es:[bp+DBFParX],bit4 ; je to logick‚ pole ?
         jz        EditBIC3                 ; nen¡ to logick‚ pole

         xchg      ax,bx                    ; AL <- znak
         call      far ptr UpCase           ; konverze na velk‚ p¡smeno
         xchg      ax,bx                    ; BL <- znak

         mov       bh," "                   ; hodnota pro mezeru
         cmp       bl,"?"                   ; nedefinovan˜ znak ?
         je        EditBI24                 ; nedefinovan˜ znak
         cmp       bl," "                   ; mezera ?
         je        EditBI24                 ; mezera

         mov       bh,"T"                   ; hodnota pro TRUE
         cmp       bl,"A"
         je        EditBI24                 ; je "Ano"
         cmp       bl,"Y"
         je        EditBI24                 ; je "Yes"
         cmp       bl,"J"
         je        EditBI24                 ; je "Ja"
         cmp       bl,"T"
         je        EditBI24                 ; je "True"

         mov       bh,"F"                   ; hodnota pro FALSE
         cmp       bl,"F"
         je        EditBI24                 ; je "False"
         cmp       bl,"N"
         jne       EditBI19                 ; nepovolen˜ znak

EditBI24:mov       es:[di],bh               ; ulo‘en¡ znaku
         jmp       EditBIC8

; ------ datum a ‡as

EditBIC3:test      byte ptr es:[bp+DBFParX],bit3+bit6 ; je datum/‡as ?
         jnz       EdiBIC31
         jmp       EditBIC4                 ; nen¡ datum ani ‡as

EdiBIC31:cmp       bl,CR
         je        EdiBIC32                 ; ENTER - konec editace
         cmp       bl,"."
         je        EdiBIC32                 ; te‡ka - konec editace
         cmp       bl,ds:[OddelDes]         ; je to oddˆlova‡ desetin ?
         je        EdiBIC32                 ; je to oddˆlova‡ desetin

         call      EditTstN                 ; test, zda to je ‡¡slice/mezera
         jc        EditBI19                 ; nen¡ to ‡¡slice

EdiBIC32:or        byte ptr ds:[DBFEParm],bit3 ; p©¡znak modifikace ‡¡sel. pole
         push      si
         mov       si,es:[DBFKurz]          ; kurzor
         mov       cl,cs:[si+EditBIIT]      ; po‡et zbyl˜ch pozic
         pop       si

         test      byte ptr es:[bp+DBFParX],bit6 ; je to ‡as ?
         jz        EdiBIC33                 ; nen¡ to ‡as
         mov       cl,es:[DBFKurz]          ; kurzor
         and       cl,bit0
         xor       cl,bit0
         inc       cx                       ; po‡et znak– do konce pole

EdiBIC33:call      EditBIII                 ; odsun zbytku textu
         mov       es:[di],bl               ; ulo‘en¡ znaku
         mov       cx,es:[DBFAktP]          ; aktivn¡ polo‘ka
         cmp       bl,CR
         je        EdiBIC34
         cmp       bl,"."
         je        EdiBIC34
         cmp       bl,ds:[OddelDes]         ; je to oddˆlova‡ desetin ?
         je        EdiBIC34                 ; oddˆlova‡ desetin
         jmp       EditBIC8                 ; posun kurzoru vpravo

EdiBIC34:call      EditPRgh                 ; posun kurzoru vpravo
         jc        EdiBIC36
         cmp       cx,es:[DBFAktP]          ; polo‘ka zmˆnˆna ?
         jne       EdiBIC36                 ; polo‘ka zmˆnˆna
         cmp       bl,CR
         je        EdiBIC34

         test      byte ptr es:[bp+DBFParX],bit6 ; je to ‡as ?
         jz        EdiBIC35                 ; nen¡ to ‡as
         test      byte ptr es:[DBFKurz],bit0
         jnz       EdiBIC34
         jmp       short EdiBIC36

EdiBIC35:cmp       byte ptr es:[DBFKurz],0
         je        EdiBIC36
         cmp       byte ptr es:[DBFKurz],4
         je        EdiBIC36
         cmp       byte ptr es:[DBFKurz],6
         jne       EdiBIC34
EdiBIC36:jmp       EdiBIC88

; ------ ‡¡seln‚ pole

EditBIC4:test      byte ptr es:[bp+DBFParX],bit1+bit2 ; je ‡¡slo ?
         jz        EditBIC7                 ; nen¡ ‡¡slo

         cmp       bl,"-"
         je        EditBI41
         cmp       bl,"+"
         je        EditBI41
         cmp       bl,"."
         je        EditBI41
         cmp       bl,ds:[OddelDes]         ; oddˆlova‡ desetin ?
         je        EditBI41
         cmp       bl,CR
         je        EditBI41

         call      EditTstN                 ; test, zda to je ‡¡slice/mezera
         jc        EditBIC9                 ; nen¡ to ‡¡slice

EditBI41:push      di
         mov       di,es:[bp+DBFNamX]       ; popisova‡ polo‘ky
         mov       cx,es:[di+16]            ; CL <- d‚lka pol., CH <- desetiny
         cmp       ch,0                     ; jsou desetiny ?
         je        EditBI42                 ; nejsou desetiny
         sub       cl,ch                    ; ode‡ten¡ desetin
         dec       cx                       ; oddˆlovac¡ te‡ka -> CL=cel  ‡ st
EditBI42:pop       di

         or        byte ptr ds:[DBFEParm],bit3 ; p©¡znak modifikace ‡¡sel. pole
         sub       cl,es:[DBFKurz]          ; je celo‡¡seln  ‡ st ?
         ja        EditBI44                 ; je celo‡¡seln  ‡ st
         add       ch,cl                    ; zbytek do konce polo‘ky
         mov       cl,ch                    ; zbytek do konce polo‘ky
         inc       cx                       ; bez oddˆlovac¡ te‡ky

EditBI44:call      EditBIII                 ; odsun zbytku textu
         mov       es:[di],bl               ; ulo‘en¡ znaku

         mov       cl,es:[DBFAktP]          ; aktivn¡ polo‘ka LOW

         inc       di

         cmp       bl,CR
         je        EdiBIC45
         cmp       bl,ds:[OddelDes]         ; oddˆlova‡ desetin ?
         je        EdiBIC45                 ; oddˆlova‡ desetin
         cmp       bl,"."
         jne       EditBIC8

EdiBIC45:inc       di
         call      EditPRgh                 ; posun kurzoru vpravo
         jc        EdiBIC88
         cmp       cl,es:[DBFAktP]          ; polo‘ka zmˆnˆna ?
         jne       EdiBIC88                 ; polo‘ka zmˆnˆna
         cmp       es:[di-1],bl
         jne       EdiBIC45
         jmp       short EdiBIC88

; ------ znakov‚ pole

EditBIC7:mov       cl,es:[bp+DBFDelX]       ; d‚lka polo‘ky
         sub       cl,es:[DBFKurz]          ; po‡et znak– za kurzorem
EditBI76:call      EditBIII                 ; odsun zbytku textu
EditBI77:mov       es:[di],bl               ; ulo‘en¡ znaku

; ------ znak platn˜ - posun kurzoru

EditBIC8:call      EditPRgh                 ; posun kurzoru vpravo
EdiBIC88:or        byte ptr ds:[DBFEParm],bit1 ; p©¡znak modifikace
EditBIC9:ret

EditBICh ENDP

EditBIIT db        4,3,2,1,2,1,2,1          ; po‡ty zbyl˜ch pozic v poli data

; -----------------------------------------------------------------------------
;        odsun CX znak– od kurzoru DI
; -----------------------------------------------------------------------------

EditBIII PROC      NEAR

         test      byte ptr ds:[DBFEParm],bit2 ; je zapnut INSERT ?
         jz        EditBII7                 ; nen¡ zapnut INSERT

EditBII2:push      si
         push      di
         push      cx

         mov       ch,0
         dec       cx                       ; bez 1 znaku
         add       di,cx
         mov       si,di                    ; posledn¡ znak
         dec       si                       ; p©edposledn¡ znak
         std
         rep       movs byte ptr es:[di],es:[si] ; odsun zbytku textu o pozici

         pop       cx
         pop       di
         pop       si
EditBII7:ret

EditBIII ENDP

; -----------------------------------------------------------------------------
;        test, zda je znak BL ‡¡slice (CY=nen¡)
; -----------------------------------------------------------------------------

EditTstN PROC      NEAR

         cmp       bl," "
         je        EdiTstN1
         cmp       bl,"0"
         jb        EdiTstN1
         cmp       bl,"9"+1
         cmc
EdiTstN1:ret

EditTstN ENDP

; -----------------------------------------------------------------------------
;        korekce aktivn¡ho ‡¡seln‚ho pole (segment okna ES)
; -----------------------------------------------------------------------------

EditNmEn PROC      NEAR

         test      byte ptr ds:[DBFEParm],bit3 ; numerick‚ pole modifikov no ?
         jnz       EditMnE0                 ; modifikov no
         ret

; ------ £schova registr–

EditMnE0:and       byte ptr ds:[DBFEParm],not bit3 ; zru¨en¡ p©¡znaku modifikace
         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      ds

         push      es
         pop       ds                       ; DS <- segment bufferu

; ------ adresa aktivn¡ polo‘ky -> DS:DI a popisova‡e polo‘ky -> DS:BX

         mov       cx,ds:[DBFAktP]          ; aktivn¡ polo‘ka
         mov       bx,ds:[DBFAdrP]          ; ukazatel polo‘ek
         mov       si,ds:[DBFAdrE]          ; edita‡n¡ buffer
         inc       si                       ; za‡ tek prvn¡ polo‘ky
         jcxz      EditNmE2                 ; je prvn¡ polo‘ka
EditNmE1:add       si,ds:[bx+DBFDelX]       ; adresa dal¨¡ polo‘ky
         add       bx,DBFX                  ; adresa dal¨¡ho popisova‡e
         loop      EditNmE1                 ; dal¨¡ polo‘ka
EditNmE2:mov       di,si                    ; DI <- za‡ tek polo‘ky
         cld

; ------ korekce data (CH=0)

         test      byte ptr ds:[bx+DBFParX],bit3 ; je to datum ?
         jz        EditNmE3                 ; nen¡ datum

         mov       cl,4                     ; d‚lka polo‘ky
         call      EditNKR                  ; korekce polo‘ky data

         push      si
         dec       si                       ; 4. ‡¡slice roku
         dec       si                       ; 3. ‡¡slice roku
         call      EditNKT                  ; korekce ‡¡slice - des¡tky roku
         dec       si                       ; 2. ‡¡slice roku
         call      EditNKT                  ; korekce ‡¡slice - stovky roku
         dec       si                       ; 1. ‡¡slice roku
         call      EditNKT                  ; korekce ‡¡slice - tis¡cilet¡

         cmp       word ptr ds:[si],"00"    ; zad no stolet¡ ?
         jne       EditNE25                 ; stolet¡ zad no
         mov       word ptr ds:[si],"91"    ; stolet¡ 19xx
         cmp       byte ptr ds:[si+2],"3"   ; rok 1930 a v¡ce ?
         jae       EditNE25                 ; je rok 1930 a v¡ce
         mov       word ptr ds:[si],"02"    ; stolet¡ 20xx
EditNE25:pop       si

         mov       ax,"01"                  ; mˆs¡c minim lnˆ
         mov       dx,"12"                  ; mˆs¡c maxim lnˆ
         call      EditNKO                  ; omezen¡ mˆs¡ce
         mov       dx,"31"                  ; den maxim lnˆ
         call      EditNKO                  ; omezen¡ dne
         jmp       short EditNE39

; ------ test, zda je ‡as

EditNmE3:test      byte ptr ds:[bx+DBFParX],bit6 ; je to ‡as ?
         jz        EditNmE4                 ; nen¡ to ‡as

         mov       ax,"00"                  ; hodina minim lnˆ
         mov       dx,"23"                  ; hodina maxim lnˆ
         call      EditNKO                  ; omezen¡ hodiny
         mov       dx,"59"                  ; minuta maxim lnˆ
         call      EditNKO                  ; omezen¡ minuty
         call      EditNKO                  ; omezen¡ sekundy
EditNE39:jmp       short EditNmE9

; ------ test, zda je numerick  polo‘ka

EditNmE4:test      byte ptr ds:[bx+DBFParX],bit1+bit2 ; je ‡¡seln  polo‘ka ?
         jz        EditNE39                 ; nen¡ ‡¡seln  polo‘ka

; ------ p©¡prava d‚lky polo‘ky -> CL + CH

         push      di
         mov       di,ds:[bx+DBFNamX]       ; popisova‡ polo‘ky
         mov       cx,ds:[di+16]            ; CL <- d‚lka polo‘ky, CH <- deset.
         or        ch,ch                    ; jsou desetiny ?
         jz        EditNmE5                 ; nejsou desetiny
         sub       cl,ch                    ; ode‡ten¡ desetin
         dec       cx                       ; bez oddˆlovac¡ te‡ky
EditNmE5:pop       di

; ------ korekce celo‡¡seln‚ ‡ sti

         push      cx
         mov       ch,0                     ; CX = d‚lka celo‡¡seln‚ ‡ sti
         call      EditNKR                  ; korekce celo‡¡seln‚ ‡ sti
         pop       cx
         mov       cl,ch                    ; CL <- d‚lka desetinn‚ ‡ sti
         mov       ch,0                     ; CX = d‚lka desetinn‚ ‡ sti
         jcxz      EditNmE9                 ; nejsou desetiny

; ------ korekce desetinn‚ ‡ sti

         inc       si                       ; za‡ tek polo‘ky
         inc       di                       ; za‡ tek polo‘ky
         mov       bx,di                    ; BX <- za‡ tek polo‘ky
         add       bx,cx                    ; BX = konec polo‘ky

EditNmE6:cmp       si,bx
         jae       EditNmE7                 ; je konec bufferu
         lodsb                              ; na‡ten¡ znaku
         cmp       al,"."
         je        EditNmE7                 ; je konec ‡¡sla
         cmp       al,cs:[DBFOdDes]         ; je oddˆlova‡ desetin ?
         je        EditNmE7                 ; konec ‡¡sla
         cmp       al,CR
         je        EditNmE7                 ; je konec ‡¡sla
         cmp       al,"0"
         jb        EditNmE6                 ; nen¡ ‡¡slice - ignoruje se
         cmp       al,"9"
         ja        EditNmE6                 ; nen¡ ‡¡slice - ignoruje se
         stosb                              ; ulo‘en¡ ‡¡slice
         dec       cx                       ; sn¡‘en¡ ‡¡ta‡e znak–
         jmp       short EditNmE6           ; dal¨¡ znak

EditNmE7:mov       al,"0"
         rep       stosb                    ; vyplnˆn¡ zbytku polo‘ky nulami

; ------ n vrat registr–

EditNmE9:pop       ds
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

EditNmEn ENDP

; ------ korekce ‡¡slice DS:SI (nastaven¡ na "0", nen¡-li to znak ‡¡slice)

EditNKT: cmp       byte ptr ds:[si],"0"
         jb        EditNKT1
         cmp       byte ptr ds:[si],"9"
         jbe       EditNKT2
EditNKT1:mov       byte ptr ds:[si],"0"
EditNKT2:ret

; ------ korekce 2-‡¡sl¡ DS:SI s omezen¡m na AX a‘ DX (ASCII) (ni‡¡ CX !)

EditNKO: mov       cx,2                     ; d‚lka polo‘ky
         call      EditNKR                  ; korekce polo‘ky (2 znaky)

         push      si
         dec       si                       ; 2. ‡¡slice
         dec       si                       ; 1. ‡¡slice
         call      EditNKT                  ; korekce 1. ‡¡slice
         mov       cx,ds:[si]               ; 2 znaky ‡¡sla
         xchg      cl,ch                    ; oprava po©ad¡ ‡¡slic
         cmp       ax,cx                    ; minim ln¡ ‡¡slo
         jbe       EditNKO2                 ; je to OK
         mov       cx,ax                    ; CX <- minim ln¡ ‡¡slo
EditNKO2:cmp       dx,cx                    ; maxim ln¡ ‡¡slo
         jae       EditNKO4                 ; je to OK
         mov       cx,dx                    ; CX <- maxim ln¡ ‡¡slo
EditNKO4:xchg      cl,ch                    ; n vrat po©ad¡ ‡¡slic
         mov       ds:[si],cx               ; ulo‘en¡ ‡¡slic
         pop       si
         ret

; -----------------------------------------------------------------------------
;        korekce ‡¡sla s posunem vpravo
; -----------------------------------------------------------------------------
; VSTUP: DS:SI,DI=za‡ tek bufferu ‡¡sla
;        CX=d‚lka bufferu ‡¡sla
;        CLD
; VSTUP: DS:DI,SI=n sleduj¡c¡ adresa za bufferem
; -----------------------------------------------------------------------------

; ------ £schova registr–

EditNKR: add       di,cx                    ; konec ‡¡sla
         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di

         mov       bx,di                    ; £schova adresy konce bufferu
         mov       dx,si                    ; £schova adresy za‡ tku bufferu

; ------ nalezen¡ konce ‡¡sla (nalezen¡ oddˆlovac¡ te‡ky nebo konce bufferu)

EditNKR1:cmp       si,bx                    ; je konec bufferu ?
         jae       EditNKR2                 ; je konec bufferu ‡¡sla
         lodsb                              ; na‡ten¡ znaku
         cmp       al,CR                    ; ENTER
         je        EditNKR2                 ; je konec ‡¡sla
         cmp       al,cs:[DBFOdDes]         ; oddˆlovac¡ te‡ka ?
         je        EditNKR2                 ; je oddˆlovac¡ te‡ka
         cmp       al,"."                   ; je oddˆlovac¡ te‡ka ?
         jne       EditNKR1                 ; nalezen¡ oddˆlovac¡ te‡ky

; ------ p©enesen¡ ‡¡sla na konec bufferu

EditNKR2:mov       ah,0                     ; p©¡znak - nen¡ znam‚nko
EditNKR3:cmp       si,dx                    ; je ji‘ za‡ tek bufferu ?
         jbe       EditNKR5                 ; je ji‘ za‡ tek bufferu
         dec       si
         mov       al,ds:[si]               ; znak ‡¡sla
         cmp       al,"-"                   ; znam‚nko ?
         jne       EditNKR4
         mov       ah,al                    ; £schova znam‚nka "-"
         jmp       short EditNKR3

EditNKR4:cmp       al,"0"
         jb        EditNKR3
         cmp       al,"9"
         ja        EditNKR3
         dec       di
         mov       ds:[di],al               ; ulo‘en¡ znaku ‡¡slice
         jmp       short EditNKR3

; ------ vypu¨tˆn¡ £vodn¡ch nul ‡¡sla

EditNKR5:cmp       di,bx                    ; je ji‘ konec bufferu ?
         jae       EditNKR6                 ; je ji‘ konec bufferu
         cmp       byte ptr ds:[di],"0"     ; je nula ?
         jne       EditNKR6                 ; nen¡ nula
         inc       di                       ; vypu¨tˆn¡ nuly
         jmp       short EditNKR5

; ------ zaji¨tˆn¡ alespo¤ jedn‚ nuly, nen¡-li jin  ‡¡slice

EditNKR6:cmp       di,bx                    ; je nˆjak  ‡¡slice ?
         jb        EditNKR7                 ; je nˆjak  ‡¡slice
         dec       di
         mov       byte ptr ds:[di],"0"     ; alespo¤ 1 nula

; ------ ulo‘en¡ znam‚nka "-"

EditNKR7:cmp       di,dx                    ; je ji‘ za‡ tek bufferu ?
         jbe       EditNKR8                 ; je ji‘ za‡ tek bufferu
         cmp       ah,"-"                   ; je znam‚nko "-" ?
         jne       EditNKR8                 ; nen¡ znam‚nko "-"
         dec       di
         mov       ds:[di],ah               ; ulo‘en¡ znam‚nka "-"

; ------ vymaz n¡ zbytku bufferu

EditNKR8:cmp       di,dx                    ; je ji‘ za‡ tek bufferu ?
         jbe       EditNKR9                 ; je ji‘ za‡ tek bufferu
         dec       di
         mov       byte ptr ds:[di]," "     ; £vodn¡ mezera
         jmp       short EditNKR8

; ------ n vrat registr–

EditNKR9:pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         add       si,cx                    ; konec bufferu ‡¡sla
         ret

; -----------------------------------------------------------------------------
;        posun za konec textu v polo‘ce (ES=segment okna)
; -----------------------------------------------------------------------------

EditTEnd PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx

; ------ posun na konec polo‘ky

         call      EditPEnd                 ; posun na konec polo‘ky

; ------ parametry polo‘ky

         mov       bx,es:[DBFAktP]          ; ‡¡slo aktivn¡ polo‘ky
         shl       bx,1
         shl       bx,1
         shl       bx,1
         add       bx,es:[DBFAdrP]          ; adresa popisova‡e

; ------ polo‘ky D, T, L a M jsou ji‘ na konci

         test      byte ptr es:[bx+DBFParX],bit3+bit4+bit5+bit6 ; polo‘ky DLMT ?
         jnz       EditTEn8                 ; polo‘ky jsou ji‘ na konci

; ------ test, zda je na konci polo‘ky text

         call      EdiBGetC                 ; poskytnut¡ znaku na pozici kurzoru
         cmp       al," "                   ; je text ?
         jne       EditTEn4                 ; je text

; ------ nalezen¡ konce textu v polo‘ce

EditTEn2:cmp       byte ptr es:[DBFKurz],0  ; je ji‘ za‡ tek polo‘ky ?
         je        EditTEn4                 ; je ji‘ za‡ tek polo‘ky
         call      EditPLft                 ; posun kurzoru vlevo
         jc        EditTEn4                 ; chyba, ale zde by nemˆla nastat
         call      EdiBGetC                 ; poskytnut¡ znaku na pozici kurzoru
         cmp       al," "                   ; je text ?
         je        EditTEn2                 ; je text - dal¨¡ znak
         call      EditPRgh                 ; n vrat kurzoru

; ------ korekce textov‚ polo‘ky

EditTEn4:mov       bx,es:[DBFKurz]          ; kurzor v polo‘ce
         call      EditPBeg                 ; kurzor na za‡ tek polo‘ky
EditTEn6:cmp       bx,es:[DBFKurz]          ; dosa‘eno po‘adovan‚ pozice ?
         je        EditTEn8                 ; dosa‘eno po‘adovan‚ pozice
         call      EditPRgh                 ; posun kurzoru vpravo
         jnc       EditTEn6

; ------ n vrat registr–

EditTEn8:pop       bx
         pop       ax
         ret

EditTEnd ENDP

; -----------------------------------------------------------------------------
;        poskytnut¡ znaku na pozici kurzoru -> AL (ES=segment okna)
; -----------------------------------------------------------------------------

EdiBGetC PROC      NEAR

; ------ £schova registr–

         push      bx
         push      cx
         push      si

; ------ p©¡prava k vyhled v n¡ polo‘ky

         mov       si,es:[DBFAdrE]          ; edita‡n¡ buffer
         inc       si                       ; adresa prvn¡ polo‘ky
         mov       cx,es:[DBFAktP]          ; aktivn¡ polo‘ka
         mov       bx,es:[DBFAdrP]          ; adresa popisova‡– polo‘ek

; ------ nalezen¡ aktivn¡ polo‘ky

         jcxz      EdiBGtC2                 ; je prvn¡ polo‘ka
EdiBGtC1:add       si,es:[bx+DBFDelX]       ; adresa dal¨¡ polo‘ky
         add       bx,DBFX                  ; popisova‡ dal¨¡ polo‘ky
         loop      EdiBGtC1                 ; dal¨¡ polo‘ka

; ------ adresa aktivn¡ polo‘ky

EdiBGtC2:add       si,es:[DBFKurz]          ; adresa aktivn¡ polo‘ky
         mov       al,es:[si]               ; znak na pozici kurzoru

; ------ n vrat registr–

         pop       si
         pop       cx
         pop       bx
         ret

EdiBGetC ENDP

; -----------------------------------------------------------------------------
;        posun na konec polo‘ky (ES=segment okna)
; -----------------------------------------------------------------------------

EditPEnd PROC      NEAR

; ------ £schova registr–

         push      ax

; ------ nalezen¡ za‡ tku © dku

EditPEn2:mov       ax,es:[DBFAktP]          ; aktivn¡ polo‘ka
         call      EditPRgh                 ; posun kurzoru vpravo
         jc        EditPEn6                 ; nen¡ dal¨¡ pozice
         cmp       ax,es:[DBFAktP]          ; je dal¨¡ polo‘ka ?
         je        EditPEn2                 ; nalezen¡ dal¨¡ polo‘ky
         call      EditPLft                 ; n vrat na konec polo‘ky

; ------ n vrat registr–

EditPEn6:pop       ax
         ret

EditPEnd ENDP

; -----------------------------------------------------------------------------
;        posun na za‡ tek aktivn¡ polo‘ky (ES=segment okna)
; -----------------------------------------------------------------------------

EditPBeg PROC      NEAR

; ------ £schova registr–

         push      ax

; ------ nalezen¡ za‡ tku © dku

EditPBg2:mov       ax,es:[DBFAktP]          ; aktivn¡ polo‘ka
         call      EditPLft                 ; posun kurzoru vlevo
         jc        EditPBg6                 ; nen¡ dal¨¡ pozice
         cmp       ax,es:[DBFAktP]          ; je dal¨¡ polo‘ka ?
         je        EditPBg2                 ; nalezen¡ p©ede¨l‚ polo‘ky
         call      EditPRgh                 ; n vrat na za‡ tek polo‘ky

; ------ n vrat registr–

EditPBg6:pop       ax
         ret

EditPBeg ENDP

; -----------------------------------------------------------------------------
;        posun na dal¨¡ edit. polo‘ku (ES=segment okna, CY=nen¡ dal¨¡ polo‘ka)
; -----------------------------------------------------------------------------

EditPNxt PROC      NEAR

; ------ £schova registr–

         push      ax

; ------ posun kurzoru na dal¨¡ editovanou polo‘ku

EditPNx1:mov       ax,es:[DBFAktP]          ; aktivn¡ polo‘ka
         call      EditPRgh                 ; posun kurzoru vpravo
         jc        EditPNx8                 ; nen¡ dal¨¡ pozice
         cmp       ax,es:[DBFAktP]          ; zmˆnˆna aktivn¡ polo‘ka ?
         je        EditPNx1                 ; nen¡ dal¨¡ polo‘ka - dal¨¡ posun
         clc                                ; p©¡znak operace OK

; ------ n vrat registr–

EditPNx8:pop       ax
         ret

EditPNxt ENDP

; -----------------------------------------------------------------------------
;     posun na p©ede¨lou edit. polo‘ku (ES=segment okna, CY=nen¡ dal¨¡ polo‘ka)
; -----------------------------------------------------------------------------

EditPPre PROC      NEAR

; ------ £schova registr–

         push      ax

; ------ posun kurzoru na p©ede¨lou editovanou polo‘ku

EditPPr1:mov       ax,es:[DBFAktP]          ; aktivn¡ polo‘ka
         call      EditPLft                 ; posun kurzoru vlevo
         jc        EditPPr8                 ; nen¡ dal¨¡ pozice
         cmp       ax,es:[DBFAktP]          ; zmˆnˆna aktivn¡ polo‘ka ?
         je        EditPPr1                 ; polo‘ka nezmˆnˆna - dal¨¡ posun
         clc                                ; p©¡znak operace OK

; ------ n vrat registr–

EditPPr8:pop       ax
         ret

EditPPre ENDP

; -----------------------------------------------------------------------------
;        posun kurzoru o slovo vpravo (ES=segment okna)
; -----------------------------------------------------------------------------

EditPCRg PROC      NEAR

; ------ posun kurzoru o pozici vpravo

EditPCR1:call      EditPRgh                 ; posun kurzoru vpravo
         jc        EditPCR9                 ; nen¡ dal¨¡ pozice

; ------ test, zda je za‡ tek slova

         call      EditPCT                  ; test, zda je za‡ tek slova
         jc        EditPCR1                 ; nen¡ za‡ tek slova - dal¨¡ posun
EditPCR9:ret

EditPCRg ENDP

; -----------------------------------------------------------------------------
;        posun kurzoru o slovo vlevo (ES=segment definice okna)
; -----------------------------------------------------------------------------

EditPCLf PROC      NEAR

; ------ posun kurzoru o pozici vlevo

EditPCL1:call      EditPLft                 ; posun kurzoru vlevo
         jc        EditPCL9                 ; nen¡ dal¨¡ pozice

; ------ test, zda je za‡ tek slova

         call      EditPCT                  ; test, zda je za‡ tek slova
         jc        EditPCL1                 ; nen¡ za‡ tek slova - dal¨¡ posun
EditPCL9:ret

EditPCLf ENDP

; -----------------------------------------------------------------------------
;        test, zda je za‡ tek slova (CY=nen¡ za‡ tek slova)
; -----------------------------------------------------------------------------

EditPCT  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx

; ------ definice aktivn¡ polo‘ky -> ES:BX, AL, AH

         mov       bx,es:[DBFAktP]          ; BX <- aktivn¡ polo‘ka
         shl       bx,1
         shl       bx,1
         shl       bx,1
         add       bx,es:[DBFAdrP]          ; adresa definice polo‘ky
         mov       ah,es:[bx+DBFParX]       ; AH <- parametry polo‘ky
         mov       al,es:[DBFKurz]          ; AL <- kurzor

; ------ konec, je-li za‡ tek kter‚koliv polo‘ky

         cmp       al,0                     ; je za‡ tek polo‘ky ?
         je        EditPCT9                 ; je za‡ tek polo‘ky

; ------ posun kurzoru v ‡asu

         test      ah,bit6                  ; je ‡as ?
         jz        EditPCT2                 ; nen¡ to ‡as
         shr       al,1
         jmp       short EditPCT9           ; CY=je dal¨¡ posun

; ------ posun kurzoru v datu na dal¨¡ polo‘ku

EditPCT2:test      ah,bit3                  ; je to datum ?
         jz        EditPCT4                 ; nen¡ to datum
         cmp       al,4                     ; za‡ tek mˆs¡ce ?
         je        EditPCT9                 ; za‡ tek nov‚ polo‘ky
         cmp       al,6                     ; za‡ tek dne ?
         stc
         jne       EditPCT9                 ; nen¡ - dal¨¡ posun

; ------ posouv n¡ po slovech pouze pro text a ‡¡slo

EditPCT4:test      ah,bit0+bit1+bit2+bit7   ; je text nebo ‡¡slo ?
         jz        EditPCT9                 ; nˆco jin‚ho - konec OK

; ------ test, zda je za‡ tek desetinn‚ ‡ sti pro ‡¡slo

         test      ah,bit1+bit2             ; je ‡¡slo ?
         jz        EditPCT6                 ; nen¡ ‡¡slo
         mov       bx,es:[bx+DBFNamX]       ; adresa popisova‡e polo‘ky
         sub       al,es:[bx+16]            ; ode‡ten¡ d‚lky polo‘ky
         add       al,es:[bx+17]            ; test, zda je desetinn  ‡ st
         clc                                ; p©¡znak konce posunu
         jz        EditPCT9                 ; je za‡ tek desetinn‚ ‡ sti

; ------ £schova znaku na pozici kurzoru -> AH

EditPCT6:call      EdiBGetC                 ; znak na pozici kurzoru
         mov       ah,al                    ; £schova aktu ln¡ho znaku

; ------ posun o pozici vlevo (nen¡ je¨tˆ za‡ tek polo‘ky)

         call      EditPLft                 ; posun kurzoru vlevo

; ------ £schova p©edch zej¡c¡ho znaku

         call      EdiBGetC                 ; znak na pozici kurzoru

; ------ n vrat kurzoru

         call      EditPRgh                 ; n vrat kurzoru

; ------ test, zda je za‡ tek slova (pokud ne, je dal¨¡ posun)

         call      far ptr TestWrd          ; je to za‡ tek slova ?

; ------ n vrat registr–

EditPCT9:pop       bx
         pop       ax
         ret

EditPCT  ENDP

; -----------------------------------------------------------------------------
;       posun kurzoru v polo‘ce vpravo (ES=segment okna, CY=nen¡ dal¨¡ pozice)
; -----------------------------------------------------------------------------

EditPRgh PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      dx
         push      di

; ------ adresa popisova‡e aktivn¡ polo‘ky -> DI

         mov       di,es:[DBFAktP]          ; ‡¡slo aktivn¡ polo‘ky
         shl       di,1
         shl       di,1
         shl       di,1                     ; offset v tabulce popisova‡–
         add       di,es:[DBFAdrP]          ; adresa popisova‡e

; ------ zv˜¨en¡ offsetu v polo‘ce, je-li datum -> AL

         mov       al,es:[DBFKurz]          ; kurzor
         mov       ah,0
         test      byte ptr es:[di+DBFParX],bit3 ; je to datum ?
         jz        EditPRg2                 ; nen¡ datum
         mov       bh,0
         mov       bl,ds:[FormDat]          ; form t data (0=MDR, 1=DMR, 2=RMD)
         shl       bx,1
         shl       bx,1
         shl       bx,1

         push      bx
         add       bx,ax                    ; pozice kurzoru v polo‘ce
         mov       al,cs:[bx+EditPPT1]      ; n sleduj¡c¡ offset kurzoru
         pop       bx
         add       bx,ax                    ; aktu ln¡ pozice kurzoru
         mov       dl,cs:[bx+EditPPT3]      ; pozice na displeji

         cmp       al,6
         je        EditPRg0                 ; za‡ tek dne - konec editace
         cmp       al,4
         je        EditPRg0
         cmp       al,0
         jne       EditPRg1
EditPRg0:call      EditNmEn                 ; ukon‡en¡ editace ‡¡seln‚ polo‘ky
EditPRg1:jmp       short EditPR44

; ------ zv˜¨en¡ offsetu v polo‘ce, je-li ‡¡slo

EditPRg2:test      byte ptr es:[di+DBFParX],bit1+bit2 ; je ‡¡slo ?
         jz        EditPRg3                 ; nen¡ ‡¡slo
         inc       ax                       ; zv˜¨en¡ offsetu kurzoru
         mov       bx,es:[di+DBFNamX]       ; adresa definice polo‘ky
         cmp       byte ptr es:[bx+17],0    ; jsou desetiny ?
         je        EditPRg5                 ; nejsou desetiny
         mov       dl,es:[bx+16]            ; d‚lka polo‘ky
         sub       dl,es:[bx+17]            ; zbytek na cel‚ ‡¡slo
         dec       dx                       ; bez oddˆlovac¡ te‡ky
         cmp       al,dl                    ; je pozice te‡ky ?
         jne       EditPRg5                 ; nen¡ pozice te‡ky
         call      EditNmEn                 ; ukon‡en¡ editace ‡¡seln‚ polo‘ky
         inc       ax                       ; p©esko‡en¡ te‡ky
         jmp       short EditPRg5

; ------ zv˜¨en¡ offsetu, je-li pole MEMO

EditPRg3:test      byte ptr es:[di+DBFParX],bit5 ; je pole MEMO ?
         jz        EditPRg4

         mov       al,10                    ; pozice za polem MEMO
         jmp       short EditPRg5

; ------ zv˜¨en¡ offsetu v textov‚ polo‘ce, ‡asu a logick‚ polo‘ce

EditPRg4:inc       ax
         test      byte ptr es:[di+DBFParX],bit6 ; je to ‡asov‚ pole ?
         jz        EditPRg5                 ; nen¡ to ‡as
         mov       dl,al                    ; DL <- offset kurzoru
         cmp       al,2                     ; minuta nebo sekunda ?
         jb        EditPR42                 ; hodina
         inc       dx                       ; p©esko‡en¡ oddˆlova‡e
EditPR42:cmp       al,4                     ; sekunda ?
         jb        EditPR43                 ; nen¡
         inc       dx                       ; p©esko‡en¡ oddˆlova‡e
EditPR43:test      al,bit0                  ; je za‡ tek dal¨¡ podpolo‘ky ?
         jnz       EditPR44                 ; nen¡
         call      EditNmEn                 ; ukon‡en¡ editace ‡¡seln‚ polo‘ky

; ------ test, zda je ji‘ konec polo‘ky (DL=relativn¡ pozice na displeji)

EditPR44:cmp       dl,es:[di+DBFSirX]       ; p©ekro‡en konec zobrazen¡ ?
         jae       EditPR52                 ; p©ekro‡en konec

EditPRg5:cmp       al,es:[di+DBFDelX]       ; p©ekro‡en konec polo‘ky ?
         jb        EditPRg7                 ; nen¡ je¨tˆ konec polo‘ky
EditPR52:call      EditNmEn                 ; ukon‡en¡ editace ‡¡seln‚ polo‘ky

; ------ dal¨¡ popisova‡ polo‘ek

         mov       dx,es:[DBFAktP]          ; aktivn¡ polo‘ka
         inc       dx                       ; zv˜¨en¡ ‡¡sla polo‘ky
         cmp       dx,es:[DBFNumP]          ; je to platn  polo‘ka ?
         cmc
         jc        EditPRg9                 ; nen¡ dal¨¡ platn  polo‘ka
         add       di,DBFX                  ; adresa dal¨¡ho popisova‡e
         mov       es:[DBFAktP],dx          ; nov  aktivn¡ polo‘ka

; ------ stanoven¡ nov‚ pozice kurzoru

         xor       ax,ax                    ; kurzor na po‡ tek polo‘ky
         mov       es:[DBFOffT],ax          ; offset po‡ tku polo‘ky
         test      byte ptr es:[di+DBFParX],bit3 ; je to datum ?
         jz        EditPRg7                 ; nen¡ to datum
         cmp       byte ptr ds:[FormDat],1  ; je form t data DMR ?
         ja        EditPRg7                 ; form t data je RMD
         mov       al,4
         jb        EditPRg7                 ; form t data je MDR
         mov       al,6                     ; jinak form t data DMR

; ------ ulo‘en¡ nov‚ pozice kurzoru

EditPRg7:mov       es:[DBFKurz],ax          ; nov  pozice kurzoru

; ------ korekce po‡ tku textov‚ polo‘ky

         test      byte ptr es:[di+DBFParX],bit0+bit1+bit2+bit7 ; text/‡¡slo ?
         jz        EditPRg8                 ; nen¡ to text/‡¡slo
         sub       ax,es:[DBFOffT]          ; offset kurzoru od po‡ tku
         jbe       EditPRg8                 ; OK
         sub       ax,es:[di+DBFSirX]       ; p©ekro‡ena ¨¡©ka pole ?
         jb        EditPRg8                 ; nep©ekro‡ena ¨¡©ka pole
         inc       ax                       ; p©ete‡en¡ p©es konec
         add       es:[DBFOffT],ax          ; korekce po‡ tku polo‘ky
EditPRg8:clc                                ; p©¡znak operace OK

; ------ n vrat registr–

EditPRg9:pop       di
         pop       dx
         pop       bx
         pop       ax
         ret

EditPRgh ENDP

; ------ tabulka maxim ln¡ch offset– v datu (podle ¨¡©ky pole)

EditPPT0 db        4,4,5,5,6,7,7,0,1,2,3    ; USA:      MM.DD.RRRR
         db        6,6,7,7,4,5,5,0,1,2,3    ; EVROPA:   DD.MM.RRRR
         db        0,0,1,2,3,3,4,5,5,6,7    ; JAPONSKO: RRRR.MM.DD

; ------ tabulka dal¨¡ch offset– v datu
;                  0 1 2 3 4 5 6 7 8
;                  R R R R M M D D

EditPPT1 db        1,2,3,8,5,6,7,0          ; USA:      MM.DD.RRRR
         db        1,2,3,8,5,0,7,4          ; EVROPA:   DD.MM.RRRR
         db        1,2,3,4,5,6,7,8          ; JAPONSKO: RRRR.MM.DD

; ------ tabulka p©ede¨l˜ch offset– v datu

EditPPT2 db       7,0,1,2,-1,4,5,6          ; USA:      MM.DD.RRRR
         db       5,0,1,2,7,4,-1,6          ; EVROPA:   DD.MM.RRRR
         db       -1,0,1,2,3,4,5,6          ; JAPONSKO: RRRR.MM.DD

; ------ tabulka pozic na displeji
;                                                       0123456789
EditPPT3 db        6,7,8,9,0,1,2,3          ; USA:      MM.DD.RRRR
         db        6,7,8,9,3,4,0,1          ; EVROPA:   DD.MM.RRRR
         db        0,1,2,3,5,6,8,9          ; JAPONSKO: RRRR.MM.DD

; -----------------------------------------------------------------------------
;       posun kurzoru v polo‘ce vlevo (ES=segment okna, CY=nen¡ dal¨¡ pozice)
; -----------------------------------------------------------------------------
;þ
EditPLft PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      dx
         push      di

; ------ adresa popisova‡e aktivn¡ polo‘ky -> DI

         mov       di,es:[DBFAktP]          ; ‡¡slo aktivn¡ polo‘ky
         shl       di,1
         shl       di,1
         shl       di,1                     ; offset v tabulce popisova‡–
         add       di,es:[DBFAdrP]          ; adresa popisova‡e

; ------ sn¡‘en¡ offsetu v polo‘ce, je-li datum -> AL

         mov       al,es:[DBFKurz]          ; kurzor
         mov       ah,0
         test      byte ptr es:[di+DBFParX],bit3 ; je to datum ?
         jz        EditPLf2                 ; nen¡ datum
         mov       bh,0
         mov       bl,ds:[FormDat]          ; form t data (0=MDR, 1=DMR, 2=RMD)
         shl       bx,1
         shl       bx,1
         shl       bx,1

         add       bx,ax                    ; pozice kurzoru v polo‘ce
         mov       al,cs:[bx+EditPPT2]      ; p©ede¨l˜ offset kurzoru

         cmp       al,7
         je        EditPLf0
         cmp       al,5
         je        EditPLf0
         cmp       al,3
         jne       EditPLf1
EditPLf0:call      EditNmEn                 ; ukon‡en¡ editace ‡¡seln‚ polo‘ky
EditPLf1:cbw                                ; konverze na slovo
         jmp       short EditPLf5

; ------ sn¡‘en¡ offsetu v polo‘ce, je-li ‡¡slo

EditPLf2:test      byte ptr es:[di+DBFParX],bit1+bit2 ; je ‡¡slo ?
         jz        EditPLf3                 ; nen¡ ‡¡slo
         dec       ax                       ; sn¡‘en¡ offsetu kurzoru
         js        EditPLf5                 ; p©ekro‡en za‡ tek polo‘ky
         mov       bx,es:[di+DBFNamX]       ; adresa definice polo‘ky
         cmp       byte ptr es:[bx+17],0    ; jsou desetiny ?
         je        EditPLf5                 ; nejsou desetiny
         mov       dl,es:[bx+16]            ; d‚lka polo‘ky
         sub       dl,es:[bx+17]            ; zbytek na cel‚ ‡¡slo
         dec       dx                       ; bez oddˆlovac¡ te‡ky
         cmp       al,dl                    ; je pozice te‡ky ?
         jne       EditPLf5                 ; nen¡ pozice te‡ky
         call      EditNmEn                 ; ukon‡en¡ editace ‡¡seln‚ polo‘ky
         dec       ax                       ; p©esko‡en¡ te‡ky
         jmp       short EditPLf5

; ------ sn¡‘en¡ offsetu, je-li pole MEMO

EditPLf3:test      byte ptr es:[di+DBFParX],bit5 ; je pole MEMO ?
         jz        EditPLf4

         mov       ax,-1                    ; pozice p©ed polem MEMO
         jmp       short EditPLf5

; ------ sn¡‘en¡ offsetu v textov‚ polo‘ce

EditPLf4:dec       ax
         js        EditPLf5                 ; je podte‡en¡

; ------ ukon‡en¡ editace v polo‘ce ‡asu

         test      byte ptr es:[di+DBFParX],bit6 ; je ‡as ?
         jz        EditPLf5                 ; nen¡ ‡as
         test      al,bit0                  ; je druh˜ znak ?
         jnz       EditPLf0                 ; je nov  podpolo‘ka

; ------ test, zda je ji‘ za‡ tek polo‘ky

EditPLf5:or        ax,ax                    ; p©ekro‡en za‡ tek polo‘ky ?
         jns       EditPLf7                 ; nebyl je¨tˆ za‡ tek polo‘ky
         call      EditNmEn                 ; ukon‡en¡ editace ‡¡seln‚ polo‘ky

; ------ dal¨¡ popisova‡ polo‘ek

         mov       dx,es:[DBFAktP]          ; aktivn¡ polo‘ka
         or        dx,dx                    ; je ji‘ prvn¡ popisova‡ ?
         jnz       EditPL50
         stc
         jmp       EditPLf9                 ; nen¡ dal¨¡ platn  polo‘ka

EditPL50:sub       di,DBFX                  ; adresa dal¨¡ho popisova‡e
         dec       dx
         mov       es:[DBFAktP],dx          ; nov  aktivn¡ polo‘ka

; ------ stanoven¡ nov‚ pozice kurzoru

         xor       ax,ax                    ; AX <- 0
         mov       es:[DBFOffT],ax          ; offset polo‘ky <- 0
         test      byte ptr es:[di+DBFParX],bit5 ; je MEMO ?
         jnz       EditPLf7                 ; je MEMO pole
         mov       al,es:[di+DBFDelX]       ; d‚lka polo‘ky
         dec       ax                       ; posledn¡ pozice v polo‘ce

         test      byte ptr es:[di+DBFParX],bit6 ; je to ‡as ?
         jz        EditPL56                 ; nen¡ to ‡as

         mov       ax,es:[di+DBFSirX]       ; ¨¡©ka pole
         dec       ax                       ; posledn¡ znak
         jns       EditPL51
         xor       ax,ax                    ; omezen¡ p©i podte‡en¡
EditPL51:cmp       al,7                     ; je dostate‡n  ¨¡©ka ?
         jbe       EditPL52                 ; mal  ¨¡©ka
         mov       al,7                     ; omezen¡
EditPL52:cmp       al,5                     ; zak zan˜ znak ?
         jb        EditPL53                 ; OK
         dec       ax
EditPL53:cmp       al,2                     ; zak zan˜ znak
         jb        EditPL56
         dec       ax

EditPL56:test      byte ptr es:[di+DBFParX],bit3 ; je to datum ?
         jz        EditPLf7                 ; nen¡ to datum

         mov       ah,11
         mov       al,ds:[FormDat]          ; form t data (0=MDR, 1=DMR, 2=RMD)
         mul       ah                       ; adresa seznamu offset–
         mov       bx,es:[di+DBFSirX]       ; ¨¡©ka pole
         cmp       bl,10
         jbe       EditPLf6
         mov       bl,10
EditPLf6:add       bx,ax
         mov       al,cs:[EditPPT0+bx]

; ------ ulo‘en¡ nov‚ pozice kurzoru

EditPLf7:mov       es:[DBFKurz],ax          ; nov  pozice kurzoru

; ------ korekce po‡ tku textov‚ a ‡¡seln‚ polo‘ky

         test      byte ptr es:[di+DBFParX],bit0+bit1+bit2+bit7 ; je to text/‡¡slo ?
         jz        EditPLf8                 ; nen¡ to text/‡¡slo
         cmp       ax,es:[DBFOffT]          ; je kurzor za po‡ tkem ?
         jae       EditPL72                 ; po‡ tek je OK
         mov       word ptr es:[DBFOffT],ax ; korekce po‡ tku polo‘ky
EditPL72:inc       ax
         sub       ax,es:[di+DBFSirX]       ; minim ln¡ po‡ tek
         jbe       EditPLf8                 ; kurzor je OK
         cmp       ax,es:[DBFOffT]          ; p©ete‡en¡ konce ?
         jbe       EditPLf8                 ; nen¡ p©ete‡en¡ konce
         mov       es:[DBFOffT],ax          ; omezen¡ po‡ tku

EditPLf8:clc                                ; p©¡znak operace OK

; ------ n vrat registr–

EditPLf9:pop       di
         pop       dx
         pop       bx
         pop       ax
         ret

EditPLft ENDP

; -----------------------------------------------------------------------------
;        Insert - ozna‡en¡ polo‘ky/p©¡znak vkl d n¡
; -----------------------------------------------------------------------------

; ------ p©i editaci zmˆna p©¡znaku INSERT

EditBIns:cmp       word ptr es:[DBFNumP],0  ; jsou nˆjak‚ polo‘ky ?
         je        EditBI02                 ; nejsou ‘ dn‚ polo‘ky
         test      byte ptr ds:[DBFEParm],bit0 ; prob¡h  editace ?
         jz        EditBIn1                 ; nen¡ editace
         xor       byte ptr ds:[DBFEParm],bit2 ; zmˆna p©¡znaku editace
EditBI02:ret

; ------ test, zda bude ozna‡en¡ z znamu

EditBIn1:test      byte ptr es:[DBFParm],bit0 ; je str nkov˜ re‘im ?
         jnz       EditBI02                 ; je str nkov˜ re‘im - nic
         test      byte ptr es:[DBFParm],bit1 ; je z kaz z pisu ?
         jnz       EditBIn9                 ; z kaz z pisu - posun kurzoru dol–

; ------ £schova data a ‡asu souboru

         call      DBFGetDT                 ; £schova data a ‡asu souboru
         jc        EditBIn9                 ; chyba

; ------ na‡ten¡ aktu ln¡ho z znamu

         call      far ptr GetADBF          ; poskytnut¡ aktivn¡ polo‘ky DBF
         jc        EditBIn9                 ; chyba

; ------ zmˆna p©¡znaku ozna‡en¡ z znamu

         cmp       byte ptr es:[di]," "     ; je mezera ?
         je        EditBIn2                 ; je mezera OK
         mov       byte ptr es:[di],"*"     ; p©¡znak ozna‡en¡ polo‘ky
EditBIn2:xor       byte ptr es:[di],"*" XOR " " ; zmˆna p©¡znaku ozna‡en¡

; ------ ulo‘en¡ zmˆnˆn‚ho z znamu

         call      far ptr SetADBF          ; ulo‘en¡ aktivn¡ polo‘ky

; ------ vypr zdnˆn¡ z pisov‚ho bufferu a n vrat data a ‡asu

         call      DBFSetDT                 ; nastaven¡ data a ‡asu

; ------ posun kurzoru dol–

EditBIn9:jmp       EditBDwn                 ; posun kurzoru dol–

; -----------------------------------------------------------------------------
;        nulov n¡ ozna‡en¡ v¨ech z znam–
; -----------------------------------------------------------------------------

; ------ test, zda je funkce povolena

EditBNul:cmp       word ptr es:[DBFNumP],0  ; jsou nˆjak‚ polo‘ky ?
         je        EditBNl0                 ; nejsou ‘ dn‚ polo‘ky
         test      byte ptr ds:[DBFEParm],bit0 ; je editace ?
         jnz       EditBNl0                 ; je editace
         test      byte ptr es:[DBFParm],bit1 ; je z kaz z pisu ?
         jnz       EditBNl0                 ; je z kaz z pisu

; ------ potvrzen¡ operace

         mov       di,offset DBNMLin
         call      EditBLM                  ; obsluha © dkov‚ho menu
         jnc       EditBNl1                 ; nen¡ p©eru¨en¡ operace
EditBNl0:ret

; ------ zobrazen¡ hl ¨en¡ o nulov n¡

EditBNl1:push      si
         mov       si,offset DBNMLHls       ; hl ¨en¡
         call      far ptr InfDisp0         ; zobrazen¡ hl ¨en¡
         pop       si

; ------ £schova registr–

         push      ax
         push      dx

; ------ £schova data a ‡asu

         call      DBFGetDT                 ; £schova data a ‡asu
         jc        EditBNl8                 ; chyba

; ------ poskytnut¡ dal¨¡ho z znamu

         xor       ax,ax                    ; ukazatel z znamu LOW
         xor       dx,dx                    ; ukazatel z znamu HIGH
EditBNl2:call      far ptr GetDBF           ; poskytnut¡ z znamu DX:AX
         jc        EditBNl6                 ; konec - nen¡ dal¨¡ z znam

; ------ nulov n¡ p©¡znaku ozna‡en¡

         cmp       byte ptr es:[di]," "     ; je polo‘ka ozna‡ena ?
         je        EditBNl4                 ; polo‘ka nen¡ ozna‡ena - dal¨¡
         mov       byte ptr es:[di]," "     ; zru¨en¡ ozna‡en¡ polo‘ky
         call      far ptr SetDBF           ; ulo‘en¡ polo‘ky na disk

; ------ dal¨¡ z znam

EditBNl4:add       ax,1                     ; zv˜¨en¡ ‡¡sla z znamu
         adc       dx,0

; ------ test, zda je p©eru¨en¡ operace

         call      far ptr TestBEsc         ; test p©eru¨en¡ operace
         jnc       EditBNl2
         call      far ptr FlushChr         ; vypr zdnˆn¡ bufferu kl vesnice

; ------ n vrat data a ‡asu, pokud byla alespo¤ 1 modifikace souboru

EditBNl6:call      DBFSetDT                 ; n vrat data a ‡asu

; ------ n vrat registr–

EditBNl8:pop       dx
         pop       ax
         call      far ptr InfoClos         ; uzav©en¡ informa‡n¡ho © dku

EditBNl9:ret

; -----------------------------------------------------------------------------
;        export dat (SI=podmenu)
; -----------------------------------------------------------------------------
;þ
EditBExp PROC      FAR

         cmp       word ptr es:[DBFNumP],0  ; jsou nˆjak‚ polo‘ky ?
         je        EditBEx0                 ; nejsou ‘ dn‚ polo‘ky
         test      byte ptr ds:[DBFEParm],bit0 ; je editace ?
         jnz       EditBEx0                 ; je editace

         and       byte ptr ds:[DBFEParm],not bit5 ; v¨echny z znamy
         mov       cx,word ptr ds:[DBMnVert+MnuPoz]
         add       cx,7+7*HI
         mov       word ptr ds:[si+MnuPoz],cx

         mov       dl,ds:[DBExpPar]         ; parametry
         call      far ptr SetSwch          ; nastaven¡ p©ep¡na‡–
         jmp       far ptr VertMenu         ; volba operace

EditBEx0:ret

EditBExp ENDP

; ------ zmˆna p©ep¡na‡–

EditBEx1 PROC      FAR

         mov       cl,byte ptr ds:[si+MnuAkt]        ; aktivn¡ zvolen  polo‘ka
         dec       cx
         mov       dl,bit0
         shl       dl,cl                    ; nov˜ p©ep¡na‡
         mov       ds:[DBExpPar],dl         ; nov˜ p©ep¡na‡
         call      far ptr SetSwch          ; nastaven¡ p©ep¡na‡e
         call      far ptr DispMnu          ; nov‚ zobrazen¡ okna menu
         ret

EditBEx1 ENDP

; ------ start volby

EditBEx2:or        byte ptr ds:[DBFEParm],bit5 ; pouze ozna‡en‚ z znamy
EditBEx3:add       sp,4                     ; zru¨en¡ n vratov‚ adresy
         call      far ptr ClosMnu          ; uzav©en¡ okna podmenu
         call      far ptr ClosMnu          ; uzav©en¡ okna menu
         pop       dx
         pop       si

; ------ zad n¡ jm‚na souboru

         push      ax

         mov       di,offset DBXMSLin
         call      EditBLM                  ; obsluha © dkov‚ho menu
         jc        EditBEx9

; ------ normalizace jm‚na souboru

         push      si
         push      es
         call      far ptr NormFilL         ; normalizace jm‚na souboru
         pop       es
         pop       si
         jz        EditBEx9                 ; nen¡ nic zad no

; ------ otev©en¡/vytvo©en¡ souboru

         push      si
         push      es

         push      ds
         pop       es
         mov       si,offset LinBuff        ; buffer jm‚na souboru
         call      far ptr OpenRW           ; otev©en¡ souboru
         jnc       EditBEx4                 ; soubor otev©en OK
         call      far ptr CreatFil         ; vytvo©en¡ souboru

EditBEx4:pop       es
         pop       si
         jc        EditBEx9                 ; chyba
         call      far ptr SizeFile         ; ukazatel na konec souboru

; ------ export dat do souboru AX

         call      EDBExp                   ; export dat do souboru AX

         call      far ptr ClosFile         ; uzav©en¡ souboru AX

EditBEx9:pop       ax
         ret

; -----------------------------------------------------------------------------
;        export dat do souboru AX (SI=definice okna, ES=segment okna)
; -----------------------------------------------------------------------------
; lok ln¡ promˆnn‚: SS:[BP-2] (2) adresa definice okna
;                   SS:[BP-4] (2) identifik tor v˜stupn¡ho souboru
;                   SS:[BP-8] (4) ukazatel ‡¡sla z znamu
;                   SS:[BP-10] (2) adresa konce bufferu
;                   SS:[BP-11] (1) znak oddˆlova‡e (0=nen¡)
; -----------------------------------------------------------------------------

EDBExp   PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         mov       bp,sp
         sub       sp,12

; ------ p©¡prava lok ln¡ch promˆnn˜ch

         mov       ss:[bp-2],si             ; adresa definice okna
         mov       ss:[bp-4],ax             ; identifik tor v˜stup. souboru
         xor       ax,ax
         mov       ss:[bp-8+2],ax           ; ukazatel ‡¡sla z znamu
         mov       ss:[bp-8],ax
         mov       ax,es:[DBFAdrE]          ; adresa edita‡n¡ho bufferu
         mov       di,ax                    ; adresa edita‡n¡ho bufferu
         add       ax,es:[DBFBytZ]          ; adresa konce bufferu
         mov       ss:[bp-10],ax            ; adresa konce bufferu

; ------ p©¡prava znaku oddˆlova‡e dat

         mov       al,ds:[DBExpPar]         ; parametry pro export
         mov       ah,","
         shr       al,1
         jc        EDBExp1                  ; oddˆlovac¡ ‡ rka
         mov       ah,";"
         shr       al,1
         jc        EDBExp1                  ; oddˆlovac¡ st©edn¡k
         mov       ah,TAB
         shr       al,1
         jc        EDBExp1                  ; oddˆlovac¡ tabul tor
         mov       ah,"³"
         shr       al,1
         jc        EDBExp1                  ; oddˆlovac¡ r m
         mov       ah,0                     ; nen¡ oddˆlova‡
EDBExp1: mov       ss:[bp-11],ah            ; znak oddˆlova‡e polo‘ek

; ------ poskytnut¡ dal¨¡ho z znamu

EDBExp2: push      di                       ; ukl dac¡ adresa do bufferu
         mov       si,ss:[bp-2]             ; adresa definice okna
         mov       ax,ss:[bp-8]             ; ‡¡slo z znamu LOW
         mov       dx,ss:[bp-8+2]           ; ‡¡slo z znamu HIGH
         call      far ptr GetDBF           ; poskytnut¡ dal¨¡ho z znamu
         mov       si,di                    ; SI <- za‡ tek z znamu
         pop       di
         jnc       EDBExp24                 ; je dal¨¡ z znam OK
         jmp       EDBExp8                  ; nen¡ dal¨¡ z znam

; ------ test, zda je z znam platn˜

EDBExp24:test      byte ptr ds:[DBFEParm],bit5 ; pouze ozna‡en‚ z znamy ?
         jz        EDBExp3                  ; jsou v¨echny z znamy
         cmp       byte ptr es:[si],"*"     ; je to ozna‡en˜ z znam ?
         je        EDBExp3                  ; je to ozna‡en˜ z znam
         jmp       EDBExp7                  ; nen¡ to ozna‡en˜ z znam

; ------ nalezen¡ d‚lky dat v z znamu -> CX

EDBExp3: mov       cx,es:[DBFBytZ]          ; po‡et bajt– na z znam
         dec       cx                       ; bez £vodn¡ho bajtu
         inc       si                       ; po‡ tek z znamu
         push      si
         test      byte ptr ds:[DBExpPar],bit3 ; je r m ?
         jnz       EDBExp34                 ; je r m
         add       si,cx                    ; adresa konec dat v z znamu
EDBExp32:dec       si                       ; posledn¡ bajt
         cmp       byte ptr es:[si]," "     ; jsou data ?
         jne       EDBExp34                 ; jsou data
         loop      EDBExp32                 ; dal¨¡ data
EDBExp34:pop       si

; ------ p©¡prava k dek¢dov n¡ z znamu

         mov       bx,es:[DBFAdrP]          ; tabulka popisova‡–
         jcxz      EDBExp43                 ; je konec dat

; ------ p©¡prava k dek¢dov n¡ jedn‚ polo‘ky

EDBExp4: mov       ah,es:[bx+DBFDelX]       ; d‚lka polo‘ky (bajt–)

; ------ MEMO pole se neexportuje

         test      byte ptr es:[bx+DBFParX],bit5 ; je MEMO pole ?
         jz        EDBExp41                 ; nen¡ MEMO pole
         mov       al,ah                    ; d‚lka polo‘ky
         mov       ah,0
         add       si,ax                    ; adresa dal¨¡ polo‘ky
         sub       cx,ax                    ; zbyl‚ znaky
         jnc       EDBExp40
         xor       cx,cx
EDBExp40:jcxz      EDBExp43                 ; konec © dku
         jmp       EDBExp5A                 ; MEMO pole se neukl d 

; ------ nalezen¡ za‡ tku textu v polo‘ce

EDBExp41:test      byte ptr es:[bx+DBFParX],bit0+bit7 ; je textov  polo‘ka ?
         jnz       EDBExp5                  ; je textov  polo‘ka
         test      byte ptr ds:[DBExpPar],bit3+bit4 ; je r m nebo pozice ?
         jnz       EDBExp5                  ; je r m nebo pozice
EDBExp42:cmp       byte ptr es:[si]," "     ; je platn˜ znak ?
         jne       EDBExp5                  ; je za‡ tek textu polo‘ky
         inc       si                       ; dal¨¡ znak
         dec       cx                       ; ‡¡ta‡ zbytku textu
EDBExp43:jcxz      EDBExp6                  ; konec © dku
         dec       ah                       ; ‡¡ta‡ d‚lky polo‘ky
         jnz       EDBExp42                 ; nen¡ konec polo‘ky
         jmp       short EDBExp59           ; je konec polo‘ky

; ------ nalezen¡ konce textu polo‘ky

EDBExp5: push      si
         push      ax

         mov       al,ah                    ; d‚lka zbytku polo‘ky
         mov       ah,0
         add       si,ax                    ; konec polo‘ky
         mov       dx,si                    ; DX <- adresa konce polo‘ky

         test      byte ptr ds:[DBExpPar],bit3+bit4 ; je r m nebo pozice ?
         jz        EDBExp51                 ; nen¡ r m ani pozice
         cmp       cx,ax                    ; bude je¨tˆ dal¨¡ polo‘ka ?
         ja        EDBExp52                 ; bude dal¨¡ polo‘ka - konec plat¡

EDBExp51:dec       si
         cmp       byte ptr es:[si]," "     ; nalezen konec polo‘ky ?
         jne       EDBExp52                 ; nalezen konec polo‘ky
         dec       dx                       ; sn¡‘en¡ ukazatele konce polo‘ky
         jmp       short EDBExp51

EDBExp52:pop       ax
         pop       si

; ------ dek¢dov n¡ polo‘ky

         cmp       si,dx                    ; je nˆjak˜ text ?
         jae       EDBExp57                 ; nen¡ ‘ dn˜ text
         call      EDBExpV                  ; ulo‘en¡ znaku uvozovek
EDBExp54:cmp       si,dx                    ; je konec textu polo‘ky ?
         jae       EDBExp56                 ; konec textu polo‘ky

         mov       al,es:[si]               ; na‡ten¡ znaku polo‘ky
         inc       si
         cmp       al,'"'
         jne       EDBExp55
         call      EDBExpV                  ; ulo‘en¡ znaku uvozovek
EDBExp55:call      EDBExpU0                 ; ulo‘en¡ znaku (ne TAB)

         dec       ah                       ; ‡¡ta‡ zbytku polo‘ky
         loop      EDBExp54                 ; dal¨¡ znak

EDBExp56:call      EDBExpV                  ; ulo‘en¡ znaku uvozovek
EDBExp57:mov       al,ah                    ; zbytek polo‘ky
         mov       ah,0
         add       si,ax                    ; adresa dal¨¡ polo‘ky
         sub       cx,ax                    ; zbyl‚ znaky
         jnc       EDBExp59
         xor       cx,cx

; ------ p©¡prava pro dal¨¡ polo‘ku v z znamu

EDBExp59:jcxz      EDBExp6                  ; je konec dat

; ------ ulo‘en¡ znaku oddˆlova‡e dat

         mov       al,ss:[bp-11]            ; znak oddˆlova‡e dat
         or        al,al                    ; je oddˆlova‡ dat ?
         jz        EDBExp5A                 ; nen¡ oddˆlova‡ dat
         call      EDBExpU                  ; ulo‘en¡ oddˆlova‡e dat

EDBExp5A:add       bx,DBFX                  ; adresa dal¨¡ho popisova‡e
         cmp       bx,es:[DBFAdrE]          ; konec popisova‡– ?
         jae       EDBExp6                  ; jsou ji‘ v¨echny polo‘ky
         jmp       EDBExp4                  ; dal¨¡ polo‘ka

; ------ ulo‘en¡ konce © dku

EDBExp6: mov       al,CR
         call      EDBExpU
         mov       al,LF
         call      EDBExpU

; ------ p©¡prava pro dal¨¡ z znam

EDBExp7: add       word ptr ss:[bp-8],1     ; zv˜¨en¡ ‡¡sla z znamu
         adc       word ptr ss:[bp-8+2],0
         call      far ptr TestBEsc         ; je p©eru¨en¡ ?
         jc        EDBExp8                  ; p©eru¨en¡ operace
         jmp       EDBExp2                  ; dal¨¡ z znam

; ------ ulo‘en¡ zbytku dat do souboru

EDBExp8: call      EDBExpW                  ; ulo‘en¡ zbytku dat z bufferu
         clc

; ------ n vrat registr–

EDBExp9: mov       sp,bp
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

EDBExp   ENDP


; ------ ulo‘en¡ znaku uvozovek

EDBExpV: test      byte ptr es:[bx+DBFParX],bit0+bit7 ; je to textov  polo‘ka ?
         jz        EDBExpU2                 ; nen¡ to textov  polo‘ka
         test      byte ptr ds:[DBExpPar],bit2+bit3+bit4 ; TAB, r m, pozice ?
         jnz       EDBExpU2                 ; je TAB, r m, pozice - nejsou uvoz.
         mov       al,'"'

EDBExpU0:cmp       al,TAB                   ; je tabul tor ?
         jne       EDBExpU                  ; nen¡ tabul tor
         mov       al," "                   ; n hrada tabul toru mezerou

; ------ ulo‘en¡ znaku AL do bufferu (ukl dac¡ adresa DI)

EDBExpU: cld
         stosb                              ; ulo‘en¡ znaku AL do bufferu
         cmp       di,ss:[bp-10]            ; je buffer pln˜ ?
         jae       EDBExpW                  ; buffer je ji‘ pln˜
EDBExpU2:ret

; ------ vypr zdnˆn¡ bufferu (ukl dac¡ adresa DI, smˆr CLD)

EDBExpW: push      cx
         push      si
         push      ax

         mov       si,es:[DBFAdrE]          ; adresa edita‡n¡ho bufferu
         sub       di,si                    ; po‡et bajt– v bufferu
         mov       cx,di                    ; CX <- po‡et bajt– v bufferu
         mov       ax,ss:[bp-4]             ; identifik tor souboru
         call      far ptr WritFile         ; z pis dat do souboru
         mov       di,si                    ; adresa bufferu

         pop       ax
         pop       si
         pop       cx
         cld
         jnc       EDBExpU2                 ; operace OK

         add       sp,2                     ; zru¨en¡ n vratov‚ adresy
         stc                                ; p©¡znak chyby
         jmp       EDBExp9                  ; n vrat p©i chybˆ

; -----------------------------------------------------------------------------
;        import dat (SI=podmenu)
; -----------------------------------------------------------------------------
;þ
EditBImp PROC      FAR

         cmp       word ptr es:[DBFNumP],0  ; jsou nˆjak‚ polo‘ky ?
         je        EditBIm0                 ; nejsou ‘ dn‚ polo‘ky
         test      byte ptr ds:[DBFEParm],bit0 ; je editace ?
         jnz       EditBIm0                 ; je editace

         and       byte ptr ds:[DBFEParm],not bit5 ; z znamy na konec souboru
         mov       cx,word ptr ds:[DBMnVert+MnuPoz]
         add       cx,7+1*HI
         mov       word ptr ds:[si+MnuPoz],cx

         mov       dl,ds:[DBImpPar]         ; parametry
         call      far ptr SetSwch          ; nastaven¡ p©ep¡na‡–
         jmp       far ptr VertMenu         ; volba operace

EditBIm0:ret

EditBImp ENDP

; ------ zmˆna p©ep¡na‡–

EditBIm1 PROC      FAR

         mov       cl,byte ptr ds:[si+MnuAkt]        ; aktivn¡ zvolen  polo‘ka
         dec       cx
         mov       dl,bit0
         shl       dl,cl                    ; nov˜ p©ep¡na‡
         mov       ds:[DBImpPar],dl         ; nov˜ p©ep¡na‡
         call      far ptr SetSwch          ; nastaven¡ p©ep¡na‡e
         call      far ptr DispMnu          ; nov‚ zobrazen¡ okna menu
         ret

EditBIm1 ENDP

; ------ start volby

EditBIm2:or        byte ptr ds:[DBFEParm],bit5 ; vlo‘it z znamy
EditBIm3:add       sp,4                     ; zru¨en¡ n vratov‚ adresy
         call      far ptr ClosMnu          ; uzav©en¡ okna podmenu
         call      far ptr ClosMnu          ; uzav©en¡ okna menu
         pop       dx
         pop       si

; ------ zad n¡ jm‚na souboru

         push      ax

         mov       di,offset DBIMSLin
         call      EditBLM                  ; obsluha © dkov‚ho menu
         jc        EdiBIm34

; ------ normalizace jm‚na souboru

         push      si
         push      es
         call      far ptr NormFilL         ; normalizace jm‚na souboru
         pop       es
         pop       si
         jz        EdiBIm34                 ; nen¡ nic zad no

; ------ otev©en¡ souboru

         push      si
         push      es

         push      ds
         pop       es
         mov       si,offset LinBuff        ; buffer jm‚na souboru
         call      far ptr OpenR            ; otev©en¡ souboru pro ‡ten¡

         pop       es
         pop       si
         jnc       EdiBIm36
EdiBIm34:jmp       EditBIm9                 ; chyba

; ------ polo‘ky se vkl daj¡ na konec souboru

EdiBIm36:mov       bx,es:[DBFMaxZ+2]        ; posledn¡ z znam v souboru
         mov       cx,es:[DBFMaxZ]
         test      byte ptr ds:[DBFEParm],bit5 ; je vkl d n¡ od kurzoru ?
         jz        EditBIm7                 ; je ukl d n¡ na konec souboru

; ------ spo‡ten¡ po‡tu © dk– v souboru

         push      dx

         push      ax
         mov       ax,es:[DBFBytZ]          ; po‡et bajt– na z znam
         mul       word ptr es:[DBFMaxD]    ; velikost bufferu (bajt–)
         xchg      ax,cx                    ; CX <- velikost bufferu (bajt–)
         pop       ax

         xor       bx,bx                    ; ‡¡ta‡ © dk– LOW
         xor       dx,dx                    ; ‡¡ta‡ © dk– HIGH
         mov       es:[DBFNumD],bx          ; po‡et z znam– v bufferu = 0

EditBIm4:mov       di,es:[DBFAdrD]          ; adresa datov‚ho bufferu
         call      far ptr ReadFile         ; na‡ten¡ dat ze souboru
         jcxz      EditBIm6                 ; konec souboru
         push      ax
         push      cx

         cld
         mov       al,LF                    ; hledan˜ znak LF
EdiBIm42:repne     scasb                    ; nalezen¡ znaku LF
         jne       EditBIm5                 ; dal¨¡ znak nenalezen
         add       bx,1                     ; ‡¡ta‡ © dk– LOW
         adc       dx,0                     ; ‡¡ta‡ © dk– HIGH
         or        cx,cx                    ; jsou dal¨¡ data ?
         jnz       EdiBIm42                 ; jsou dal¨¡ data
EditBIm5:pop       cx
         pop       ax
         jmp       short EditBIm4           ; dal¨¡ data

EditBIm6:mov       di,dx                    ; DI <- po‡et © dk– HIGH
         or        dx,bx                    ; jsou nˆjak‚ © dky ?
         pop       dx
         jz        EditBIm8                 ; nejsou ‘ dn‚ © dky

; ------ vlo‘en¡ z znam– do souboru

         push      ax
         push      dx
         mov       dx,es:[DBFAktZ+2]
         mov       ax,es:[DBFAktZ]          ; aktivn¡ z znam
         call      EdiBZIns                 ; vlo‘en¡ nov˜ch z znam–
         pop       dx
         pop       ax
         jc        EditBIm8                 ; chyba

; ------ resetov n¡ ukazatele v souboru

         call      far ptr ResFile          ; resetov n¡ ukazatele v souboru
         mov       bx,es:[DBFAktZ+2]
         mov       cx,es:[DBFAktZ]          ; aktivn¡ z znam

; ------ import dat ze souboru AX

EditBIm7:call      EDBImp                   ; import dat ze souboru AX

EditBIm8:call      far ptr ClosFile         ; uzav©en¡ souboru AX

EditBIm9:pop       ax
         ret

; -----------------------------------------------------------------------------
;        import dat ze souboru
; -----------------------------------------------------------------------------
; VSTUP: AX=identifik tor souboru
;        SI=adresa definice okna
;        ES=segment okna
;        BX:CX=po‡ te‡n¡ z znam
; VSTUP: CY=chyba
; -----------------------------------------------------------------------------
; lok ln¡ promˆnn‚: SS:[BP-2] (2) adresa definice okna
;                   SS:[BP-4] (2) identifik tor vstupn¡ho souboru
;                   SS:[BP-8] (4) ukazatel ‡¡sla z znamu
;                   SS:[BP-10] (2) adresa konce bufferu (konce dat v bufferu)
;                   SS:[BP-11] (1) znak oddˆlova‡e (0=nen¡)
; -----------------------------------------------------------------------------
;þ
EDBImp   PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         mov       bp,sp
         sub       sp,12

; ------ p©¡prava lok ln¡ch promˆnn˜ch

         mov       ss:[bp-2],si             ; adresa definice okna
         mov       ss:[bp-4],ax             ; identifik tor souboru
         mov       ss:[bp-8],cx             ; ukazatel ‡¡sla z znamu
         mov       ss:[bp-8+2],bx
         mov       ax,es:[DBFMaxZ]          ; maxim lnˆ z znam– v bufferu
         mul       word ptr es:[DBFBytZ]    ; v˜po‡et velikosti bufferu
         add       ax,es:[DBFAdrD]          ; adresa konce bufferu
         mov       ss:[bp-10],ax            ; adresa konce bufferu

; ------ p©¡prava znaku oddˆlova‡e dat

         mov       al,ds:[DBImpPar]         ; parametry pro import
         mov       ah,","
         shr       al,1
         jc        EDBImp1                  ; oddˆlovac¡ ‡ rka
         mov       ah,";"
         shr       al,1
         jc        EDBImp1                  ; oddˆlovac¡ st©edn¡k
         mov       ah,TAB
         shr       al,1
         jc        EDBImp1                  ; oddˆlovac¡ tabul tor
         mov       ah,"³"
         shr       al,1
         jc        EDBImp1                  ; oddˆlovac¡ r m
         mov       ah,0                     ; nen¡ oddˆlova‡
EDBImp1: mov       ss:[bp-11],ah            ; znak oddˆlova‡e polo‘ek

; ------ na‡ten¡ dat do bufferu

         call      EDBImpCR                 ; na‡ten¡ obsahu bufferu
         dec       si                       ; n vrat ukazatele dat v bufferu

; ------ vymaz n¡ bufferu jednoho z znamu

EDBImp2: mov       al," "                   ; mazac¡ znak mezery
         mov       di,es:[DBFAdrE]          ; adresa edita‡n¡ho bufferu
         mov       cx,es:[DBFBytZ]          ; po‡et bajt– na z znam
         cld
         rep       stosb                    ; vymaz n¡ bufferu

; ------ p©¡prava k dek¢dov n¡ dat z znamu

         mov       bx,es:[DBFAdrP]          ; adresa popisova‡– polo‘ek
         mov       di,es:[DBFAdrE]          ; adresa edita‡n¡ho bufferu
         inc       di                       ; za‡ tek prvn¡ polo‘ky

; ------ MEMO pole se p©esko‡¡

EDBImp3: mov       ah,es:[bx+DBFDelX]       ; d‚lka polo‘ky
         test      byte ptr es:[bx+DBFParX],bit5 ; je to MEMO pole ?
         jnz       EDBImp44                 ; je MEMO pole - dal¨¡ polo‘ka

; ------ dek¢dov n¡ polo‘ky v m¢du pozice

EDBImp4: test      byte ptr ds:[DBImpPar],bit4 ; je ur‡eno pozic¡ ?
         jz        EDBImp5                  ; nen¡ m¢d pozice
         mov       cx,es:[bx+DBFDelX]       ; d‚lka polo‘ky (pozic)
EDBImp42:call      EDBImpC                  ; na‡ten¡ znaku
         call      EDBImpU                  ; ulo‘en¡ znaku
         loop      EDBImp42                 ; dal¨¡ znak
EDBImp44:jmp       EDBImp7                  ; dal¨¡ polo‘ka

; ------ dek¢dov n¡ textov‚ polo‘ky

EDBImp5: call      EDBImpC                  ; na‡ten¡ prvn¡ho znaku
         cmp       al,'"'
         jne       EDBImp6
EDBImp51:call      EDBImpC                  ; na‡ten¡ znaku
         cmp       al,'"'
         je        EDBImp53                 ; je konec polo‘ky
EDBImp52:call      EDBImpU                  ; ulo‘en¡ znaku
         jmp       short EDBImp51

EDBImp53:call      EDBImpC                  ; dal¨¡ znak
         cmp       al,'"'
         je        EDBImp52                 ; zdvojen‚ uvozovky
EDBImp54:cmp       al,ss:[bp-11]            ; oddˆlova‡ ?
         je        EDBImp44                 ; oddˆlova‡ - konec polo‘ky
         call      EDBImpC                  ; dal¨¡ znak
         jmp       short EDBImp54

; ------ dek¢dov n¡ jin‚ polo‘ky

EDBImp6: cmp       al,ss:[bp-11]
         je        EDBImp7                  ; je oddˆlova‡
EDBImp62:call      EDBImpU                  ; ulo‘en¡ znaku
         call      EDBImpC                  ; dal¨¡ znak
         cmp       al,ss:[bp-11]
         jne       EDBImp62                 ; nen¡ oddˆlova‡ - dal¨¡ znak

; ------ p©¡prava pro dal¨¡ polo‘ku

EDBImp7: mov       al,ah
         mov       ah,0
         add       di,ax                    ; dal¨¡ polo‘ka
         add       bx,DBFX                  ; popisova‡ dal¨¡ho z znamu
         cmp       bx,es:[DBFAdrE]          ; je ji‘ konec popisova‡– ?
         jae       EDBImp72                 ; je ji‘ konec popisova‡–
         jmp       EDBImp3                  ; dek¢dov n¡ dal¨¡ polo‘ky

; ------ nalezen¡ konce © dku

EDBImp72:call      EDBImpC                  ; dal¨¡ znak
         cmp       al,LF
         jne       EDBImp72                 ; nalezen¡ konce © dku

; ------ ulo‘en¡ dek¢dovan‚ho z znamu (sem se sk ‡e z EDBImpC)

EDBImp8: push      si
         call      EDBImpK                  ; korekce z znamu
         mov       ax,ss:[bp-8]             ; ‡¡slo z znamu
         mov       dx,ss:[bp-8+2]
         mov       si,ss:[bp-2]             ; adresa definice okna
         mov       di,es:[DBFAdrE]          ; adresa z znamu
         call      far ptr SetDBF           ; ulo‘en¡ z znamu
         pop       si
         jc        EDBImp88                 ; nˆjak  chyba

; ------ zv˜¨en¡ po‡tu z znam–

         cmp       dx,es:[DBFMaxZ+2]
         jne       EDBImp82
         cmp       ax,es:[DBFMaxZ]
EDBImp82:jb        EDBImp85                 ; nen¡ zv˜¨en¡ po‡tu z znam–
         add       word ptr es:[DBFMaxZ],1
         adc       word ptr es:[DBFMaxZ+2],0

; ------ p©¡prava pro dal¨¡ z znam

EDBImp85:add       word ptr ss:[bp-8],1     ; zv˜¨en¡ ukazatele ‡¡sla z znamu
         adc       word ptr ss:[bp-8+2],0
         jmp       EDBImp2                  ; dal¨¡ z znam

; ------ ulo‘en¡ nov‚ho z hlav¡

EDBImp88:mov       si,ss:[bp-2]             ; adresa definice okna
         call      far ptr HlavDBF          ; z pis z hlav¡ souboru
         mov       word ptr es:[DBFNumD],0  ; datov˜ buffer je neplatn˜

; ------ n vrat registr–

EDBImp9: mov       sp,bp
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

EDBImp   ENDP

; ------ ulo‘en¡ znaku AL do polo‘ky v z znamu ES:DI (AH=omezen¡ d‚lky polo‘ky)

EDBImpU: or        ah,ah                    ; je konec polo‘ky ?
         jz        EDBImpU2                 ; je ji‘ konec polo‘ky
         cld
         stosb                              ; ulo‘en¡ bajtu
         dec       ah                       ; sn¡‘en¡ ‡¡ta‡e zbyl‚ d‚lky
EDBImpU2:ret

; ------ na‡ten¡ znaku AL z bufferu SI

EDBImpC: cmp       si,ss:[bp-10]            ; je konec dat v bufferu ?
         jae       EDBImpCR                 ; je ji‘ konec dat v bufferu
EDBImpC1:mov       al,es:[si]               ; znak z bufferu
         inc       si                       ; zv˜¨en¡ ukazatele dat
         cmp       al,CR                    ; je znak CR ?
         je        EDBImpC                  ; znak CR se ignoruje
         cmp       al,LF                    ; je konec © dku ?
         je        EDBImpC2                 ; konec © dku - konec z znamu
         ret

EDBImpC2:add       sp,2                     ; zru¨en¡ n vratov‚ adresy
         jmp       EDBImp8                  ; ulo‘en¡ z znamu

; ------ na‡ten¡ obsahu bufferu

EDBImpCR:push      di
         push      ax
         push      cx

         mov       ax,ss:[bp-4]             ; adresa bufferu
         mov       di,es:[DBFAdrD]          ; adresa datov‚ho bufferu
         mov       cx,ss:[bp-10]            ; adresa konce bufferu
         sub       cx,di                    ; velikost bufferu
         call      far ptr ReadFile         ; na‡ten¡ obsahu bufferu
         mov       si,di                    ; za‡ tek bufferu

         add       di,cx                    ; nov˜ konec bufferu
         mov       ss:[bp-10],di            ; nov˜ konec bufferu
         or        cx,cx                    ; bylo nˆco na‡teno ?

         pop       cx
         pop       ax
         pop       di
         jnz       EDBImpC1                 ; bylo nˆco na‡teno
         add       sp,2                     ; zru¨en¡ n vratov‚ adresy
         clc                                ; p©¡znak operace OK
         jmp       EDBImp88                 ; nejsou dal¨¡ data - konec

; -----------------------------------------------------------------------------
;        korekce z znamu
; -----------------------------------------------------------------------------

EDBImpK: mov       bx,es:[DBFAdrP]          ; adresa popisova‡–
         mov       di,es:[DBFAdrE]          ; adresa bufferu
         inc       di                       ; za‡ tek prvn¡ polo‘ky

; ------ test, zda je ‡¡seln  polo‘ka

EDBImpK2:test      byte ptr es:[bx+DBFParX],bit1+bit2 ; je ‡¡seln  polo‘ka ?
         jz        EDBImpK8                 ; nen¡ ‡¡seln  polo‘ka

; ------ p©¡prava parametr– polo‘ky

         mov       si,es:[bx+DBFNamX]       ; popisova‡
         xor       cx,cx
         mov       cl,es:[si+16]            ; CX = d‚lka polo‘ky
         mov       dx,cx                    ; DX = d‚lka polo‘ky
         cmp       byte ptr es:[si+17],0    ; jsou desetiny ?
         je        EDBImpK3                 ; nejsou desetiny
         sub       dl,es:[si+17]            ; offset desetinn˜ch m¡st
         dec       dx                       ; offset desetinn‚ te‡ky
EDBImpK3:add       dx,di                    ; adresa desetinn‚ te‡ky

; ------ nalezen¡ za‡ tku textu polo‘ky

EDBImK34:push      cx
         mov       si,di                    ; adresa za‡ tku polo‘ky
EDBImpK4:cmp       byte ptr es:[si]," "     ; je za‡ tek textu ?
         jne       EDBImpK5                 ; nalezen za‡ tek textu
         inc       si                       ; dal¨¡ znak
         loop      EDBImpK4                 ; dal¨¡ znak
EDBImpK5:jcxz      EDBImpK7                 ; nen¡ text

; ------ nalezen¡ oddˆlovac¡ te‡ky

         push      ax
         mov       al,ds:[OddelDes]         ; oddˆlova‡ desetin
EDBImpK6:cmp       byte ptr es:[si]," "     ; konec polo‘ky ?
         je        EDBImK62                 ; nalezen konec polo‘ky
         cmp       byte ptr es:[si],al
         je        EDBImK62
         cmp       byte ptr es:[si],"."
         je        EDBImK62
         inc       si
         loop      EDBImpK6
EDBImK62:pop       ax

         pop       cx

; ------ kontrola pozice te‡ky

         cmp       si,dx                    ; je te‡ka OK ?
         je        EDBImpK8                 ; te‡ka je OK
         ja        EDBImK66                 ; te‡ka m  b˜t d le

; ------ vlo‘en¡ mezery

         call      EditBII2                 ; vlo‘en¡ mezery
         mov       byte ptr es:[di]," "
         jmp       short EDBImK34

; ------ vypu¨tˆn¡ znaku

EDBImK66:call      EditBDLC                 ; zru¨en¡ znaku
         jmp       short EDBImK34           ; dal¨¡ korekce

EDBImpK7:pop       cx

; ------ dal¨¡ polo‘ka

EDBImpK8:add       di,es:[bx+DBFDelX]       ; adresa dal¨¡ polo‘ky
         add       bx,DBFX                  ; dal¨¡ popisova‡
         cmp       bx,es:[DBFAdrE]          ; je konec popisova‡– ?
         jb        EDBImpK2                 ; nen¡ je¨tˆ konec popisova‡–
         ret

; -----------------------------------------------------------------------------
;        Ctrl-U n vrat zmˆn
; -----------------------------------------------------------------------------

EditBUnd:call      EdiBEBeg                 ; zah jen¡ editace
         ret

; -----------------------------------------------------------------------------
;        ESC - konec editace
; -----------------------------------------------------------------------------

EditBBE1:call      UlozDBF                  ; ulo‘en¡ editovan‚ho bufferu
         and       byte ptr ds:[DBFEParm],not bit0 ; ukon‡en¡ editace
         ret

; -----------------------------------------------------------------------------
;        F4 - zah jen¡ editace © dku
; -----------------------------------------------------------------------------

EditBBEd:test      byte ptr ds:[DBFEParm],bit0 ; je editace ?
         jnz       EditBBE1                 ; je ji‘ editace - ukon‡en¡ editace

         cmp       word ptr es:[DBFNumP],0  ; jsou nˆjak‚ popisova‡e ?
         je        EditBBE0                 ; nejsou popisova‡e

         or        byte ptr ds:[DBFEParm],bit0 ; p©¡znak editace
         call      EdiBEBeg                 ; zah jen¡ editace
EditBBE0:ret

; -----------------------------------------------------------------------------
;        zah jen¡ editace © dku okna SI (CY=nen¡ platn  polo‘ka)
; -----------------------------------------------------------------------------

EdiBEBeg PROC      NEAR

         test      byte ptr ds:[DBFEParm],bit0 ; prob¡h  editace ?
         jz        EdiBEBg9                 ; nen¡ editace

; ------ £schova registr–

         push      bx
         push      cx
         push      si
         push      di
         push      es

; ------ inicializa‡n¡ vymaz n¡ bufferu

         push      ax
         and       byte ptr ds:[DBFEParm],not bit1+bit3 ; nen¡ modifikace
         call      DBFAdrES                 ; aktualizace adresy okna -> ES
         mov       cx,es:[DBFBytZ]          ; po‡et bajt– na z znam
         mov       di,es:[DBFAdrE]          ; adresa edita‡n¡ho bufferu
         cld
         mov       al," "
         rep       stosb                    ; vymaz n¡ bufferu
         pop       ax

; ------ adresa aktivn¡ polo‘ky -> ES:DI

         call      far ptr GetADBF          ; poskytnut¡ aktivn¡ polo‘ky
         jc        EdiBEBg4                 ; chyba

; ------ £schova polo‘ky do bufferu (uchovat p©¡znak CY !)

         mov       si,di                    ; SI <- aktivn¡ polo‘ka
         mov       cx,es:[DBFBytZ]          ; po‡et bajt– na z znam
         mov       di,es:[DBFAdrE]          ; adresa edita‡n¡ho bufferu
         cld
         rep       movs byte ptr es:[di],es:[si] ; kopie aktivn¡ polo‘ky

; ------ inicializace ‡¡seln˜ch polo‘ek

EdiBEBg4:pushf
         mov       di,es:[DBFAdrE]          ; adresa edita‡n¡ho bufferu
         inc       di                       ; za‡ tek edita‡n¡ho bufferu
         mov       cx,es:[DBFNumP]          ; po‡et popisova‡–
         jcxz      EdBEBg72                 ; nen¡ ‘ dn  polo‘ka
         mov       bx,es:[DBFAdrP]          ; adresa popisova‡–
EdiBEBg5:test      byte ptr es:[bx+DBFParX],bit1+bit2 ; je to ‡¡seln  polo‘ka ?
         jz        EdiBEBg7                 ; nen¡ to ‡¡seln  polo‘ka
         mov       si,es:[bx+DBFNamX]       ; popisova‡ polo‘ky

         push      bx
         mov       bx,es:[si+16]            ; d‚lka polo‘ky, po‡et desetin
         or        bh,bh                    ; jsou desetiny ?
         jz        EdiBEBg6                 ; nejsou desetiny
         sub       bl,bh                    ; offset desetinn‚ te‡ky
         mov       bh,0
         add       bx,di                    ; adresa desetinn‚ te‡ky
         mov       byte ptr es:[bx-1],"."   ; ulo‘en¡ desetinn‚ te‡ky
EdiBEBg6:pop       bx

EdiBEBg7:add       di,es:[bx+DBfDelX]       ; dal¨¡ polo‘ka
         add       bx,DBFX                  ; dal¨¡ popisova‡
         loop      EdiBEBg5                 ; dal¨¡ polo‘ka
EdBEBg72:popf

; ------ n vrat registr–

EdiBEBg8:pop       es
         pop       di
         pop       si
         pop       cx
         pop       bx
EdiBEBg9:ret

EdiBEBeg ENDP

; -----------------------------------------------------------------------------
;        F5: kopie z znamu do aktivn¡ho editovan‚ho z znamu
; -----------------------------------------------------------------------------

; ------ test, zda je operace povolena

EditBKop:test      byte ptr ds:[DBFEParm],bit0 ; je editace z znamu ?
         jz        EditBKp9                 ; nen¡ editace z znamu
         test      byte ptr es:[DBFParm],bit1 ; je z kaz z pisu ?
         jnz       EditBKp9                 ; je z kaz z pisu

; ------ zad n¡ ‡¡sla z znamu

         mov       di,offset DBKMSLin       ; menu zad n¡ ‡¡sla z znamu
         call      EditBLM                  ; obsluha © dkov‚ho menu
         jc        EditBKp9                 ; p©eru¨en¡ operace

; ------ £schova registr–

         push      ax
         push      dx

; ------ na‡ten¡ zadan‚ho ‡¡sla z znamu

         call      DBGetZNm                 ; vstup zadan‚ho ‡¡sla -> DX:AX
         jc        EditBKp8                 ; nen¡ nic zad no

; ------ oprava ‡¡sla z znamu

         sub       ax,1                     ; oprava ‡¡sla z znamu
         sbb       dx,0
         jc        EditBKp8

; ------ na‡ten¡ zadan‚ho z znamu

         call      far ptr GetDBF           ; poskytnut¡ zadan‚ho z znamu
         jc        EditBKp8                 ; chyba

; ------ kopie z znamu do edita‡n¡ho bufferu

         push      si
         mov       cx,es:[DBFBytZ]          ; po‡et bajt– na z znam
         mov       si,di                    ; za‡ tek z znamu
         mov       di,es:[DBFAdrE]          ; adresa edita‡n¡ho bufferu
         cld
         rep       movs byte ptr es:[di],es:[si] ; kopie dat ze z znamu
         pop       si
         or        byte ptr ds:[DBFEParm],bit1 ; p©¡znak modifikace bufferu

; ------ nulov n¡ MEMO pol¡

         mov       word ptr ds:[DBZobOld+2],-1 ; pot©eba na‡ten¡ MEMO

         mov       bx,es:[DBFAdrP]          ; adresa index– popisova‡–
         mov       di,es:[DBFAdrE]          ; edita‡n¡ bufferu
         mov       cx,es:[DBFNumP]          ; po‡et popisova‡–
         inc       di                       ; za‡ tek polo‘ek
         jcxz      EditBKp8                 ; nen¡ ‘ dn  polo‘ka
EditBKp4:test      byte ptr es:[bx+DBFParX],bit5 ; je to MEMO pole ?
         jz        EditBKp6                 ; nen¡ to MEMO pole
         push      di
         push      cx
         mov       cx,10
EditBKp5:mov       byte ptr es:[di]," "
         inc       di
         loop      EditBKp5
         pop       cx
         pop       di
EditBKp6:add       di,es:[bx+DBFDelX]       ; adresa dal¨¡ polo‘ky
         add       bx,DBFX                  ; dal¨¡ index
         loop      EditBKp4                 ; dal¨¡ polo‘ka

; ------ n vrat registr–

EditBKp8:pop       dx
         pop       ax
EditBKp9:ret

; -----------------------------------------------------------------------------
;        F6 skok na z znam
; -----------------------------------------------------------------------------

; ------ £schova registr–

EditBSko:push      ax
         push      dx

; ------ volba nov‚ho z znamu

         mov       di,offset DBSMSLin
         call      EditBLM                  ; obsluha © dkov‚ho menu
         jc        EditBSk8                 ; p©eru¨en¡ operace

; ------ dek¢dov n¡ ‡¡sla z znamu

         call      DBGetZNm                 ; vstup zadan‚ho ‡¡sla -> DX:AX
         jc        EditBSk8                 ; nezad no platn‚ ‡¡slo

; ------ ulo‘en¡ aktu ln¡ho editovan‚ho z znamu

         call      UlozDBF                  ; ulo‘en¡ editovan‚ho bufferu

; ------ oprava ‡¡sla z znamu

         sub       ax,1
         sbb       dx,0
         jc        EditBSk8                 ; neplatn‚ ‡¡slo z znamu

; ------ zru¨en¡ registr– DX a AX, je-li platn‚ ‡¡slo z znamu

         add       sp,4
         jmp       short EditBSk9

; ------ n vrat registr– p©i p©eru¨en¡ operace

EditBSk8:pop       dx
         pop       ax

EditBSk9:ret

; -----------------------------------------------------------------------------
;        vstup ‡¡sla z p©¡kazov‚ho © dku -> DX:AX (CY=chyba)
; -----------------------------------------------------------------------------

DBGetZNm PROC      NEAR

; ------ £schova registr–

         push      bx
         push      cx
         push      si
         push      di

; ------ p©¡prava ukazatel– bufferu

         mov       si,offset LinBuff        ; buffer p©¡kazov‚ho © dku
         mov       cl,ds:[LinNum]           ; po‡et znak– v p©¡kazov‚m © dku
         mov       ch,0

; ------ p©¡prava st©ada‡e

         xor       bx,bx                    ; st©ada‡ LOW
         xor       di,di                    ; st©ada‡ HIGH

; ------ na‡ten¡ prvn¡ ‡¡slice

         call      DBGetZN7                 ; prvn¡ ‡¡slice
         jc        DBGetZN6                 ; nen¡ ‘ dn  platn  ‡¡slice

; ------ vyn soben¡ st©ada‡e

DBGetZN2:push      ax
         mov       ax,10
         mul       bx                       ; n soben¡ st©ada‡e LOW
         xchg      ax,bx                    ; BX <- nov˜ st©ada‡ LOW
         xchg      ax,di                    ; AX <- star˜ st©ada‡ HIGH
         mov       di,dx                    ; DI <- nov˜ st©ada‡ HIGH
         mov       dx,10
         mul       dx                       ; n soben¡ st©ada‡e HIGH
         add       di,ax                    ; nov˜ st©ada‡ HIGH

         jnc       DBGetZN3
         mov       di,-1                    ; omezen¡ st©ada‡e
         mov       bx,di

DBGetZN3:pop       ax
         mov       ah,0
         add       bx,ax                    ; p©i‡ten¡ nov‚ ‡¡slice
         adc       di,0

         jnc       DBGetZN4
         mov       di,-1
         mov       bx,di

DBGetZN4:call      DBGetZN7                 ; dal¨¡ ‡¡slice
         jnc       DBGetZN2
         clc

; ------ na‡ten‚ ‡¡slo

DBGetZN6:xchg      ax,bx                    ; AX <- st©ada‡ LOW
         mov       dx,di                    ; DX <- st©ada‡ HIGH

; ------ n vrat registr–

         pop       di
         pop       si
         pop       cx
         pop       bx
         ret

DBGetZNm ENDP

; ------ na‡ten¡ jedn‚ ‡¡slice

DBGetZN7:jcxz      DBGetZN9                 ; nen¡ dal¨¡ znak
         cld
         lodsb                              ; na‡ten¡ znaku z bufferu
         dec       cx
         cmp       al," "
         je        DBGetZN7                 ; mezera se ignoruje
         cmp       al,"."
         je        DBGetZN7                 ; te‡ka se ignoruje
         cmp       al,cs:[DBFOdDes]         ; je oddˆlova‡ desetin ?
         je        DBGetZN7                 ; te‡ka se ignoruje
         cmp       al,"0"
         jb        DBGetZN8                 ; nen¡ platn  ‡¡slice
         cmp       al,"9"
         ja        DBGetZN8                 ; nen¡ platn  ‡¡slice
         sub       al,"0"                   ; zadan‚ ‡¡slo
         ret

DBGetZN8:dec       si                       ; n vrat znaku
         inc       cx                       ; n vrat ‡¡ta‡e znak–
DBGetZN9:stc
         ret

; -----------------------------------------------------------------------------
;        F7 hled n¡ dat
; -----------------------------------------------------------------------------

; ------ pokra‡ov n¡ v hled n¡ Ctrl-L

EditBHlL:cmp       word ptr es:[DBFNumP],0  ; jsou nˆjak‚ polo‘ky ?
         je        EditBH01                 ; nejsou polo‘ky
         test      byte ptr ds:[DBFEParm],bit0 ; je editace ?
         jz        EditBH02                 ; nen¡ editace
EditBH01:ret

; ------ p©¡prava pro pokra‡ov n¡ v hled n¡

EditBH02:push      ax                       ; kurzor LOW
         push      dx                       ; kurzor HIGH
         push      word ptr es:[DBFTopZ]    ; po‡ te‡n¡ z znam LOW
         push      word ptr es:[DBFTopZ+2]  ; po‡ te‡n¡ z znam HIGH

         add       ax,1                     ; pokra‡uje se dal¨¡m z znamem
         adc       dx,0

         and       byte ptr ds:[DBFEPar2],not bit1+bit2 ; nebude ozna‡ov n¡
         mov       cx,ds:[TextFndN]         ; d‚lka textu
         or        cx,cx                    ; je nˆjak˜ text ?
         jnz       EditBH18                 ; je nˆjak˜ text v bufferu

EditBH08:jmp       EditBHl9                 ; operace p©eru¨ena

; ------ test, zda je operace povolena

EditBHle:cmp       word ptr es:[DBFNumP],0  ; jsou nˆjak‚ polo‘ky ?
         je        EditBH01                 ; nejsou polo‘ky
         test      byte ptr ds:[DBFEParm],bit0 ; je editace ?
         jz        EditBH10                 ; nen¡ editace
         ret

; ------ £schova registr–

EditBH10:push      ax                       ; kurzor LOW
         push      dx                       ; kurzor HIGH
         push      word ptr es:[DBFTopZ]    ; po‡ te‡n¡ z znam LOW
         push      word ptr es:[DBFTopZ+2]  ; po‡ te‡n¡ z znam HIGH

; ------ zad n¡ textu k hled n¡

         call      EditBHPr                 ; p©¡prava p©ep¡na‡–
         mov       di,offset DBHMSLin
         call      EditBLM                  ; obsluha © dkov‚ho menu
         jc        EditBH08

; ------ d‚lka zadan‚ho textu

         mov       ch,0
         mov       cl,ds:[LinNum]           ; d‚lka hledan‚ho textu
         jcxz      EditBH08                 ; nen¡ zad n ‘ dn˜ text
         mov       ds:[TextFndN],cx         ; d‚lka textu

; ------ £schova textu do bufferu

         push      ax
         push      si
         push      cx
         push      es

         push      ds
         pop       es                       ; ES <- datov˜ segment
         mov       di,offset TextFnd        ; buffer hledan‚ho textu
         mov       si,offset LinBuff        ; buffer zadan‚ho textu
         cld
EditBH12:lodsb
         call      far ptr UpCase           ; konverze na velk‚ p¡smeno
         stosb
         loop      EditBH12

         pop       es
         pop       cx
         pop       si
         pop       ax

; ------ omezen¡ d‚lky hledan‚ho textu podle d‚lky z znamu -> CX, BX

EditBH18:mov       bx,es:[DBFBytZ]          ; BX <- d‚lka z znamu
         sub       bx,cx                    ; po‡et ©etˆzc– v z znamu
         jnc       EditBHl1                 ; je to OK
         xor       bx,bx                    ; omezen¡ na 1 ©etˆzec
         mov       cl,es:[DBFBytZ]          ; omezen¡ d‚lky textu
EditBHl1:inc       bx                       ; korekce po‡tu ©etˆzc–

; ------ zobrazen¡ hl ¨en¡ o vyhled v n¡

         push      ax
         push      cx
         push      si

         mov       si,offset DBHMLTxt
         call      far ptr InfDisp0         ; zobrazen¡ hl ¨en¡

         mov       word ptr ds:[InfoKCit],ax ; ‡¡ta‡ kurzoru
         mov       word ptr ds:[InfoKCit+2],dx

         mov       ax,es:[DBFMaxZ+2]        ; po‡et z znam– HIGH
         mov       word ptr ds:[InfoKMax+2],ax
         mov       ax,es:[DBFMaxZ]
         mov       word ptr ds:[InfoKMax],ax

         xor       cx,cx                    ; CX <- 0
         call      far ptr InfoKurz         ; nulov n¡ kurzoru

         pop       si
         pop       cx
         pop       ax

; ------ p©¡prava k vyhled v n¡

         mov       word ptr ds:[DBCitFnd],0 ; nulov n¡ ‡¡ta‡e nalezen˜ch ©etˆzc–
         mov       word ptr ds:[DBCitFnd+2],0
         call      DBFGetDT                 ; £schova data a ‡asu

; ------ poskytnut¡ z znamu DX:AX

EditBHl2:mov       bp,15                    ; ‡¡ta‡ krok– pro zobrazen¡
EditBHl3:mov       es:[DBFAktZ],ax          ; aktivn¡ z znam
         mov       es:[DBFAktZ+2],dx
         call      far ptr GetDBF           ; dal¨¡ polo‘ka
         jc        EditBHl7                 ; nen¡ dal¨¡ polo‘ka - konec

; ------ prohled n¡ bufferu

         call      EditBHBf                 ; prohled n¡ bufferu
         jc        EditBHl6                 ; ©etˆzec v z znamu nenalezen

; ------ text nalezen - zru¨en¡ registr– v z sobn¡ku

         test      byte ptr ds:[DBFEPar2],bit1+bit2 ; bude ozna‡en¡ z znamu ?
         jnz       EditBHl4                 ; je ozna‡en¡ z znamu
         add       sp,4*2                   ; zru¨en¡ registr– v z sobn¡ku
         ret

; ------ zv˜¨en¡ ‡¡ta‡e nalezen˜ch ©etˆzc–

EditBHl4:add       word ptr ds:[DBCitFnd],1 ; zv˜¨en¡ ‡¡ta‡e nalezen˜ch ©etˆzc–
         adc       word ptr ds:[DBCitFnd+2],0

; ------ nastaven¡ ozna‡en¡ polo‘ky

         test      byte ptr ds:[DBFEPar2],bit1 ; m  b˜t ozna‡en¡ ?
         jz        EditBHl5                 ; nem  b˜t ozna‡en¡
         cmp       byte ptr es:[di]," "     ; je polo‘ka ozna‡ena ?
         jne       EditBHl6                 ; polo‘ka je ji‘ ozna‡ena
         mov       byte ptr es:[di],"*"     ; ozna‡en¡ polo‘ky
         jmp       short EditBH52           ; dal¨¡ polo‘ka

; ------ nulov n¡ ozna‡en¡ polo‘ky

EditBHl5:cmp       byte ptr es:[di]," "     ; je polo‘ka vynulov na ?
         je        EditBHl6                 ; je ji‘ vynulov na
         mov       byte ptr es:[di]," "     ; vynulov n¡ ozna‡en¡ polo‘ky
EditBH52:call      far ptr SetDBF           ; ulo‘en¡ polo‘ky na disk

; ------ obsluha zobrazen¡ okna a kurzoru

EditBHl6:add       ax,1                     ; zv˜¨en¡ ‡¡sla dal¨¡ho z znamu
         adc       dx,0
         dec       bp                       ; ‡¡ta‡ pro zobrazen¡ okna
         jnz       EditBHl3                 ; pokra‡ov n¡ dal¨¡m z znamem
         call      far ptr DispBMnu         ; zobrazen¡ okna

         push      cx
         mov       cx,15                    ; po‡et z znam–
         call      far ptr InfoKurz         ; zobrazen¡ informa‡n¡ho kurzoru
         pop       cx

         call      far ptr TestBEsc         ; test p©eru¨en¡ operace
         jnc       EditBHl2                 ; nen¡ p©eru¨en¡ operace
         call      far ptr FlushChr         ; vypr zdnˆn¡ bufferu kl vesnice

; ------ konec hled n¡

EditBHl7:call      DBFSetDT                 ; n vrat data a ‡asu souboru

; ------ test, zda bylo nˆco nalezeno

         mov       di,offset DBHMFnd        ; text nenalezen
         mov       ax,word ptr ds:[DBCitFnd] ; ‡¡ta‡ nalezen˜ch z znam–
         or        ax,word ptr ds:[DBCitFnd+2] ; bylo nˆco nalezeno ?
         jz        EditBHl8                 ; nebylo nic nalezeno

; ------ dek¢dov n¡ ‡¡sla po‡tu z znam–

         push      es
         push      ds
         pop       es
         mov       di,offset DBH1MFBN+1
         mov       ax,word ptr ds:[DBCitFnd] ; po‡et z znam–
         mov       dx,word ptr ds:[DBCitFnd+2]
         xor       bx,bx                    ; nen¡ oddˆlova‡ ani barva
         call      far ptr DekNum           ; dek¢dov n¡ ‡¡sla
         xchg      ax,di
         sub       ax,offset DBH1MFBN+1
         mov       ds:[DBH1MFBN],al         ; d‚lka ‡¡sla
         pop       es

; ------ zobrazen¡ hl ¨en¡

         call      far ptr NulBreak         ; nulov n¡ p©¡znaku p©eru¨en¡
         mov       di,offset DBH1MFnd       ; text nalezen

EditBHl8:call      far ptr TestBEsc         ; bylo p©eru¨en¡ operace ?
         jc        EditBHl9                 ; nic se nehl s¡
         call      EditBLM                  ; hl ¨en¡

; ------ n vrat ukazatel–

EditBHl9:pop       word ptr es:[DBFTopZ+2]  ; po‡ te‡n¡ z znam HIGH
         pop       word ptr es:[DBFTopZ]    ; po‡ te‡n¡ z znam LOW
         pop       dx
         pop       ax
         ret

; ------ p©ep¡na‡ zad n¡ textu k hled n¡ "Ozna‡it"

EditBHS1 PROC      FAR

         xor       byte ptr ds:[DBFEPar2],bit1 ; zmˆna p©ep¡na‡e "Ozna‡it"
         and       byte ptr ds:[DBFEPar2],not bit2
EditBHS0:call      EditBHPr                 ; p©¡prava p©ep¡na‡–
         call      far ptr DispMnu          ; nov‚ zobrazen¡ menu
         ret

EditBHS1 ENDP

EditBHS2:xor       byte ptr ds:[DBFEPar2],bit2 ; zmˆna p©ep¡na‡e "Nulovat"
         and       byte ptr ds:[DBFEPar2],not bit1
         jmp       short EditBHS0

; ------ p©¡prava p©ep¡na‡–

EditBHPr PROC      NEAR

         push      dx

         mov       dl,ds:[DBFEPar2]
         shr       dl,1
         test      dl,bit0
         mov       dh,ds:[ZnakSwch]         ; p©ep¡na‡ nastaven
         jnz       EditBHP2                 ; je nastaven
         mov       dh,ds:[ZnakSwch+1]       ; nenastaven
EditBHP2:mov       ds:[DBHMLPl1],dh         ; znak p©ep¡na‡e

         test      dl,bit1
         mov       dh,ds:[ZnakSwch]         ; p©ep¡na‡ nastaven
         jnz       EditBHP3                 ; je nastaven
         mov       dh,ds:[ZnakSwch+1]       ; nenastaven
EditBHP3:mov       ds:[DBHMLPl2],dh         ; znak p©ep¡na‡e

         call      far ptr SetSwch

         pop       dx
         ret

EditBHPr ENDP

; ------ prohled n¡ bufferu ES:DI (d‚lka ©etˆzce CX, po‡et pokus– BX) -> CY=nen¡

EditBHBf:push      ax
         push      bx
         push      si
         push      di
         cld

EditBHB2:push      di
         push      cx

         mov       si,offset TextFnd-1      ; hledan˜ text - 1
EditBHB6:mov       al,es:[di]               ; znak z bufferu
         inc       di
         inc       si
         call      far ptr UpCase           ; konverze na velk‚ p¡smeno
         cmp       al,ds:[si]               ; porovn n¡ s hledan˜m ©etˆzcem
         loope     EditBHB6                 ; dal¨¡ znak

         pop       cx
         pop       di
         je        EditBHB8                 ; ©etˆzec nalezen OK

         inc       di                       ; zv˜¨en¡ adresy v bufferu
         dec       bx                       ; ‡¡ta‡ pokus–
         jnz       EditBHB2                 ; dal¨¡ pokus
         stc                                ; p©¡znak nenalezen¡ textu

EditBHB8:pop       di
         pop       si
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------
;        £schova data a ‡asu souboru p©ed operac¡ (ES=okno)
; -----------------------------------------------------------------------------

DBFGetDT PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx

; ------ na‡ten¡ data a ‡asu souboru

         and       byte ptr ds:[DBFEPar2],not bit5 ; nebyl z pis z znamu
         mov       ax,es:[DBFIdent]         ; identifik tor souboru
         call      far ptr GetDFile         ; poskytnut¡ data a ‡asu souboru
         mov       ds:[DBFETime],cx         ; ‡as souboru
         mov       ds:[DBFEDate],dx         ; datum souboru

; ------ n vrat registr–

         pop       dx
         pop       cx
         pop       ax
         ret

DBFGetDT ENDP

; -----------------------------------------------------------------------------
;        n vrat data a ‡asu souboru, pokud byl modifikov n (ES=okno)
; -----------------------------------------------------------------------------

DBFSetDT PROC      NEAR

; ------ £schova registr–

         pushf
         push      ax
         push      cx
         push      dx

; ------ na‡ten¡ data a ‡asu souboru

         test      byte ptr ds:[DBFEPar2],bit5 ; nebyl z pis z znamu ?
         jz        DBFStDT9                 ; nebyl z pis z znamu

         call      far ptr ZapKrit          ; zapnut¡ kritick‚ho m¡sta
         mov       cx,ds:[DBFETime]         ; ‡as souboru
         mov       dx,ds:[DBFEDate]         ; datum souboru
         mov       ax,es:[DBFIdent]         ; identifik tor souboru
         call      far ptr SetDFile         ; nastaven¡ data a ‡asu souboru
         call      far ptr FlshFile         ; ulo‘en¡ zmˆn na disk
         call      far ptr VypKrit          ; vypnut¡ kritick‚ho m¡sta

; ------ n vrat registr–

DBFStDT9:pop       dx
         pop       cx
         pop       ax
         popf
         ret

DBFSetDT ENDP

; -----------------------------------------------------------------------------
;        F9 vlo‘en¡ nov˜ch z znam–
; -----------------------------------------------------------------------------
;þ
EditBVlo:test      byte ptr ds:[DBFEParm],bit0 ; je editace ?
         jnz       EditBVl9                 ; je editace
         test      byte ptr es:[DBFParm],bit1 ; je z kaz z pisu ?
         jnz       EditBVl9                 ; je z kaz z pisu
         cmp       word ptr es:[DBFNumP],0  ; jsou nˆjak‚ polo‘ky ?
         je        EditBVl9                 ; nejsou polo‘ky

; ------ zad n¡ po‡tu z znam– k vlo‘en¡

         mov       di,offset DBVMSLin
         call      EditBLM                  ; obsluha © dkov‚ho menu
         jc        EditBVl9                 ; p©eru¨en¡ operace

; ------ p©¡prava zadan‚ho po‡tu z znam– k vlo‘en¡ -> DI:BX

         push      ax
         push      dx

         call      DBGetZNm                 ; vstup zadan‚ho ‡¡sla -> DX:AX
         jc        EditBVl8                 ; chyba zad n¡

         mov       bx,ax
         or        bx,dx
         jz        EditBVl8                 ; nebylo nic zad no

         mov       bx,ax                    ; po‡et z znam– LOW
         mov       di,dx                    ; po‡et z znam– HIGH

         pop       dx
         pop       ax

; ------ vlo‘en¡ z znam–

         mov       word ptr ds:[DBZobOld+2],-1 ; pot©eba na‡ten¡ MEMO

         call      ClosBMem                 ; uzav©en¡ souboru MEMO
         call      EdiBZIns                 ; vlo‘en¡ z znam–
         jmp       short EditBVl9

; ------ n vrat registr– p©i p©eru¨en¡

EditBVl8:pop       dx
         pop       ax

EditBVl9:ret

; -----------------------------------------------------------------------------
;                  vlo‘en¡ nov˜ch z znam– do souboru
; -----------------------------------------------------------------------------
; VSTUP: DI:BX=po‡et z znam– k vlo‘en¡
;        DX:AX=‡¡slo z znamu k vlo‘en¡ nov˜ch z znam–
;        SI=definice okna datab ze
;        DS=datov˜ segment
; VSTUP: CY=chyba
; -----------------------------------------------------------------------------
; lok ln¡ promˆnn‚: SS:[BP-2] (2) definice okna datab ze
;                   SS:[BP-4] (2) adresa star‚ho okna
;                   SS:[BP-6] (2) segment nov‚ho okna
;                   SS:[BP-8] (2) adresa nov‚ho okna
;                   SS:[BP-10] (2) identifik tor star‚ho souboru
;                   SS:[BP-12] (2) identifik tor nov‚ho souboru
;                   SS:[BP-16] (4) ‡¡ta‡ z znam– ke zkop¡rov n¡
;                   SS:[BP-20] (4) ‡¡ta‡ z znam– p©ed po‡ tkem vkl d n¡
;                   SS:[BP-24] (4) ‡¡ta‡ z znam– k vlo‘en¡
; -----------------------------------------------------------------------------

EdiBZIns PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      di
         push      bp
         mov       bp,sp

; ------ p©¡prava lok ln¡ch promˆnn˜ch

         sub       sp,24
         mov       ss:[bp-2],si             ; adresa definice okna
         mov       ss:[bp-20],ax            ; z znam p©ed po‡ tkem vkl d n¡
         mov       ss:[bp-20+2],dx
         mov       ss:[bp-24],bx            ; ‡¡ta‡ z znam– k vlo‘en¡
         mov       ss:[bp-24+2],di

; ------ zobrazen¡ hl ¨en¡ o vkl d n¡ z znam–

         push      si
         mov       word ptr ds:[InfoKCit],0 ; ‡¡ta‡ ukazatele kurzoru
         mov       word ptr ds:[InfoKCit+2],0
         mov       si,offset DBFInsHl       ; hl ¨en¡ o vkl d n¡
         call      far ptr InfDisp0         ; zobrazen¡ hl ¨en¡
         pop       si

; ------ vytvo©en¡ v˜stupn¡ho souboru

         call      DBFTOpen                 ; vytvo©en¡ p©echodn‚ho souboru
         jnc       EdiBZIn2                 ; soubor vytvo©en OK
EdiBZIn1:jmp       EdiBZIn9                 ; chyba

EdiBZIn2:mov       ss:[bp-6],ax             ; segment nov‚ho okna

; ------ maximalizace star‚ho okna

         call      far ptr MaxiDBF          ; maximalizace star‚ho okna

; ------ adresa star‚ho okna

         call      DBFAdrES                 ; aktualizace adresy okna -> ES
         mov       ss:[bp-4],es             ; adresa star‚ho okna

; ------ adresa nov‚ho okna

         push      es                       ; adresa star‚ho okna

         call      far ptr GetDat           ; adresa nov‚ho okna -> ES
         mov       ss:[bp-8],es             ; adresa nov‚ho okna

; ------ identifik tor nov‚ho souboru

         mov       ax,es:[DBFIdent]         ; identifik tor nov‚ho souboru
         mov       ss:[bp-12],ax            ; identifik tor nov‚ho souboru

; ------ z pis z hlav¡ do nov‚ho souboru (ukazatel resetov n, chyba se ignoruje)

         mov       si,DBFHlava              ; z hlav¡ souboru
         mov       cx,es:[DBFBytH]          ; d‚lka z hlav¡ souboru
         call      far ptr WritFile         ; z pis z hlav¡ do souboru

         pop       es                       ; ES <- adresa star‚ho okna

; ------ identifik tor star‚ho souboru

         mov       ax,es:[DBFIdent]         ; identifik tor star‚ho souboru
         mov       ss:[bp-10],ax            ; identifik tor star‚ho souboru

; ------ nastaven¡ ukazatele ve star‚m souboru za z hlav¡ souboru

         xor       dx,dx                    ; DX <- 0 offset HIGH
         mov       cx,es:[DBFBytH]          ; d‚lka z hlav¡ (bajt–)
         call      far ptr SetUFile         ; nastaven¡ ukazatele v souboru

; ------ po‡et z znam– ve star‚m souboru, inicializace ukazatele operace

         mov       ax,es:[DBFMaxZ]          ; po‡et z znam– v souboru
         mov       ss:[bp-16],ax            ; ‡¡ta‡ z znam– ke zkop¡rov n¡
         add       ax,ss:[bp-24]            ; + po‡et vkl dan˜ch z znam–
         mov       word ptr ds:[InfoKMax],ax ; maximum pro kurzor
         mov       ax,es:[DBFMaxZ+2]
         mov       ss:[bp-16+2],ax
         adc       ax,ss:[bp-24+2]
         mov       word ptr ds:[InfoKMax+2],ax
         xor       cx,cx                    ; CX <- 0
         call      far ptr InfoKurz         ; zobrazen¡ kurzoru operace

; ------ zkop¡rov n¡ dat p©ed vkl dan˜mi z znamy

         call      EdiBZIC                  ; zkop¡rov n¡ prvn¡ ‡ sti dat
         jc        EdiBZIn8                 ; chyba nebo p©eru¨en¡ operace

; ------ vytvo©en¡ pr zdn‚ho z znamu

         mov       es,ss:[bp-4]             ; adresa star‚ho okna
         mov       di,es:[DBFAdrD]          ; adresa datov‚ho bufferu
         mov       cx,es:[DBFBytZ]          ; po‡et bajt– na z znam
         mov       al," "                   ; mazac¡ znak mezery
         cld
         rep       stosb                    ; vymaz n¡ z znamu

; ------ ‡¡ta‡ z znam– k vlo‘en¡

EdiBZIn4:mov       cx,1                     ; z pis 1 z znamu
         sub       ss:[bp-24],cx            ; ‡¡ta‡ z znam–
         sbb       word ptr ss:[bp-24+2],0
         jc        EdiBZIn6                 ; nen¡ dal¨¡ z znam

; ------ z pis jednoho z znamu

         call      EdiBZIW                  ; z pis 1 z znamu
         jc        EdiBZIn8                 ; chyba nebo p©eru¨en¡ operace

; ------ test, zda je p©eru¨en¡ operace

         call      far ptr TestBEsc         ; je p©eru¨en¡ operace ?
         jc        EdiBZIn8                 ; je p©eru¨en¡ operace
         jmp       short EdiBZIn4           ; dal¨¡ z znam

; ------ zkop¡rov n¡ druh‚ ‡ sti dat (za vkl dan˜mi daty)

EdiBZIn6:dec       word ptr ss:[bp-20+2]    ; neplatn˜ ‡¡ta‡ p©ed za‡ tkem
         call      EdiBZIC                  ; zkop¡rov n¡
         jc        EdiBZIn8                 ; chyba nebo p©eru¨en¡ operace

; ------ konec konverze souboru

         mov       es,ss:[bp-8]             ; adresa nov‚ho okna
         call      UlozDEOF                 ; z pis koncov‚ho znaku EOF

         mov       ax,ss:[bp-6]             ; segment nov‚ho okna
         mov       si,ss:[bp-2]             ; adresa definice okna
         call      DBFTClos                 ; uzav©en¡ v˜stupn¡ho souboru
         jnc       EdiBZIn9                 ; operace OK

; ------ chyba z pisu do v˜stupn¡ho souboru

EdiBZIn8:mov       di,offset DBHMWrt        ; hl ¨en¡ - chyba z pisu
         call      far ptr Lin0MenP         ; chybov‚ hl ¨en¡

; ------ zru¨en¡ v˜stupn¡ho souboru, zru¨en¡ bufferu

         mov       ax,ss:[bp-6]             ; segment nov‚ho okna
         mov       si,ss:[bp-2]             ; adresa definice okna
         call      DBFTDel                  ; zru¨en¡ segmentu AX p©i chybˆ
         stc

; ------ n vrat registr–

EdiBZIn9:mov       sp,bp
         pop       bp
         pop       di
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         call      far ptr InfoClos         ; uzav©en¡ informa‡n¡ho © dku
         call      DBFAdrES                 ; aktualizace adresy okna -> ES
         ret

EdiBZIns ENDP

; ------ zkop¡rov n¡ ‡ sti dat (CY=chyba)

EdiBZIC: call      far ptr TestBEsc         ; je p©eru¨en¡ operace ?
         jc        EdiBZIC9                 ; je p©eru¨en¡ operace

         mov       es,ss:[bp-4]             ; adresa star‚ho okna
         mov       cx,es:[DBFMaxD]          ; max. z znam– v bufferu
         cmp       word ptr ss:[bp-16+2],0  ; zb˜v  dost z znam– ?
         jne       EdiBZIC2                 ; zb˜v  dost z znam–
         cmp       cx,ss:[bp-16]            ; zb˜v  dost z znam– ?
         jbe       EdiBZIC2                 ; zb˜v  dost z znam–
         mov       cx,ss:[bp-16]            ; omezen¡ po‡tu z znam–

EdiBZIC2:cmp       word ptr ss:[bp-20+2],0  ; je dost z znam– k po‡ tku ?
         jne       EdiBZIC4                 ; zb˜v  dost z znam–
         cmp       cx,ss:[bp-20]            ; zb˜v  dost z znam– ?
         jbe       EdiBZIC4                 ; zb˜v  dost z znam–
         mov       cx,ss:[bp-20]            ; omezen¡ po‡tu z znam–

EdiBZIC4:clc                                ; p©¡znak operace OK
         jcxz      EdiBZIC9                 ; nejsou dal¨¡ data

         sub       ss:[bp-16],cx            ; ‡¡ta‡ z znam– ke zkop¡rov n¡
         sbb       word ptr ss:[bp-16+2],0
         sub       ss:[bp-20],cx            ; ‡¡ta‡ z znam– p©ed po‡ tkem
         sbb       word ptr ss:[bp-20+2],0

         mov       ax,es:[DBFBytZ]          ; po‡et bajt– na z znam
         mul       cx                       ; p©epo‡et z znam– na bajty
         push      cx                       ; £schova po‡tu z znam–
         xchg      ax,cx                    ; CX <- po‡et bajt– k na‡ten¡
         mov       ax,ss:[bp-10]            ; identifik tor star‚ho souboru
         mov       di,es:[DBFAdrD]          ; adresa datov‚ho bufferu
         call      far ptr Read0Fil         ; na‡ten¡ dat ze souboru
         pop       cx                       ; CX <- po‡et z znam–
         jc        EdiBZIC9                 ; chyba

         call      EdiBZIW                  ; z pis CX z znam– na v˜stup
         jnc       EdiBZIC                  ; dal¨¡ blok dat

EdiBZIC9:ret

; ------ z pis CX z znam–

EdiBZIW: mov       es,ss:[bp-4]             ; adresa star‚ho okna
         mov       ax,es:[DBFBytZ]          ; po‡et bajt– na z znam
         mul       cx                       ; po‡et bajt– k z pisu

         push      cx
         push      si

         xchg      ax,cx                    ; CX <- po‡et bajt– k z pisu
         mov       ax,ss:[bp-12]            ; identifik tor nov‚ho souboru
         mov       si,es:[DBFAdrD]          ; adresa datov‚ho bufferu
         call      far ptr WritFile         ; z pis dat

         pop       si
         pop       cx
         jc        EdiBZIW8                 ; byla chyba operace

         call      far ptr InfoKurz         ; zobrazen¡ ukazatele

         mov       es,ss:[bp-8]             ; adresa nov‚ho okna
         add       es:[DBFMaxZ],cx          ; ‡¡ta‡ ulo‘en˜ch z znam–
         adc       word ptr es:[DBFMaxZ+2],0
         clc                                ; p©¡znak operace OK

EdiBZIW8:ret

; -----------------------------------------------------------------------------
;        F8 zru¨en¡ z znam–
; -----------------------------------------------------------------------------
;þ
; ------ test, zda je operace povolena

EditBRus:test      byte ptr ds:[DBFEParm],bit0 ; je editace ?
         jnz       EditBRs9                 ; je editace
         test      byte ptr es:[DBFParm],bit1 ; je z kaz z pisu ?
         jnz       EditBRs9                 ; je z kaz z pisu
         cmp       word ptr es:[DBFNumP],0  ; jsou nˆjak‚ polo‘ky ?
         je        EditBRs9                 ; nejsou polo‘ky

; ------ potvrzen¡ operace ru¨en¡ polo‘ek

         call      far ptr FlushChr         ; vypr zdnˆn¡ bufferu kl vesnice
         mov       di,offset DBPMLin
         call      EditBLM                  ; obsluha © dkov‚ho menu
         jc        EditBRs9

; ------ druh‚ potvrzen¡ operace ru¨en¡ polo‘ek

         call      far ptr FlushChr         ; vypr zdnˆn¡ bufferu kl vesnice
         mov       di,offset DBP2Lin
         call      EditBLM                  ; obsluha © dkov‚ho menu
         jc        EditBRs9

; ------ ru¨en¡ ozna‡en˜ch z znam–

         mov       word ptr ds:[DBZobOld+2],-1 ; pot©eba na‡ten¡ MEMO

         call      ClosBMem                 ; uzav©en¡ souboru MEMO
         call      EdiBODel                 ; zru¨en¡ ozna‡en˜ch z znam–
EditBRs9:ret

; -----------------------------------------------------------------------------
;                                EdiBODel
;                        zru¨en¡ ozna‡en˜ch z znam–
; -----------------------------------------------------------------------------
; VSTUP: SI=definice okna
;        DS=datov˜ segment
; VSTUP: CY=chyba
;         ES=obnoven  adresa okna
; -----------------------------------------------------------------------------
; lok ln¡ promˆnn‚: SS:[BP-2] (2) adresa definice okna (SI)
;                   SS:[BP-4] (2) adresa star‚ho okna
;                   SS:[BP-6] (2) segment nov‚ho okna
;                   SS:[BP-8] (2) adresa nov‚ho okna
;                   SS:[BP-10] (2) identifik tor star‚ho souboru
;                   SS:[BP-12] (2) identifik tor nov‚ho souboru
;                   SS:[BP-14] (2) ukazatel dat v bufferu k z pisu
;                   SS:[BP-16] (2) ukazatel dat v bufferu
;                   SS:[BP-18] (2) ukazatel konce dat v bufferu
;                   SS:[BP-20] (2)
;                   SS:[BP-24] (4) po‡et z znam– ke ‡ten¡
; -----------------------------------------------------------------------------

EdiBODel PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         mov       bp,sp

; ------ p©¡prava lok ln¡ch promˆnn˜ch

         sub       sp,24
         mov       ss:[bp-2],si             ; adresa definice okna
         and       byte ptr ds:[DBFEPar2],not bit5 ; nebyl zru¨en˜ z znam

; ------ zobrazen¡ hl ¨en¡

         push      si
         mov       word ptr ds:[InfoKCit],0 ; ‡¡ta‡ ukazatele kurzoru
         mov       word ptr ds:[InfoKCit+2],0
         mov       si,offset DBFDelHl       ; hl ¨en¡ o ru¨en¡
         call      far ptr InfDisp0         ; zobrazen¡ hl ¨en¡
         pop       si

; ------ vytvo©en¡ v˜stupn¡ho souboru

         call      DBFTOpen                 ; vytvo©en¡ p©echodn‚ho souboru
         jnc       EdiBODl2                 ; soubor vytvo©en OK
EdiBODl1:jmp       EdiBODl9                 ; chyba

EdiBODl2:mov       ss:[bp-6],ax             ; segment nov‚ho okna

; ------ maximalizace star‚ho okna

         call      far ptr MaxiDBF          ; maximalizace star‚ho okna

; ------ adresa star‚ho okna

         call      DBFAdrES                 ; aktualizace adresy okna -> ES
         mov       ss:[bp-4],es             ; adresa star‚ho okna

; ------ adresa nov‚ho okna

         push      es                       ; adresa star‚ho okna

         call      far ptr GetDat           ; adresa nov‚ho okna -> ES
         mov       ss:[bp-8],es             ; adresa nov‚ho okna

; ------ identifik tor nov‚ho souboru

         mov       ax,es:[DBFIdent]         ; identifik tor nov‚ho souboru
         mov       ss:[bp-12],ax            ; identifik tor nov‚ho souboru

; ------ z pis z hlav¡ do nov‚ho souboru (ukazatel resetov n, chyba se ignoruje)

         mov       si,DBFHlava              ; z hlav¡ souboru
         mov       cx,es:[DBFBytH]          ; d‚lka z hlav¡ souboru
         call      far ptr WritFile         ; z pis z hlav¡ do souboru

         pop       es                       ; ES <- adresa star‚ho okna

; ------ identifik tor star‚ho souboru

         mov       ax,es:[DBFIdent]         ; identifik tor star‚ho souboru
         mov       ss:[bp-10],ax            ; identifik tor star‚ho souboru

; ------ nastaven¡ ukazatele ve star‚m souboru za z hlav¡ souboru

         xor       dx,dx                    ; DX <- 0 offset HIGH
         mov       cx,es:[DBFBytH]          ; d‚lka z hlav¡ (bajt–)
         call      far ptr SetUFile         ; nastaven¡ ukazatele v souboru

; ------ po‡et z znam– ve star‚m souboru, inicializace ukazatele operace

         mov       ax,es:[DBFMaxZ]          ; po‡et z znam– v souboru
         mov       word ptr ds:[InfoKMax],ax
         mov       ss:[bp-24],ax            ; po‡et z znam– ke ‡ten¡
         mov       ax,es:[DBFMaxZ+2]
         mov       word ptr ds:[InfoKMax+2],ax
         mov       ss:[bp-24+2],ax
         xor       cx,cx                    ; CX <- 0
         call      far ptr InfoKurz         ; zobrazen¡ kurzoru operace

; ------ po‡et z znam– pro dal¨¡ operaci ‡ten¡ -> CX

EdiBODl3:mov       es,ss:[bp-4]             ; ES <- adresa star‚ho okna
         mov       ax,es:[DBFMaxD]          ; max. z znam– v bufferu
         cmp       word ptr ss:[bp-24+2],0  ; je je¨tˆ dost z znam– ?
         jne       EdiBODl4                 ; je je¨tˆ dost z znam–
         cmp       ax,ss:[bp-24]            ; zb˜v  dost z znam– ?
         jbe       EdiBODl4                 ; zb˜v  dost z znam–
         mov       ax,ss:[bp-24]            ; omezen¡ po‡tu z znam–
EdiBODl4:sub       ss:[bp-24],ax            ; sn¡‘en¡ zbyl˜ch z znam–
         sbb       word ptr ss:[bp-24+2],0
         mul       word ptr es:[DBFBytZ]    ; p©epo‡et na bajty
         xchg      ax,cx                    ; CX <- po‡et bajt– ke ‡ten¡
         jcxz      EdiBODl7                 ; konec souboru

; ------ p©¡prava ukazatel– pro dal¨¡ blok dat

         mov       di,es:[DBFAdrD]          ; adresa datov‚ho bufferu
         mov       ss:[bp-18],di            ; adresa konce dat v bufferu
         mov       ss:[bp-16],di            ; ukazatel dat v bufferu
         mov       ss:[bp-14],di            ; ukazatel dat v bufferu k z pisu

; ------ test, zda je p©eru¨en¡ operace

         call      far ptr TestBEsc         ; je p©eru¨en¡ operace ?
         jc        EdiBOD82                 ; je p©eru¨en¡ operace

; ------ na‡ten¡ z znam– ze souboru

         mov       ax,ss:[bp-10]            ; identifik tor star‚ho souboru
         call      far ptr Read0Fil         ; na‡ten¡ dat ze souboru
         jc        EdiBODl8                 ; chyba ‡ten¡ dat
         add       ss:[bp-18],cx            ; posun adresy konce dat v bufferu

; ------ test, zda je dal¨¡ z znam v bufferu

EdiBODl5:mov       si,ss:[bp-16]            ; ukazatel dat v bufferu
         cmp       si,ss:[bp-18]            ; je dal¨¡ z znam v bufferu ?
         jb        EdiBODl6                 ; je dal¨¡ z znam v bufferu
         call      EdiBODW                  ; z pis bufferu na disk
         jc        EdiBODl8                 ; chyba z pisu
         jmp       short EdiBODl3           ; dal¨¡ blok dat

; ------ test, zda je z znam ozna‡en ke zru¨en¡

EdiBODl6:cmp       byte ptr es:[si],"*"     ; je z znam ozna‡en ke zru¨en¡ ?
         jne       EdiBOD62                 ; z znam nen¡ ozna‡en ke zru¨en¡

; ------ vypr zdnˆn¡ dat p©ed ru¨en˜m z znamem

         call      EdiBODW                  ; z pis bufferu na disk
         jc        EdiBODl8                 ; chyba z pisu

; ------ p©esko‡en¡ zru¨en‚ho z znamu

         mov       ax,es:[DBFBytZ]          ; po‡et bajt– na z znam
         add       ss:[bp-14],ax            ; zv˜¨en¡ z pisov‚ adresy
         add       ss:[bp-16],ax            ; zv˜¨en¡ ukazatele dat
         or        byte ptr ds:[DBFEPar2],bit5 ; byl zru¨en z znam
         jmp       short EdiBODl5           ; dal¨¡ z znam

; ------ zv˜¨en¡ ukazatel–, je-li z znam platn˜

EdiBOD62:mov       ax,es:[DBFBytZ]          ; po‡et bajt– na z znam
         add       ss:[bp-16],ax            ; zv˜¨en¡ ukazatele dat
         jmp       short EdiBODl5           ; dal¨¡ z znam

; ------ konec konverze souboru

EdiBODl7:test      byte ptr ds:[DBFEPar2],bit5 ; byl zru¨en˜ z znam ?
         jz        EdiBOD82                 ; nebyl zru¨en˜ z znam

         mov       es,ss:[bp-8]             ; adresa nov‚ho okna
         call      UlozDEOF                 ; z pis koncov‚ho znaku EOF

         mov       ax,ss:[bp-6]             ; segment nov‚ho okna
         mov       si,ss:[bp-2]             ; adresa definice okna
         call      DBFTClos                 ; uzav©en¡ v˜stupn¡ho souboru
         jnc       EdiBODl9                 ; operace OK

; ------ chyba z pisu do v˜stupn¡ho souboru

EdiBODl8:mov       di,offset DBHMWrt        ; hl ¨en¡ - chyba z pisu
         call      far ptr Lin0MenP         ; chybov‚ hl ¨en¡

; ------ zru¨en¡ v˜stupn¡ho souboru, zru¨en¡ bufferu

EdiBOD82:mov       ax,ss:[bp-6]             ; segment nov‚ho okna
         mov       si,ss:[bp-2]             ; adresa definice okna
         call      DBFTDel                  ; zru¨en¡ segmentu AX p©i chybˆ
         stc

; ------ n vrat registr–

EdiBODl9:mov       sp,bp
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         call      far ptr InfoClos         ; uzav©en¡ informa‡n¡ho © dku
         call      DBFAdrES                 ; aktualizace adresy okna -> ES
         ret

EdiBODel ENDP

; ------ z pis bufferu na disk (CY=chyba)

EdiBODW: mov       si,ss:[bp-14]            ; adresa dat k z pisu
         mov       cx,ss:[bp-16]            ; ukazatel dat v bufferu
         mov       ss:[bp-14],cx            ; posun adresy dat k z pisu
         sub       cx,si                    ; po‡et bajt– k z pisu
         jz        EdiBODW2                 ; nen¡ nic k z pisu (-> NC)

         mov       ax,ss:[bp-12]            ; identifik tor nov‚ho souboru
         call      far ptr WritFile         ; z pis dat do souboru
         jc        EdiBODW2                 ; chyba z pisu

         xor       dx,dx                    ; DX <- 0
         xchg      ax,cx                    ; AX <- po‡et zapsan˜ch bajt–
         div       word ptr es:[DBFBytZ]    ; AX <- po‡et ulo‘en˜ch z znam–

         push      es
         mov       es,ss:[bp-8]             ; adresa nov‚ho okna
         add       word ptr es:[DBFMaxZ],ax  ; zv˜¨en¡ po‡tu z znam–
         adc       word ptr es:[DBFMaxZ+2],0
         pop       es

         xchg      ax,cx                    ; CX <- po‡et ulo‘en˜ch z znam–
         call      far ptr InfoKurz         ; zobrazen¡ kurzoru
         clc                                ; p©¡znak operace OK

EdiBODW2:ret

; -----------------------------------------------------------------------------
;        vytvo©en¡ p©echodn‚ho v˜stupn¡ho souboru DBF
; -----------------------------------------------------------------------------
; VSTUP: DS:SI=adresa menu
; VSTUP: AX=‡¡slo bloku s nov˜m otev©en˜m souborem DBF (p©i CY je AX=0)
;         CY=chyba (hl s¡ chybu), AX=0
;         ES=obnoven  adresa star‚ho okna
;         polo‘ka DBFSoubN (d‚lka jm‚na souboru) z–st v  nezmˆnˆna !
; -----------------------------------------------------------------------------

DBFTOpen PROC      NEAR

; ------ £schova registr–

         push      bx
         push      cx
         push      dx
         push      di
         push      bp

; ------ minimalizace star‚ datab ze

         call      far ptr MiniDBF          ; minimalizace datab ze

; ------ pot©ebn  velikost pro novou datab zi -> BX, CX

         call      DBFAdrES                 ; aktualizace adresy okna -> ES
         mov       bx,es:[DBFByts]          ; velikost segmentu
         mov       cx,bx                    ; CX <- velikost segmentu

; ------ aktualizace adres ©e

         push      si
         mov       si,DBFSoub               ; soubor DBF
         call      far ptr ModiWDir         ; aktualizace adres ©e
         pop       si

; ------ vytvo©en¡ segmentu nov‚ datab ze

         call      far ptr CreatSeg         ; vytvo©en¡ segmentu datab ze
         jc        DBFTOpn1                 ; chyba
         mov       bp,ax                    ; BP <- segment nov‚ datab ze
         call      far ptr ModiSegS         ; nastaven¡ velikosti segmentu
         jnc       DBFTOpn2                 ; je to OK
         call      far ptr DelSeg           ; zru¨en¡ segmentu p©i chybˆ

; ------ chyba pamˆti

DBFTOpn1:call      far ptr MemErr           ; chyba pamˆti
         jmp       short DBFTOpn8

; ------ kopie definice datab ze (CX=velikost v bajtech)

DBFTOpn2:push      si
         push      ds

         call      far ptr GetDat           ; adresa nov‚ho segmentu -> ES
         mov       ax,ds:[si+DBMnuSeg]      ; AX <- segment star‚ datab ze
         call      far ptr GetSgAdr         ; adresa segmentu -> DX
         mov       ds,dx                    ; DS <- adresa star‚ datab ze
         xor       si,si                    ; SI <- 0
         xor       di,di                    ; DI <- 0
         cld
         rep       movsb                    ; kopie definice datab ze

         pop       ds
         pop       si

; ------ nulov n¡ ukazatel–

         xchg      ax,cx                    ; AX <- 0
         mov       es:[DBFMaxZ],ax          ; nejsou z znamy
         mov       es:[DBFMaxZ+2],ax
         mov       es:[DBFIdent],ax         ; soubor DBF nen¡ otev©en
         mov       es:[DBFIdntT],ax         ; soubor DBT nen¡ otev©en

; ------ vytvo©en¡ p©echodn‚ho souboru (d‚lka jm‚na souboru se neaktualizuje !)

         push      si
         mov       si,DBFSoub               ; jm‚no souboru DBF
         call      far ptr CreatPF          ; vytvo©en¡ p©echodn‚ho souboru
         pop       si
         mov       es:[DBFIdent],ax         ; identifik tor souboru
         jnc       DBFTOpn9                 ; soubor vytvo©en OK

; ------ hl ¨en¡ - chyba z pisu do souboru

         mov       di,offset DBHMWrt        ; chyba z pisu
         call      far ptr Lin0MenP         ; chybov‚ hl ¨en¡

; ------ zru¨en¡ nov‚ datab ze p©i chybˆ

         xchg      ax,bp                    ; AX <- segment nov‚ datab ze
         call      far ptr DelSeg           ; zru¨en¡ segmentu

; ------ maximalizace p–vodn¡ datab ze p©i chybˆ

DBFTOpn8:call      far ptr MaxiDBF          ; maximalizace buffer–
         xor       bp,bp                    ; BP <- segment neplatn˜
         stc                                ; p©¡znak chyby

; ------ n vrat registr–

DBFTOpn9:xchg      ax,bp                    ; AX <- segment nov‚ datab ze
         pop       bp
         pop       di
         pop       dx
         pop       cx
         pop       bx
         call      DBFAdrES                 ; aktualizace adresy okna -> ES
         ret

DBFTOpen ENDP

; -----------------------------------------------------------------------------
;        zru¨en¡ p©echodn‚ho v˜stupn¡ho souboru DBF p©i chybˆ
; -----------------------------------------------------------------------------
; VSTUP: AX=‡¡slo bloku s p©echodn˜m souborem (bude zru¨en !) (m–‘e b˜t = 0)
;        DS:SI=definice star‚ho okna
; -----------------------------------------------------------------------------

DBFTDel  PROC      NEAR

; ------ zapnut¡ kritick‚ sekce

         call      far ptr ZapKrit          ; zapnut¡ kritick‚ sekce

; ------ adresa bloku AX -> ES

         call      far ptr GetDat           ; adresa bloku -> ES
         jc        DBFTDel9                 ; neplatn˜ blok

; ------ uzav©en¡ souboru, nen¡-li uzav©en

         call      DBFClosF                 ; uzav©en¡ souboru v oknˆ

; ------ zru¨en¡ souboru

         call      DBFDelF                  ; zru¨en¡ p©echodn‚ho souboru

; ------ zru¨en¡ segmentu okna

         call      far ptr DelSeg           ; zru¨en¡ segmentu AX

; ------ vypnut¡ kritick‚ sekce

DBFTDel9:call      far ptr VypKrit          ; vypnut¡ kritick‚ sekce

; ------ aktualizace adresy okna DS:SI -> ES

         call      DBFAdrES                 ; aktualizace adresy okna -> ES
         ret

DBFTDel  ENDP

; -----------------------------------------------------------------------------
;        uzav©en¡ p©echodn‚ho v˜stupn¡ho souboru
; -----------------------------------------------------------------------------
; VSTUP: AX=‡¡slo bloku s p©echodn˜m souborem
;        DS:SI=definice star‚ho okna
; VSTUP: CY=chyba operace (nehl s¡ chybu) (nic se nezmˆnilo)
; -----------------------------------------------------------------------------

DBFTClos PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      di

; ------ z pis z hlav¡ souboru

         xchg      ax,ds:[si+DBMnuSeg]      ; z mˆna segmentu bufferu
         call      HlavDBF                  ; z pis z hlav¡ souboru
         jc        DBFTCls8                 ; chyba

; ------ adresa bufferu v˜stupn¡ho souboru -> ES

         call      DBFAdrES                 ; aktualizace adresy okna -> ES

; ------ uzav©en¡ v˜stupn¡ho souboru

         call      DBFClosF                 ; uzav©en¡ v˜stupn¡ho souboru

; ------ adresa bufferu star‚ho souboru -> ES

         call      far ptr GetDat           ; adresa bufferu -> ES

; ------ uzav©en¡ vstupn¡ho souboru

         call      DBFClosF                 ; uzav©en¡ vstupn¡ho souboru

; ------ zru¨en¡ star‚ho (vstupn¡ho) souboru

         call      DBFDelF                  ; zru¨en¡ star‚ho souboru
         jc        DBFTCls7                 ; chyba - nelze zru¨it

; ------ p©ejmenov n¡ nov‚ho souboru (chyba se ignoruje - nelze s n¡ nic dˆlat)

         push      si
         push      ds

         mov       dx,es                    ; segment star‚ho souboru
         call      DBFAdrES                 ; aktualizace adresy okna -> ES
         mov       si,DBFSoub               ; SI <- nov‚ jm‚no (star˜ soubor)
         mov       di,si                    ; DI <- star‚ jm‚no (nov˜ soubor)
         call      far ptr RenFile          ; p©ejmenov n¡ souboru

; ------ kopie jm‚na souboru

         mov       ds,dx                    ; DS <- segment nov‚ho jm‚na
         mov       cx,FileMax               ; d‚lka jm‚na souboru
         cld
         rep       movsb                    ; p©enos jm‚na souboru

         pop       ds
         pop       si

; ------ nov‚ otev©en¡ v˜stupn¡ho souboru

         push      ax
         push      si
         mov       si,DBFSoub               ; jm‚no souboru
         call      far ptr OpenRW           ; otev©en¡ souboru
         jc        DBFTCls4                 ; s chybou se ned  nic dˆlat
         mov       es:[DBFIdent],ax         ; identifik tor souboru
DBFTCls4:pop       si
         pop       ax

; ------ zru¨en¡ star‚ho segmentu

         call      far ptr DelSeg           ; zru¨en¡ star‚ho segmentu

; ------ korekce zobrazen¡ nov‚ datab ze

         call      ModiDKor                 ; korekce zobrazen¡ datab ze

; ------ maximalizace bufferu datab ze

         call      far ptr MaxiDBF          ; maximalizace buffer–
         clc                                ; p©¡znak operace OK
         jmp       short DBFTCls9

; ------ otev©en¡ star‚ho souboru p©i chybˆ (ES=segment star‚ho okna)

DBFTCls7:push      ax
         push      si

         mov       si,DBFSoub               ; jm‚no souboru
         call      far ptr OpenRW           ; otev©en¡ souboru
         jnc       DBFTCl72                 ; soubor otev©en OK
         call      far ptr OpenR            ; otev©en¡ pro ‡ten¡
         jc        DBFTCl74                 ; s chybou se ned  nic dˆlat
         or        byte ptr es:[DBFParm],bit1 ; z kaz z pisu
DBFTCl72:mov       es:[DBFIdent],ax         ; identifik tor souboru

DBFTCl74:pop       si
         pop       ax

; ------ n vrat star‚ho segmentu p©i chybˆ

DBFTCls8:xchg      ax,ds:[si+DBMnuSeg]      ; n vrat segmentu bufferu p©i chybˆ
         stc

; ------ n vrat registr–

DBFTCls9:pop       di
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         call      DBFAdrES                 ; aktualizace adresy okna -> ES
         ret

DBFTClos ENDP

; -----------------------------------------------------------------------------
;        uzav©en¡ souboru v oknˆ ES
; -----------------------------------------------------------------------------

DBFClosF PROC      NEAR

         push      ax
         mov       ax,es:[DBFIdent]         ; identifik tor souboru
         call      far ptr ClosFile         ; uzav©en¡ v˜stupn¡ho souboru
         mov       es:[DBFIdent],ax         ; p©¡znak uzav©en¡ souboru
         pop       ax
         ret

DBFClosF ENDP

; -----------------------------------------------------------------------------
;        zru¨en¡ souboru v oknˆ ES
; -----------------------------------------------------------------------------

DBFDelF  PROC      NEAR

         push      si
         mov       si,DBFSoub               ; jm‚no souboru DBF
         call      far ptr DelFile          ; zru¨en¡ star‚ho souboru
         pop       si
         ret

DBFDelF  ENDP

CodeDbf  ENDS

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;
;                                 Data
;
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;þ
Data     SEGMENT

; ------ data k vyhled n¡ v seznamu

DBHMSLin label     byte

         db        3                        ; typ menu - © dkov‚ menu

         db        0                        ; hladina k zobrazen¡ menu
         dw        1                        ; aktivn¡ polo‘ka menu
         dw        5                        ; po‡et platn˜ch polo‘ek menu
         dw        7                        ; celkov˜ po‡et polo‘ek menu

         dw        DBHMLPol                 ; za‡ tek definice polo‘ek menu
         dw        DBHMLHlp                 ; adresa tabulky text– n povˆdy
         dw        Hlp@VyhledavaniTextuVDatabazi ; ‡¡slo str nky velk‚ n povˆdy
         dw        DBHMLBlk                 ; adresa blokovac¡ tabulky
         dw        DBHMLSwc                 ; adresa tabulky p©ep¡na‡–
         dw        DBHMLTit                 ; adresa titulu okna
         dw        DBHMLExe                 ; tabulka obsluh voleb menu
         dw        LinMenTM                 ; tabulka adres podmenu
         dd        0                        ; n vratov  adresa p©i p©eru¨en¡

         db        20                       ; po‡ te‡n¡ pozice okna
         db        0                        ; po‡ te‡n¡ © dek okna
         db        57                       ; ¨¡©ka okna
         db        8                        ; v˜¨ka okna

         dw        TextFndX                 ; maxim ln¡ d‚lka textu © dku
         dw        0                        ; prvn¡ zobrazen˜ © dek
         dw        0                        ; aktivn¡ zobrazen˜ © dek
         dw        1                        ; celkov˜ po‡et © dk–
         dw        1                        ; po‡et zobrazen˜ch © dk–

         db        bit2                     ; maska zad van˜ch znak– - v¨echny
         db        1,26,'     '             ; zak zan‚ znaky

         db        HistFind                 ; historie hled n¡
         db        0                        ; po‡et p©edvoleb

DBHMLPol db        bit6,27,'Text k vyhled n¡ v seznamu:'
         db        0,0
         db        bit6+bit7,0
         db        1,6,'Hledej'
         db        1+bit7,9,'Ozna‡it '
DBHMLPl1 db        '-'
         db        1+bit7,9,'Nulovat '
DBHMLPl2 db        '-'
         db        1,8,'P©eru¨it'

DBHMLBlk db        0,0,0,0,0                ; blokovac¡ tabulka
DBHMLSwc db        0,0                      ; p©ep¡na‡e

DBHMLTit db        6,'hledej'

DBHMLTxt db        18,'Vyhled v m text...'

HelpS    SEGMENT   BYTE PUBLIC
DBHMLHlp db        65,'Zadejte textov˜ ©etˆzec k vyhled n¡ (od kurzoru ke konci seznamu)'
         db        24,'Vyhled n¡ zadan‚ho textu'
         db        39,'V¨echny nalezen‚ z znamy budou ozna‡eny'
         db        47,'Bude nulov no ozna‡en¡ v¨ech nalezen˜ch z znam–'
         db        3,1
         dw        MenuHlP2
HelpS    ENDS

DBHMLExe label     dword
         dd        Lin0MenR                 ; © dky
         dd        Lin0MenR                 ; Hledej
         dd        EditBHS1                 ; Ozna‡it
         dd        EditBHS2                 ; Nulovat
         dd        Lin0MenR                 ; P©eru¨it

; ------ chyba - nenalezen ‘ dn˜ text

DBHMFnd  label     byte

         db        3                        ; typ menu - © dkov‚ menu

         db        0                        ; hladina k zobrazen¡ menu
         dw        1                        ; aktivn¡ polo‘ka menu
         dw        1                        ; po‡et platn˜ch polo‘ek menu
         dw        5                        ; celkov˜ po‡et polo‘ek menu

         dw        DBHMFndP                 ; za‡ tek definice polo‘ek menu
         dw        ChbLnMeH                 ; adresa tabulky text– n povˆdy
         dw        Hlp@VyhledavaniTextuVDatabazi ; ‡¡slo str nky velk‚ n povˆdy
         dw        DBHMFndB                 ; adresa blokovac¡ tabulky
         dw        0                        ; adresa tabulky p©ep¡na‡–
         dw        DBHMFndT                 ; adresa titulu okna
         dw        LinMenTE                 ; tabulka obsluh voleb menu
         dw        LinMenTM                 ; tabulka adres podmenu
         dd        0                        ; n vratov  adresa p©i p©eru¨en¡

         db        23                       ; po‡ te‡n¡ pozice okna
         db        7                        ; po‡ te‡n¡ © dek okna
         db        49                       ; ¨¡©ka okna
         db        9                        ; v˜¨ka okna

         dw        255                      ; maxim ln¡ d‚lka textu © dku
         dw        0                        ; prvn¡ zobrazen˜ © dek
         dw        0                        ; aktivn¡ zobrazen˜ © dek
         dw        0                        ; celkov˜ po‡et © dk–
         dw        0                        ; po‡et zobrazen˜ch © dk–

         db        bit3                     ; maska zad van˜ch znak–
         db        0,'      '               ; zak zan‚ znaky

         db        0                        ; historie
         db        0                        ; po‡et p©edvoleb

DBHMFndP db        bit6,0
         db        bit6,34,'Zadan˜ textov˜ ©etˆzec nenalezen !'
         db        bit6,0
         db        bit6+bit7,0
         db        1,6,'N vrat'

DBHMFndB db        0                        ; blokovac¡ tabulka

DBHMFndT db        14,'text nenalezen'

; ------ po‡et nalezen˜ch z znam–

DBH1MFnd label     byte

         db        3                        ; typ menu - © dkov‚ menu

         db        0                        ; hladina k zobrazen¡ menu
         dw        1                        ; aktivn¡ polo‘ka menu
         dw        1                        ; po‡et platn˜ch polo‘ek menu
         dw        6                        ; celkov˜ po‡et polo‘ek menu

         dw        DBH1MFnP                 ; za‡ tek definice polo‘ek menu
         dw        ChbLnMeH                 ; adresa tabulky text– n povˆdy
         dw        Hlp@VyhledavaniTextuVDatabazi ; ‡¡slo str nky velk‚ n povˆdy
         dw        DBH1MFnB                 ; adresa blokovac¡ tabulky
         dw        0                        ; adresa tabulky p©ep¡na‡–
         dw        DBH1MFnT                 ; adresa titulu okna
         dw        LinMenTE                 ; tabulka obsluh voleb menu
         dw        LinMenTM                 ; tabulka adres podmenu
         dd        0                        ; n vratov  adresa p©i p©eru¨en¡

         db        23                       ; po‡ te‡n¡ pozice okna
         db        7                        ; po‡ te‡n¡ © dek okna
         db        49                       ; ¨¡©ka okna
         db        10                       ; v˜¨ka okna

         dw        255                      ; maxim ln¡ d‚lka textu © dku
         dw        0                        ; prvn¡ zobrazen˜ © dek
         dw        0                        ; aktivn¡ zobrazen˜ © dek
         dw        0                        ; celkov˜ po‡et © dk–
         dw        0                        ; po‡et zobrazen˜ch © dk–

         db        bit3                     ; maska zad van˜ch znak–
         db        0,'      '               ; zak zan‚ znaky

         db        0                        ; historie
         db        0                        ; po‡et p©edvoleb

DBH1MFnP db        bit6,0
         db        bit6,30,'Zadan˜ textov˜ ©etˆzec nalezen'
         db        bit6,18,'v ',0,1
         dw        DBH1MFBN                 ; buffer s ‡¡slem
         db        0,' z znamech.'
         db        bit6,0
         db        bit6+bit7,0
         db        1,6,'N vrat'

DBH1MFnB db        0                        ; blokovac¡ tabulka

DBH1MFBN db        0,10 dup(0)              ; buffer k dek¢dov n¡ ‡¡sla

DBH1MFnT db        12,'text nalezen'

; -----------------------------------------------------------------------------
;        menu ru¨en¡ z znam– v souboru
; -----------------------------------------------------------------------------

DBPMLin  label     byte
         db        3                        ; typ menu - © dkov‚ menu

         db        0                        ; hladina k zobrazen¡ menu
         dw        1                        ; aktivn¡ polo‘ka menu
         dw        2                        ; po‡et platn˜ch polo‘ek menu
         dw        7                        ; celkov˜ po‡et polo‘ek menu

         dw        DBPMLPol                 ; adresa definice polo‘ek
         dw        DBPMLHlp                 ; adresa n povˆd
         dw        Hlp@DatabazoveMenu       ; ‡¡slo str nky velk‚ n povˆdy
         dw        DBPMLBlk                 ; adresa blokovac¡ tabulky
         dw        0                        ; adresa tabulky p©ep¡na‡–
         dw        DBPMLTit                 ; adresa titulu okna
         dw        LinMenTE                 ; adresa obsluh voleb
         dw        LinMenTM                 ; adresy podmenu
         dd        0                        ; n vratov  adresa

         db        19                       ; po‡ te‡n¡ pozice
         db        7                        ; po‡ te‡n¡ © dek
         db        50                       ; ¨¡©ka
         db        10                       ; v˜¨ka

         dw        0                        ; maxim ln¡ d‚lka © dku
         dw        0                        ; prvn¡ zobrazen˜ © dek
         dw        0                        ; aktivn¡ zobrazen˜ © dek
         dw        0                        ; celkov˜ po‡et © dk–
         dw        0                        ; po‡et zobrazen˜ch © dk–

         db        0                        ; maska znak–
         db        0,'      '               ; zak zan‚ znaky

         db        0                        ; identifik tor historie
         db        0                        ; po‡et p©edvoleb

DBPMLPol db        bit6,0
         db        bit6,34,'V¨echny ozna‡en‚ z znamy v souboru'
         db        bit6,18,'budou odstranˆny !'
         db        bit6,0
         db        bit6+bit7,0
         db        1,10,'Pokra‡ovat'
         db        1,6,'N vrat'

DBPMLBlk db        0,0
DBPMLTit db        6,'ru¨en¡'

HelpS    SEGMENT   BYTE PUBLIC
DBPMLHlp db        37,'Zru¨en¡ ozna‡en˜ch z znam– ze souboru'
         db        3,1
         dw        MenuHlP2
HelpS    ENDS

; ------ druh‚ potvrzen¡ ru¨en¡

DBP2Lin  label     byte
         db        3                        ; typ menu - © dkov‚ menu

         db        0                        ; hladina k zobrazen¡ menu
         dw        1                        ; aktivn¡ polo‘ka menu
         dw        2                        ; po‡et platn˜ch polo‘ek menu
         dw        8                        ; celkov˜ po‡et polo‘ek menu

         dw        DBP2LPol                 ; adresa definice polo‘ek
         dw        DBP2LHlp                 ; adresa n povˆd
         dw        Hlp@DatabazoveMenu       ; ‡¡slo str nky velk‚ n povˆdy
         dw        DBP2LBlk                 ; adresa blokovac¡ tabulky
         dw        0                        ; adresa tabulky p©ep¡na‡–
         dw        DBPMLTit                 ; adresa titulu okna
         dw        LinMenTE                 ; adresa obsluh voleb
         dw        LinMenTM                 ; adresy podmenu
         dd        0                        ; n vratov  adresa

         db        19                       ; po‡ te‡n¡ pozice
         db        7                        ; po‡ te‡n¡ © dek
         db        44                       ; ¨¡©ka
         db        11                       ; v˜¨ka

         dw        0                        ; maxim ln¡ d‚lka © dku
         dw        0                        ; prvn¡ zobrazen˜ © dek
         dw        0                        ; aktivn¡ zobrazen˜ © dek
         dw        0                        ; celkov˜ po‡et © dk–
         dw        0                        ; po‡et zobrazen˜ch © dk–

         db        0                        ; maska znak–
         db        0,'      '               ; zak zan‚ znaky

         db        0                        ; identifik tor historie
         db        0                        ; po‡et p©edvoleb

DBP2LPol db        bit6,0
         db        bit6,10,0,'VAROVN‹:'
         db        bit6,23,'Zru¨¡m v¨echny ozna‡en‚'
         db        bit6,19,'z znamy v souboru !'
         db        bit6,0
         db        bit6+bit7,0
         db        1,5,'Ru¨it'
         db        1,6,'N vrat'

DBP2LBlk db        0,0

HelpS    SEGMENT   BYTE PUBLIC
DBP2LHlp db        42,'Zah jen¡ operace ru¨en¡ ozna‡en˜ch z znam–'
         db        3,1
         dw        MenuHlP2
HelpS    ENDS

DBFDelHl db        25,'Ru¨¡m ozna‡en‚ z znamy...'

; ------ vlo‘en¡ z znam–
;þ
DBVMSLin label     byte

         db        3                        ; typ menu - © dkov‚ menu

         db        0                        ; hladina k zobrazen¡ menu
         dw        1                        ; aktivn¡ polo‘ka menu
         dw        3                        ; po‡et platn˜ch polo‘ek menu
         dw        5                        ; celkov˜ po‡et polo‘ek menu

         dw        DBVMLPol                 ; za‡ tek definice polo‘ek menu
         dw        DBVMLHlp                 ; adresa tabulky text– n povˆdy
         dw        Hlp@DatabazoveMenu       ; ‡¡slo str nky velk‚ n povˆdy
         dw        DBVMLBlk                 ; adresa blokovac¡ tabulky
         dw        0                        ; adresa tabulky p©ep¡na‡–
         dw        DBVMLTit                 ; adresa titulu okna
         dw        LinMenTE                 ; tabulka obsluh voleb menu
         dw        LinMenTM                 ; tabulka adres podmenu
         dd        0                        ; n vratov  adresa p©i p©eru¨en¡

         db        20                       ; po‡ te‡n¡ pozice okna
         db        0                        ; po‡ te‡n¡ © dek okna
         db        40                       ; ¨¡©ka okna
         db        8                        ; v˜¨ka okna

         dw        13                       ; maxim ln¡ d‚lka textu © dku
         dw        0                        ; prvn¡ zobrazen˜ © dek
         dw        0                        ; aktivn¡ zobrazen˜ © dek
         dw        1                        ; celkov˜ po‡et © dk–
         dw        1                        ; po‡et zobrazen˜ch © dk–

         db        bit4                     ; maska zad van˜ch znak– - ‡¡slo
         db        6,'. +-:,'               ; zak zan‚ znaky

         db        HistCis                  ; historie ‡¡sel
         db        1                        ; po‡et p©edvoleb

         dw        1
         dd        DBVMLPr1                 ; p©edvolba "1"

DBVMLPr1 db        '1'

DBVMLPol db        bit6,24,'Po‡et z znam– k vlo‘en¡:'
         db        0,0
         db        bit6+bit7,0
         db        1,6,'Vlo‘it'
         db        1,6,'N vrat'

DBVMLBlk db        0,0,0                    ; blokovac¡ tabulka

DBVMLTit db        8,'vkl d n¡'

HelpS    SEGMENT   BYTE PUBLIC
DBVMLHlp db        56,'Zadejte po‡et nov˜ch z znam– k vlo‘en¡ na pozici kurzoru'
         db        44,'Vlo‘en¡ po‘adovan‚ho mno‘stv¡ nov˜ch z znam–'
         db        3,1
         dw        MenuHlP2
HelpS    ENDS

DBFInsHl db        23,'Vkl d m nov‚ z znamy...'

; ------ chyba z pisu do v˜stupn¡ho souboru (pou‘ito t‚‘ p©i exportu dat z TABULKY)

DBHMWrt  label     byte

         db        3                        ; typ menu - © dkov‚ menu

         db        0                        ; hladina k zobrazen¡ menu
         dw        1                        ; aktivn¡ polo‘ka menu
         dw        1                        ; po‡et platn˜ch polo‘ek menu
         dw        5                        ; celkov˜ po‡et polo‘ek menu

         dw        DBHMWrtP                 ; za‡ tek definice polo‘ek menu
         dw        ChbLnMeH                 ; adresa tabulky text– n povˆdy
         dw        Hlp@PodmenuZvlastni      ; ‡¡slo str nky velk‚ n povˆdy
         dw        DBHMWrtB                 ; adresa blokovac¡ tabulky
         dw        0                        ; adresa tabulky p©ep¡na‡–
         dw        DBHMWrtT                 ; adresa titulu okna
         dw        LinMenTE                 ; tabulka obsluh voleb menu
         dw        LinMenTM                 ; tabulka adres podmenu
         dd        0                        ; n vratov  adresa p©i p©eru¨en¡

         db        23                       ; po‡ te‡n¡ pozice okna
         db        7                        ; po‡ te‡n¡ © dek okna
         db        49                       ; ¨¡©ka okna
         db        9                        ; v˜¨ka okna

         dw        255                      ; maxim ln¡ d‚lka textu © dku
         dw        0                        ; prvn¡ zobrazen˜ © dek
         dw        0                        ; aktivn¡ zobrazen˜ © dek
         dw        0                        ; celkov˜ po‡et © dk–
         dw        0                        ; po‡et zobrazen˜ch © dk–

         db        bit3                     ; maska zad van˜ch znak–
         db        0,'      '               ; zak zan‚ znaky

         db        0                        ; historie
         db        0                        ; po‡et p©edvoleb

DBHMWrtP db        bit6,6,0,'CHYBA'
         db        bit6,36,'Chyba z pisu do v˜stupn¡ho souboru !'
         db        bit6,0
         db        bit6+bit7,0
         db        1,6,'N vrat'

DBHMWrtB db        0                        ; blokovac¡ tabulka

DBHMWrtT db        12,'chyba z pisu'

; ------ data

DBFETime dw        0                        ; uschovan˜ ‡as souboru
DBFEDate dw        0                        ; uschovan‚ datum souboru

DBFEPar2 db        0                        ; parametry 2
                                            ;   bit 0: 1=je nastaven¡ ¨¡©ky pole
                                            ;   bit 1: 1=ozna‡en¡ p©i hled n¡
                                            ;   bit 2: 1=nulov n¡ ozna‡en¡ hled n¡

                                            ;   bit 5: 1=byl z pis z znamu

DBCitFnd dd        0                        ; ‡¡ta‡ nalezen˜ch z znam–

Data     ENDS
         END
