

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                          Form tov n¡ disket
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

NUMVOL   EQU       15                       ; po‡et polo‘ek voleb parametr–
FORMOFFS EQU       9                        ; offset definice jednoho form tu
VOLOFFS  EQU       7                        ; offset definice jedn‚ polo‘ky
MAXSEKT  EQU       36                       ; maxim ln¡ po‡et sektor–
BUFFSIZE EQU       36*200h                  ; velikost jednoho diskov‚ho bufferu
MAXFAT   EQU       17                       ; maxim ln¡ po‡et sektor– FAT
BUFFFAT  EQU       17*200h                  ; velikost bufferu pro FAT

COMMENT %                        Vzhled obrazovky:
ÉÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍ»
º 12.06.1993 º       FORM verze 2.0    (c) Miroslav Nˆme‡ek       º  12:33:40  º
ÌÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍ¹
º   Opera‡n¡ syst‚m  û      Zobrazen¡ obsahu  û      Opakov n¡ p©i chybˆ   1   º
º   N vˆ¨t¡ disku    û      Zrychlen˜ start   û      Testovac¡ z pis       0   º
º   Zachov n¡ dat DISK      Zpˆtn˜ smˆr       û      Form tovac¡ znak     F6   º
º   Jedna strana     û      Zvukov  indikace  û      €ernob¡l˜ displej     û   º
º   Polo‘ek v Root 224      Verifikace        û      €e¨tina     Kamenick˜ch   º
ÈÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼
  ±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
°                                                                              °
°                ÉÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ»          ÉÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ»                  °
°                º    Disk A:    º°°        º    Disk  B:   º°°                °
°                ÌÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹°°        ÌÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹°°                °
°                º  Automatick˜  º°°        º  Automatick˜  º°°                °
°                º   1:  1.2M    º°°        º   1: 1.44M    º°°                °
°                º   2:  720K    º°°        º   2:  720K    º°°                °
°                º   3:  360K    º°°        º   3: 2.88M    º°°                °
°                º   4:  320K    º°°        º   4:  360K    º°°                °
°                º               º°°        º               º°°                °
°                º               º°°        º               º°°                °
°                ÈÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼°°        ÈÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼°°                °
°                  °°°°°°°°°°°°°°°°°          °°°°°°°°°°°°°°°°°                °
°                                                                              °
°  F1=n povˆda; vlevo/vpravo=volba disku; TAB=zmˆna okna; ENTER=volba form tu  °
%

; -----------------------------------------------------------------------------
;        konstanty
; -----------------------------------------------------------------------------

HI       EQU       256

b0       EQU       1
b1       EQU       2
b2       EQU       4
b3       EQU       8
b4       EQU       10h
b5       EQU       20h
b6       EQU       40h
b7       EQU       80h
b8       EQU       100h
b9       EQU       200h
b10      EQU       400h
b11      EQU       800h
b12      EQU       1000h
b13      EQU       2000h
b14      EQU       4000h
b15      EQU       8000h

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                          Start programu
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

Code     SEGMENT
         ASSUME    cs:Code,ds:Code
         org       100h

; ------ p©edefinov n¡ z sobn¡ku

Start:   mov       dx,offset MemTxt         ; text - nedostatek pamˆti
         cmp       sp,offset Konec
         jb        Chyba                    ; nedostatek pamˆti
         mov       sp,offset Konec

; ------ zmen¨en¡ velikosti bloku programu

         mov       ah,4ah
         mov       bx,(offset(Konec-Start)+10fh)/16
         int       21h                      ; zmen¨en¡ bloku programu
         jc        Chyba                    ; chyba

; ------ vytvo©en¡ bufferu text–

         mov       bx,(offset(TextEnd-TextBeg)+0fh)/16 ; velikost bufferu text–
         mov       ah,48h
         int       21h                      ; vytvo©en¡ bufferu text–
         jc        Chyba                    ; chyba pamˆti
         mov       ds:[BuffTxt],ax          ; adresa bufferu text–

; ------ £schova text– do bufferu

         mov       es,ax                    ; adresa bufferu
         xor       di,di
         mov       si,offset TextBeg        ; za‡ tek text–
         mov       cx,offset(TextEnd-TextBeg)
         cld
         rep       movsb                    ; £schova text– do bufferu

; ------ vytvo©en¡ datov‚ho bloku pro buffery

         mov       bx,4*(BUFFSIZE/16)+17    ; velikost buffer–
         mov       ah,48h
         int       21h                      ; vytvo©en¡ datov˜ch buffer–
         jc        Chyba                    ; chyba - nedostatek pamˆti

; ------ adresy buffer–

         call      InitBuff                 ; adresa bufferu 1
         mov       ds:[BufferW],cx          ; adresa z pisov‚ho bufferu
         call      InitBuff                 ; adresa bufferu 2
         mov       ds:[BufferR],cx          ; adresa ‡tec¡ho bufferu
         call      InitBuff                 ; adresa bufferu 3
         mov       ds:[BufferD],cx          ; adresa datov‚ho bufferu
         call      InitBuff                 ; je¨tˆ p©¡padn˜ buffer pro FAT
         cmp       word ptr ds:[BufferF],0  ; byl buffer FAT vytvo©en ?
         jne       Start1                   ; byl vytvo©en OK
         mov       ds:[BufferF],cx          ; adresa bufferu FAT

; ------ stanoven¡ po‡tu a parametr– disketov˜ch mechanik

Start1:  call      GetDTyp                  ; stanoven¡ po‡tu a parametr– mech.

; ------ chyba - nen¡ ‘ dn  disketov  mechanika

         cmp       byte ptr ds:[NumDisk],0
         jne       Start2                   ; je nˆjak  mechanika OK
         mov       dx,offset DiskTxt        ; chyba - nen¡ ‘ dn  mechanika
Chyba:   mov       ah,9
         int       21h
         int       20h

; ------ instalace obsluh p©eru¨en¡

Start2:  mov       ax,2523h
         mov       dx,offset INT23
         int       21h
         mov       ax,2524h
         mov       dx,offset INT24
         int       21h

; ------ instalace INT 08h

         mov       ax,3508h
         int       21h
         mov       word ptr ds:[Old08],bx
         mov       word ptr ds:[Old08+2],es
         mov       dx,offset Int08
         mov       ax,2508h
         int       21h

; ------ instalace p©¡znaku p©eru¨en¡

         mov       ax,3300h
         int       21h                      ; poskytnut¡ BREAK
         mov       ds:[OldBreak],dl         ; £schova p©¡znaku BREAK
         mov       dl,0                     ; BREAK je vypnut
         mov       ax,3301h
         int       21h                      ; z kaz p©¡znaku BREAK

; ------ inicializace videom¢du

         call      DetCard                  ; detekce videokarty EGA/VGA
         call      InitFont                 ; inicializace font– a videom¢du
         call      InitMous                 ; inicializace ovlada‡e my¨i
         call      InitRnd                  ; inicializace gener toru n hody

; ------ stanoven¡ pozic k zobrazen¡ jednotliv˜ch disk–

         mov       bh,ds:[NumDisk]          ; po‡et disk–
         mov       al,18                    ; ¨¡©ka jednoho okna
         mul       bh                       ; ¨¡©ka na v¨echna okna
         sub       ax,80
         neg       ax                       ; zbytek na mezery
         inc       bh                       ; po‡et disk– + 1
         div       bh                       ; jedna mezera
         mov       bl,al                    ; £schova mezery
         inc       ax                       ; trochu korekce
         mov       cx,4
         mov       ds:[PozDiskT],al         ; pozice disku A:
         add       al,18
         add       al,bl
         mov       ds:[PozDiskT+1],al       ; pozice disku B:
         add       al,18
         add       al,bl
         mov       ds:[PozDiskT+2],al       ; pozice disku C:
         add       al,18
         add       al,bl
         mov       ds:[PozDiskT+3],al       ; pozice disku D:
         call      IniDPar                  ; inicializace parametr– akt. disku

; ------ zobrazen¡ obrazovky pro volbu form tu

         call      DispRam                  ; zobrazen¡ podkladov‚ho r mu

Start5:  call      DispPod                  ; zobrazen¡ podkladu

; ------ zobrazen¡ v¨ech disk–

Start51: call      DispADsk                 ; zobrazen¡ v¨ech disk–
         call      DispVol                  ; zobrazen¡ voleb

; ------ ‡ek n¡ na stisk kl vesy

Start53: call      KurzOff
         call      MouseGet                 ; vstup z ovlada‡e my¨i
         cmp       bl,4                     ; stisk prav‚ho tla‡¡tka ?
         jne       Start54
         jmp       Start9                   ; stisk prav‚ho tla‡¡tka - konec
Start54: cmp       bl,6                     ; druh˜ stisk lev‚ho tla‡¡tka ?
         je        Strt5402
Start540:cmp       bl,3                     ; stisk lev‚ho tla‡¡tka ?
         je        Strt5402
Strt5401:jmp       Start55                  ; nen¡ stisk lev‚ho tla‡¡tka
Strt5402:cmp       dh,9
         ja        Start541
         and       byte ptr ds:[MouseKey],not b0 ; zru¨en¡ lev‚ho tla‡¡tka
         xor       byte ptr ds:[MouseTim+1],1 ; neplatn˜ ‡as my¨i
         jmp       Start8                   ; volba parametr–

; ------ volba form tu v oknech

Start541:cmp       dh,11
         jb        Strt5401
         cmp       dh,21
         ja        Strt553
         mov       al,0                     ; ukazatel ‡¡sla disku
         mov       si,offset PozDiskT       ; tabulka pozic oken disk–
Start542:mov       bh,ds:[si]               ; po‡ te‡n¡ pozice okna disku
         cmp       dl,bh
         jb        Start543
         add       bh,17
         cmp       dl,bh
         jb        Start544
Start543:inc       al
         inc       si
         cmp       al,ds:[NumDisk]
         jb        Start542
         jmp       short Start55

Start544:cmp       al,ds:[AktDisk]
         je        Strt5442
         mov       bl,3
Strt5442:mov       ds:[AktDisk],al          ; nov˜ aktivn¡ disk
         mov       bh,ds:[AktForm]
         mov       byte ptr ds:[AktForm],-1 ; aktivn¡ form t neplatn˜
         call      IniDPar                  ; inicializace parametr– akt. disku
         sub       dl,ds:[si]
         je        Strt5445
         cmp       dl,16
         je        Strt5445
         sub       dh,14
         jb        Strt5445
         cmp       dh,ds:[NumForm]
         jae       Strt5445
         mov       ds:[AktForm],dh
         cmp       bh,ds:[AktForm]
         je        Strt5443
Strt5445:mov       bl,3
Strt5443:call      IniDPar
Start545:cmp       bl,6
         mov       bx,0dh
         je        Start56
Strt5444:jmp       Start51

; ------ ovl d n¡ my¨¡ pomoc¡ n povˆdy

Strt553: cmp       dh,24
         jne       Start55
         cmp       bl,3
         jne       Start55
         cmp       dl,39
         jb        Start55
         mov       bx,0dh                   ; Enter
         cmp       dl,54
         jbe       Start56
         mov       bl,1bh                   ; Esc
         cmp       dl,65
         jbe       Start56
         mov       bx,3b00h                 ; F1
         jmp       short Start56

Start55: call      TestKey
         jnc       Start552
         jmp       Start53                  ; nen¡ znak z kl vesnice

Start552:
         call      InpKey

Start56: mov       al,ds:[AktForm]          ; aktivn¡ form t
         mov       ah,ds:[NumForm]          ; po‡et mo‘n˜ch form t–
         call      JumpBX                   ; skok na obsluhu podle kl vesy

         dw        9,Start66                ; Tab
         dw        0f00h,Start65            ; Shift-Tab
         dw        4d00h,Start64            ; vpravo
         dw        4b00h,Start63            ; vlevo
         dw        4800h,Start62            ; nahoru
         dw        5000h,Start61            ; dol–
         dw        4700h,Start611           ; Home
         dw        4f00h,Start621           ; End
         dw        4900h,Start611           ; Page Up
         dw        5100h,Start621           ; Page Down
         dw        4300h,Start8             ; F9
         dw        13,Start7                ; ENTER
         dw        27,Start9                ; ESC

         dw        0,Start53

; ------ kurzor dol–

Start61: inc       al
         cmp       al,ah
         jb        Start612
Start611:mov       al,0
Start612:mov       ds:[AktForm],al
Start613:call      IniDPar                  ; inicializace parametr– akt. disku
         jmp       Start51

; ------ kurzor nahoru

Start62: dec       al
         jns       Start612
Start621:mov       al,ah
         dec       al
         jmp       short Start612

; ------ kurzor vlevo

Start63: mov       al,ds:[AktDisk]
         or        al,al
         jz        Start632
         dec       al
Start632:mov       ds:[AktDisk],al          ; nov˜ aktivn¡ disk
         mov       byte ptr ds:[AktForm],-1 ; aktivn¡ form t neplatn˜
         jmp       short Start613           ; inicializace parametr–

; ------ kurzor vpravo

Start64: mov       al,ds:[AktDisk]
         cmp       al,ds:[NumDisk]
         jae       Start632
         inc       ax
         jmp       short Start632

; ------ Shift-Tab

Start65: mov       al,ds:[AktDisk]
         dec       al
         jns       Start632
         mov       al,ds:[NumDisk]          ; po‡et disk–
         dec       ax                       ; ‡¡slo posledn¡ho disku
         jmp       short Start632

; ------ Tab

Start66: mov       al,ds:[AktDisk]
         inc       al
         cmp       al,ds:[NumDisk]
         jb        Start632
         mov       al,0
         jmp       short Start632

; ------ form tov n¡ disku

Start7:  call      FormDsk
         jmp       Start5

; ------ volba parametr–

Start8:  call      Volby                    ; ovl d n¡ voleb
         cmp       byte ptr ds:[MousePoz+1],24
         je        Start82
         and       byte ptr ds:[MouseKey],not b0 ; zru¨en¡ lev‚ho tla‡¡tka
         xor       byte ptr ds:[MouseTim+1],1 ; neplatn˜ ‡as
Start82: jmp       Start53

; ------ ESC: konec programu

Start9:  call      MouseOff
         mov       ah,0fh
         call      Int10
         mov       ah,0
         call      Int10
         mov       dl,ds:[OldBreak]         ; p–vodn¡ p©¡znak p©eru¨en¡
         mov       ax,3301h
         int       21h                      ; n vrat p©¡znaku BREAK
         lds       dx,ds:[Old08]
         mov       ax,2508h
         int       21h
         int       20h

; -----------------------------------------------------------------------------
;        stanoven¡ adresy diskov‚ho bufferu (AX=adresa) -> CX
; -----------------------------------------------------------------------------

InitBuff PROC      NEAR

; ------ adresa nejbli‘¨¡ vy¨¨¡ hranice 64 KB

         mov       bx,ax                    ; aktu ln¡ adresa bufferu
         and       bx,not 0fffh             ; zarovn n¡ na 64 KB
         add       bh,10h                   ; adresa dal¨¡ hranice 64 KB
         jnc       InitBuf1                 ; nen¡ p©elom p©es 1 MB
         mov       bx,-1                    ; omezen¡ na 1 MB

; ------ kontrola, zda je dost m¡sta pro datov˜ buffer

InitBuf1:mov       cx,bx                    ; nejbli‘¨¡ p©elom 64 KB
         sub       cx,ax                    ; zbyl‚ m¡sto do 64 KB
         cmp       cx,BUFFSIZE/16           ; sta‡¡ pro diskov˜ buffer ?
         ja        InitBuf3                 ; velikost pro buffer je dostate‡n 

; ------ test, zda m¡sto sta‡¡ alespo¤ pro buffer FAT

         cmp       cx,BUFFFAT/16            ; sta‡¡ alespo¤ pro buffer FAT ?
         jbe       InitBuf2                 ; m lo m¡sta pro buffer FAT
         cmp       word ptr ds:[BufferF],0  ; je buffer FAT ji‘ vytvo©en ?
         jne       InitBuf2                 ; buffer FAT je ji‘ vytvo©en
         mov       ds:[BufferF],ax          ; adresa bufferu FAT

; ------ posun adresy bufferu na za‡ tek dal¨¡ho p©elomu

InitBuf2:mov       ax,bx                    ; posun za‡ tku bufferu na p©elom
InitBuf3:mov       cx,ax                    ; adresa bufferu
         add       ax,BUFFSIZE/16           ; posun adresy
         ret

InitBuff ENDP

; -----------------------------------------------------------------------------
;        obsluha INT 23h a INT 24h
; -----------------------------------------------------------------------------

INT24    PROC      FAR

         mov       al,0
INT23:   iret

INT24    ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                          Form tov n¡ disku
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;þ
FormDsk  PROC      NEAR

; ------ instalace INT 1eh

         call      Inst1e                   ; instalace INT 1Eh

; ------ p©¡prava BOOT sektoru

FormDsk0:call      SetSer                   ; nastaven¡ s‚riov‚ho ‡¡sla

; ------ inicializace ukazatel–

         xor       ax,ax
         mov       byte ptr ds:[AktStop],al ; ukazatel stop
         mov       byte ptr ds:[AktStran],al ; strana
         mov       word ptr ds:[CitVol],ax
         mov       word ptr ds:[CitVol+2],ax
         mov       word ptr ds:[CitBad],ax
         mov       word ptr ds:[CitBad+2],ax
         mov       ds:[ErrDat],al           ; p©¡znak chyby 1. sektoru bloku

;         mov       word ptr ds:[AktSekt],ax ; ukazatel absolutn¡ho sektoru
;         and       byte ptr ds:[Priznaky],not bit0 + bit1 ; nulov n¡ p©¡znak–
;         mov       word ptr ds:[VolSekt],ax
;         mov       word ptr ds:[BadSekt],ax
;         mov       word ptr ds:[FrstChar],ax
;         mov       byte ptr ds:[FillCh],0   ; plnic¡ znak pro form tov n¡

         mov       al,1
         mov       ds:[ErrBoot],al
         mov       ds:[ErrFat1],al
         mov       ds:[ErrFat2],al
         mov       ds:[ErrRoot],al

         mov       al,ds:[ParBack]          ; smˆr
         mov       ds:[Smer],al             ; smˆr form tov n¡
         cmp       al,0
         je        FormDs02
         mov       al,ds:[NumStop]          ; po‡et stop
         dec       ax
         mov       ds:[AktStop],al          ; posledn¡ stopa
         mov       al,ds:[NumStran]
         dec       ax
         mov       ds:[AktStran],al         ; posledn¡ strana
FormDs02:

; ------ nulov n¡ tabulky FAT

         mov       es,ds:[BufferF]          ; segment bufferu s FAT
         mov       ch,ds:[FatSekt]          ; po‡et sektor– tabulky FAT
         mov       cl,0
         xor       ax,ax
         xor       di,di
         cld
         rep       stosw                    ; nulov n¡ tabulky FAT
         dec       ax
         mov       es:[1],ax                ; za‡ tek tabulky FAT
         mov       al,ds:[Popis]            ; popisova‡ m‚dia
         mov       es:[0],al                ; popisova‡ m‚dia

; ------ p©¡prava ukazatele k zobrazen¡ mapy disku

         cmp       byte ptr ds:[NumStran],2 ; jsou 2 strany ?
         je        FormDs21                 ; jsou 2 strany
         mov       al,4                     ; v˜¨ka - 1 strana 40 stop
         cmp       byte ptr ds:[NumStop],40 ; je 40 stop ?
         je        FormDs22                 ; je 40 stop
         mov       al,9                     ; v˜¨ka - 1 strana 80 stop
         jmp       short FormDs22           ; je 80 stop

FormDs21:mov       al,5                     ; v˜¨ka - 2 strany 40 stop
         cmp       byte ptr ds:[NumStop],60 ; je 40 stop ?
         jb        FormDs22                 ; je 40 stop
         mov       al,11                    ; v˜¨ka - 2 strany 80 stop
FormDs22:mov       byte ptr ds:[SirMap+1],al ; v˜¨ka mapy disku
         sub       al,11
         neg       al                       ; zbytek na okraje
         shr       al,1
         add       al,11                    ; po‡ te‡n¡ © dek
         mov       byte ptr ds:[PozMap+1],al ; po‡ te‡n¡ © dek k zobrazen¡ mapy

         mov       al,ds:[NumStop]          ; po‡et stop
         cmp       al,60
         jb        FormDs23                 ; je 40 stop
         inc       ax                       ; zaokrouhlen¡ nahoru
         shr       al,1                     ; po‡et stop / 2
FormDs23:add       al,13                    ; korekce na okraje
         mov       byte ptr ds:[SirMap],al  ; ¨¡©ka okna
         sub       al,58
         neg       al
         shr       al,1
         mov       byte ptr ds:[PozMap],al  ; po‡ te‡n¡ pozice okna

; ------ rozto‡en¡ mechaniky

         xor       bx,bx
         mov       es,bx
         mov       ax,401h
         mov       cx,1
         call      Int13                    ; verifikace pro rozto‡en¡ mechaniky
         call      ResDisk                  ; £vodn¡ reset disku

; ------ nastaven¡ form tu disku

         call      SetForm                  ; nastaven¡ form tu diskety
         jnc       FormDs40
         jmp       FormDsk9


FormDs40:
         call      DispPod                  ; zobrazen¡ podkladu
         call      DispVol                  ; zobrazen¡ voleb

         call      DispMap                  ; zobrazen¡ mapy disky
         call      DispInf                  ; zobrazen¡ informa‡n¡ho okna

         xor       ax,ax
         mov       es,ax
         mov       ax,es:[46ch]
         mov       ds:[BegTime],ax

         mov       byte ptr ds:[ParsTime],1 ; p©¡znak zobrazen¡ doby akce

FormDsk4:

; ------ ozna‡en¡ tabulky chyb - v¨echny sektory OK

         push      cs
         pop       es
         mov       di,offset TabBad
         mov       cx,word ptr ds:[NumSekt] ; po‡et sektor– na stopu
         mov       al,0                     ; nulovac¡ hodnota
         cld
         rep       stosb                    ; vymaz n¡ tabulky vadn˜ch stop

         cmp       byte ptr ds:[ParDat],0
         je        FormD411

         mov       cl,1
         mov       al,ds:[NumSekt]
         mov       es,ds:[bufferD]
         xor       bx,bx
         call      Cteni
         jc        FormD416

FormD411:
         call      Format
         jc        FormD416

         call      TestRnd                  ; test n hodn˜m z pisem
         jc        FormD416

         cmp       byte ptr ds:[AktStran],0
         jne       FormD413
         cmp       byte ptr ds:[AktStop],0
         jne       FormD413

         mov       es,ds:[BufferW]
         xor       di,di
         mov       si,offset Boot
         mov       cx,offset(BootEnd-Boot)
         cld
         rep       movsb
         xor       ax,ax
         mov       cx,offset(Boot-BootEnd)+512
         rep       stosb
         mov       al,ds:[Popis]
         stosb
         mov       ax,-1
         stosw
         mov       cx,512-3
         mov       al,0
         rep       stosb
         mov       cl,1
         mov       al,2
         xor       bx,bx
         call      Zapis
         jc        FormD416

FormD413:
         cmp       byte ptr ds:[ParDat],0
         je        FormD412

         mov       cl,1
         mov       al,ds:[NumSekt]
         mov       es,ds:[BufferD]
         xor       bx,bx
         call      Zapis
         jc        FormD416


FormD412:cmp       byte ptr ds:[ParVerif],0
         je        FormD414

         call      Verif                    ; verifikace
         jnc       FormD414

FormD416:
         call      ReadBad                  ; ‡ten¡ vadn‚ stopy
         stc

FormD414:

         mov       ah,ds:[ColOK]
         mov       al,"²"
         jnc       FormDs42
FormDs41:mov       ah,ds:[ColErr]
         mov       al,"x"
FormDs42:call      DispFCh


         mov       ah,1
         int       16h
         jz        FormDs76
         call      InpKey
         cmp       bl,27
         je        FormDsk9

; ------ za©azen¡ na‡ten˜ch sektor–

FormDs76:call      StorSekt                 ; za©azen¡ sektor– stopy
         call      DispKap                  ; zobrazen¡ kapacit disku

; ------ zv˜¨en¡ ‡¡sla stopy

         mov       al,ds:[AktStran]         ; aktu ln¡ strana
         cmp       byte ptr ds:[Smer],0
         je        FormDs77                 ; je smˆr nahoru
         dec       al                       ; sn¡‘en¡ ‡¡sla strany
         jz        FormDs78

         mov       al,ds:[AktStop]
         dec       al
         js        FormDs79                 ; jsou ji‘ v¨echny stopy
         mov       ds:[AktStop],al

         mov       al,ds:[NumStran]
         dec       ax
         jmp       short FormDs78

FormDs77:inc       ax                       ; zv˜¨en¡ ‡¡sla aktu ln¡ strany
         cmp       al,ds:[NumStran]         ; jsou ji‘ v¨echny strany ?
         jb        FormDs78                 ; nejsou je¨tˆ v¨echny strany

         mov       al,ds:[AktStop]          ; aktu ln¡ stopa
         inc       ax                       ; zv˜¨en¡ ‡¡sla stopy
         cmp       al,ds:[NumStop]          ; jsou ji‘ v¨echny stopy ?
         jae       FormDs79                 ; jsou ji‘ v¨echny stopy
         mov       ds:[AktStop],al          ; aktu ln¡ stopa
         mov       al,0                     ; strana 0
FormDs78:mov       ds:[AktStran],al         ; aktu ln¡ strana

         jmp       FormDsk4                 ; form tov n¡ dal¨¡ stopy

FormDs79:


FormDsk8:

         mov       byte ptr ds:[ParsTime],0 ; zru¨en¡ p©¡znaku zobrazen¡ ‡asu
         call      InpKey

FormDsk9:

         call      DeInst1E                 ; odinstalov n¡ INT 1eh
         mov       byte ptr ds:[ParsTime],0 ; zru¨en¡ p©¡znaku zobrazen¡ ‡asu

         ret

FormDsk  ENDP

; -----------------------------------------------------------------------------
;        test stopy n hodn˜m z pisem
; -----------------------------------------------------------------------------

TestRnd  PROC      NEAR

; ------ kontrola, zda se prov dˆj¡ testovac¡ z pisy

         mov       al,ds:[ParTest]          ; po‡et testovac¡ch z pis–
         cmp       al,0                     ; jsou testovac¡ z pisy ?
         jne       TestRnd1                 ; jsou testovac¡ z pisy
         ret

; ------ p©¡prava registr–

TestRnd1:mov       ah,0
         mov       bp,ax                    ; ‡¡ta‡ testovac¡ch z pis–

; ------ je-li posledn¡ z pis, je nulovac¡ konstanta

TestRnd2:mov       es,ds:[BufferW]          ; buffer k z pisu
         xor       di,di                    ; po‡ tek bufferu
         mov       cx,word ptr ds:[NumSekt] ; po‡et sektor– na stopu
         cmp       bp,1                     ; je posledn¡ z pis
         jne       TestRnd3                 ; nen¡ posledn¡ z pis
         mov       ch,cl                    ; po‡et sektor–
         mov       cl,0                     ; CX = po‡et slov dat
         mov       al,ds:[ParChar]          ; nulovac¡ konstanta
         mov       ah,al
         cld
         rep       stosw                    ; vymaz n¡ dat
         jmp       short TestRnd4

; ------ p©¡prava dat k z pisu - n hodn  data

TestRnd3:call      RandomS                  ; vygenerov n¡ sektoru
         loop      TestRnd3                 ; dal¨¡ sektor

; ------ z pis dat na disk

TestRnd4:mov       al,ds:[NumSekt]          ; po‡et sektor– k z pisu
         mov       cl,1                     ; po‡ te‡n¡ sektor
         xor       bx,bx                    ; po‡ tek dat
         call      Zapis                    ; z pis dat na disk
         jc        TestRnd9                 ; chyba dat

; ------ kontroln¡ ‡ten¡ dat

         mov       al,ds:[NumSekt]
         mov       cl,1
         xor       bx,bx
         mov       es,ds:[BufferR]          ; ‡tec¡ buffer
         call      Cteni
         jc        TestRnd9

; ------ porovn n¡ dat

         push      ds
         mov       ch,ds:[NumSekt]
         mov       cl,0
         mov       ds,ds:[BufferW]
         xor       si,si
         xor       di,di
         cld
         repe      cmpsw                    ; porovn n¡ dat
         pop       ds
         stc
         jne       TestRnd9                 ; data nesouhlas¡

         dec       bp
         jnz       TestRnd2                 ; dal¨¡ z pis
         clc

TestRnd9:ret

TestRnd  ENDP

; -----------------------------------------------------------------------------
;        na‡ten¡ BOOT sektoru
; -----------------------------------------------------------------------------

ReadBoot PROC      NEAR






         ret

ReadBoot ENDP

; -----------------------------------------------------------------------------
;        instalace obsluhy INT 1eh
; -----------------------------------------------------------------------------

Inst1e   PROC      NEAR

; ------ £schova adresy INT 1eh

         mov       ax,351eh
         int       21h
         mov       word ptr ds:[Old1e],bx
         mov       word ptr ds:[Old1e+2],es

; ------ instalace nov‚ adresy INT 1eh

         mov       ax,251eh
         mov       dx,offset Int1e
         int       21h
         ret

Inst1e   ENDP

; -----------------------------------------------------------------------------
;        odinstalov n¡ INT 1eh
; -----------------------------------------------------------------------------

DeInst1e PROC      NEAR

         push      ds
         mov       ax,251eh
         lds       dx,ds:[Old1e]
         int       21h                      ; n vrat INT 1eh
         pop       ds
         ret

DeInst1e ENDP

; -----------------------------------------------------------------------------
;        nastaven¡ s‚riov‚ho ‡¡sla
; -----------------------------------------------------------------------------

SetSer   PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx

; ------ stanoven¡ syst‚mov‚ho ‡asu

         mov       ah,2ch
         int       21h                      ; poskytnut¡ syst‚mov‚ho ‡asu

         mov       ah,0
         mov       al,dl                    ; setina sekundy 7 bit–
         shr       al,1                     ; setina sekundy 6 bit–

         mov       dl,0
         shr       dx,1
         shr       dx,1
         or        ax,dx                    ; sekunda 6 bit– (p©esahuj¡ 4 bity)

         mov       dh,0
         mov       dl,cl                    ; minuta
         mov       cl,4                     ; po‡et rotac¡
         shl       dx,cl
         or        ah,dl                    ; minuty 4 bity
         mov       dl,dh                    ; minuty 2 bity

         mov       dh,ch                    ; hodina
         shl       dh,1
         shl       dh,1
         or        dl,dh                    ; hodina 5 bit– (zb˜v  1 bit)
         mov       dh,0

         mov       word ptr ds:[BSerie+2],ax
         mov       word ptr ds:[BSerie],dx  ; v DX je 7 bit–

; ------ stanoven¡ syst‚mov‚ho data

         mov       ah,2ah
         int       21h                      ; poskytnut¡ syst‚mov‚ho data

         mov       ah,dh                    ; mˆs¡c
         mov       cl,4
         shl       ah,cl                    ; mˆs¡c 4 bity
         mov       dh,0
         mov       cl,7
         shl       dx,cl                    ; den 5 bit–
         or        ax,dx                    ; mˆs¡c + den
         or        word ptr ds:[BSerie],ax  ; + mˆs¡c a den

; ------ n vrat registr–

         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

SetSer   ENDP

; -----------------------------------------------------------------------------
;        za©azen¡ sektor– stopy
; -----------------------------------------------------------------------------

StorSekt PROC      NEAR

; ------ p©¡prava registr–

         mov       cx,word ptr ds:[NumSekt] ; po‡et sektor– na stopu
         mov       si,offset TabBad         ; tabulka vadn˜ch sektor–
         mov       al,ds:[AktStop]          ; aktu ln¡ stopa
         mul       byte ptr ds:[NumStran]   ; po‡et ji‘ zpracovan˜ch stop
         add       al,ds:[AktStran]         ; p©i‡ten¡ aktu ln¡ strany
         mul       cl                       ; absolutn¡ ‡¡slo sektoru stopy
         xchg      ax,bx                    ; BX <- ukazatel ‡¡sla sektoru

; ------ na‡ten¡ parametr– sektoru

StorSkt1:cld
         lodsb                              ; na‡ten¡ sektoru
         push      bx

; ------ ozna‡en¡ sektoru BOOT

         sub       bx,1                     ; je BOOT sektor ?
         ja        StorSkt2                 ; nen¡ BOOT sektor
         je        StorSk22
         mov       ds:[ErrBoot],al          ; p©¡znak BOOT sektoru
         jmp       short StorSkt9

; ------ ozna‡en¡ sektoru FAT 1

StorSk22:mov       ds:[ErrFat1],al
StorSkt2:sub       bx,word ptr ds:[FatSekt] ; ode‡ten¡ sektor– FAT 1
         ja        StorSkt3
         je        StorSk32
         or        ds:[ErrFat1],al          ; p©¡znak FAT 1
         jmp       short StorSkt9

; ------ ozna‡en¡ sektoru FAT 2

StorSk32:mov       ds:[ErrFat2],al
StorSkt3:sub       bx,word ptr ds:[FatSekt] ; ode‡ten¡ sektor– FAT 2
         ja        StorSkt4
         je        StorSk42
         or        ds:[ErrFat2],al          ; p©¡znak FAT 2
         jmp       short StorSkt9

; ------ ozna‡en¡ sektoru ROOT

StorSk42:mov       ds:[ErrRoot],al
StorSkt4:sub       bx,word ptr ds:[RootSekt] ; ode‡ten¡ sektor– na ROOT
         jae       StorSkt5
         or        ds:[ErrRoot],al          ; p©¡znak ROOT
         jmp       short StorSkt9

; ------ vadn  data - ozna‡en¡ sektoru v tabulce FAT

StorSkt5:cmp       al,0                     ; je sektor OK ?
         je        StorSk52                 ; sektor je OK
         push      ax
         push      bx
         cmp       byte ptr ds:[BlokSekt],1
         je        StorSk51
         shr       bx,1                     ; sektor / 2
StorSk51:mov       ax,0ff7h
         inc       bx
         inc       bx                       ; za‡¡n  se od bloku 2
         call      SetFat                   ; ozna‡en¡ vadn‚ho bloku BX ve FAT
         pop       bx
         pop       ax

; ------ zv˜¨en¡ ‡¡ta‡e dat

StorSk52:cmp       byte ptr ds:[BlokSekt],1 ; je 1 sektor na blok ?
         je        StorSkt7                 ; je 1 sektor na blok
         test      bl,1                     ; je lich˜ datov˜ sektor ?
         jnz       StorSkt6                 ; je lich˜ datov˜ sektor
         mov       ds:[ErrDat],al           ; £schova p©¡znaku sektoru
         jmp       short StorSkt9
StorSkt6:or        al,ds:[ErrDat]           ; slou‡en¡ se sud˜m sektorem
StorSkt7:cmp       al,0                     ; je blok OK ?
         mov       ax,ds:[BlokByte]         ; po‡et bajt– na aloka‡n¡ blok
         je        StorSkt8                 ; blok je OK
         add       word ptr ds:[CitBad],ax  ; ‡¡ta‡ vadn˜ch blok–
         adc       word ptr ds:[CitBad+2],0
         jmp       short StorSkt9

StorSkt8:add       word ptr ds:[CitVol],ax  ; ‡¡ta‡ voln˜ch blok–
         adc       word ptr ds:[CitVol+2],0

; ------ p©¡prava pro dal¨¡ sektor

StorSkt9:pop       bx
         inc       bx
         dec       cx
         jz        StorSktA
         jmp       StorSkt1                 ; dal¨¡ sektor
StorSktA:ret

StorSekt ENDP

; -----------------------------------------------------------------------------
;        nastaven¡ polo‘ky BX v tabulce FAT na hodnotu AX
; -----------------------------------------------------------------------------

SetFat   PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      ds

; ------ stanoven¡ adresy polo‘ky ve FAT tabulce

         mov       cx,bx                    ; ‡¡slo bloku
         shr       bx,1                     ; ‡¡slo bloku / 2
         add       bx,cx                    ; ‡¡slo bloku * 1.5
         mov       ds,ds:[BufferF]          ; buffer s tabulkou FAT

; ------ nastaven¡ hodnoty FAT - sud  polo‘ka

         test      cl,1                     ; je sud  polo‘ka ?
         jnz       SetFat3                  ; je lich  polo‘ka
         and       word ptr ds:[bx],0f000h  ; nulov n¡ star‚ hodnoty
         jmp       short SetFat4

; ------ nastaven¡ hodnoty FAT - lich  polo‘ka

SetFat3: and       word ptr ds:[bx],0fh     ; nulov n¡ star‚ hodnoty
         mov       cl,4                     ; po‡et rotac¡
         shl       ax,cl                    ; rotace hodnoty na pozici
SetFat4: or        ds:[bx],ax               ; nastaven¡ nov‚ hodnoty

; ------ n vrat registr–

SetFat9: pop       ds
         pop       cx
         pop       bx
         pop       ax
         ret

SetFat   ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                              Obsluha disku
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;þ
; -----------------------------------------------------------------------------
;        na‡ten¡ obsahu vadn‚ stopy
; -----------------------------------------------------------------------------

ReadBad  PROC      NEAR

; ------ inicializace tabulky vadn˜ch stop

         push      ds
         pop       es
         mov       di,offset TabBad         ; tabulka vadn˜ch stop
         mov       cx,word ptr ds:[NumSekt] ; po‡et sektor– na stopu
         mov       al,-1                    ; nulovac¡ hodnota
         cld
         rep       stosb                    ; vymaz n¡ tabulky vadn˜ch stop

; ------ p©¡prava ukazatel–

         cmp       byte ptr ds:[ParOptim],0 ; dˆl  se optimalizace chyb ?
         je        ReadBad9                 ; optimalizace se nedˆl 
         mov       es,ds:[BufferR]          ; buffer pro ‡ten¡
         mov       dh,0
         mov       ch,ds:[NumSekt]          ; po‡et sektor– na stopu

; ------ p©¡prava ukazatel– pro ‡ten¡ skupiny blok– velikosti CL

ReadBad3:shr       ch,1
         adc       ch,0                     ; polovina po‡tu sektor–
         shr       ch,1
         adc       ch,0                     ; ‡tvrtina po‡tu sektor–
         mov       cl,1                     ; ‡¡slo po‡ te‡n¡ho sektoru
         xor       bx,bx                    ; adresa za‡ tku bufferu
         mov       di,offset TabBad         ; tabulka vadn˜ch sektor–

; ------ stanoven¡ skute‡n‚ho po‡tu zbyl˜ch sektor–

ReadBad4:mov       dl,ch                    ; DH <- po‡et sektor– ke ‡ten¡
         add       dl,cl                    ; ‡¡slo dal¨¡ho sektoru
         cmp       dl,ds:[NumSekt]          ; p©ete‡en¡ po‡tu sektor– ?
         jbe       ReadBad5                 ; ‡¡slo sektoru je OK
         mov       dl,ds:[NumSekt]          ; omezen¡ max. ‡¡sla sektoru
         inc       dx                       ; n sleduj¡c¡ sektor
ReadBad5:sub       dl,cl                    ; po‡et sektor– ke ‡ten¡

; ------ test, zda je pot©eba ‡¡st zvolenou skupinku sektor–

         push      cx
         push      di
         push      es
         push      cs
         pop       es                       ; ES <- CS
         mov       al,-1                    ; hledan˜ vadn˜ sektor
         mov       cx,dx                    ; po‡et sektor– ke ‡ten¡
         cld
         repne     scasb                    ; nalezen¡ vadn‚ho sektoru
         pop       es
         pop       di
         pop       cx
         jne       ReadBad7                 ; vadn˜ sektor nen¡, nen¡ t©eba ‡¡st

; ------ na‡ten¡ jedn‚ skupinky sektor–

         push      cx
         push      dx
         xchg      ax,dx                    ; AL <- po‡et sektor– ke ‡ten¡
         call      Cteni                    ; ‡ten¡ jedn‚ skupiny sektor–
         pop       dx
         pop       cx
         jc        ReadBad7                 ; ‡ten¡ ne£spˆ¨n‚

; ------ p©i £spˆ¨n‚m ‡ten¡ nulov n¡ p©e‡ten˜ch sektor–

         push      cx
         push      es
         push      di
         push      cs
         pop       es
         cld
         mov       al,0
         mov       cx,dx                    ; po‡et na‡ten˜ch sektor–
         rep       stosb                    ; vynulov n¡ p©e‡ten˜ch sektor–
         pop       di
         pop       es
         pop       cx

; ------ zv˜¨en¡ ukazatele sektor–

ReadBad7:add       bh,ch
         add       bh,ch                    ; zv˜¨en¡ adresy dat v bufferu
         add       di,dx                    ; zv˜¨en¡ ukazatele v tabulce chyb
         add       cl,dl                    ; zv˜¨en¡ ukazatele sektor–
         cmp       cl,ds:[NumSekt]          ; jsou ji‘ v¨echny sektory ?
         jbe       ReadBad4                 ; ‡ten¡ dal¨¡ skupinky sektor–

; ------ p©¡prava pro ‡ten¡ jemnˆj¨¡ho dˆlen¡ skupin

         cmp       ch,1                     ; byl ji‘ 1 sektor ?
         jne       ReadBad3                 ; ‡ten¡ dal¨¡ho dˆlen¡ sektor–
ReadBad9:ret

ReadBad  ENDP

; -----------------------------------------------------------------------------
;        p©¡prava parametr– aktivn¡ho disku
; -----------------------------------------------------------------------------

IniDPar  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si

; ------ p©¡prava ukazatele aktivn¡ho disku

         mov       bl,ds:[AktDisk]          ; aktivn¡ disk
         cmp       bl,ds:[NumDisk]          ; je platn‚ ‡¡slo disku ?
         jb        IniDPar1                 ; ‡¡slo aktivn¡ho disku je OK
         mov       bl,ds:[NumDisk]          ; po‡et disk–
         dec       bx                       ; omezen¡ na posledn¡ disk
         mov       ds:[AktDisk],bl          ; nov˜ aktivn¡ disk
IniDPar1:mov       bh,0

; ------ £schova aktivn¡ho form tu aktivn¡ho disku

         mov       al,ds:[AktForm]          ; aktivn¡ form t
         cmp       al,-1                    ; je aktivn¡ form t platn˜ ?
         je        IniDPar2                 ; aktivn¡ form t nen¡ platn˜
         mov       ds:[AktFormT+bx],al      ; £schova aktivn¡ho form tu

; ------ tabulka definice form t– disku

IniDPar2:mov       al,ds:[TypDiskT+bx]      ; typ aktivn¡ho disku
         mov       ds:[TypDisk],al          ; typ aktivn¡ho disku
         mov       ah,0
         shl       ax,1                     ; typ aktivn¡ho disku * 2
         xchg      ax,si                    ; SI <- typ aktivn¡ho disku * 2
         mov       si,ds:[TabForm+si-2]     ; adresa definice form tu disku

; ------ po‡et form t– disku

         mov       ah,ds:[si]               ; po‡et platn˜ch form t– disku
         mov       ds:[NumForm],ah          ; po‡et form t– disku
         inc       si                       ; adresa prvn¡ho form tu

; ------ n vrat aktivn¡ho form tu disku

         mov       al,ds:[AktFormT+bx]      ; aktivn¡ form t aktivn¡ho disku
         cmp       al,ah                    ; je ‡¡slo form tu platn‚ ?
         jb        IniDPar3                 ; ‡¡slo form tu je platn‚
         mov       al,ah                    ; omezen¡ ‡¡sla form tu
         dec       al
IniDPar3:mov       ds:[AktForm],al          ; aktivn¡ form t

; ------ adresa aktivn¡ho form tu disku

;         dec       al                       ; aktivn¡ form t - 1
;         jns       IniDPar4
;         jmp       IniDPar9                 ; je automatick˜ form t
IniDPar4:mov       ah,FORMOFFS              ; d‚lka definice jednoho form tu
         mul       ah                       ; offset aktivn¡ho form tu
         add       si,ax                    ; adresa aktivn¡ho form tu

; ------ po‡et stop

         mov       al,39                    ; 40 stop
         test      byte ptr ds:[si+0],b0    ; je 80 stop ?
         jz        IniDPr41                 ; je 40 stop
         mov       al,79                    ; jinak 80 stop
IniDPr41:mov       ds:[TypStop],al          ; jmenovit˜ po‡et stop - 1
         inc       ax                       ; jmenovit˜ po‡et stop
         add       al,ds:[ParStop]          ; p©i‡ten¡ dopl¤uj¡c¡ch stop
         mov       ds:[NumStop],al          ; po‡et stop

; ------ po‡et stran

         mov       al,1                     ; 1 strana
         cmp       byte ptr ds:[ParStra],0  ; je 1 strana ?
         jne       IniDPr42                 ; je 1 strana
         mov       al,2                     ; 2 strany
IniDPr42:mov       ds:[NumStran],al         ; po‡et stran

; ------ p©¡znak dvojit‚ho krokov n¡

         mov       al,1                     ; p©¡znak dvojit‚ho krokov n¡
         test      byte ptr ds:[si+0],b2    ; je dvojit‚ krokov n¡ ?
         jnz       IniDPr43                 ; je dvojit‚ krokov n¡
         mov       al,0                     ; nen¡ dvojit‚ krokov n¡
IniDPr43:mov       ds:[Krokov],al           ; p©¡znak dvojit‚ho krokov n¡

; ------ po‡et sektor– na blok

         mov       al,1                     ; 1 sektor na blok
         cmp       byte ptr ds:[NumStran],1 ; je 1 strana ?
         je        IniDPr44                 ; je 1 strana
         test      byte ptr ds:[si+0],b1    ; 2 sektory na blok ?
         jz        IniDPr44                 ; 1 sektor na blok
         mov       al,2                     ; 2 sektory na blok
IniDPr44:mov       ds:[BlokSekt],al         ; po‡et sektor– na blok

; ------ po‡et sektor– na stopu

         mov       al,ds:[si+1]             ; po‡et sektor– na stopu
         mov       ds:[NumSekt],al          ; po‡et sektor– na stopu

; ------ odsazen¡ sektor– SLIDING

         mov       byte ptr ds:[SlidX],0    ; vypnut¡ funkce Sliding
         mov       byte ptr ds:[SlidY],0
         test      byte ptr ds:[si+0],b5    ; prov d¡ se SLIDING ?
         jz        IniDP444                 ; SLIDING se neprov d¡
         cmp       al,13                    ; je disketa HD ?
         mov       al,1                     ; SLIDING pro disketu DD = 1
         jbe       IniDP442                 ; je disketa DD
         inc       ax                       ; SLIDING pro disketu HD = 2
IniDP442:mov       ds:[SlidX],al            ; Sliding stran
         inc       ax
         mov       ds:[SlidY],al            ; Sliding stop

; ------ faktor prokl d n¡ sektor–

IniDP444:mov       al,1                     ; faktor prokl d n¡ = 1
         test      byte ptr ds:[si+0],b4    ; je prokl d n¡ sektor– = 2 ?
         jz        IniDP445                 ; je prokl d n¡ = 1
         inc       ax                       ; faktor prokl d n¡ = 2
IniDP445:mov       ds:[Inter],al            ; faktor prokl d n¡ sektor–

; ------ po‡et sektor– na ROOT

         mov       al,ds:[si+2]             ; po‡et sektor– na ROOT
         cmp       byte ptr ds:[NumStran],1 ; je 1 strana ?
         jne       IniDPr45                 ; jsou 2 strany
         shr       al,1                     ; po‡et sektor–/2
         adc       al,0                     ; p©enos CF
IniDPr45:cmp       byte ptr ds:[ParRoot],0  ; zad n po‡et polo‘ek na ROOT ?
         je        IniDPr46                 ; nen¡ zad n po‡et polo‘ek na Root
         mov       byte ptr ds:[BlokSekt],1 ; v‘dy je 1 sektor na blok
         mov       al,ds:[ParRoot]          ; po‡et polo‘ek na ROOT
IniDPr46:mov       ds:[RootSekt],al         ; po‡et sektor– na ROOT
         mov       ah,0
         shl       ax,1
         shl       ax,1
         shl       ax,1
         shl       ax,1
         mov       ds:[RootMax],ax          ; po‡et polo‘ek na ROOT

; ------ popisova‡ m‚dia

         mov       al,ds:[si+3]             ; popisova‡ m‚dia
         cmp       byte ptr ds:[NumStran],1 ; je 1 strana ?
         je        IniDPr47                 ; je 1 strana
         or        al,1                     ; korekce pro 2 strany
IniDPr47:mov       ds:[Popis],al            ; popisova‡ m‚dia

; ------ celkov˜ po‡et sektor– m‚dia

         mov       al,ds:[NumSekt]          ; po‡et sektor– na stopu
         mul       byte ptr ds:[NumStran]   ; vyn soben¡ po‡tem stran
         mul       byte ptr ds:[NumStop]    ; vyn soben¡ po‡tem sektor–
         mov       ds:[CelkSekt],ax         ; celkov˜ po‡et sektor– m‚dia

; ------ p©¡prava k nalezen¡ dostate‡n‚ho po‡tu sektor– na FAT

         mov       byte ptr ds:[FatSekt],1  ; v˜choz¡ hodnota - 1 sektor na FAT

; ------ po‡et aloka‡n¡ch blok–

IniDPar5:mov       ax,ds:[CelkSekt]         ; celkov˜ po‡et sektor–
         dec       ax                       ; ode‡ten¡ BOOT sektoru
         mov       bh,0
         mov       bl,ds:[FatSekt]          ; po‡et sektor– na FAT
         sub       ax,bx                    ; ode‡ten¡ sektor– na FAT 1
         sub       ax,bx                    ; ode‡ten¡ sektor– na FAT 2
         sub       al,ds:[RootSekt]         ; ode‡ten¡ sektor– na ROOT
         sbb       ah,0
         xor       dx,dx
         mov       bl,ds:[BlokSekt]         ; po‡et sektor– na aloka‡n¡ blok
         div       bx                       ; v˜po‡et po‡tu aloka‡n¡ch blok–
         mov       ds:[CelkBlok],ax         ; celkov˜ po‡et aloka‡n¡ch blok–

; ------ v˜po‡et kapacity tabulek FAT

         mov       ah,ds:[FatSekt]          ; po‡et sektor– na FAT
         mov       al,0
         shl       ax,1                     ; po‡et bajt– na FAT
         shl       ax,1                     ; po‡et tetr d
         xor       dx,dx
         mov       bl,3                     ; po‡et tetr d na polo‘ku FAT
         div       bx                       ; v˜po‡et po‡tu polo‘ek FAT
         sub       ax,2 + 16                ; ode‡ten¡ rezervovan˜ch hodnot

; ------ kontrola, zda je po‡et polo‘ek FAT posta‡uj¡c¡

         cmp       ax,ds:[CelkBlok]         ; sta‡¡ po‡et polo‘ek FAT ?
         jae       IniDPar6                 ; po‡et polo‘ek FAT je posta‡uj¡c¡
         inc       byte ptr ds:[FatSekt]    ; zv˜¨en¡ po‡tu polo‘ek FAT
         jmp       short IniDPar5           ; nov˜ test

; ------ po‡et bajt– na aloka‡n¡ blok

IniDPar6:mov       ah,ds:[BlokSekt]         ; po‡et sektor– na aloka‡n¡ blok
         mov       al,0
         shl       ax,1                     ; po‡et sektor– * 512 bajt–
         mov       ds:[BlokByte],ax         ; po‡et bajt– na aloka‡n¡ blok

; ------ v˜po‡et celkov‚ kapacity disku

         mov       ax,ds:[CelkBlok]         ; celkov˜ po‡et aloka‡n¡ch blok–
         mul       word ptr ds:[BlokByte]   ; vyn soben¡ po‡tem bajt– na blok
         mov       word ptr ds:[CelkKap],ax ; celkov  kapacita LOW
         mov       word ptr ds:[CelkKap+2],dx ; celkov  kapacita HIGH

; ------ n vrat registr–

IniDPar9:pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

IniDPar  ENDP

; -----------------------------------------------------------------------------
;        stanoven¡ po‡tu a typ– disketov˜ch mechanik
; -----------------------------------------------------------------------------

GetDTyp  PROC      NEAR

; ------ stanoven¡ po‡tu disketov˜ch mechanik v syst‚mu

         push      ds
         int       11h                      ; poskytnut¡ v˜pisu vybaven¡
         pop       ds
         test      al,b0                    ; je v syst‚mu nˆjak  mechanika ?
         jz        GetDTyp9                 ; v syst‚mu nen¡ ‘ dn  mechanika
         rol       al,1
         rol       al,1
         and       al,3                     ; po‡et disketov˜ch mechanik - 1
         inc       ax                       ; po‡et disketov˜ch mechanik
         mov       ds:[NumDisk],al          ; po‡et disketov˜ch mechanik

; ------ stanoven¡ typ– jednotliv˜ch disk–

         mov       si,offset TypDiskT       ; tabulka typ– disk–
         mov       dl,0                     ; ukazatel ‡¡sla disku

GetDTyp1:push      dx
         push      si

         mov       ax,86eh                  ; funkce 8, AL=nesmysl pro kontrolu
         xor       cx,cx                    ; p©ednastaven¡ nesmysl–
         mov       bl,0                     ; p©ednastaven¡ - nezn m˜ typ
         call      Int13                    ; poskytnut¡ parametr– disku

         pop       si
         pop       dx

; ------ stanoven¡ typu disku

         or        ax,ax
         jnz       GetDTyp3                 ; funkce nen¡ podporovan 
         cmp       bl,0                     ; je typ mechaniky zn m ?
         je        GetDTyp2                 ; typ mechaniky nen¡ zn m
         cmp       bl,5
         jbe       GetDTyp4                 ; typ mechaniky je OK
GetDTyp2:cmp       ch,79                    ; je 80 stop ?
         jb        GetDTyp3                 ; je asi mechanika 360 KB
         mov       bl,3                     ; mechanika 702 KB
         cmp       cl,13
         jbe       GetDTyp4                 ; mechanika 720 KB
         mov       bl,2                     ; mechanika 1.2 MB
         cmp       cl,16
         jbe       GetDTyp4                 ; mechanika 1.2 MB
         mov       bl,4                     ; mechanika 1.44 MB
         cmp       cl,20
         jbe       GetDTyp4                 ; mechanika 1.44 MB
         mov       bl,5                     ; jinak mechanika 2.88 MB
         jmp       short GetDTyp4

; ------ p©¡prava pro dal¨¡ disk

GetDTyp3:mov       bl,1                     ; mechanika 360 KB
GetDTyp4:mov       ds:[si],bl               ; typ disketov‚ mechaniky
         inc       si                       ; zv˜¨en¡ ukazatele v tabulce
         inc       dx                       ; zv˜¨en¡ ‡¡sla disku
         cmp       dl,ds:[NumDisk]          ; jsou ji‘ v¨echny disky ?
         jb        GetDTyp1                 ; stanoven¡ dal¨¡ho disku

GetDTyp9:ret

GetDTyp  ENDP

; -----------------------------------------------------------------------------
;        zform tov n¡ aktu ln¡ stopy (CY=chyba, AH=k¢d chyby)
; -----------------------------------------------------------------------------

Format   PROC      NEAR

; ------ zobrazen¡ indik toru form tov n¡

         mov       al,"F"
         mov       ah,ds:[ColForm]          ; barva
         call      DispFCh                  ; zobrazen¡ indik toru

; ------ stanoven¡ ‡¡sla po‡ te‡n¡ho sektoru

         mov       al,ds:[SlidX]            ; odsazen¡ sektor– stran
         add       al,ds:[SlidY]            ; selkov‚ odsazen¡ na stopu
         mul       byte ptr ds:[AktStop]    ; posun odsazen¡m p©ede¨l˜ch stop
         cmp       byte ptr ds:[AktStran],0 ; je strana 0 ?
         je        Format02                 ; je strana 0
         add       al,ds:[SlidX]            ; je¨tˆ jedno odsazen¡ strany
         adc       ah,0                     ; p©enos
Format02:div       byte ptr ds:[NumSekt]    ; maskov n¡ MOD po‡et sektor–
         mov       al,ah                    ; po‡ te‡n¡ ‡¡slo sektoru - 1
         inc       ax                       ; po‡ te‡n¡ ‡¡slo sektoru

; ------ p©¡prava registr– ke generov n¡ tabulky

         clc
         xor       di,di
         mov       es,ds:[BufferW]          ; z pisov˜ buffer
         mov       dl,ds:[AktStop]          ; aktu ln¡ form tovan  stopa
         mov       dh,ds:[AktStran]         ; aktu ln¡ form tovan  strana
         mov       ah,2                     ; velikost sektoru
         mov       ch,0
         mov       cl,ds:[NumSekt]          ; po‡et sektor– na stopu
         mov       bp,cx                    ; po‡et sektor– na stopu
         shl       bp,1
         shl       bp,1                     ; * 4 = offset konce tabulky
         mov       bl,ds:[Inter]            ; faktor prokl d n¡
         mov       bh,0
         dec       bx
         shl       bx,1
         shl       bx,1                     ; faktor prokl d n¡ * 4 - 4

; ------ generov n¡ defini‡n¡ tabulky stopy

Format1: xchg      ax,dx
         stosw                              ; ‡¡slo v lce a hlavy
         xchg      ax,dx
         stosw                              ; ‡¡slo a velikost sektoru
         add       di,bx                    ; korekce prokl d n¡
         inc       ax                       ; zv˜¨en¡ ‡¡sla stopy
         cmp       al,ds:[NumSekt]          ; p©ekro‡eno ‡¡slo sektoru ?
         jbe       Format2                  ; ‡¡slo sektoru je OK
         mov       al,1                     ; korekce na sektor 1
Format2: cmp       di,bp                    ; p©ekro‡en konec tabulky ?
         jb        Format3                  ; adresa je OK
         mov       di,4                     ; korekce adresy na 2. sektor
Format3: loop      Format1

; ------ form tov n¡ stopy

         mov       ah,5
         inc       cl                       ; po‡ te‡n¡ ‡¡slo sektoru = 1
         mov       al,ds:[NumSekt]          ; po‡et sektor–
         xor       bx,bx                    ; adresa tabulky definice sektor–
         mov       dl,ds:[AktDisk]          ; aktivn¡ disk
         mov       ch,ds:[AktStop]          ; aktu ln¡ stopa
         call      Int13                    ; form tov n¡ stopy
         ret

Format   ENDP

; -----------------------------------------------------------------------------
;     ‡ten¡ AL sektor– od sektoru CL do bufferu ES:BX -> CY chyba, AH=k¢d chyby
; -----------------------------------------------------------------------------

Cteni    PROC      NEAR

; ------ zobrazen¡ indik toru

         push      ax
         mov       al,"R"
         mov       ah,ds:[ColRead]          ; barva
         call      DispFCh                  ; zobrazen¡ indik toru
         pop       ax

; ------ ‡ten¡ dat z disku

         mov       ah,2
         mov       ch,ds:[AktStop]          ; ‡¡slo v lce
         mov       dh,ds:[AktStran]         ; ‡¡slo strany
         call      Int130                   ; z pis dat na disk
         ret

Cteni    ENDP

; -----------------------------------------------------------------------------
;     z pis AL sektor– od sektoru CL z bufferu ES:BX -> CY chyba, AH=k¢d chyby
; -----------------------------------------------------------------------------

Zapis    PROC      NEAR

; ------ zobrazen¡ indik toru

         push      ax
         mov       al,"W"
         mov       ah,ds:[ColWrite]         ; barva
         call      DispFCh                  ; zobrazen¡ indik toru
         pop       ax

; ------ z pis dat na disk

         mov       ah,3
         mov       ch,ds:[AktStop]          ; ‡¡slo v lce
         mov       dh,ds:[AktStran]         ; ‡¡slo strany
         call      Int130                   ; z pis dat na disk
         ret

Zapis    ENDP

; -----------------------------------------------------------------------------
;        verifikace dat diskety (CY=chyba, AH=k¢d chyby)
; -----------------------------------------------------------------------------

Verif    PROC      NEAR

; ------ zobrazen¡ indik toru

         mov       al,"V"
         mov       ah,ds:[ColVerif]         ; barva
         call      DispFCh                  ; zobrazen¡ indik toru

; ------ verifikace stopy z disku

         xor       bx,bx                    ; offset adresy
         mov       es,bx
         mov       al,ds:[NumSekt]          ; po‡et sektor–
         mov       ah,4
         mov       ch,ds:[AktStop]          ; ‡¡slo v lce
         mov       cl,1                     ; po‡ te‡n¡ ‡¡slo sektoru
         mov       dh,ds:[AktStran]         ; strana
         call      Int130                   ; ‡ten¡ dat z disku
         ret

Verif    ENDP

; -----------------------------------------------------------------------------
;        nastaven¡ form tu diskety pro form tov n¡ (CY=chyba, AH=k¢d chyby)
; -----------------------------------------------------------------------------

SetForm  PROC      NEAR

; ------ nastaven¡ form tu slu‘bou 18h

         mov       bp,3                     ; po‡et pokus–
SetForm1:mov       ah,18h
         mov       ch,ds:[TypStop]          ; jmenovit˜ po‡et stop disku
         mov       cl,ds:[NumSekt]          ; max. po‡et sektor–
         cmp       cl,8
         jne       SetForm3
         mov       cl,9
SetForm3:call      Int13                    ; nastaven¡ form tu diskety
         jnc       SetForm4
         cmp       ah,1
         je        SetForm5                 ; funkce nepodporovan 

         call      ResDisk
         dec       bp
         jnz       SetForm1                 ; dal¨¡ pokus
         stc
         jmp       short SetForm9           ; chyba

; ------ inicializace tabulky parametr–

SetForm4:mov       bl,cl
         mov       cx,12
         push      ds
         push      ds
         push      es
         pop       ds
         pop       es
         mov       si,di
         mov       di,offset Int1e
         rep       movsb                    ; £schova tabulky parametr–
         pop       ds
         mov       cl,bl

SetForm5:mov       bl,cl
         mov       si,offset Int1e
         mov       byte ptr ds:[si+2],4     ; ‡as pro vypnut¡ motoru
         mov       byte ptr ds:[si+3],2     ; velikost sektoru 512 bajt–
         mov       byte ptr ds:[si+4],bl    ; po‡et sektor– na stopu
         mov       byte ptr ds:[si+8],0     ; plnic¡ znak form tov n¡
         clc                                ; p©¡znak operace OK

SetForm9:
         ret

SetForm  ENDP

; -----------------------------------------------------------------------------
;        reset diskov‚ho syst‚mu
; -----------------------------------------------------------------------------

ResDisk  PROC      NEAR

         push      ax
         mov       ah,0
         call      Int130
         pop       ax
         ret

ResDisk  ENDP

; -----------------------------------------------------------------------------
;        obsluha INT 13 s £schovou v¨ech registr–
; -----------------------------------------------------------------------------

Int130   PROC      NEAR

         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      es

         call      Int13

         pop       es
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         ret

Int130   ENDP

; -----------------------------------------------------------------------------
;        obsluha INT 13 s £schovou registr–
; -----------------------------------------------------------------------------

Int13    PROC      NEAR

         push      bp
         push      ds

         sti
         mov       dl,ds:[AktDisk]          ; aktivn¡ disk
         int       13h

         pop       ds
         pop       bp
         ret

Int13    ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                               Volby parametr–
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;þ
; -----------------------------------------------------------------------------
;        volby parametr–
; -----------------------------------------------------------------------------

Volby    PROC      NEAR

; ------ zobrazen¡ voleb

         mov       byte ptr ds:[AktVolby],1 ; p©¡znak aktivn¡ch voleb
         call      DispVol                  ; zobrazen¡ voleb
         call      DispADsk                 ; zobrazen¡ disk–
         call      KurzOff

; ------ vstup z ovlada‡e my¨i

Volby1:  call      MouseGet                 ; test my¨i
         cmp       bl,4
         jne       Volby11
         jmp       Volby7                   ; stisk prav‚ho tla‡¡tka
Volby11: cmp       bl,6
         je        Volby12                  ; druh˜ stisk lev‚ho tla‡¡tka
         cmp       bl,3
         je        Volby12                  ; stisk lev‚ho tla‡¡tka
         test      byte ptr ds:[MouseKey],b0 ; je lev‚ tla‡¡tko stisknuto ?
         je        Volby5                   ; nen¡ lev‚ tla‡¡tko stisknuto
         jmp       short Volby22            ; vodˆn¡ kurzoru po polo‘k ch

; ------ test, zda je ovl d n¡ n povˆdy

Volby12: cmp       dh,24
         jne       Volby2
         cmp       dl,42
         jb        Volby5                   ; neplatn  kl vesa
         mov       bx,0dh
         cmp       dl,54
         jbe       Volby6                   ; ENTER
         cmp       dl,65
         jbe       Volby7                   ; ESC
         mov       bx,3b00h
         jmp       short Volby6             ; jinak F1

; ------ test, zda je platn˜ © dek polo‘ek

Volby2:  cmp       dh,9
         ja        Volby7                   ; p©eru¨en¡ voleb
Volby22: cmp       dh,3
         jb        Volby5
         cmp       dh,7
         ja        Volby5

; ------ rozli¨en¡ sloupce polo‘ek

         or        dl,dl
         jz        Volby5
         cmp       dl,79
         je        Volby5
         cmp       dl,25
         jae       Volby3
         mov       dl,4
         jmp       short Volby4
Volby3:  cmp       dl,50
         mov       dl,28
         jb        Volby4
         mov       dl,53

; ------ test, zda je platn  polo‘ka

Volby4:  call      SrcPol
         jc        Volby5                   ; nen¡ platn  polo‘ka
         cmp       bl,3
         je        Volby42
         cmp       bl,6
         je        Volby42
         cmp       al,ds:[AktVol]
         je        Volby5
         mov       ds:[AktVol],al           ; ‡¡slo nov‚ aktivn¡ polo‘ky
         call      DispVol
         jmp       short Volby5

Volby42: mov       ds:[AktVol],al           ; ‡¡slo nov‚ aktivn¡ polo‘ky
         call      DispVol
         mov       bx,0dh                   ; kl vesa ENTER
         jmp       short Volby6             ; zmˆna polo‘ky

; ------ vstup znaku z kl vesnice

Volby5:  call      TestKey
         jnc       Volby52
Volby51: jmp       Volby1                   ; ‡ek n¡ na stisk kl vesy

Volby52: call      InpKey
         cmp       bl,1bh
         je        Volby7                   ; p©eru¨en¡ ESC

Volby6:  call      EditVol                  ; editace voleb
         jmp       short Volby51            ; dal¨¡ kl vesa

; ------ zobrazen¡ voleb

Volby7:  mov       byte ptr ds:[AktVolby],0 ; p©¡znak aktivn¡ch voleb
         call      IniDPar                  ; inicializace diskov˜ch parametr–
         call      DispVol                  ; zobrazen¡ voleb
         call      DispADsk                 ; zobrazen¡ disk–
         ret

Volby    ENDP

; -----------------------------------------------------------------------------
;        editace voleb (BX=kl vesa)
; -----------------------------------------------------------------------------

EditVol  PROC      NEAR

         mov       al,ds:[AktVol]
         call      GetPol
         mov       di,ds:[si+1]
         mov       al,ds:[di]
         mov       dx,ds:[si+3]
         mov       cl,ds:[si+6]

         call      JumpBX                   ; obsluha kl vesy BX

         dw        5100h,EditVl41           ; Page Down
         dw        4900h,EditVl30           ; Page Up
         dw        4f00h,EditVl82           ; End
         dw        4700h,EditVl72           ; Home
         dw        0f00h,EditVol8           ; Shift-Tab
         dw        9,EditVol7               ; Tabel tor
         dw        4d00h,EditVol6           ; vpravo
         dw        4b00h,EditVol5           ; vlevo
         dw        4800h,EditVol8           ; nahoru
         dw        5000h,EditVol7           ; dol–
         dw        20h,EditVol2             ; mezera
         dw        0dh,EditVol2             ; ENTER
         dw        0,EditVol1

EditVol1:ret

; ------ mezera, Enter

EditVol2:test      byte ptr ds:[si+0],b0
         jz        EditVl22
         cmp       al,0
         mov       al,0
         jne       EditVl21
         not       al
EditVl21:mov       ds:[di],al
         call      DispVol
         ret

; ------ ‡¡seln  polo‘ka

EditVl22:test      byte ptr ds:[si+0],b1
         jz        EditVl24

         push      dx
         push      si
         mov       ah,ds:[ColHelp]
         mov       si,offset PocZHlp        ; po‡et ovˆ©ovac¡ch z pis–
         cmp       dh,4
         je        EditVl23
         mov       si,offset PocPHlp        ; po‡et dopl¤uj¡c¡ch stop
EditVl23:mov       dx,24*HI
         call      Disp0Txt
         pop       si
         pop       dx

         mov       ch,ds:[ColEdi]
         add       dl,ds:[si+5]
         mov       ah,0
         cmp       dh,4
         je        EditV232
         cmp       byte ptr ds:[TypStop],40
         ja        EditV232
         shr       al,1
EditV232:call      EditNum
         cmp       dh,4
         je        EditV234
         cmp       byte ptr ds:[TypStop],40
         ja        EditV233
         shl       al,1
EditV233:cmp       al,6
         jbe       EditV234
         mov       al,6
EditV234:mov       ds:[di],al
         call      DispVol
         ret

; ------ hexadecim ln¡ polo‘ka

EditVl24:test      byte ptr ds:[si+0],b2
         jz        EditVl26

         push      dx
         push      si
         mov       ah,ds:[ColHelp]
         mov       si,offset HexFHlp        ; znak pro form tov n¡
         mov       dx,24*HI
         call      Disp0Txt
         pop       si
         pop       dx

         mov       ah,ds:[ColEdi]
         add       dl,ds:[si+5]
         call      EditHByt
         jmp       short EditVl21

; ------ volba ‡e¨tiny

EditVl26:test      byte ptr ds:[si+0],b3
         jz        EditVl28
         inc       ax
         cmp       al,3
EditVl27:jbe       EditV271
         mov       al,0
EditV271:mov       ds:[di],al
         call      InitFont                 ; inicializace font–
         call      DispADsk                 ; zobrazen¡ v¨ech disk–
         ret

; ------ volba zachov n¡ dat

EditVl28:
;test      byte ptr ds:[si+0],b4
         ;jz        EditVl29
         ;inc       ax
         ;cmp       al,2

; ------ volba po‡tu polo‘ek v ROOT

EditVl29:test      byte ptr ds:[si+0],b5
         jz        EditVl2a

         push      dx
         push      si
         mov       ah,ds:[ColHelp]
         mov       dx,24*HI
         mov       si,offset RootHlp
         call      Disp0Txt
         pop       si
         pop       dx

         mov       ch,ds:[ColEdi]
         add       dl,ds:[si+5]
         mov       ah,0
         shl       ax,1
         shl       ax,1
         shl       ax,1
         shl       ax,1                     ; po‡et polo‘ek ROOT
         call      EditNum
         add       ax,15                    ; zaokrouhlen¡ nahoru
         shr       ax,1
         shr       ax,1
         shr       ax,1
         shr       ax,1
         cmp       al,62
         jbe       EditV211
         mov       al,62
EditV211:jmp       EditVL21

EditVl2a:ret

; ------ Page Down

EditVl41:mov       dh,7
         jmp       short EditVl31

; ------ vlevo

EditVol5:cmp       dl,4
         jne       EditVl53
         mov       dl,53
         jmp       short EditVl31
EditVl53:cmp       dl,28
         mov       dl,4
         je        EditVl31
         mov       dl,28
         jmp       short EditVl31

; ------ Page Up

EditVl30:mov       dh,3
EditVl31:call      SrcPol
EditVl32:mov       ds:[AktVol],al
         call      DispVol
         ret

; ------ vpravo

EditVol6:cmp       dl,53
         jne       EditVl62
         mov       dl,4
         jmp       short EditVl31
EditVl62:cmp       dl,28
         mov       dl,53
         je        EditVl31
         mov       dl,28
         jmp       short EditVl31

; ------ tabel tor

EditVol7:mov       al,ds:[AktVol]
         inc       al
         cmp       al,NUMVOL
         jb        EditVl32
EditVl72:mov       al,0
         jmp       short EditVl32

; ------ zpˆtn˜ tabel tor

EditVol8:mov       al,ds:[AktVol]
         dec       al
         jns       EditVl32
EditVl82:mov       al,NUMVOL-1
         jmp       short EditVl32

EditVol  ENDP

; -----------------------------------------------------------------------------
;        nalezen¡ polo‘ky se sou©adnicemi DX -> AL (CY=nenalezeno)
; -----------------------------------------------------------------------------

SrcPol   PROC      NEAR

         push      cx
         push      si

         mov       ch,0
         mov       si,offset VolDef
         mov       al,0
SrcPol1: cmp       dx,ds:[si+3]
         je        SrcPol9
         add       si,VOLOFFS
         inc       ax
         cmp       si,offset VolDef0
         jb        SrcPol1
         stc

SrcPol9: pop       si
         pop       cx
         ret

SrcPol   ENDP

; -----------------------------------------------------------------------------
;        nalezen¡ adresy polo‘ky AL -> SI
; -----------------------------------------------------------------------------

GetPol   PROC      NEAR

         push      ax
         mov       ah,VOLOFFS
         mul       ah
         add       ax,offset VolDef
         xchg      ax,si
         pop       ax
         ret

GetPol   ENDP

; -----------------------------------------------------------------------------
;     editace ‡¡sla AX (CL=¨¡©ka pole, CH=barva, DX=pozice)
; -----------------------------------------------------------------------------

EditNum  PROC      NEAR

; ------ £schova registr–

         push      bx
         push      dx
         push      si

; ------ prvn¡ zobrazen¡ ‡¡sla

         mov       si,ax                    ; p–vodn¡ hodnota ‡¡sla
         xor       bx,bx
         push      dx
         call      MouseOff
         call      DispNum                  ; zobrazen¡ ‡¡sla
         call      MouseOn
         dec       dl
         call      SetKurz                  ; zobrazen¡ kurzoru
         pop       dx

; ------ ‡ek n¡ na prvn¡ kl vesu

EditNum1:push      dx
         call      MouseGet
         pop       dx
         cmp       bl,3
         jne       EditNm11
EditNm10:jmp       EditNum8                 ; lev‚ tla‡¡tko
EditNm11:cmp       bl,4
         je        EditNm10                 ; prav‚ tla‡¡tko
         call      TestKey
         jc        EditNum1

         call      InpKey                   ; vstup znaku z kl vesnice
         cmp       bh,53h
         jne       EditNm14
EditNm13:jmp       EditNum7                 ; Delete
EditNm14:cmp       bl,8
         je        EditNm13                 ; Bs
         cmp       bl,1bh                   ; ESC
         je        EditNm10                 ; p©eru¨en¡ editace
         cmp       bl,0dh                   ; ENTER
         je        EditNm10                 ; ‡¡slo se nemˆn¡
         sub       bx,"0"
         jb        EditNum1                 ; neplatn  kl vesa
         cmp       bl,9
         ja        EditNum1                 ; neplatn  kl vesa
         xor       ax,ax                    ; v˜choz¡ hodnota ‡¡sla

; ------ p©id n¡ nov‚ ‡¡slice k ‡¡slu

EditNum2:push      dx
         mov       dx,10
         mul       dx                       ; ‡¡slo * 10
         pop       dx
         add       ax,bx                    ; p©id n¡ zadan‚ho ‡¡sla

; ------ stanoven¡ maxim ln¡ hodnoty ‡¡sla

         mov       bl,10
         cmp       cl,1
         je        EditNum3
         mov       bl,100
         cmp       cl,2
         je        EditNum3
         mov       bx,1000
         cmp       cl,3
         je        EditNum3
         mov       bx,10000

; ------ omezen¡ velikosti ‡¡sla

EditNum3:cmp       ax,bx
         jb        EditNum4                 ; nen¡ p©esah ‡¡sla
         push      dx
         xor       dx,dx
         div       bx                       ; zarovn n¡ ‡¡sla
         xchg      ax,dx                    ; AX <- zbytek ‡¡sla
         pop       dx

; ------ zobrazen¡ nov‚ hodnoty ‡¡sla

EditNum4:xor       bx,bx
         push      dx
         call      MouseOff
         call      DispNum
         call      MouseOn
         pop       dx

; ------ ‡ek n¡ na dal¨¡ kl vesu

EditNum6:push      dx
         call      MouseGet
         pop       dx
         cmp       bl,3
         jne       EditNm62                 ; nen¡ lev‚ tla‡¡tko
         cmp       dh,byte ptr ds:[MousePoz+1]
         jne       EditNum8
         cmp       dl,byte ptr ds:[MousePoz]
         ja        EditNum8
         mov       bl,dl
         add       bl,cl
         cmp       bl,byte ptr ds:[MousePoz]
         ja        EditNum9
         jmp       short EditNum8

EditNm62:cmp       bl,4
         je        EditNum8                 ; prav‚ tla‡¡tko
         call      TestKey
         jc        EditNum6

EditNm64:call      TestKey
         jc        EditNum6

         call      InpKey
         cmp       bh,53h
         je        EditNum7                 ; Delete
         cmp       bl,8
         je        EditNum7                 ; Bs
         cmp       bl,1bh
         je        EditNum8                 ; p©eru¨en¡ editace
         cmp       bl,0dh
         je        EditNum9                 ; ukon‡en¡ volby ENTER
         sub       bx,"0"
         jb        EditNum6
         cmp       bl,9
         ja        EditNum6
         jmp       EditNum2

; ------ zru¨en¡ posledn¡ ‡¡slice BS, DELETE

EditNum7:mov       bx,10
         push      dx
         xor       dx,dx
         div       bx
         pop       dx
         jmp       short EditNum4           ; dal¨¡ kl vesa

; ------ p©eru¨en¡ ESC, navr cen¡ p–vodn¡ho ‡¡sla

EditNum8:mov       ax,si                    ; p–vodn¡ ‡¡slo
EditNum9:xor       bx,bx
         call      MouseOff
         call      DispNum                  ; zobrazen¡ v˜sledn‚ hodnoty ‡¡sla
         call      MouseOn
         call      KurzOff                  ; vypnut¡ kurzoru

; ------ n vrat registr–

         pop       si
         pop       dx
         pop       bx
         ret

EditNum  ENDP

; -----------------------------------------------------------------------------
;        editace hexadecim ln¡ho bajtu AL (AH=barva, DX=pozice)
; -----------------------------------------------------------------------------

EditHByt PROC      NEAR

; ------ £schova registr–

         push      bx
         push      cx
         push      dx

; ------ prvn¡ zobrazen¡ ‡¡sla

         mov       ch,al                    ; p–vodn¡ hodnota ‡¡sla
         push      dx
         call      MouseOff
         call      DispHByt
         call      MouseOn
         dec       dl
         call      SetKurz                  ; zobrazen¡ kurzoru
         pop       dx

; ------ ‡ek n¡ na prvn¡ kl vesu

EditHBy1:push      dx
         call      MouseGet
         pop       dx
         cmp       bl,3
         jne       EditHB11
EditHB10:jmp       EditHBy8                 ; lev‚ tla‡¡tko
EditHB11:cmp       bl,4
         je        EditHB10                 ; prav‚ tla‡¡tko
         call      TestKey
         jc        EditHBy1

         call      InpKey                   ; vstup znaku z kl vesnice
         cmp       bh,53h
         jne       EditHB14
EditHB13:jmp       EditHBy7                 ; Delete
EditHB14:cmp       bl,8
         je        EditHB13                 ; Bs
         cmp       bl,1bh                   ; ESC
         je        EditHB10                 ; p©eru¨en¡ editace
         cmp       bl,0dh                   ; ENTER
         je        EditHB10                 ; ‡¡slo se nemˆn¡
         sub       bl,"0"
         jb        EditHBy1                 ; neplatn  kl vesa
         cmp       bl,9
         jbe       EditHB17
         sub       bl,7
         cmp       bl,15
         ja        EditHBy1
         cmp       bl,10
         jb        EditHBy1
EditHB17:mov       al,0                     ; v˜choz¡ hodnota ‡¡sla

; ------ p©id n¡ nov‚ ‡¡slice k ‡¡slu

EditHBy2:shl       al,1
         shl       al,1
         shl       al,1
         shl       al,1
         or        al,bl                    ; p©id n¡ zadan‚ho ‡¡sla

; ------ zobrazen¡ nov‚ hodnoty ‡¡sla

EditHBy4:push      dx
         call      MouseOff
         call      DispHByt
         call      MouseOn
         pop       dx

; ------ ‡ek n¡ na dal¨¡ kl vesu

EditHBy6:push      dx
         call      MouseGet
         pop       dx
         cmp       bl,3
         jne       EditHB62                 ; nen¡ lev‚ tla‡¡tko
         cmp       dh,byte ptr ds:[MousePoz+1]
         jne       EditHBy8
         cmp       dl,byte ptr ds:[MousePoz]
         ja        EditHBy8
         mov       bl,dl
         add       bl,2
         cmp       bl,byte ptr ds:[MousePoz]
         ja        EditHBy9
         jmp       short EditHBy8

EditHB62:cmp       bl,4
         je        EditHBy8                 ; prav‚ tla‡¡tko
         call      TestKey
         jc        EditHBy6

EditHB64:call      TestKey
         jc        EditHBy6

         call      InpKey
         cmp       bh,53h
         je        EditHBy7                 ; Delete
         cmp       bl,8
         je        EditHBy7                 ; Bs
         cmp       bl,1bh                   ; ESC
         je        EditHBy8                 ; p©eru¨en¡ editace
         cmp       bl,0dh                   ; ENTER
         je        EditHBy9                 ; konec editace
         sub       bl,"0"
         jb        EditHBy6                 ; neplatn  kl vesa
         cmp       bl,9
         jbe       EditHBy2
         sub       bl,7
         cmp       bl,15
         ja        EditHBy6
         cmp       bl,10
         jb        EditHBy6
         jmp       short EditHBy2

; ------ Delete

EditHBy7:shr       al,1
         shr       al,1
         shr       al,1
         shr       al,1
         jmp       short EditHBy4

; ------ p©eru¨en¡ ESC, navr cen¡ p–vodn¡ho ‡¡sla

EditHBy8:mov       al,ch                    ; p–vodn¡ ‡¡slo
EditHBy9:call      MouseOff
         call      DispHByt                 ; zobrazen¡ v˜sledn‚ hodnoty ‡¡sla
         call      MouseOn
         call      KurzOff                  ; vypnut¡ kurzoru

; ------ n vrat registr–

         pop       dx
         pop       cx
         pop       bx
         ret

EditHByt ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                           Obsluha zobrazen¡
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;þ
; -----------------------------------------------------------------------------
;        zobrazen¡ znaku AX na pozici form tov n¡
; -----------------------------------------------------------------------------

DispFCh  PROC      NEAR

         push      cx
         push      dx

         mov       dx,ds:[PozMap]           ; po‡ tek okna
         inc       dh
         add       dl,11
         mov       cl,ds:[AktStop]          ; stopa
         mov       ch,ds:[NumStop]
         cmp       ch,60
         jb        DispFCh1
         shr       ch,1
DispFCh1:cmp       cl,ch
         jb        DispFCh2
         sub       cl,ch
         add       dh,6
         cmp       byte ptr ds:[NumStran],2
         je        DispFCh2
         dec       dh
DispFCh2:add       dl,cl
         add       dh,ds:[AktStran]
         call      Disp1Chr

         pop       dx
         pop       cx
         ret

DispFCh  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ informa‡n¡ho okna o pr–bˆhu form tov n¡
; -----------------------------------------------------------------------------
;þ
DispInf  PROC      NEAR

         call      MouseOff
         push      ax
         push      bx
         push      cx
         push      dx
         push      si

         mov       al,ds:[SelChar]
         mov       ds:[TextOK+2],al

; ------ zobrazen¡ st¡nu pod oknem

         mov       dx,11*HI+59
         mov       cx,11*HI+18
         call      StinWin

; ------ zobrazen¡ horn¡ linky okna

         mov       dx,11*HI+59
         mov       ah,ds:[ColRFR]
         mov       al,"É"
         call      Disp1Chr
         mov       cl,16
         mov       al,"Í"
         call      DispChr
         mov       al,"»"
         call      Disp1Chr

; ------ vnit©n¡ © dky okna

         mov       ch,9                     ; po‡et © dk–
DispInf2:mov       dl,59
         inc       dh
         mov       al,"º"
         call      Disp1Chr
         mov       al," "
         call      DispChr
         mov       al,"º"
         call      Disp1Chr
         dec       ch
         jnz       DispInf2

; ------ spodn¡ © dek okna

         inc       dh
         mov       dl,59
         mov       al,"È"
         call      Disp1Chr
         mov       al,"Í"
         call      DispChr
         mov       al,"¼"
         call      Disp1Chr

; ------ zobrazen¡ text– nadpis–

         mov       dx,12*HI+60
         mov       si,offset TextKap
         mov       ah,ds:[ColRF]
         call      Disp0Txt

         mov       dx,13*HI+61
         mov       si,offset TextVol
         call      Disp0Txt

         mov       dx,14*HI+61
         mov       si,offset TextBad
         call      Disp0Txt

; ------ text "doba"

         mov       dx,19*HI+63
         mov       si,offset TextDob
         call      Disp0Txt

; ------ oddˆlovac¡ linky

         mov       dx,15*HI+60
         mov       cl,16
         mov       ah,ds:[ColRFR]
         mov       al,"Ä"
         call      DispChr
         mov       dx,18*HI+60
         call      DispChr
         mov       byte ptr ds:[CitSek],1 ; p©¡znak k okam‘it‚mu zobrazen¡ ‡asu

; ------ n vrat registr–

         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         call      MouseOn
                                            ; pokra‡uje zobrazen¡ kapacit !!!

DispInf  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ kapacit disku v informa‡n¡m oknˆ
; -----------------------------------------------------------------------------

DispKap  PROC      NEAR

         call      MouseOff
         push      ax
         push      bx
         push      cx
         push      dx
         push      si

; ------ zobrazen¡ celkov‚ kapacity

         mov       dx,12*HI+67
         mov       ch,ds:[ColRF0]
         mov       ax,word ptr ds:[CelkKap]
         mov       bx,word ptr ds:[CelkKap+2]
         mov       cl,1
         call      DispNum

; ------ zobrazen¡ voln‚ kapacity

         mov       dx,13*HI+67
         mov       ax,word ptr ds:[CitVol]
         mov       bx,word ptr ds:[CitVol+2]
         mov       cl,1
         call      DispNum

; ------ zobrazen¡ vadn‚ kapacity

         mov       dx,14*HI+67
         mov       ax,word ptr ds:[CitBad]
         mov       bx,word ptr ds:[CitBad+2]
         mov       cl,1
         call      DispNum

; ------ zobrazen¡ p©¡znak–

         mov       al,ds:[ErrBoot]
         mov       si,offset TextBoot
         mov       dx,16*HI+61
         call      DispInS

         mov       al,ds:[ErrRoot]
         mov       si,offset TextRoot
         mov       dx,17*HI+61
         call      DispInS

         mov       al,ds:[ErrFat1]
         mov       si,offset TextFat1
         mov       dx,16*HI+69
         call      DispInS

         mov       al,ds:[ErrFat2]
         mov       si,offset TextFat2
         mov       dx,17*HI+69
         call      DispInS

; ------ n vrat registr–

         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         call      MouseOn
         ret

DispKap  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ jednoho p©¡znaku (pozice DX, text SI, stav AL)
; -----------------------------------------------------------------------------

DispInS  PROC      NEAR

         mov       ah,ds:[ColRF]
         call      Disp0Txt
         mov       si,offset TextOK
         cmp       al,0                     ; je OK ?
         je        DispInS1
         mov       si,offset TextNOK
         cmp       al,0ffh                  ; vadn˜ ?
         je        DispInS1                 ; vadn˜
         mov       si,offset TextXOK
DispInS1:mov       ah,ds:[ColRF0]
         call      Disp0Txt
         ret

DispInS  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ mapy disku
; -----------------------------------------------------------------------------

DispMap  PROC      NEAR

; ------ £schova registr–

         call      MouseOff
         push      ax
         push      bx
         push      cx
         push      dx
         push      si

; ------ p©edˆlov  stopa mapy

         mov       al,ds:[NumStop]
         cmp       al,60
         jb        DispMp10
         shr       al,1
DispMp10:mov       ds:[CitMap],al           ; ‡¡ta‡ stop mapy

; ------ zobrazen¡ st¡nu pod oknem

         mov       dx,ds:[PozMap]           ; po‡ te‡n¡ pozice
         mov       cx,ds:[SirMap]           ; rozmˆry okna
         call      StinWin

; ------ zobrazen¡ r mu

         push      dx
         mov       ah,ds:[ColRF]
DispMp11:push      dx
         mov       al," "
         call      DispChr
         pop       dx
         inc       dh
         dec       ch
         jnz       DispMp11
         pop       dx

; ------ zobrazen¡ mapy

         mov       bl,0                     ; po‡ te‡n¡ ‡¡slo stopy
DispMap1:mov       bh,0                     ; ‡¡slo strany

; ------ zobrazen¡ horn¡ho okraje

         mov       ah,ds:[ColRF0]           ; barva r me‡ku
         push      dx
         add       dl,10
         mov       al,"Ú"
         call      Disp1Chr
         mov       al,"Â"
         mov       cl,ds:[CitMap]
         call      DispChr
         mov       al,"¿"
         call      Disp1Chr
         pop       dx
         inc       dh

; ------ lev˜ okraj © dku

DispMap2:push      dx
         inc       dl
         mov       ah,ds:[ColRF]
         mov       si,offset TextStra
         call      Disp0Txt
         mov       al,bh
         add       al,"0"
         mov       ah,ds:[ColRF0]
         call      Disp1Chr
         inc       dx
;         mov       ah,ds:[ColRF]
         mov       al,"³"
         call      Disp1Chr

; ------ zobrazen¡ vnit©ku © dku

         mov       cl,ds:[CitMap]
         mov       ah,ds:[ColRFNul]
         mov       al,"°"
         call      DispChr

; ------ prav˜ okraj

         mov       ah,ds:[ColRF0]           ; barva r me‡ku
         mov       al,"³"
         call      Disp1Chr                 ; zobrazen¡ prav‚ho okraje
         pop       dx
         inc       dh
         inc       bh                       ; zv˜¨en¡ ‡¡sla strany
         cmp       bh,ds:[NumStran]         ; jsou ji‘ v¨echny strany ?
         jb        DispMap2                 ; dal¨¡ strana

; ------ zobrazen¡ spodn¡ho okraje

         push      bx
         push      dx
         add       dl,10
         mov       al,"À"
         call      Disp1Chr                 ; zobrazen¡ lev‚ho spodn¡ho rohu

         mov       bh,ds:[CitMap]           ; po‡et stop k zobrazen¡
DispMap4:push      ax
         mov       al,bl                    ; ‡¡slo stopy
         mov       ah,0
         mov       cl,5
         div       cl                       ; dˆlitelnost 5
         or        ah,ah                    ; je ‡¡slo dˆliteln‚ 5 ?
         pop       ax
         mov       al,"Å"                   ; dˆliteln‚ 5
         jz        DispMp42
         mov       al,"Á"
DispMp42:call      Disp1Chr

         inc       bl
         dec       bh
         jnz       DispMap4

         mov       al,"Ù"
         call      Disp1Chr                 ; zobrazen¡ prav‚ho doln¡ho rohu
         pop       dx
         pop       bx
         inc       dh

; ------ zobrazen¡ ‡¡sel stop

         push      dx
         add       dl,10
         mov       bh,ds:[CitMap]           ; po‡et stop k zobrazen¡
;         mov       ah,ds:[ColRF0]
DispMap5:
         push      ax
         mov       al,bl                    ; ‡¡slo stopy
         mov       ah,0
         mov       cl,5
         div       cl
         or        ah,ah                    ; je dˆliteln‚ 5 ?
         pop       ax
         jnz       DispMp52                 ; nen¡ dˆliteln‚ 5

         push      ax
         push      bx
         push      cx
         push      dx

         mov       al,bl
         mov       ch,ah
         xor       bx,bx
         xor       ah,ah
         mov       cl,2
         call      DispNum                  ; zobrazen¡ ‡¡sla stopy

         pop       dx
         pop       cx
         pop       bx
         pop       ax

DispMp52:inc       dl                       ; zv˜¨en¡ pozice
         inc       bx                       ; zv˜¨en¡ ‡¡sla stopy
         dec       bh
         jnz       DispMap5                 ; dal¨¡ stopa
         pop       dx

; ------ p©¡prava k zobrazen¡ dal¨¡ho r me‡ku

         add       dh,2
         cmp       bl,ds:[NumStop]          ; jsou ji‘ v¨echny stopy ?
         jae       DispMap6
         mov       al,ds:[NumStop]
         sub       al,ds:[CitMap]
         mov       ds:[CitMap],al
         jmp       DispMap1                 ; dal¨¡ stopy

; ------ oddˆlovac¡ ‡ ra mezi mapami pro 80 stop

DispMap6:cmp       byte ptr ds:[NumStop],60
         jbe       DispMap9
         sub       dh,7
         cmp       byte ptr ds:[NumStran],1
         jne       DispMap7
         inc       dh
DispMap7:mov       al,"Ä"
;         mov       ah,ds:[ColRF0]
         mov       cl,byte ptr ds:[SirMap]
         call      DispChr

; ------ n vrat registr–

DispMap9:pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         call      MouseOn
         ret

DispMap  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ v¨ech disk–
; -----------------------------------------------------------------------------

DispADsk PROC      NEAR

         push      ax
         mov       al,0
DispADs2:call      DispDisk                 ; zobrazeni disku A:
         inc       ax
         cmp       al,ds:[NumDisk]
         jb        DispADs2
         pop       ax
         ret

DispADsk ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ disku AL
; -----------------------------------------------------------------------------

DispDisk PROC      NEAR

; ------ £schova registr–

         call      MouseOff                 ; vypnut¡ kurzoru my¨i
         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di

; ------ p©¡prava registr–

         mov       bh,0
         mov       bl,al                    ; ‡¡slo disku
         mov       dh,11                    ; po‡ te‡n¡ © dek k zobrazen¡
         mov       dl,ds:[PozDiskT+bx]      ; pozice k zobrazen¡ disku

; ------ zobrazen¡ st¡nu

         mov       cl,17
         mov       ch,11
         call      StinWin                  ; zobrazen¡ st¡nu pod oknem

; ------ horn¡ okraj okna

         mov       ah,ds:[ColDiskR]         ; barva r mu
         mov       al,"É"
         call      Disp1Chr
         mov       al,"Í"
         mov       cl,15
         call      DispChr
         mov       al,"»"
         call      Disp1Chr
         sub       dl,17
         inc       dh

; ------ st©edn¡ © dek nadpisu - ‡¡slo disku

         mov       al,"º"
         call      Disp1Chr
         mov       al," "
         mov       cl,4
         call      DispChr
         mov       si,offset TextDisk
         mov       ah,ds:[ColDisk1]
         call      Disp0Txt
         mov       al,bl
         add       al,"A"
         call      Disp1Chr
         mov       al,":"
         call      Disp1Chr
         mov       ah,ds:[ColDiskR]         ; barva r mu
         mov       cl,4
         mov       al," "
         call      DispChr
         mov       al,"º"
         call      Disp1Chr
         sub       dl,17
         inc       dh

; ------ oddˆlovac¡ linka

         mov       al,"Ì"
         call      Disp1Chr
         mov       al,"Í"
         mov       cl,15
         call      DispChr
         mov       al,"¹"
         call      Disp1Chr
         sub       dl,17
         inc       dh

; ------ © dek volby "Automatick˜"

;         mov       al,"º"
;         call      Disp1Chr
;         mov       al," "
;         call      Disp1Chr
;         mov       bh,ds:[ColDisk1]         ; bˆ‘n  barva polo‘ek
;         mov       ah,bh
;         cmp       bl,ds:[AktDisk]          ; je to aktivn¡ disk ?
;         jne       DispDsk2                 ; nen¡ to aktivn¡ disk
;         cmp       byte ptr ds:[AktVolby],0 ; jsou aktivn¡ volby ?
;         jne       DispDsk2                 ; jsou aktivn¡ volby
;         mov       ah,ds:[ColDisk4]         ; vysv¡cen  barva
;         cmp       byte ptr ds:[AktForm],0  ; je to atkivn¡ form t ?
;         jne       DispDsk2                 ; nen¡ to aktivn¡ format
;         mov       ah,ds:[ColDisk3]         ; barva kurzoru
;         mov       bh,ah
;DispDsk2:call      Disp1Chr                 ; zobrazen¡ jedn‚ mezery
;         mov       al,"A"
;         call      Disp1Chr                 ; zobrazen¡ hork‚ kl vesy
;         mov       si,offset TextAut        ; text "Automatick˜"
;         mov       ah,bh
;         call      Disp0Txt                 ; zobrazen¡ textu
;         mov       al," "
;         call      Disp1Chr                 ; zobrazen¡ mezery
;         mov       ah,ds:[ColDiskR]         ; barva r mu
;         call      Disp1Chr
;         mov       al,"º"
;         call      Disp1Chr
;         sub       dl,17
;         inc       dh

; ------ p©¡prava k zobrazen¡ text– form t–

         mov       bh,0
         mov       al,ds:[TypDiskT+bx]      ; typ disku
         mov       ah,0
         shl       ax,1                     ; typ disku * 2
         xchg      ax,si                    ; SI <- typ disku * 2
         mov       si,ds:[si+TabForm-2]     ; adresa popisova‡e form tu
         mov       cl,ds:[si]               ; po‡et polo‘ek form t–
         inc       si
         mov       ch,0                     ; ‡¡slo form tu k zobrazen¡

; ------ lev˜ okraj

DispDsk3:mov       ah,ds:[ColDiskR]         ; barva r mu
         mov       al,"º"
         call      Disp1Chr
         mov       al," "
         call      Disp1Chr

; ------ ur‡en¡ barvy © dku

         mov       bh,ds:[ColDisk1]         ; bˆ‘n  barva polo‘ek
         mov       ah,bh
         cmp       bl,ds:[AktDisk]          ; je to aktivn¡ disk ?
         jne       DispDsk4                 ; nen¡ to aktivn¡ disk
         cmp       byte ptr ds:[AktVolby],0 ; jsou aktivn¡ volby ?
         jne       DispDsk4                 ; jsou aktivn¡ volby
         mov       ah,ds:[ColDisk4]         ; vysv¡cen  barva
         cmp       ds:[AktForm],ch          ; je to aktivn¡ form t ?
         jne       DispDsk4                 ; nen¡ to aktivn¡ format
         mov       ah,ds:[ColDisk3]         ; barva kurzoru
         mov       bh,ah
DispDsk4:call      Disp1Chr                 ; zobrazen¡ mezery
         call      Disp1Chr

; ------ zobrazen¡ pr zdn‚ho © dku

         dec       cl
         jns       DispDsk5
         push      cx
         mov       cl,11
         call      DispChr
         pop       cx
         jmp       short DispDsk6

; ------ zobrazen¡ textu volby form tu

DispDsk5:mov       al,ch                    ; ‡¡slo form tu
         add       al,"1"
         call      Disp1Chr                 ; zobrazen¡ ‡¡sla form tu
         mov       ah,bh
         mov       al,":"
         call      Disp1Chr
         mov       al," "
         call      Disp1Chr
         test      byte ptr ds:[si+0],b3    ; je to nestandardn¡ form t ?
         jz        DispDs52                 ; je to standardn¡ form t
         mov       al,"*"                   ; p©¡znak nestandardn¡ho form tu
DispDs52:call      Disp1Chr
         add       si,4
         push      cx
         mov       cl,5
         call      DispTxt
         mov       al," "
         mov       cl,2
         call      DispChr
         pop       cx

; ------ prav˜ okraj

DispDsk6:mov       ah,ds:[ColDiskR]         ; barva r mu
         call      Disp1Chr
         mov       al,"º"
         call      Disp1Chr

; ------ p©¡prava pro dal¨¡ polo‘ku

         sub       dl,17
         inc       dh
         inc       ch
         cmp       ch,7
         jae       DispDsk7
         jmp       DispDsk3

; ------ spodn¡ okraj okna

DispDsk7:mov       ah,ds:[ColDiskR]         ; barva r mu
         mov       al,"È"
         call      Disp1Chr
         mov       al,"Í"
         mov       cl,15
         call      DispChr
         mov       al,"¼"
         call      Disp1Chr

; ------ zobrazen¡ n povˆdy pro aktivn¡ form t aktivn¡ho disku

         cmp       byte ptr ds:[AktVolby],0 ; jsou aktivn¡ volby ?
         jne       DispDs99                 ; jsou aktivn¡ volby
         cmp       bl,ds:[AktDisk]          ; je to aktivn¡ disk ?
         je        DispDs80                 ; je to aktivn¡ disk
DispDs99:jmp       DispDsk9
DispDs80:mov       dx,24*HI

; ------ zobrazen¡ n povˆdy pro automatick˜ form t

;         cmp       byte ptr ds:[AktForm],0  ; automatick˜ form t ?
;         jne       DispDsk8                 ; nen¡ automatick˜ form t
;         mov       ah,ds:[ColHelp]
;         mov       si,offset FormHlpA
;         call      Disp0Txt
;         jmp       short DispDs88

; ------ p©¡prava prvn¡ ‡ sti textu

DispDsk8:mov       si,offset FormHlp1
         mov       ax,"45"                  ; disk 5 1/4"
         cmp       byte ptr ds:[TypDisk],3  ; je disk 3 1/2" ?
         jb        DispDs82                 ; je disk 5 1/4"
         mov       ax,"23"                  ; jinak disk 3 1/2"
DispDs82:mov       ds:[si+2],al
         mov       ds:[si+6],ah             ; ozna‡en¡ disku 3 1/2 nebo 5 1/4

         mov       al,"D"                   ; disketa DD
         cmp       byte ptr ds:[NumSekt],13
         jbe       DispDs83                 ; disketa DD
         mov       al,"H"                   ; disketa HD
         cmp       byte ptr ds:[NumSekt],30
         jb        DispDs83
         mov       al,"Q"                   ; jinak QD
DispDs83:mov       ds:[si+9],al             ; ozna‡en¡ DD, HD, QD

         mov       al,"S"                   ; 1 strana
         cmp       byte ptr ds:[NumStran],1 ; je 1 strana ?
         je        DispDs84                 ; je 1 strana
         mov       al,"D"                   ; 2 strany
DispDs84:mov       ds:[si+12],al            ; ozna‡en¡ SS nebo DS

; ------ zobrazen¡ prvn¡ ‡ sti tetxu

         mov       ah,ds:[ColHelp]
         call      Disp0Txt

; ------ zobrazen¡ kapacity disku

         mov       ch,ah                    ; barva
         mov       ax,word ptr ds:[CelkKap] ; celkov  kapacita disku
         mov       bx,word ptr ds:[CelkKap+2]
         mov       cl,0                     ; CL = ¨¡©ka pole
         call      DispNum                  ; zobrazen¡ celkov‚ kapacity disku
         mov       ah,ch

; ------ zobrazen¡ druh‚ ‡ sti textu

         mov       si,offset FormHlp2
         call      Disp0Txt

; ------ zobrazen¡ po‡tu polo‘ek v ROOT

         mov       ch,ah                    ; barva
         mov       ax,ds:[RootMax]          ; po‡et polo‘ek v ROOT
         xor       bx,bx
         mov       cl,0                     ; CL ¨¡©ka pole
         call      DispNum                  ; zobrazen¡ po‡tu polo‘ek v ROOT
         mov       ah,ch

; ------ zobrazen¡ zbytku mezer

DispDs88:mov       cl,38
         sub       cl,dl
         jbe       DispDs89
         mov       al," "
         call      DispChr

; ------ zobrazen¡ zbytku textu n povˆdy

DispDs89:mov       ch,ds:[ColHelp1]         ; vysv¡cen  barva textu
         mov       si,offset FormHlp0       ; text
         call      Disp0Txt                 ; zobrazen¡ textu n povˆdy

; ------ n vrat registr–

DispDsk9:pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         call      MouseOn                  ; zapnut¡ kurzoru my¨i
         ret

DispDisk ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ voleb
; -----------------------------------------------------------------------------

DispVol  PROC      NEAR

; ------ £schova registr–

         call      MouseOff                 ; vypnut¡ kurzoru my¨i
         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp

; ------ p©¡prava k zobrazen¡ polo‘ek

         mov       si,offset VolDef         ; definice polo‘ek
         mov       bp,offset VolText        ; texty n povˆd k volb m
         mov       bl,0                     ; ukazatel ‡¡sla volby

; ------ p©¡prava barvy polo‘ky

DispVol1:mov       ah,ds:[ColVol0]          ; bˆ‘n  barva polo‘ky
         mov       bh,ds:[ColVol]
         cmp       bl,ds:[AktVol]           ; je to aktivn¡ polo‘ka ?
         jne       DispVol2                 ; nen¡ to aktivn¡ polo‘ka
         cmp       byte ptr ds:[AktVolby],0 ; jsou aktivn¡ volby ?
         je        DispVol2                 ; volby nejsou aktivn¡
         mov       ah,ds:[ColVolK]          ; jinak barva kurzoru
         mov       bh,ah

; ------ zobrazen¡ £vodn¡ mezery polo‘ky

DispVol2:mov       dx,ds:[si+3]             ; po‡ te‡n¡ pozice na obrazovce
         dec       dl
         mov       al," "
         call      Disp1Chr                 ; zobrazen¡ £vodn¡ mezery polo‘ky

; ------ zobrazen¡ n zvu polo‘ky

         push      si
         mov       si,bp
         mov       cl,ds:[si]
         mov       ch,0
         call      Disp0Txt
         pop       si
         inc       bp
         add       bp,cx

; ------ zobrazen¡ oddˆlovac¡ mezery

         sub       cl,ds:[si+5]             ; offset po‡ tku parametru
         neg       cl
         call      DispChr                  ; zobrazen¡ oddˆlovac¡ mezery

; ------ zobrazen¡ jednoduch‚ho p©ep¡na‡e

         mov       ah,bh
         mov       di,ds:[si+1]             ; adresa parametru polo‘ky
         mov       al,ds:[di]               ; parametr polo‘ky
         mov       cl,ds:[si+6]             ; d‚lka pole parametru polo‘ky
         test      byte ptr ds:[si+0],b0    ; je to jednoduch˜ p©ep¡na‡ ?
         jz        DispVol4                 ; nen¡ to jednoduch˜ p©ep¡na‡
         cmp       al,0                     ; je p©ep¡na‡ zapnut ?
         mov       al,ds:[SelChar]          ; volba - p©ep¡na‡ zapnut
         jne       DispVol3                 ; p©ep¡na‡ je zapnut
         mov       al,"-"                   ; jinak p©ep¡na‡ neaktivn¡
DispVol3:call      Disp1Chr                 ; zobrazen¡ p©ep¡na‡e
         jmp       short DispVol8

; ------ zobrazen¡ dekadick‚ ‡¡seln‚ polo‘ky

DispVol4:test      byte ptr ds:[si+0],b1    ; je dekadick  ‡¡seln  polo‘ka ?
         jz        DispVol5                 ; nen¡ dekadick  ‡¡seln  polo‘ka
         push      bx
         push      ax
         xor       bx,bx
         mov       ch,ah                    ; barva ‡¡sla
         cmp       word ptr ds:[si+1],offset ParStop ; je volba dal¨¡ch stop ?
         jne       DispVl42                 ; nen¡ volba dal¨¡ch stop
         mov       ah,"6"                   ; max. 6 stop

         cmp       byte ptr ds:[TypStop],40
         ja        DispVl41                 ; je 80 stop
         shr       al,1                     ; po‡et stop/2
         mov       ah,"3"                   ; max. 3 stopy

DispVl41:mov       ds:[PocPHlp1],ah         ; max. po‡et stop
         mov       ds:[PocPHlp2],ah
DispVl42:mov       ah,0
         call      DispNum                  ; zobrazen¡ ‡¡seln‚ polo‘ky
         pop       ax
         pop       bx
         jmp       short DispVol8

; ------ zobrazen¡ hexadecim ln¡ polo‘ky - bajt

DispVol5:test      byte ptr ds:[si+0],b2    ; je hexadecim ln¡ polo‘ka ?
         jz        DispVol6
         call      DispHByt                 ; zobrazen¡ hexadecim ln¡ho bajtu
         jmp       short DispVol8

; ------ zobrazen¡ k¢du ‡e¨tiny

DispVol6:test      byte ptr ds:[si+0],b3    ; je volba ‡e¨tiny ?
         jz        DispVol7                 ; nen¡ volba ‡e¨tiny
         push      si
         mov       si,offset VolTCest
DispVl62:push      ax
         mul       cl
         add       si,ax
         pop       ax
         call      DispTxt
         pop       si
         jmp       short DispVol8

; ------ zobrazen¡ k¢du zachov n¡ dat

DispVol7:;test      byte ptr ds:[si+0],b4    ; je volba zachov n¡ dat ?
         ;jz        DispVl72
         ;push      si
         ;mov       si,offset VolTZach
         ;jmp       short DispVl62

; ------ zobrazen¡ po‡tu polo‘ek na Root

DispVl72:test      byte ptr ds:[si+0],b5    ; je volba po‡tu polo‘ek na Root ?
         jz        DispVol8
         or        al,al
         jz        DispVl73
         push      bx
         push      ax
         mov       ch,ah                    ; barva ‡¡sla
         xor       bx,bx
         mov       ah,0
         shl       ax,1
         shl       ax,1
         shl       ax,1
         shl       ax,1
         call      DispNum                  ; zobrazen¡ ‡¡seln‚ polo‘ky
         pop       ax
         pop       bx
         jmp       short DispVol8

DispVl73:push      si
         mov       si,offset VolTStd
         jmp       short DispVl62

; ------ zobrazen¡ z vˆre‡n‚ mezery

DispVol8:mov       al," "
         call      Disp1Chr

; ------ adresa dal¨¡ polo‘ky

         add       si,VOLOFFS
         inc       bx                       ; zv˜¨en¡ ukazatele ‡¡sla polo‘ky
         cmp       bl,NUMVOL                ; jsou ji‘ v¨echny polo‘ky ?
         je        DispVol9                 ; jsou ji‘ v¨echny polo‘ky
         jmp       DispVol1                 ; zobrazen¡ dal¨¡ polo‘ky

; ------ adresa n povˆdy k zobrazen¡

DispVol9:cmp       byte ptr ds:[AktVolby],0 ; jsou volby aktivn¡ ?
         je        DispVolC                 ; volby nejsou aktivn¡
         mov       cl,ds:[AktVol]           ; Aktivn¡ polo‘ka voleb
         mov       ch,0
         mov       si,offset VolHlp
         jcxz      DispVolB
         cld
         mov       ah,0
DispVolA:lodsb
         add       si,ax
         loop      DispVolA

; ------ zobrazen¡ prvn¡ ‡ sti n povˆdy

DispVolB:mov       dx,24*HI
         mov       ah,ds:[ColHelp]
         mov       al," "
         call      Disp1Chr                 ; zobrazen¡ £vodn¡ mezery
         call      Disp0Txt                 ; zobrazen¡ textu n povˆdy

; ------ zobrazen¡ mezer do zbytku © dku

         mov       cl,41
         sub       cl,dl
         mov       al," "
         call      DispChr

; ------ zobrazen¡ druh‚ ‡ sti n povˆdy

         mov       si,offset VolHlp0
         mov       ch,ds:[ColHelp1]
         call      Disp0Txt

; ------ n vrat registr–

DispVolC:pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         call      MouseOn                  ; zapnut¡ kurzoru my¨i
         ret

DispVol  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ podkladov‚ho r mu
; -----------------------------------------------------------------------------

DispRam  PROC      NEAR

; ------ £schova registr–

         call      MouseOff                 ; vypnut¡ kurzoru my¨i
         push      ax
         push      bx
         push      cx
         push      dx
         push      si

; ------ zobrazen¡ horn¡ linky nadpisu

         mov       dh,0                     ; po‡ te‡n¡ © dek
         mov       ah,ds:[ColRamN]          ; barva r mu nadpisu
         mov       al,"É"                   ; lev˜ horn¡ roh
         mov       bx,"ÍË"                  ; oddˆlovac¡ znak a linka
         mov       ch,"»"                   ; prav˜ horn¡ roh
         call      DispRL1                  ; zobrazen¡ horn¡ linky nadpisu

; ------ zobrazen¡ st©edn¡ linky nadpisu

         mov       al,"º"                   ; lev˜ okraj
         mov       bx," º"                  ; oddˆlovac¡ znak a linka
         mov       ch,"º"                   ; prav˜ okraj
         call      DispRL1                  ; zobrazen¡ st©edn¡ linky nadpisu

; ------ zobrazen¡ spodn¡ linky nadpisu

         mov       al,"Ì"                   ; lev˜ okraj
         mov       bx,"ÍÊ"                  ; oddˆlovac¡ znak a linka
         mov       ch,"¹"                   ; prav˜ okraj
         call      DispRL1                  ; zobrazen¡ spodn¡ linky nadpisu

; ------ zobrazen¡ © dk– voleb

         mov       bx,"º "                  ; lev˜ a st©edn¡ znak
         mov       cx,"º"*HI + 5            ; prav˜ znak, 5 © dk–
         call      DispRL2

; ------ oddˆlovac¡ linka pod volbami

         mov       bx,"ÈÍ"                  ; lev˜ a st©edn¡ znak
         mov       cx,"¼"*HI + 1            ; prav˜ znak, 1 © dek
         call      DispRL2                  ; oddˆlovac¡ linka

; ------ zobrazen¡ podkladu

         mov       ah,ds:[ColPod]           ; barva podkladu
         mov       bx,"°°"                  ; lev˜ a st©edn¡ znak
         mov       cx,"°"*HI + 15           ; prav˜ znak, 15 © dk–
         call      DispRL2

; ------ © dek n povˆdy

         mov       ah,ds:[ColHelp]          ; bavra r mu n povˆdy
         mov       bx,"  "                  ; lev˜ a st©edn¡ znak
         mov       cx," "*HI + 1            ; prav˜ znak, 1 © dek
         call      DispRL2

; ------ zobrazen¡ licen‡n¡ho textu

         mov       si,offset LicTxt         ; licen‡n¡ text
         mov       dx,1*HI+20               ; po‡ te‡n¡ pozice
         mov       ah,ds:[ColNadp]          ; barva textu
         call      Disp0Txt                 ; zobrazen¡ textu

; ------ zobrazen¡ st¡nu pod volbami

         mov       dx,9*HI+2
         mov       cl,78
         call      DispStin

; ------ n vrat registr–

         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         call      MouseOn                  ; zapnut¡ kurzoru my¨i

; ------ zobrazen¡ data a ‡asu

         call      DispDate                 ; zobrazen¡ aktu ln¡ho data
         mov       byte ptr ds:[CitSek],1
         ret

DispRam  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ podkladov‚ plochy
; -----------------------------------------------------------------------------

DispPod  PROC      NEAR

; ------ £schova registr–

         call      MouseOff                 ; vypnut¡ kurzoru my¨i
         push      ax
         push      bx
         push      cx
         push      dx

; ------ zobrazen¡ podkladu

         mov       dh,10
         mov       ah,ds:[ColPod]           ; barva podkladu
         mov       bx,"°°"                  ; lev˜ a st©edn¡ znak
         mov       cx,"°"*HI + 14           ; prav˜ znak, 14 © dk–
         call      DispRL2

; ------ n vrat registr–

         pop       dx
         pop       cx
         pop       bx
         pop       ax
         call      MouseOn                  ; zapnut¡ kurzoru my¨i
         ret

DispPod  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ horn¡ linky r mu
; (© dek DH, AX=prvn¡ znak, BH=linka, BL=oddˆlovac¡ znak, CH=posledn¡ znak)
; -----------------------------------------------------------------------------

DispRL1  PROC      NEAR

         mov       dl,0
         call      Disp1Chr                 ; zobrazen¡ lev‚ho okraje
         mov       al,bh                    ; vodorovn  linka
         mov       cl,12                    ; d‚lka linky
         call      DispChr                  ; zobrazen¡ vodorvn‚ linky
         mov       al,bl                    ; oddˆlovac¡ znak
         call      Disp1Chr                 ; zobrazen¡ oddˆlovac¡ho znaku
         mov       al,bh                    ; vodorovn  linka
         mov       cl,52                    ; d‚lka linky
         call      DispChr                  ; zobrazen¡ vodorovn‚ linky
         mov       al,bl                    ; oddˆlovac¡ znak
         call      Disp1Chr                 ; zobrazen¡ oddˆlovac¡ho znaku
         mov       al,bh                    ; vodorovn  linka
         mov       cl,12                    ; d‚lka linky
         call      DispChr                  ; zobrazen¡ vodorovn‚ linky
         mov       al,ch                    ; posledn¡ znak
         call      Disp1Chr                 ; zobrazen¡ posledn¡ho znaku
         inc       dh                       ; zv˜¨en¡ ukazatele © dku
         ret

DispRL1  ENDP

; -----------------------------------------------------------------------------
;       zobrazen¡ linky (© dek DH, BH=lev˜, BL=st©edn¡, CH=prav˜ znak, CL=© dk–)
; -----------------------------------------------------------------------------

DispRL2  PROC      NEAR

DispRL22:push      cx
         mov       dl,0                     ; po‡ te‡n¡ pozice
         mov       al,bh                    ; lev˜ okraj
         call      Disp1Chr                 ; zobrazen¡ lev‚ho okraje
         mov       al,bl                    ; st©edn¡ znak
         mov       cl,78                    ; d‚lka © dku
         call      DispChr                  ; zobrazen¡ st©edn¡ho znaku
         mov       al,ch                    ; prav˜ znak
         call      Disp1Chr                 ; zobrazen¡ prav‚ho znaku
         inc       dh                       ; zv˜¨en¡ ukazatele © dku
         pop       cx
         dec       cl
         jnz       DispRL22
         ret

DispRL2  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ aktu ln¡ho data
; -----------------------------------------------------------------------------

DispDate PROC      NEAR

; ------ £schova registr–

         call      MouseOff                 ; vypnut¡ kurzoru my¨i
         push      ax
         push      bx
         push      cx
         push      dx

; ------ zji¨tˆn¡ syst‚mov‚ho data

         mov       ah,2ah
         int       21h                      ; zji¨tˆn¡ syst‚mov‚ho data

; ------ p©¡prava pro zobrazen¡

         mov       bx,dx                    ; £schova dne a mˆs¡ce
         mov       dx,102h                  ; po‡ te‡n¡ pozice k zobrazen¡
         mov       ah,ds:[ColDat]           ; barva k zobrazen¡ data

; ------ zobrazen¡ data

         mov       al,bl                    ; aktu ln¡ den
         call      Disp2NmS                 ; zobrazen¡ dne
         mov       al,"."
         call      Disp1Chr                 ; zobrazen¡ oddˆlovac¡ te‡ky
         mov       al,bh                    ; mˆs¡c
         call      Disp2Nm0                 ; zobrazen¡ mˆs¡ce
         mov       al,"."
         call      Disp1Chr                 ; zobrazen¡ oddˆlovac¡ te‡ky
         push      ax
         push      dx
         mov       ax,100
         xchg      ax,cx
         xor       dx,dx
         div       cx                       ; v˜po‡et stolet¡ a roku
         mov       ah,dl                    ; rok
         xchg      ax,cx
         pop       dx
         pop       ax
         mov       al,cl                    ; stolet¡
         call      Disp2Nm0                 ; zobrazen¡ stolet¡
         mov       al,ch                    ; rok
         call      Disp2Nm0                 ; zobrazen¡ roku

; ------ n vrat registr–

         pop       dx
         pop       cx
         pop       bx
         pop       ax
         call      MouseOn                  ; zapnut¡ kurzoru my¨i
         ret

DispDate ENDP

; -----------------------------------------------------------------------------
;        inicializace font–
; -----------------------------------------------------------------------------

InitFont PROC      NEAR

; ------ n vrat text– z bufferu

         push      ds
         push      cs
         pop       es
         xor       si,si
         mov       ds,ds:[BuffTxt]
         mov       di,offset TextBeg
         mov       cx,offset(TextEnd-TextBeg)
         cld
         rep       movsb                    ; n vrat text–
         pop       ds

; ------ stanoven¡ konverzn¡ tabulky pro texty

         mov       byte ptr ds:[SelChar],"*" ; znak pro ozna‡en¡ v˜bˆru voleb
         mov       bx,offset TbKamLat       ; konverze na k¢d Latin 2
         cmp       byte ptr ds:[ParCest],3  ; je k¢d Latin 2 ?
         je        InitFnt2                 ; je k¢d Latin 2
         mov       byte ptr ds:[SelChar],"û" ; znak pro ozna‡en¡ v˜bˆru voleb
         mov       bx,offset TbKamCS0       ; tabulka k odstranˆn¡ diakritiky
         cmp       byte ptr ds:[ParCest],0  ; je bez diakritiky ?
         je        InitFnt2                 ; je bez diakritiky
         cmp       byte ptr ds:[ParCest],1  ; je vlastn¡ diakritika ?
         jne       InitFnt5                 ; nen¡ vlastn¡ - je Kamenick˜ch
         cmp       byte ptr ds:[EgaVga],0   ; je karta EGA nebo VGA ?
         jne       InitFnt5                 ; je EGA nebo VGA

; ------ p©ekonvertov n¡ text–

InitFnt2:mov       si,offset TextBeg
         mov       cx,offset(TextEnd-TextBeg)
InitFnt3:lodsb
         sub       al,128
         jb        InitFnt4
         xlat
         mov       ds:[si-1],al
InitFnt4:loop      InitFnt3

; ------ inicializace videom¢du

InitFnt5:call      MouseOff                 ; vypnut¡ kurzoru my¨i
         mov       ah,0fh
         call      Int10                    ; poskytnut¡ aktivn¡ho videom¢du

         mov       byte ptr ds:[SegmVRAM+1],0b0h
         cmp       al,7
         je        InitFnt6
         mov       byte ptr ds:[SegmVRAM+1],0b8h
         cmp       al,2
         je        InitFnt6
         mov       al,3
InitFnt6:mov       ah,0
         call      Int10                    ; inicializace videom¢du

; ------ p©edefinov n¡ vlastn¡ch font–

         cmp       byte ptr ds:[ParCest],1  ; jsou vlastn¡ fonty ?
         jne       InitFnt9                 ; nejsou vlastn¡ fonty
         cmp       byte ptr ds:[EgaVga],0   ; je karta EGA/VGA ?
         je        InitFnt9                 ; nen¡ karta EGA/VGA

         xor       ax,ax
         mov       es,ax                    ; ES <- 0
         mov       bh,14                    ; 14 bajt– na znak
         cmp       es:[485h],bh             ; je 14 linek na znak ?
         mov       bp,offset Font14         ; font 14 linek na znak
         je        InitFnt8                 ; je 14 linek na znak

         cmp       byte ptr es:[485h],16
         je        InitFnt7                 ; je font 16 linek na znak
         mov       ax,1114h
         mov       bl,0
         call      Int10                    ; na‡ten¡ standard. fontu 16 linek
InitFnt7:mov       bp,offset Font16         ; font 16 linek na znak
         mov       bh,16                    ; 16 bajt– na znak

InitFnt8:mov       cx,128                   ; po‡et znak– k p©edefinov n¡
         mov       dx,cx                    ; po‡ te‡n¡ znak k p©edefinov n¡
         mov       ax,1110h                 ; na‡ten¡ u‘ivatelsk˜ch font–
         push      cs
         pop       es                       ; ES <- CS
         mov       bl,0                     ; blok fontu
         call      Int10                    ; p©edefinov n¡ font–

; ------ zobrazen¡ univerz ln¡ ‡ sti

InitFnt9:call      MouseOn                  ; zapnut¡ kurzoru my¨i
         call      DispRam                  ; zobrazen¡ podkladov‚ho r mu
         call      DispPod                  ; zobrazen¡ podkladu
         call      DispVol                  ; zobrazen¡ voleb
         call      KurzOff                  ; vypnut¡ kurzoru
         ret

InitFont ENDP

; -----------------------------------------------------------------------------
;        obsluha zobrazen¡ ‡asu bˆhem INT 08h
; -----------------------------------------------------------------------------

Int08    PROC      FAR

; ------ vol n¡ p–vodn¡ obsluhy

         pushf
         call      dword ptr cs:[Old08]

; ------ ‡¡t n¡ zobrazen¡ ‡asu

         dec       byte ptr cs:[CitSek]
         jz        Int081
         iret

; ------ £schova registr–

Int081:  push      ax
         push      bx
         push      cx
         push      dx
         push      ds

; ------ aktu ln¡ stav ‡¡ta‡e ‡asu

         xor       ax,ax
         mov       ds,ax                    ; DS <- 0
         mov       ax,ds:[046ch]            ; ‡ten¡ ni‘¨¡ ‡ sti ‡¡ta‡e ‡asu
         mov       dx,ds:[046eh]            ; ‡ten¡ vy¨¨¡ ‡ sti ‡¡ta‡e ‡asu

         push      cs
         pop       ds                       ; DS <- datov˜ segment

         mov       cl,4                     ; po‡et rotac¡
         shl       dx,cl                    ; vy¨¨¡ slovo * 16
         mov       bx,ax                    ; ni‘¨¡ slovo ‡¡ta‡e ‡asu
         mov       cl,12
         shr       bx,cl                    ; nejvy¨¨¡ 4 bity ni‘¨¡ho slova
         add       dx,bx                    ; p©enos 4 bit– do vy¨¨¡ho slova
         mov       cl,4
         shl       ax,cl                    ; ni‘¨¡ slovo * 16
         mov       bx,17478
         div       bx                       ; ‡¡ta‡ ‡asu / 1092.375
         push      dx                       ; £schova zbytku v minutˆ
         mov       bx,60                    ; po‡et minut v hodinˆ
         xor       dx,dx                    ; DX <- 0, AX=po‡et minut
         div       bx                       ; v˜po‡et hodiny a minuty
         push      dx                       ; £schova minuty

                                          ;* zobrazen¡ £daje hodin
         mov       dx,1*HI+69               ; po‡ te‡n¡ pozice textu
         mov       ah,ds:[ColDat]
         call      Disp2Nm0
         mov       al,":"                   ; oddˆlovac¡ znak
         call      Disp1Chr                 ; zobrazen¡ oddˆlovac¡ho znaku

                                          ;* zobrazen¡ £daje minut
         pop       ax                       ; n vrat minut
         mov       ah,ds:[ColDat]
         call      Disp2Nm0
         mov       al,":"                   ; oddˆlovac¡ znak
         call      Disp1Chr                 ; zobrazen¡ oddˆlovac¡ho znaku

                                          ;* zobrazen¡ £daje sekund
         pop       ax                       ; n vrat zbytku do 1 minuty
         push      dx                       ; £schova pozice textu
         mov       bx,60                    ; po‡et sekund v minutˆ
         mul       bx                       ; v˜po‡et po‡tu sekund a setin
         mov       bx,17478                 ; p©evod na abs. £daj
         div       bx                       ; v˜po‡et po‡tu sekund

                                          ;* v˜po‡et ‡ sti sekundy
         push      ax                       ; £schova po‡tu sekund
         mov       ax,dx                    ; zbytek do 1 sekundy
         mov       bx,18                    ; po‡et impuls– v 1 sekundˆ
         mul       bx                       ; v˜po‡et po‡tu impuls– v sekundˆ
         mov       bx,17478                 ; p©evod na abs. £daj
         div       bx                       ; v˜po‡et po‡tu ‡ sti sekundy
         sub       al,18                    ; -zbytek do sekundy
         neg       al                       ; zbytek do sekundy
         inc       al
         mov       ds:[CitSek],al           ; ‡¡ta‡ obsluhy hodin
         pop       ax                       ; n vrat po‡tu sekund

         pop       dx                       ; n vrat pozice textu
         mov       ah,ds:[ColDat]
         call      Disp2Nm0

; ------ p©¡prava k zobrazen¡ uplynul‚ doby akce

         cmp       byte ptr ds:[ParsTime],0 ; zobraz¡ se uplynul˜ ‡as ?
         je        Int089                   ; nezobraz¡ se

; ------ v˜po‡et po‡tu minut

         xor       ax,ax
         mov       ds,ax
         mov       ax,ds:[46ch]
         push      cs
         pop       ds
         sub       ax,ds:[BegTime]          ; uplynul  doba
         xor       dx,dx
         mov       cx,1092                  ; po‡et impuls– na minutu
         div       cx                       ; v˜po‡et po‡tu minut
         push      dx

         mov       dx,19*HI+67
         xor       bx,bx
         mov       ah,0
         mov       cl,2                     ; ¨¡©ka pole
         mov       ch,ds:[ColRF0]
         call      DispNum

; ------ oddˆlovac¡ te‡ka

         mov       ah,ch
         mov       al,":"
         call      Disp1Chr

; ------ zobrazen¡ £daje sekund

         pop       ax
         mov       cl,18
         div       cl
         mov       ah,ch
         call      Disp2Nm0

; ------ n vrat registr–

Int089:  pop       ds
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         iret

Int08    ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                   Univerz ln¡ obsluha kl vesnice a my¨i
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;þ
; -----------------------------------------------------------------------------
;        vypr zdnˆn¡ bufferu kl vesnice
; -----------------------------------------------------------------------------

FlushKey PROC      NEAR

         push      bx

FlshKey1:call      TestKey                  ; test, zda je p©ipraven znak
         jc        FlshKey2                 ; nen¡ dal¨¡ znak z kl vesnice
         call      InpKey                   ; zru¨en¡ znaku z kl vesnice
         jmp       short FlshKey1           ; test dal¨¡ho znaku

FlshKey2:pop       bx
         ret

FlushKey ENDP

; -----------------------------------------------------------------------------
;      test, zda je p©ipraven znak z kl vesnice (CY=nen¡, jinak BX=k¢d kl vesy)
; -----------------------------------------------------------------------------

TestKey  PROC      NEAR

         push      ax

; ------ nulov n¡ p©¡znaku stisku INSERT (pro opakov n¡ kl vesy INSERT)

         push      ds
         xor       ax,ax
         mov       ds,ax                    ; DS <- 0
         and       byte ptr ds:[418h],not b7 ; nulov n¡ p©¡znaku stisku INSERT
         pop       ds

; ------ test znaku z kl vesnice

         mov       ah,1
         int       16h                      ; test stavu kl vesnice
         jnz       TestKey2                 ; je p©ipravena kl vesa

; ------ obsluha bˆhem ‡ek n¡ na kl vesu

         call      Int28                    ; obsluha bˆhem ‡ek n¡ na znak
         mov       ah,1
         int       16h                      ; test stavu kl vesnice
         jnz       TestKey2                 ; je p©ipravena kl vesa
         stc                                ; p©¡znak - nen¡ kl vesa
         pop       ax
         ret

TestKey2:cmp       al,0
         je        TestKey3
         mov       ah,0
TestKey3:clc
         xchg      ax,bx
         pop       ax
         ret

TestKey  ENDP

; -----------------------------------------------------------------------------
;        vstup znaku z kl vesnice -> BX
; -----------------------------------------------------------------------------

InpKey   PROC      NEAR

         push      ax

; ------ ‡ek n¡ na vstup znaku z kl vesnice

InpKey1: sti
         call      TestKey                  ; test znaku z kl vesnice
         jc        InpKey1

; ------ vstup znaku z kl vesnice

InpKey2: mov       ah,0
         int       16h

; ------ korekce k¢du kl vesy

         cmp       al,0
         je        InpKey3
         mov       ah,0
InpKey3: call      UpCase

; ------ n hrada kl vesy Ctrl-Break kl vesou Esc

         or        ax,ax                    ; je kl vesa Ctrl-Break ?
         jnz       InpKey4                  ; nen¡ kl vesa Ctrl-Break
         mov       ax,1bh                   ; n hrada kl vesou Esc

InpKey4: xchg      ax,bx                    ; BX <- kl vesa
         pop       ax
         ret

InpKey   ENDP

; -----------------------------------------------------------------------------
;        obsluha INT 28h s £schovou registr–
; -----------------------------------------------------------------------------

Int28    PROC      NEAR

         pushf
         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         push      ds
         push      es

         int       28h

         pop       es
         pop       ds
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         popf
         ret

Int28    ENDP

; -----------------------------------------------------------------------------
;        inicializace obsluhy my¨i
; -----------------------------------------------------------------------------

InitMous PROC      NEAR

; ------ kontrola adresy INT 33h, zda je platn 

         mov       ax,3533h
         int       21h                      ; poskytnut¡ adresy INT 33h
         mov       ax,es                    ; segment adresy
         cmp       ax,60h                   ; minim ln¡ segment
         jb        IniMous1                 ; neplatn  adresa INT 33h
         inc       bx                       ; offset 0ffffh je tak‚ neplatn˜
         jz        IniMous1                 ; neplatn  adresa INT 33h

; ------ test instalace my¨i

         mov       al,0
         call      Int33                    ; test instalace my¨i
         inc       ax                       ; je my¨ nainstalovan  ?
         jz        IniMous2                 ; my¨ je nainstalovan  OK
IniMous1:mov       byte ptr ds:[MouseIns],0 ; p©¡znak - nen¡ instalovan  my¨
         ret

IniMous2:mov       byte ptr ds:[MouseIns],1 ; p©¡znak nainstalovan‚ my¨i

; ------ definov n¡ kurzoru my¨i

         mov       al,10
         xor       bx,bx                    ; p©¡znak softwarov‚ho kurzoru
         mov       cx,0ffffh                ; maska obrazovky (AND)
         mov       dx,7700h                 ; maska kurzoru (XOR)
         call      Int33                    ; instalace rozmˆr– okna

; ------ definice vertik ln¡ch rozmˆr– okna

         mov       al,8
         xor       cx,cx                    ; minim ln¡ © dek
         mov       dx,24*8                  ; maxim ln¡ © dek okna
         call      Int33                    ; definice © dk– okna

; ------ definice horizont ln¡ch rozmˆr– okna

         mov       al,7
         xor       cx,cx                    ; minim ln¡ pozice
         mov       dx,79*8                  ; maxim ln¡ pozice okna
         call      Int33                    ; definov n¡ pozic okna

; ------ nastaven¡ pozice kurzoru my¨i

         mov       al,4
         xor       cx,cx
         xor       dx,dx
         mov       ds:[MousePoz],dx         ; nulov n¡ pozice kurzoru my¨i
         call      Int33                    ; nastaven¡ pozice kurzoru my¨i

; ------ zapnut¡ kurzoru my¨i

         mov       byte ptr ds:[MouseAkt],0 ; p©¡znak - kurzor my¨i nen¡ zapnut˜
         call      MouseOn                  ; zapnut¡ kurzoru my¨i

IniMous9:ret

InitMous ENDP

; -----------------------------------------------------------------------------
;        zapnut¡ kurzoru my¨i
; -----------------------------------------------------------------------------

MouseOn  PROC      NEAR

; ------ test, zda lze kurzor my¨i zapnout

         cmp       byte ptr ds:[MouseIns],0 ; je my¨ nainstalovan  ?
         je        MouseOn9                 ; my¨ nen¡ nainstalovan 
         cmp       byte ptr ds:[MouseAkt],0 ; je kurzor my¨i zapnut˜ ?
         jne       MouseOn9                 ; kurzor mu¨i je ji‘ zapnut˜

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx

; ------ zapnut¡ kurzoru my¨i

         mov       al,1
         call      Int33                    ; zapnut¡ kurzoru my¨i
         mov       byte ptr ds:[MouseAkt],1 ; p©¡znak zapnut¡ kurzoru my¨i

; ------ n vrat registr–

         pop       dx
         pop       cx
         pop       bx
         pop       ax
MouseOn9:ret

MouseOn  ENDP

; -----------------------------------------------------------------------------
;        vypnut¡ kurzoru my¨i
; -----------------------------------------------------------------------------

MouseOff PROC      NEAR

; ------ test, zda lze kurzor my¨i vypnout

         cmp       byte ptr ds:[MouseIns],0 ; je my¨ nainstalovan  ?
         je        MouseOf9                 ; my¨ nen¡ nainstalovan 
         cmp       byte ptr ds:[MouseAkt],0 ; je kurzor my¨i zapnut˜ ?
         je        MouseOn9                 ; kurzor mu¨i je ji‘ vypnut˜

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx

; ------ vypnut¡ kurzoru my¨i

         mov       al,2
         call      Int33                    ; vypnut¡ kurzoru my¨i
         mov       byte ptr ds:[MouseAkt],0 ; p©¡znak vypnut¡ kurzoru my¨i

; ------ n vrat registr–

         pop       dx
         pop       cx
         pop       bx
         pop       ax
MouseOf9:ret

MouseOff ENDP

; -----------------------------------------------------------------------------
;        poskytnut¡ stavu my¨i -> BX (CY=nen¡ ‘ dn  zmˆna)
; -----------------------------------------------------------------------------
; VSTUP:  BX:  0 = nebyla ‘ dn  zmˆna (nastaven CY)
;               1 = zmˆna sou©adnic my¨i
;               2 = uvolnˆn¡ nˆkter‚ho tla‡¡tka
;               3 = stisk lev‚ho tla‡¡tka
;               4 = stisk prav‚ho tla‡¡tka
;               5 = stisk st©edn¡ho tla‡¡tka
;               6 = druh˜ stisk lev‚ho tla‡¡tka
;               7 = druh˜ stisk prav‚ho tla‡¡tka
;               8 = druh˜ stisk st©edn¡ho tla‡¡tka
;          DX=aktu ln¡ sou©adnice kurzoru my¨i (DL=pozice, DH=© dek)
; -----------------------------------------------------------------------------

MouseGet PROC      NEAR

; ------ test, zda je my¨ nainstalovan 

         cmp       byte ptr ds:[MouseIns],0 ; je my¨ nainstalovan  ?
         jne       MouseGt2                 ; my¨ je nainstalovan 
         xor       dx,dx                    ; aktu ln¡ pozice kurzoru my¨i
         xor       bx,bx                    ; p©¡znak - nen¡ zmˆna
         stc                                ; p©¡znak, ‘e nen¡ ‘ dn  zmˆna
         ret

; ------ £schova registr–

MouseGt2:push      ax
         push      cx

; ------ zji¨tˆn¡ aktu ln¡ho stavu my¨i

         mov       al,3
         call      Int33                    ; navr cen¡ aktu ln¡ho stavu my¨i

; ------ v˜po‡et aktu ln¡ch sou©adnic my¨i

         shr       cx,1
         shr       cx,1
         shr       cx,1
         shr       dx,1
         shr       dx,1
         shr       dx,1
         mov       dh,dl                    ; aktu ln¡ © dek
         mov       dl,cl                    ; aktu ln¡ pozice

; ------ £schova aktu ln¡ho stavu tla‡¡tek

         and       bx,7                     ; pouze platn‚ bity
         mov       al,bl                    ; nov˜ stav tla‡¡tek
         xchg      bl,ds:[MouseKey]         ; ulo‘en¡ nov‚ho stavu tla‡¡tek
         mov       ah,bl                    ; star˜ stav tla‡¡tek
         mov       cx,ax                    ; £schova star‚ho a nov‚ho stavu
         not       ah                       ; maska - uvolnˆn  tla‡¡tka
         and       al,ah                    ; stisknut  tla‡¡tka
         jz        MouseGt5                 ; nebyl stisk ‘ dn‚ho tla‡¡tka

; ------ porovn n¡ ‡asu p©ede¨l‚ho stisku tla‡¡tka

         push      ax
         push      ds
         xor       ax,ax
         mov       ds,ax
         mov       ax,ds:[46ch]             ; ‡as
         pop       ds
         sub       ax,ds:[MouseTim]         ; uplynul  doba
         add       ds:[MouseTim],ax         ; nov˜ aktu ln¡ ‡as
         cmp       ax,4                     ; byla asi 1/4 sekundy ?
         pop       ax
         mov       bl,3                     ; p©¡znak - stisk lev‚ho tla‡¡tka
         ja        MouseGt4                 ; byl del¨¡ ‡as
         mov       bl,6                     ; p©¡znak - druh˜ stisk tla‡¡tka

; ------ rozli¨en¡, zda bylo nˆkter‚ tla‡¡tko stisknuto

MouseGt4:shr       al,1                     ; stisknuto lev‚ tla‡¡tko ?
         jc        MouseGt6                 ; stisknuto lev‚ tla‡¡tko
         inc       bx                       ; p©¡znak - stisk prav‚ho tla‡¡tka
         shr       al,1                     ; stisknuto prav‚ tla‡¡tko ?
         jc        MouseGt6                 ; stisknuto prav‚ tla‡¡tko
         inc       bx                       ; p©¡znak - stisk st©edn¡ho tla‡¡tka
         shr       al,1                     ; stisknuto st©edn¡ tla‡¡tko ?
         jc        MouseGt6                 ; stisknuto st©edn¡ tla‡¡tko

; ------ test, zda bylo nˆkter‚ tla‡¡tko uvolnˆno

MouseGt5:mov       bl,2                     ; p©¡znak uvolnˆn¡ tla‡¡tka
         not       cl                       ; maska uvolnˆn˜ch tla‡¡tek
         and       cl,ch                    ; bylo nˆkter‚ tla‡¡tko uvolnˆno ?
         jnz       MouseGt6                 ; bylo nˆkter‚ tla‡¡tko uvolnˆno
         xor       bx,bx                    ; jinak ‘ dn  zmˆna

; ------ ulo‘en¡ nov˜ch sou©adnic my¨i a rozli¨en¡ stavu

MouseGt6:or        bx,bx                    ; byla nˆjak  zmˆna tla‡¡tek ?
         mov       cx,ds:[MousePoz]         ; £schova star˜ch sou©adnic my¨i
         mov       ds:[MousePoz],dx         ; ulo‘en¡ nov˜ch sou©adnic my¨i
         jnz       MouseGt8                 ; byla nˆjak  zmˆna tla‡¡tek
         cmp       dx,cx                    ; byla zmˆna sou©adnic my¨i ?
         stc                                ; p©¡znak, ‘e nebyla ‘ dn  zmˆna
         je        MouseGt8                 ; nebyla zmˆna sou©adnic my¨i
         inc       bx                       ; BX <- 1 p©¡znak zmˆny sou©adnic
         clc                                ; p©¡znak, ‘e byla nˆjak  zmˆna

; ------ n vrat registr–

MouseGt8:pop       cx
         pop       ax
         ret

MouseGet ENDP

; -----------------------------------------------------------------------------
;        vol n¡ obsluhy INT 33h s £schovou registr– (funkce v AL)
; -----------------------------------------------------------------------------

Int33    PROC      NEAR

         pushf
         push      si
         push      di
         push      bp
         push      ds
         push      es

         sti
         mov       ah,0
         int       33h

         pop       es
         pop       ds
         pop       bp
         pop       di
         pop       si
         popf
         ret

Int33    ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                     Univerz ln¡ obsluha displeje
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;þ
; -----------------------------------------------------------------------------
;        vypnut¡ kurzoru
; -----------------------------------------------------------------------------

KurzOff  PROC      NEAR

         push      dx
         mov       dx,25*256                ; prvn¡ © dek "za rohem"
         call      SetKurz                  ; nastaven¡ pozice kurzoru
         pop       dx
         ret

KurzOff  ENDP

; -----------------------------------------------------------------------------
;        nastaven¡ pozice kurzoru DX
; -----------------------------------------------------------------------------

SetKurz  PROC      NEAR

         push      ax
         push      bx
         mov       bh,0                     ; aktivn¡ videostr nka 0
         mov       ah,2                     ; funkce - nastaven¡ pozice kurzoru
         call      Int10                    ; nastaven¡ pozice kurzoru
         pop       bx
         pop       ax
         ret

SetKurz  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ hexadecim ln¡ho bajtu AL (AH=barva, DX=pozice)
; -----------------------------------------------------------------------------

DispHByt PROC      NEAR

         push      ax
         shr       al,1
         shr       al,1
         shr       al,1
         shr       al,1
         call      DispHx
         pop       ax
         call      DispHx
         ret

DispHByt ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ hexadecim ln¡ho znaku AL (AH=barva, DX=pozice)
; -----------------------------------------------------------------------------

DispHx   PROC      NEAR

         push      ax
         and       al,0fh
         cmp       al,10
         jb        DispHx2
         add       al,7
DispHx2: add       al,"0"
         call      Disp1Chr
         pop       ax
         ret

DispHx   ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ ‡¡sla AL 2 ‡¡slice s dosplnˆn¡m nuly (AH=barva)
; -----------------------------------------------------------------------------

Disp2Nm0 PROC      NEAR

; ------ dek¢dov n¡ ‡¡sla

         push      cx
         push      ax

         mov       ah,0
         mov       cl,10
         div       cl                       ; v˜po‡et ‡¡slic
         add       ax,"00"                  ; korekce na ASCII ‡¡slice
         xchg      ax,cx

         pop       ax

; ------ zobrazen¡ ‡¡slice des¡tek

         mov       al,cl                    ; des¡tky
         call      Disp1Chr                 ; zobrazen¡ ‡¡slice des¡tek

; ------ zobrazen¡ ‡¡slice jednotek

         mov       al,ch                    ; jednotky
         call      Disp1Chr                 ; zobrazen¡ ‡¡slice jednotek
         pop       cx
         ret

Disp2Nm0 ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ ‡¡sla AL 2 ‡¡slice s doplnˆn¡m mezery (AH=barva)
; -----------------------------------------------------------------------------

Disp2NmS PROC      NEAR

; ------ dek¢dov n¡ ‡¡sla

         push      cx
         push      ax

         mov       ah,0
         mov       cl,10
         div       cl                       ; v˜po‡et ‡¡slic
         add       ax,"00"                  ; korekce na ASCII ‡¡slice
         xchg      ax,cx

         pop       ax

; ------ zobrazen¡ ‡¡slice des¡tek

         mov       al,cl                    ; des¡tky
         cmp       al,"0"                   ; je prvn¡ znak nula ?
         jne       Disp2NS2                 ; nen¡ nula
         mov       al," "                   ; n hrada nuly mezerou
Disp2NS2:call      Disp1Chr                 ; zobrazen¡ ‡¡slice des¡tek

; ------ zobrazen¡ ‡¡slice jednotek

         mov       al,ch                    ; jednotky
         call      Disp1Chr                 ; zobrazen¡ ‡¡slice jednotek
         pop       cx
         ret

Disp2NmS ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ ‡¡sla BX:AX; CL=¨¡©ka pole, CH=barva, DX=pozice (zmˆn¡ se)
; -----------------------------------------------------------------------------

DispNum  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      si
         push      di
         push      bp

; ------ p©¡prava registr–

         mov       di,dx                    ; DI <- £schova pozice na displeji
         mov       dx,bx                    ; ‡¡slo HIGH
         xchg      ax,bp                    ; BP <- ‡¡slo LOW
         mov       si,10                    ; SI = dˆlitel
         mov       bh,ch                    ; BH <- barva
         mov       ch,0                     ; CH = ‡¡ta‡ ‡¡slic, CL = ¨¡©ka
         mov       bl,"."                   ; BL = znak mezery, BH = barva

; ------ v˜po‡et jedn‚ ‡¡slice

DispNum1:xchg      ax,dx                    ; AX <- ‡¡slo HIGH
         xor       dx,dx                    ; DX <- 0
         div       si                       ; vydˆlen¡ ‡¡sla HIGH
         xchg      ax,bp                    ; AX <- n vrat LOW, BP<-£schova HIGH
         div       si                       ; vydˆlen¡ ‡¡sla LOW

; ------ oddˆlova‡ © d–

         cmp       ch,3
         je        DispNum2                 ; jsou tis¡ce
         cmp       ch,7
         jne       DispNum3                 ; nejsou miliony
DispNum2:push      bx                       ; ulo‘en¡ oddˆlovac¡ te‡ky
         inc       ch                       ; zv˜¨en¡ ‡¡ta‡e znak– v z sobn¡ku

; ------ ulo‘en¡ ‡¡sla

DispNum3:add       dl,"0"                   ; korekce na znak ASCII
         mov       dh,bh                    ; barva
         push      dx                       ; ulo‘en¡ ‡¡sla do z sobn¡ku
         inc       ch                       ; zv˜¨en¡ ‡¡ta‡e ‡¡slic v z sobn¡ku
         mov       dx,bp                    ; DX <- n vrat ‡¡sla HIGH
         xchg      ax,bp                    ; BP <- ‡¡slo LOW, (AX <- HIGH)

; ------ test, zda bude dal¨¡ ‡¡slice

         or        ax,bp                    ; je ‡¡slo ji‘ = 0 ?
         jnz       DispNum1                 ; dek¢dov n¡ dal¨¡ ‡¡slice

; ------ zobrazen¡ £vodn¡ch mezer

         mov       dx,di                    ; n vrat pozice na displeji
         push      cx
         sub       cl,ch                    ; ode‡ten¡ ‡¡slic k zobrazen¡
         mov       ch,0
         jbe       DispNum4                 ; nezbyly ‘ dn‚ mezery
         mov       ah,bh                    ; barva
         mov       al," "                   ; mezera k vymaz n¡ bufferu
         call      DispChr                  ; zobrazen¡ £vodn¡ch mezer
DispNum4:pop       ax
         mov       cl,ah                    ; CX = po‡et znak– v z sobn¡ku

; ------ zobrazen¡ ‡¡sla

DispNum6:pop       ax
         call      Disp1Chr
         loop      DispNum6                  ; dal¨¡ znak

; ------ n vrat registr–

         pop       bp
         pop       di
         pop       si
         pop       cx
         pop       bx
         pop       ax
         ret

DispNum  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ st¡nu pod oknem (po‡ tek DX, rozmˆry CX)
; -----------------------------------------------------------------------------

StinWin  PROC      NEAR

; ------ zobrazen¡ st¡nu po prav‚ stranˆ okna

         push      cx
         push      dx
         add       dl,cl                    ; pozice za oknem
         inc       dh                       ; druh˜ © dek okna
         mov       cl,2                     ; ¨¡©ka st¡nu 2 pozice
StinWin1:call      DispStin                 ; zobrazen¡ st¡nu na jednom © dku
         inc       dh                       ; zv˜¨en¡ ukazatele © dku
         dec       ch                       ; sn¡‘en¡ ‡¡ta‡e v˜¨ky okna
         jnz       StinWin1                 ; st¡n na dal¨¡m © dku
         pop       dx
         pop       cx

; ------ zobrazen¡ st¡nu pod oknem

         push      cx
         push      dx
         add       dh,ch                    ; © dek pod oknem
         add       dl,2                     ; po‡ te‡n¡ pozice
         sub       cl,2                     ; sn¡‘en¡ ¨¡©ky o 2
         call      DispStin                 ; zobrazen¡ st¡nu
         pop       dx
         pop       cx
         ret

StinWin  ENDP

; -----------------------------------------------------------------------------
;        Zobrazen¡ st¡nu (DX=pozice, CL=d‚lka, DL se nezmˆn¡)
; -----------------------------------------------------------------------------

DispStin PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      di
         push      es

; ------ v˜po‡et adresy ve videopamˆti

         mov       es,ds:[SegmVRAM]         ; segment videopamˆti
         mov       al,80                    ; po‡et znak– na © dek
         mul       dh                       ; p©epo‡et © dk– na pozice
         add       al,dl                    ; p©i‡ten¡ pozice
         adc       ah,0
         shl       ax,1                     ; * 2 = offset ve videostr nce
         xchg      ax,di                    ; adresa ve videopamˆti

; ------ z pis st¡nu

         cld                                ; smˆr posunu ukazatele nahoru
         mov       ch,0
         jcxz      DispStn7                 ; nen¡ ‘ dn˜ znak k z pisu
         mov       al,7                     ; barva st¡nu
DispStn6:inc       di                       ; p©esko‡en¡ znaku
         stosb                              ; z pis st¡nu
         loop      DispStn6                 ; z pis dal¨¡ho st¡nu

; ------ n vrat registr–, konec

DispStn7:pop       es
         pop       di
         pop       cx
         pop       ax
         ret

DispStin ENDP

; -----------------------------------------------------------------------------
;        Zobrazen¡ jednoho znaku AX (DX=pozice, DL se zmˆn¡)
; -----------------------------------------------------------------------------

Disp1Chr PROC      NEAR

; ------ £schova registr–

         push      di
         push      es

; ------ v˜po‡et adresy ve videopamˆti

         push      ax
         mov       es,ds:[SegmVRAM]         ; segment videopamˆti
         mov       al,80                    ; po‡et znak– na © dek
         mul       dh                       ; p©epo‡et © dk– na pozice
         add       al,dl                    ; p©i‡ten¡ pozice
         adc       ah,0
         shl       ax,1                     ; * 2 = offset ve videostr nce
         xchg      ax,di                    ; adresa ve videopamˆti
         pop       ax

; ------ zobrazen¡ znaku

         cld                                ; smˆr posunu ukazatele nahoru
         stosw                              ; z pis znaku bez kontroly snˆ‘en¡

; ------ n vrat registr–, konec

         pop       es
         pop       di
         inc       dx                       ; zv˜¨en¡ pozice
         ret

Disp1Chr ENDP

; -----------------------------------------------------------------------------
;        Zobrazen¡ znaku AX (DX=pozice, CL=po‡et znak–, DL se zmˆn¡)
; -----------------------------------------------------------------------------

DispChr  PROC      NEAR

; ------ £schova registr–

         push      cx
         push      di
         push      es

; ------ v˜po‡et adresy ve videopamˆti

         push      ax
         mov       es,ds:[SegmVRAM]         ; segment videopamˆti
         mov       al,80                    ; po‡et znak– na © dek
         mul       dh                       ; p©epo‡et © dk– na pozice
         add       al,dl                    ; p©i‡ten¡ pozice
         adc       ah,0
         shl       ax,1                     ; * 2 = offset ve videostr nce
         xchg      ax,di                    ; adresa ve videopamˆti
         pop       ax

; ------ zobrazen¡ znaku

         cld                                ; smˆr posunu ukazatele nahoru
         mov       ch,0                     ; CX = po‡et znak– k zobrazen¡
         rep       stosw                    ; z pis znaku bez kontroly snˆ‘en¡

; ------ n vrat registr–, konec

         pop       es
         pop       di
         pop       cx
         add       dl,cl                    ; zv˜¨en¡ pozice
         ret

DispChr  ENDP

; -----------------------------------------------------------------------------
;   zobrazen¡ textu DS:SI s d‚lkou (DX=pozice, AH=barva, CH=vysv¡cen  barva)
; -----------------------------------------------------------------------------

Disp0Txt PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      di
         push      es

; ------ v˜po‡et adresy ve videopamˆti

         mov       es,ds:[SegmVRAM]         ; segment videopamˆti
         push      ax                       ; £schova atributu barvy
         mov       al,80                    ; po‡et znak– na © dek
         mul       dh                       ; p©epo‡et © dk– na pozice
         add       al,dl                    ; p©i‡ten¡ pozice
         adc       ah,0
         shl       ax,1                     ; * 2 = offset ve videostr nce
         xchg      ax,di                    ; DI <- adresa ve videopamˆti
         pop       ax                       ; AH = atribut barvy

; ------ p©¡prava parametr– textu

         cld                                ; smˆr posunu ukazatele nahoru
         mov       bh,ch                    ; alternativn¡ barva
         mov       ch,0
         lodsb
         mov       cl,al                    ; po‡et bajt– textu

; ------ zobrazen¡ textu

         jcxz      Disp0Tx7                 ; nen¡ ‘ dn˜ znak k zobrazen¡
Disp0Tx3:lodsb                              ; znak k zobrazen¡
         or        al,al                    ; je p©ep¡na‡ barvy ?
         jnz       Disp0Tx4                 ; nen¡ p©ep¡na‡ barvy
         xchg      ah,bh                    ; zmˆna barvy
         jmp       short Disp0Tx5
Disp0Tx4:stosw                              ; ulo‘en¡ znaku
         inc       dx                       ; zv˜¨en¡ ukazatele pozice
Disp0Tx5:loop      Disp0Tx3                 ; p©enos dat bez kontroly snˆ‘en¡

; ------ n vrat registr–, konec

Disp0Tx7:pop       es
         pop       di
         pop       cx
         pop       bx
         pop       ax
         ret

Disp0Txt ENDP

; -----------------------------------------------------------------------------
;  Zobrazen¡ textu DS:SI (AH=barva, DX=pozice, CL=po‡et znak–, DL a SI se zmˆn¡)
; -----------------------------------------------------------------------------

DispTxt  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      di
         push      es
         mov       ch,0                     ; CX = po‡et znak–

; ------ v˜po‡et adresy ve videopamˆti

         mov       es,ds:[SegmVRAM]         ; segment videopamˆti
         push      ax                       ; £schova atributu barvy
         mov       al,80                    ; po‡et znak– na © dek
         mul       dh                       ; p©epo‡et © dk– na pozice
         add       al,dl                    ; p©i‡ten¡ pozice
         adc       ah,0
         shl       ax,1                     ; * 2 = offset ve videostr nce
         xchg      ax,di                    ; DI <- adresa ve videopamˆti
         pop       ax                       ; AH = atribut barvy

; ------ zobrazen¡ textu

         mov       ch,0
         jcxz      DispTxt7
         cld                                ; smˆr posunu ukazatele nahoru
DispTxt6:lodsb                              ; znak k zobrazen¡
         stosw                              ; ulo‘en¡ znaku
         loop      DispTxt6                 ; p©enos dat bez kontroly snˆ‘en¡

; ------ n vrat registr–, konec

DispTxt7:pop       es
         pop       di
         pop       cx
         pop       ax
         add       dl,cl                    ; zv˜¨en¡ pozice
         ret

DispTxt  ENDP

; -----------------------------------------------------------------------------
;        Detekce videokarty EGA/VGA
; -----------------------------------------------------------------------------

DetCard  PROC      NEAR

         mov       ah,12h                   ; slu‘ba EGA/VGA - informace o EGA
         mov       bx,4510h                 ; poskytnut¡ informac¡ o EGA
         call      Int10                    ; poskytnut¡ informac¡ o EGA/VGA
         cmp       bh,1                     ; kontrola navr cen‚ho m¢du displeje
         ja        DetCard1                 ; nen¡ to karta EGA/VGA
         cmp       bl,3                     ; maxim lnˆ povolen 1 MB pamˆti
         ja        DetCard1                 ; nen¡ to karta EGA/VGA
         inc       byte ptr ds:[EgaVga]     ; 1=p©¡znak karty EGA/VGA
DetCard1:ret

DetCard  ENDP

; -----------------------------------------------------------------------------
;        Vol n¡ INT 10h s £schovou registr–
; -----------------------------------------------------------------------------

Int10    PROC      NEAR

         pushf
         push      si
         push      di
         push      bp
         push      ds
         push      es

         int       10h

         pop       es
         pop       ds
         pop       bp
         pop       di
         pop       si
         popf
         ret

Int10    ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                       R–zn‚ univerz ln¡ procedury
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;þ
; -----------------------------------------------------------------------------
;        p©evod znaku AL na velk‚ p¡smeno
; -----------------------------------------------------------------------------

UpCase   PROC      NEAR

         cmp       al,"a"
         jb        UpCase2
         cmp       al,"z"
         ja        UpCase2
         sub       al,32
UpCase2: ret

UpCase   ENDP

; -----------------------------------------------------------------------------
;                    skok na obsluhu podle BX
; -----------------------------------------------------------------------------
; Procedura se vol  instrukc¡ CALL JumpBX, za kterou n sleduje tabulka
; skok–. Procedura zmˆn¡ svou n vratovou adresu na adresu podle nalezen‚
; polo‘ky v tabulce (nebo podle polo‘ky pro nenalezenou hodnotu).
; -----------------------------------------------------------------------------
; VSTUP: v z sobn¡ku slovo (n vratov  adresa NEAR) = za‡ tek tabulky skok–
;             stuktura tabulky: 1 slovo testovan  hodnota BX
;                               1 slovo adresa NEAR obsluhy
;             konec tabulky: 1 slovo = 0 (p©¡znak konce tabulky)
;                            1 slovo adresa NEAR obsluhy p©i nenalezen¡ k¢du
; -----------------------------------------------------------------------------

JumpBX   PROC      NEAR

; ------ £schova registr–

                                            ; SS:[BP+4] = IP
         push      si                       ; SS:[BP+2] = SI
         push      bp                       ; SS:[BP+0] = BP
         mov       bp,sp

; ------ nalezen¡ hodnoty v tabulce

         mov       si,ss:[bp+4]             ; offset adresy tabulky
JumpBX1: cmp       word ptr ds:[si],0       ; je konec tabulky ?
         je        JumpBX2                  ; konec tabulky - nenalezeno
         cmp       word ptr ds:[si],bx      ; je to hledan  hodnota ?
         je        JumpBX2                  ; hodnota nalezena
         add       si,4                     ; adresa dal¨¡ polo‘ky
         jmp       short JumpBX1            ; test dal¨¡ polo‘ky

; ------ nastaven¡ n vratov‚ adresy podle tabulky

JumpBX2: mov       si,ds:[si+2]             ; adresa skoku
         mov       ss:[bp+4],si             ; nov  n vratov  adresa

; ------ n vrat registr–

         pop       bp
         pop       si
         ret

JumpBX   ENDP

; -----------------------------------------------------------------------------
;        ‡ek n¡ po dobu CX (CX=po‡et impuls– 1/18 sekundy)
; -----------------------------------------------------------------------------

Cekej    PROC      NEAR

         push      ax
         push      cx
         push      ds

         xor       ax,ax
         mov       ds,ax
         jcxz      Cekej3

Cekej1:  mov       ax,ds:[46ch]
         sti
Cekej2:  cmp       ax,ds:[46ch]
         je        Cekej2
         loop      Cekej1

Cekej3:  pop       ds
         pop       cx
         pop       ax
         ret

Cekej    ENDP

; -----------------------------------------------------------------------------
;        zapnut¡ t¢nov‚ho gener toru
; -----------------------------------------------------------------------------
; VSTUP: BX=dˆlic¡ konstanta
;         dw        36485,34437,32505,30680,28958,27333 ; okt va 0
;         dw        25799,24351,22984,21694,20477,19327
;         dw        18243,17219,16252,15340,14479,13667 ; okt va 1
;         dw        12899,12175,11492,10847,10238,9664
;         dw        9121,8609,8126,7670,7240,6833       ; okt va 2
;         dw        6450,6088,5746,5424,5119,4832
;         dw        4561,4305,4063,3835,3620,3417       ; okt va 3
;         dw        3225,3044,2873,2712,2560,2416
;         dw        2280,2152,2032,1918,1810,1708       ; okt va 4
;         dw        1612,1522,1437,1356,1280,1208
;         dw        1140,1076,1016,959,905,854          ; okt va 5
;         dw        806,761,718,678,640,604
;         dw        570,538,508,479,452,427             ; okt va 6
;         dw        403,380,359,339,320,302
;         dw        285,269,254,240,226,214             ; okt va 7
;         dw        202,190,180,169,160,151
;         dw        143,135,127,120,113,107             ; okt va 8
;         dw        101,95,90,85,80,75
;         dw        71,67,63,60,57,53,50,48,45,42,40,38 ; okt va 9
; -----------------------------------------------------------------------------

Ton      PROC      NEAR

         cmp       byte ptr ds:[ParInd],0   ; je zvuk zapnut ?
         je        Ton9                     ; zvuk je vypnut
         push      ax
         mov       al,0b6h
         out       [43h],al
         mov       al,bl
         out       [42h],al
         mov       al,bh
         out       [42h],al
         in        al,[61h]
         or        al,3
         out       [61h],al
         pop       ax
Ton9:    ret

Ton      ENDP

; -----------------------------------------------------------------------------
;        vypnut¡ zvukov‚ho gener toru
; -----------------------------------------------------------------------------

TonOff   PROC      NEAR

         push      ax
         in        al,[61h]
         and       al,not 3
         out       [61h],al                 ; vypnut¡ gener toru
         pop       ax
         ret

TonOff   ENDP

; -----------------------------------------------------------------------------
;        inicializace gener toru n hody
; -----------------------------------------------------------------------------

InitRnd  PROC      NEAR

         push      ax
         push      ds
         xor       ax,ax
         mov       ds,ax
         mov       ax,ds:[46ch]             ; ‡¡ta‡ syst‚mov‚ho ‡asova‡e
         pop       ds
         mov       word ptr ds:[RandomR],ax ; inicializa‡n¡ konstanta
         pop       ax
         ret

InitRnd  ENDP

; -----------------------------------------------------------------------------
;        gener tor n hodn‚ho sektoru ES:DI (512 bajt–) -> DI nov  adresa
; -----------------------------------------------------------------------------

RandomS  PROC      NEAR

         push      ax
         push      bx
         push      cx
         push      dx
         push      bp
         push      si

         cld
         mov       si,256                   ; po‡et slov ke generov n¡
         mov       bp,8405h                 ; pomocn  konstanta
         mov       ax,word ptr ds:[RandomR]
         mov       dx,word ptr ds:[RandomR+2]

RandomS1:mov       bx,dx
         mov       cx,ax
         mul       bp
         shl       cx,1
         shl       cx,1
         shl       cx,1
         add       ch,cl
         add       dx,cx
         add       dx,bx
         shl       bx,1
         shl       bx,1
         add       dx,bx
         add       dh,bl
         mov       cl,5
         shl       bx,cl
         add       dh,bl
         add       ax,1
         adc       dx,0

         xchg      ax,dx
         stosw
         xchg      ax,dx

         dec       si
         jnz       RandomS1                 ; dal¨¡ slovo

         mov       word ptr ds:[RandomR],ax
         mov       word ptr ds:[RandomR+2],dx

         pop       si
         pop       bp
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

RandomS  ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                           BOOT sektor
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;þ
Boot:    jmp       Bstart

         db        'FORM 2.0'               ; identifikace

         dw        512                      ; d‚lka sektoru
BlokSekt db        2                        ; po‡et sektor– na aloka‡n¡ blok
  ;BBlok    db        2                        ; velikost bloku (sektor–)
         dw        1                        ; po‡et rezervovan˜ch sektor–
         db        2                        ; po‡et alok. tabulek FAT
RootMax  dw        70h                      ; maxim ln¡ polo‘ek ROOT
  ;BRoot    dw        70h                      ; max. po‡et polo‘ek ROOT
CelkSekt dw        2*40*9                   ; celkov˜ po‡et sektor– m‚dia
  ;BNMax    dw        720                      ; celkem sektor– na m‚diu
Popis    db        0ffh                     ; popisova‡ m‚dia
  ;BIdent   db        0fdh                     ; popisova‡ m‚dia
FatSekt  db        2                        ; po‡et sektor– na FAT
         db        0
  ;BNFat    dw        3                        ; po‡et sektor– v jedn‚ FAT
NumSekt  db        9                        ; po‡et sektor– na stopu
         db        0                        ; doplnˆn¡ na slovo
  ;BSekt    dw        9                        ; po‡et sektor– na stopu
NumStran db        2                        ; po‡et stran disku
         db        0
  ;BStran   dw        2                        ; po‡et hlav disku
         dd        0                        ; skryt˜ch sektor– (4-bajty)
         dd        0                        ; celkem sektor– (4-bajty)

         db        0                        ; disk (pracovn¡)
         db        0                        ; hlava (pracovn¡)

         db        29h                      ; identifik tor
BSerie   dd        0                        ; s‚riov‚ ‡¡slo

         db        'NO NAME    '
         db        'FAT12   '

Bstart:  xor       ax,ax
         mov       ss,ax
         mov       sp,7c00h
         sti

         push      ss
         pop       ds
         push      ss
         pop       es

         mov       si,offset(BText-Boot)+7c00h

BDispTxt:cld
         lodsb
         or        al,al
         jz        BRet
         mov       ah,0eh
         mov       bx,7
         int       10h
         jmp       short BDispTxt

BRet:    sti
         xor       ax,ax
         int       16h

         int       19h

BText    db        13
         db        'Nesystemova disketa. Vymente ji',13,10
         db        'a stisknete libovolnou klavesu.',13,10,0

BootEnd  label     byte

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                                 Data
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;þ

INCLUDE  FONT14.ASM
INCLUDE  FONT16.ASM

; ------ konverzn¡ tabulka z k¢du Kamenick˜ch na k¢d bez diakritiky

TbKamCS0 label     byte
         db        'CuedaDTceELIllAA'
         db        'EzZooOuUyOUSLYRt'
         db        'aiounNUOsrrR',172,173,174,175
         db        176,177,178,179,180,181,182,183,184,185,186,187,188,189,190,191
         db        192,193,194,195,196,197,198,199,200,201,202,203,204,205,206,207
         db        208,209,210,211,212,213,214,215,216,217,218,219,220,221,222,223
         db        224,225,226,227,228,229,230,231,232,233,234,235,236,237,238,239
         db        240,241,242,243,244,245,246,247,248,249,250,251,252,253,254,255

; ------ konverzn¡ tabulka z k¢du Kamenick˜ch na k¢d Latin 2

TbKamLat label     byte
         db        172,'‚',212,'„',210,155,159,216,183,145,214,150,146,'Ž',181
         db        '',167,166,'“”',224,133,233,236,'™š',230,149,237,252,156
         db        ' ¡¢£',229,213,222,226,231,253,234,232,'/',245,174,175
         db        176,177,178,179,180,179,186,187,191,185,186,187,188,188,217,191
         db        192,193,194,195,196,197,179,186,200,201,202,203,204,205,206,205
         db        188,205,196,200,192,218,201,186,205,217,218,219,220,219,219,223
         db        'a',225,'G','p','S','s','m','t','f','T','O','d','0','/','E','N'
         db        '=','+','>','<',218,217,246,'=',248,250,250,'V','n','2',254,255

; ------ chybov  hl ¨en¡ p©i startu programu

DiskTxt  db        'Neni nainstalovana zadna disketova mechanika !',13,10,'$'
MemTxt   db        'Chyba - nedostatek pameti ke spusteni programu !',13,10,'$'

SelChar  db        251                      ; znak pro ozna‡en¡ v˜bˆru voleb

BuffTxt  dw        0                        ; adresa bufferu text–

; -----------------------------------------------------------------------------
;        texty (bajty d‚lek se nekonvertuj¡, proto‘e jsou v‘dy < 128 !)
; -----------------------------------------------------------------------------

TextBeg  label     byte                     ; za‡ tek text– k transpozici

TextKap  db        6,'celkem'
TextVol  db        5,'volne'
TextBad  db        5,'vadne'
TextBoot db        4,'BOOT'
TextRoot db        4,'ROOT'
TextFat1 db        4,'FAT1'
TextFat2 db        4,'FAT2'
TextDob  db        4,'doba'

TextOK   db        2,' û'
TextNOK  db        2,' x'
TextXOK  db        2,'  '

LicTxt   db        40,'FORM verze 2.00     (c) Miroslav Nˆme‡ek'
TextDisk db        5,'Disk '
TextAut  db        10,'utomatick˜'
TextStra db        7,'strana '

VolText  label     byte                     ; texty k volb m
         db        15,'Opera‡n¡ syst‚m'
         db        13,'N vˆ¨t¡ disku'
         db        13,'Zachov n¡ dat'
         db        12,'Jedna strana'
         db        14,'Polo‘ek v Root'
         db        16,'Zobrazen¡ obsahu'
         db        15,'Zrychlen˜ start'
         db        11,'Zpˆtn˜ smˆr'
         db        16,'Zvukov  indikace'
         db        10,'Verifikace'
         db        16,'Dopl¤uj¡c¡ stopy'
         db        15,'Testovac¡ z pis'
         db        16,'Form tovac¡ znak'
         db        17,'Optimalizace chyb'
         db        7,'€e¨tina'

VolHlp   label     byte                     ; n povˆdy k volb m
         db        39,'Instalace opera‡n¡ho syst‚mu na disketu'
         db        38,'Zad n¡ n vˆ¨t¡ disku (jmenovka, LABEL)'
         db        32,'Zachov n¡ p–vodn¡ho obsahu disku'
         db        36,'Form tov n¡ disku pouze jednostrannˆ'
         db        38,'Po‡et polo‘ek z kladn¡ho adres ©e Root'
         db        39,'Zobrazen¡ obsahu diskety k zform tov n¡'
         db        37,'Zrychlen˜ start s p©edrozbˆhem motoru'
         db        39,'Form tov n¡ smˆrem od vy¨¨¡ch stop dol–'
         db        37,'Zvukov  indikace ukon‡en¡ form tov n¡'
         db        37,'Kontroln¡ ‡ten¡ stopy po zform tov n¡'
         db        38,'Po‡et dopl¤uj¡c¡ch stop disku (0 a‘ '
PocPHlp2 db        '6)'
         db        34,'Po‡et ovˆ©ovac¡ch z pis– (0 a‘ 99)'
         db        35,'Form tovac¡ znak (HEX k¢d 00 a‘ FF)'
         db        36,'Optimalizace sektor– p©i vzniku chyb'
         db        29,'K¢d ‡e¨tiny k zobrazen¡ text–'

RootHlp  db        40,' Zadejte 16 a‘ 992, 0=standardn¡ form t '
PocPHlp  db        40,' Zadejte po‡et stop 0 a‘ '
PocPHlp1 db        '6, 0=standard  '
PocZHlp  db        41,' Zadejte po‡et testovac¡ch z pis– 0 a‘ 99'
HexFHlp  db        40,' Zadejte form tovac¡ znak 00 a‘ FF (HEX)'

VolHlp0  db        44,' (',0,'ENTER',0,'=zmˆna, ',0,'ESC',0,'=konec, ',0,'F1',0,'=n povˆda) '

FormHlpA db        37,' Autom. detekce form tu podle diskety'
FormHlp1 db        15,' 5 1/4" HD SS, '
FormHlp2 db        9,' B, Root '

FormHlp0 db        48,' (',0,'ENTER',0,'=form tuj, ',0,'ESC',0,'=konec, ',0,'F1',0,'=n povˆda) '

; -----------------------------------------------------------------------------
;        p©ep¡na‡e
; -----------------------------------------------------------------------------

VolTCest db        '          -'
         db        '    vlastn¡'
         db        'Kamenick˜ch'
         db        '    Latin 2'

VolTStd  db        'STD'

TextEnd  label     byte                     ; konec text– k transpozici

; -----------------------------------------------------------------------------
;        definice voleb
; -----------------------------------------------------------------------------
; Definice jedn‚ polo‘ky voleb:
;    0: (1) parametry polo‘ky
;              bit 0: 1=p©ep¡na‡
;              bit 1: 1=dekadick  polo‘ka (bajt)
;              bit 2: 1=hexadecim ln¡ polo‘ka (bajt)
;              bit 3: 1=volba ‡e¨tiny (polo‘ky BEZ, DWN, KAM, LAT)
;                  (bit 4: 1=volba zachov n¡ dat (polo‘ky -, STOP, DISK))
;              bit 5: 1=po‡et polo‘ek v ROOT
;    1: (2) adresa parametru polo‘ky
;    3: (1) po‡ te‡n¡ pozice k zobrazen¡ volby
;    4: (1) po‡ te‡n¡ © dek k zobrazen¡ volby
;    5: (1) offset pozice k zobrazen¡ parametru polo‘ky (na obrazovce)
;    6: (1) d‚lka pole parametru polo‘ky (na obrazovce)
; -----------------------------------------------------------------------------

VolDef   label     byte
         db        b0
         dw        ParSys
         db        4,3,18,1

         db        b0
         dw        ParLab
         db        4,4,18,1

         db        b0
         dw        ParDat
         db        4,5,18,1

         db        b0
         dw        ParStra
         db        4,6,18,1

         db        b5
         dw        ParRoot
         db        4,7,16,3

         db        b0
         dw        ParObs
         db        28,3,19,1

         db        b0
         dw        ParStrt
         db        28,4,19,1

         db        b0
         dw        ParBack
         db        28,5,19,1

         db        b0
         dw        ParInd
         db        28,6,19,1

         db        b0
         dw        ParVerif
         db        28,7,19,1

         db        b1
         dw        ParStop
         db        53,3,22,1

         db        b1
         dw        ParTest
         db        53,4,21,2

         db        b2
         dw        ParChar
         db        53,5,21,2

         db        b0
         dw        ParOptim
         db        53,6,22,1

         db        b3
         dw        ParCest
         db        53,7,12,11
VolDef0  label     byte

; -----------------------------------------------------------------------------
;        £schova aktu ln¡ho zobrazen¡ data a ‡asu
; -----------------------------------------------------------------------------

OldBreak db        1                        ; £schova p©¡znaku BREAK

CitSek   db        18                       ; ‡¡ta‡ k zobrazen¡ sekund
Old08    dd        0                        ; p–vodn¡ adresa INT 08h

BegTime  dw        0                        ; ‡as za‡ tku akce (ni‘¨¡ slovo)
;ElapsTim dw        0                        ; uplynul  doba akce
ParsTime db        0                        ; p©¡znak, ‘e se zobraz¡ doba akce

RandomR  dd        21510d31h                ; promˆnn  pro gener tor n hody

; -----------------------------------------------------------------------------
;        tabulka disketov˜ch parametr–
; -----------------------------------------------------------------------------

Old1e    dd        -1                       ; p–vodn¡ vektor INT 1eh

Int1e    label     byte                     ; vlastn¡ tabulka INT 1eh
         db        0dfh                     ; bity 0 a‘ 3: rychlost krokov n¡
                                            ; bity 4 a‘ 7: ‡as zvednut¡ hlavy
         db        2                        ; bit 0: 1=je provoz DMA
                                            ; bity 1 a‘ 7: ‡as spu¨tˆn¡ hlavy
MotorOff db        37                       ; 2: vypnut¡ motoru v 1/18 sekundy
         db        2                        ; 3: velikost sektoru
         db        9                        ; 4: po‡et sektor– na stopu
         db        42                       ; 5: mezisekt. mezera ‡ten¡/z pis
         db        255                      ; 6: d‚lka p©en ¨en˜ch dat
         db        80                       ; 7: mezisekt. mezera form tov n¡
FillCh   db        0                        ; 8: plnic¡ znak form tov n¡
         db        15                       ; 9: doba ust len¡ hlav v [ms]
MotorOn  db        8                        ; 10: doba pro rozbˆh motoru v 1/8s
         db        39                       ; 11: posledn¡ stopa na disku
         db        80h                      ; 12: p©enosov  rychlost

; -----------------------------------------------------------------------------
;        parametry displeje
; -----------------------------------------------------------------------------

         EVEN
SegmVRAM dw        0b800h                   ; segment videopamˆti
EgaVga   db        0                        ; 1=p©¡znak, ‘e je karta EGA/VGA

; -----------------------------------------------------------------------------
;        parametry my¨i
; -----------------------------------------------------------------------------

MousePoz dw        0                        ; aktu ln¡ pozice my¨i
MouseKey db        0                        ; aktu ln¡ stav tla‡¡tek
                                            ;   bit 0: 1=lev‚ tla‡¡tko
                                            ;   bit 1: 1=prav‚ tla‡¡tko
                                            ;   bit 2: 1=st©edn¡ tla‡¡tko
MouseTim dw        0                        ; ‡as minul‚ho stisku tla‡¡tka
MouseIns db        0                        ; 1=p©¡znak instalace my¨i
MouseAkt db        0                        ; 1=p©¡znak zapnut¡ kurzoru my¨i

; -----------------------------------------------------------------------------
;        barvy
; -----------------------------------------------------------------------------

ColDat   db        1bh                      ; barva data a ‡asu

ColNadp  db        1bh                      ; barva textu nadpisu

ColVol0  db        1bh
ColVol   db        1fh                      ; barva textu voleb v nadpisu
ColVolK  db        30h                      ; barva kurzoru
ColEdi   db        0fh                      ; barva editovan‚ polo‘ky

ColDiskR db        1eh                      ; barva r mu volby form tu
ColDisk1 db        1bh                      ; bˆ‘n  barva
;ColDisk2 db        1fh                      ; vysv¡cen  barva polo‘ek
ColDisk3 db        30h                      ; barva kurzoru
ColDisk4 db        1eh                      ; barva hork‚ kl vesy

ColRamN  db        1eh                      ; barva r mu nadpisu

ColPod   db        70h                      ; barva podkladu obrazovky

ColHelp  db        30h                      ; z kladn¡ barva textu n povˆdy
ColHelp1 db        34h                      ; vysv¡cen  barva textu n povˆdy

ColRF    db        1bh                      ; barva r me‡ku form tov n¡
ColRFR   db        1eh                      ; barva r me‡ku
ColRF0   db        1fh                      ; vysv¡cen  barva r me‡ku
ColRFNul db        7                        ; barva - pr zdn‚ pole v r me‡ku

ColForm  db        70h                      ; zna‡ka form tov n¡
ColVerif db        70h                      ; zna‡ka verifikace
ColWrite db        70h                      ; zna‡ka z pisu
ColRead  db        70h                      ; zna‡ka ‡ten¡
ColOK    db        0ah                      ; barva OK
ColErr   db        4eh                      ; barva chyby

; -----------------------------------------------------------------------------
;        diskov‚ parametry
; -----------------------------------------------------------------------------

BufferW  dw        0                        ; segment bufferu pro z pis
BufferR  dw        0                        ; segment bufferu pro ‡ten¡
BufferD  dw        0                        ; segment bufferu pro £schovu stopy
BufferF  dw        0                        ; segment bufferu s tabulkou FAT

NumDisk  db        0                        ; po‡et disk– v syst‚mu
TypDiskT db        4 dup(1)                 ; tabulka typ– disk–
PozDiskT db        4 dup(0)                 ; tabulka pozic k zobrazen¡ disk–

TypDisk  db        1                        ; typ aktivn¡ho disku
                                            ;    1 = 360 K
                                            ;    2 = 1.2 M
                                            ;    3 = 720 K
                                            ;    4 = 1.44 M
                                            ;    5 = 2.88 M

AktVolby db        0                        ; p©¡znak aktivn¡ch voleb

AktForm  db        -1                       ; aktivn¡ form t (-1=neplat¡)
NumForm  db        0                        ; po‡et mo‘n˜ch form t– disku
AktStop  db        0                        ; aktivn¡ stopa
AktStran db        0                        ; aktivn¡ strana

Krokov   db        0                        ; p©¡znak dvojit‚ho krokov n¡
TypStop  db        39                       ; jmenovit˜ po‡et stop - 1
NumStop  db        40                       ; celkov˜ po‡et stop disku
BlokByte dw        400h                     ; po‡et bajt– na aloka‡n¡ blok
RootSekt db        7                        ; po‡et sektor– na ROOT
         db        0                        ; doplnˆk na slovo

CelkBlok dw        (2*40*9-1-7-2*2)/2       ; celkov˜ po‡et datov˜ch blok–
CelkKap  dd        362000                   ; celkov  kapacita disku
FirstDat dw        0                        ; prvn¡ datov˜ sektor absolutnˆ

CitVol   dd        0                        ; ‡¡ta‡ voln‚ kapacity disku
CitBad   dd        0                        ; ‡¡ta‡ vadn˜ch dat disku

ErrBoot  db        1                        ; p©¡znak BOOT (0=OK, FF=vadn˜, 1=?)
ErrFAT1  db        1                        ; p©¡znak FAT1 (0=OK, FF=vadn , 1=?)
ErrFAT2  db        1                        ; p©¡znak FAT2 (0=OK, FF=vadn , 1=?)
ErrRoot  db        1                        ; p©¡znak ROOT (0=OK, FF=vadn˜, 1=?)
ErrDat   db        0                        ; p©¡znak chyby 1. sektoru bloku

SlidX    db        0                        ; odsazen¡ sektor– stran (SLIDING X)
SlidY    db        0                        ; odsazen¡ sektor– v lc– (SLIDING Y)
Inter    db        1                        ; prokl d n¡ sektor– (1 nebo 2)
Smer     db        0                        ; smˆr form tov n¡ (0=nahoru)

PozMap   dw        11*HI+2                  ; pozice k zobrazen¡ mapy disku
SirMap   dw        13*HI+53                 ; ¨¡©ka a v˜¨ka okna mapy disku
CitMap   db        40                       ; ‡¡ta‡ stop mapy

; -----------------------------------------------------------------------------
;        definice form t– disk–
; -----------------------------------------------------------------------------
; Z kladn¡ parametry:
;     0: (1) p©¡znaky
;               bit 0: 1=80 stop
;               bit 1: 1=v oboustrann‚m form tu 2 sektory na blok
;               bit 2: 1=dvojit‚ krokov n¡
;               bit 3: 1=nestandardn¡ form t pro tuto mechaniku
;               bit 4: 1=faktor prokl d n¡ = 2
;               bit 5: 1=prov d¡ se odstup sektor– SLIDING (1 a 2 nebo 2 a 3)
;     1: (1) po‡et sektor– na stopu (8, 9, 15 nebo 18)
;     2: (1) po‡et sektor– na ROOT
;     3: (1) popisova‡ m‚dia v jednostrann‚m form tu
;     4: (5) n zev form tu pro zobrazen¡ v menu
; Po‡et sektor– na ROOT v jednostrann‚m form tu: vydˆlen¡ 2 + p©enos CF
; Po‡et sektor– na ROOT v £sporn‚m form tu: vydˆlen¡ 2 (bez p©enosu)
; Popisova‡ m‚dia v oboustran‚m form tu: popisova‡ OR 1
; -----------------------------------------------------------------------------

TabForm  dw        Form1                    ; adresy definic form t–
         dw        Form2
         dw        Form3
         dw        Form4
         dw        Form5

Form1    label     byte                     ; definice form t– mechaniky typu 1
         db        3
         db        b1,    9, 7,0fch,' 360K'
         db        b1,    8, 7,0feh,' 320K'
         db  b5+b3+b1,   10, 7,0fch,' 400K'

Form2    label     byte                     ; definice form t– mechaniky typu 2
         db        6
         db        b0,   15,14,0f9h,' 1.2M'
         db        b1+b2, 9, 7,0fch,' 360K'
         db        b1+b2, 8, 7,0feh,' 320K'
         db  b5+b4+b3+b0,18,14,0f9h,'1.44M'
         db  b5+b3+b0+b1,10, 7,0f9h,' 800K'
         db     b3+b0+b1, 9, 7,0f9h,' 720K'

Form3    label     byte                     ; definice form t– mechaniky typu 3
         db        6
         db        b0+b1, 9, 7,0f9h,' 720K'
         db  b5+b3+b0+b1,12, 7,0f9h,' 980K'
         db  b5+b3+b0+b1,10, 7,0f9h,' 800K'
         db  b5+b3+b1,   10, 7,0fch,' 400K'
         db     b3+b1+b2, 9, 7,0fch,' 360K'
         db     b3+b1+b2, 8, 7,0feh,' 320K'

Form4    label     byte                     ; definice form t– mechaniky typu 4
         db        7
         db        b0,   18,14,0f9h,'1.44M'
         db        b0+b1, 9, 7,0f9h,' 720K'
         db  b5+b4+b3+b0,21,14,0f9h,'1.68M'
         db     b3+b0,   15,14,0f9h,' 1.2M'
         db  b5+b3+b0+b1,12, 7,0f9h,' 980K'
         db  b5+b3+b0+b1,10, 7,0f9h,' 800K'
         db     b3+b1+b2, 9, 7,0fch,' 360K'

Form5    label     byte                     ; definice form t– mechaniky typu 5
         db        7
         db        b0,   36,28,0f9h,'2.88M'
         db        b0,   18,14,0f9h,'1.44M'
         db        b0+b1, 9, 7,0f9h,' 720K'
         db  b5+b4+b3+b0,21,14,0f9h,'1.68M'
         db     b3+b0,   15,14,0f9h,' 1.2M'
         db  b5+b3+b0+b1,12, 7,0f9h,' 980K'
         db     b3+b1+b2, 9, 7,0fch,' 360K'

; -----------------------------------------------------------------------------
;        konfigurace programu
; -----------------------------------------------------------------------------

Konfig   label     byte

KonfIdnt db        'FORM'                   ; identifika‡n¡ text souboru
KonfVer  db        20h                      ; verze programu FORM

AktDisk  db        0                        ; aktivn¡ disk
AktVol   db        0                        ; aktivn¡ polo‘ka voleb
ParSys   db        0                        ; p©¡znak - je opera‡n¡ syst‚m
ParLab   db        0                        ; p©¡znak - zad v  se n vˆ¨t¡
ParDat   db        0                        ; p©¡znak - zachov n¡ dat
ParStra  db        0                        ; p©¡znak - jedna strana
ParRoot  db        0                        ; po‡et sektor– v ROOT (0=stand.)
ParObs   db        1                        ; p©¡znak - zobrazen¡ obsahu
ParStrt  db        0                        ; p©¡znak - zrychlen˜ start
ParBack  db        0                        ; p©¡znak - zpˆtn˜ smˆr
ParInd   db        1                        ; p©¡znak - zvukov  indikace
ParVerif db        1                        ; p©¡znak - prov dˆn¡ verifikace
ParStop  db        0                        ; po‡et dopl¤uj¡c¡ch stop (0 a‘ 6)
ParTest  db        0                        ; po‡et testovac¡ch z pis–
ParChar  db        0f6h                     ; form tovac¡ znak
ParOptim db        1                        ; p©¡znak - optimalizace chyb
ParCest  db        1                        ; ‡e¨tina na displeji
                                            ;   0=bez
                                            ;   1=Vlastn¡
                                            ;   2=Kamenick˜ch
                                            ;   3=Latin 2
AktFormT db        4 dup(0)                 ; aktivn¡ form ty disk– A a‘ D

Konfig0  label     byte

TabBad   db        MAXSEKT dup(?)           ; tabulka vadn˜ch sektor– stopy

         db        300h dup(?)              ; z sobn¡k
Konec    label     byte                     ; konec programu

CODE     ENDS

         END       Start
