
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                     Ovlada‡ displeje pro KONTEXT
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

; Po p©ekladu od adresy 0 m  jm‚no SCREEN.OVL

bit0     EQU       1
bit1     EQU       2
bit2     EQU       4
bit3     EQU       8
bit4     EQU       10h
bit5     EQU       20h
bit6     EQU       40h
bit7     EQU       80h
HI       EQU       256

Code     SEGMENT
         ASSUME    cs:code,ds:code

; *****************************************************************************
;
;                         vstupn¡ bod do ovlada‡e
;
; *****************************************************************************

Screen   PROC      FAR                      ; vstupn¡ bod do ovlada‡e
                                            ; - ‡¡slo funkce je v BX
         push      ds

         push      cs
         pop       ds                       ; DS <- segment programu
         shl       bx,1                     ; ‡¡slo funkce BX * 2
         call      word ptr ds:[bx+AdrSub]  ; vyvol n¡ podprogramu podle tabulky

         pop       ds
         ret                                ; n vrat z driveru RET FAR

Screen   ENDP

AdrSub   dw        Fnc00                    ; funkce 00 - v˜stup znaku
         dw        Fnc01                    ; funkce 01 - kurzor, maz n¡
         dw        Fnc02                    ; funkce 02 - rolov n¡ nahoru
         dw        Fnc03                    ; funkce 03 - rolov n¡ dol–
         dw        Fnc04                    ; funkce 04 - prav˜ okraj (‡ ra)
         dw        Fnc05                    ; funkce 05 - inicializace parametr–
         dw        Fnc06                    ; funkce 06 - nastaven¡ text. m¢du
         dw        Fnc07                    ; funkce 07 - adr. n povˆdy (ES:SI)
         dw        Fnc08                    ; funkce 08 - adr. chybov˜ch hl ¨en¡
         dw        Fnc09                    ; funkce 09 - rozdˆlov n¡ slov
         dw        Fnc10                    ; funkce 10 - dek¢dov n¡ k¢du kl ves
         dw        Fnc11                    ; funkce 11 - v˜pis k¢du kl vesy

; *****************************************************************************
;
;                                  data
;
; *****************************************************************************
;þ
TypCard  db        0                        ; typ videokarty
                                            ;   0=neinicializov no
                                            ;   1=HGC (Hercules)
                                            ;   2=CGA
                                            ;   3=EGA
                                            ;   4=VGA

Karta    db        'TXT',0
         db        'HGC',0
         db        'CGA',0
         db        'EGA',0
         db        'VGA',0

OldVMod  db        3                        ; uschovan˜ p–vodn¡ videom¢d
AktVMod  db        6                        ; grafick˜ videom¢d
TxtVMod  db        3                        ; textov˜ videom¢d

AdrVRAM  dw        0b800h                   ; segment videopamˆti
Pozic    dw        80                       ; po‡et pozic na © dek
Radku    dw        25                       ; po‡et © dk– displeje
Vyska    db        8                        ; po‡et linek na textov˜ © dek

AdrLFont dw        Fnt6x8                   ; font pro zmen¨en‚ p¡smo
VyskaL   db        6                        ; po‡et linek na zmen¨en˜ font
AdrFont  dw        Fnt8x8                   ; adresa fontu

ChrBuf   db        0,0,0,0,0,0,0,0,0,0      ; buffer pro v˜stup znaku
         db        0,0,0,0,0,0,0,0,0
ChrBuf2  db        0,0,0,0,0,0,0,0,0,0      ; prav  polovina ¨irok‚ho znaku
         db        0,0,0,0,0,0,0,0,0

Whide    db        0                        ; 80h=dvojn sobn  ¨¡©ka znaku
Mode     db        0                        ; re‘im obrazovky (1,2,3)

; ------ definice barevn˜ch rovin pro m¢d EGA a VGA

InvMsk   db        0                        ; maska znak– (pro inverzi)
PlanNg   db        0                        ; roviny pro negaci znaku (kurzor)
Planes   db        0                        ; roviny EGA pro v˜stup znaku

; ------ barvy p¡sma pro textov˜ videom¢d

Normal   db        0                        ; barva norm ln¡ho textu
Invert   db        0                        ; barva textu menu
Cara     db        0                        ; barva okraje (hnˆd )
Zvyraz   db        0                        ; zv˜raznˆn‚ p¡smo (‘lut‚)
Sikme    db        0                        ; ¨ikm‚ p¡smo (zelen )
Podtrz   db        0                        ; podtr‘en‚ p¡smo (fialov )
Jine     db        0                        ; jin‚ kombinace typ– (b¡l )

AdrRow   dw        61 DUP (0)               ; tabulka adres © dk– na displeji

; *****************************************************************************
;
;                       Funkce 00: V˜stup znaku
;
; -----------------------------------------------------------------------------
; VSTUP: SS:[BP+4] (2) © dek (0...)
;        SS:[BP+6] (2) pozice (1...)
;        SS:[BP+8] (2) znak k zobrazen¡
;        AH=m¢d zobrazen¡ znaku
;                       bit 0: 1=nadsazen¡
;                       bit 1: 1=podsazen¡
;                       bit 2: 1=kurz¡va
;                       bit 3: 1=podtr‘en¡
;                       bit 4: 1=zv˜raznˆn¡
;                       bit 5: 1=inverze
;                       bit 6: 1=rozdˆlen¡
;                       bit 7: 1=¨irok‚ p¡smo
; *****************************************************************************
;þ
Fnc00    PROC      NEAR

; ------ kontrola, zda je povolen  pozice znaku

         xor       cx,cx                    ; CX <- 0
         mov       dx,[bp+6]                ; pozice znaku na © dku
         dec       dx
         js        Fnc001                   ; nepovolen  pozice
         cmp       dx,ds:[Pozic]            ; je povolen  pozice ?
         jae       Fnc001                   ; nen¡ povolen  pozice

; ------ p©¡znak dvojn sobn‚ ¨¡©ky znaku

         mov       al,ss:[bp+8]             ; znak k v˜stupu
         cmp       al,0ffh                  ; je znak pro dvojn sobnou ¨¡©ku ?
         jne       Fnc0013                  ; nen¡ dvojn sobn  ¨¡©ka
         cmp       byte ptr ds:[Mode],1     ; je textov˜ m¢d ?
         je        Fnc0012                  ; je textov˜ m¢d
         mov       cl,80h                   ; nastaven¡ p©¡znaku dvojit‚ ¨¡©ky
Fnc001:  mov       [Whide],cl               ; p©¡znak dvojn sobn‚ho znaku
         ret                                

; ------ v textov‚m m¢du se zobraz¡ n hradn¡ znak "_"

Fnc0012: mov       al,"_"                   ; n hradn¡ znak (nebo m–‘e b˜t 22)

; ------ zobrazen¡ znaku v textov‚m re‘imu

Fnc0013: cmp       byte ptr ds:[Mode],1     ; je textov˜ m¢d ?
         jne       Fnc002                   ; nen¡ textov˜ m¢d

         cld
         mov       di,[bp+4]                ; © dek znaku
         shl       di,1                     ; DI = po‡et linek * 2
         mov       di,[di+AdrRow]           ; p©e‡ten¡ adresy linky
         dec       di
         dec       di
         add       di,[bp+6]
         add       di,[bp+6]                ; pozice znaku na © dku
         mov       es,ds:[AdrVRAM]          ; adresa videopamˆti
         stosb                              ; ulo‘en¡ znaku

         mov       al,ds:[Normal]           ; bˆ‘n  barva
         test      ah,bit0+bit1+bit2+bit3+bit4+bit5 ; jsou nˆjak‚ atributy ?
         jz        Fnc0016                  ; je norm ln¡ barva

         mov       al,ds:[Invert]           ; inverzn¡ p¡smo
         test      ah,bit5                  ; je inverzn¡ p¡smo ?
         jnz       Fnc0016                  ; je inverzn¡ p¡smo

         mov       al,ds:[Zvyraz]           ; zv˜raznˆn‚
         test      ah,bit4
         jnz       Fnc0016                  ; je zv˜raznˆn‚ p¡smo

         mov       al,ds:[Sikme]            ; kurz¡va
         test      ah,bit2
         jnz       Fnc0016                  ; je kurz¡va

         mov       al,ds:[Podtrz]           ; podtr‘en‚
         test      ah,bit3                  ; je podtr‘en‚ ?
         jnz       Fnc0016                  ; je podtr‘en‚

         mov       al,ds:[Jine]             ; jin‚ p¡smo
Fnc0016: stosb                              ; ulo‘en¡ barvy znaku
         ret

; ------ p©¡prava k dek¢dov n¡ znaku

Fnc002:  push      cs
         pop       es                       ; ES = DS
         cld                                ; smˆr p©enosu dat nahoru
         mov       di,offset ChrBuf         ; buffer k dek¢dov n¡ znaku
         test      ah,bit0+bit1             ; je nadsazen¡ nebo podsazen¡ ?
         jnz       Fnc003                   ; je nadsazen¡ nebo podsazen¡

; ------ p©¡prava znaku v bˆ‘n‚m fontu

         mov       cl,ds:[Vyska]            ; po‡et bajt– na znak
         push      ax
         mul       cl                       ; offset v tabulce fontu
         add       ax,ds:[AdrFont]          ; adresa znaku v tabulce font–
         xchg      ax,si                    ; SI <- adresa fontu
         rep       movsb                    ; p©enos znaku do bufferu
         pop       ax
         jmp       short Fnc005

; ------ adresa zmen¨en‚ho fontu

Fnc003:  mov       cl,ds:[VyskaL]           ; po‡et bajt– na zmen¨en˜ font
         push      ax
         mul       cl                       ; offset zmen¨en‚ho znaku
         add       ax,ds:[ADrLFont]         ; adresa zmen¨en‚ho fontu
         xchg      ax,si                    ; SI <- adresa fontu
         pop       ax

; ------ dek¢dov n¡ nadsazen¡

         mov       al,0                     ; nulovac¡ bajt
         test      ah,bit0                  ; je nadsazen¡ ?
         jz        Fnc004                   ; nen¡ nadsazen¡
         rep       movsb                    ; p©enos zmen¨en‚ho znaku
         mov       cl,ds:[Vyska]            ; v˜¨ka znaku
         sub       cl,ds:[VyskaL]           ; zb˜vaj¡c¡ po‡et linek
         rep       stosb                    ; vymaz n¡ zbytku znaku
         jmp       short Fnc005

; ------ dek¢dov n¡ podsazen¡

Fnc004:  mov       cl,ds:[Vyska]            ; v˜¨ka znaku
         sub       cl,ds:[VyskaL]           ; zb˜vaj¡c¡ po‡et linek
         rep       stosb                    ; vymaz n¡ zbytku znaku
         mov       cl,ds:[VyskaL]           ; v˜¨ka zmen¨en‚ho znaku
         rep       movsb                    ; p©enos zmen¨en‚ho znaku

; ------ kurz¡va

Fnc005:  mov       si,offset ChrBuf         ; buffer znaku
         mov       cl,ds:[Vyska]            ; v˜¨ka znaku
         test      ah,bit2                  ; je kurz¡va ?
         jz        Fnc006                   ; nen¡ kurz¡va
         push      cx
         mov       cl,2
Fnc005K: call      Kurziv8                  ; proveden¡ kurz¡vy
         pop       cx

; ------ zv˜raznˆn¡

Fnc006:  test      ah,bit4
         jz        Fnc0062

         push      cx
         push      si                       ; adresa znaku v bufferu

Fnc0061: lodsb                              ; 1 linka znaku
         shr       al,1
         or        [si-1],al                ; zv˜raznˆn¡ znaku
         loop      Fnc0061                  ; dal¨¡ linka znaku

         pop       si
         pop       cx

; ------ podtr‘en¡

Fnc0062: test      ah,bit3
         jz        Fnc0063

         push      si
         add       si,cx
         mov       byte ptr ds:[si-1],-1
         mov       byte ptr ds:[si-1+ChrBuf2-ChrBuf],-1
         pop       si

; ------ dvojn sobn  ¨¡©ka p¡sma

Fnc0063: or        ah,[Whide]               ; p©¡znak dvojn sobn‚ho znaku
         jns       Fnc0066                  ; nen¡ dvojn sobn  ¨¡©ka znaku

         push      cx
         push      si                       ; adresa znaku v bufferu

Fnc0064: mov       bh,8                     ; po‡et bit– znaku = 8
         lodsb                              ; linka znaku
Fnc0065: shr       al,1                     ; rotace linky vpravo, kopie do CF
         rcr       dx,1                     ; rotace bitu CF do reg. DX
         sar       dx,1                     ; rotace vpravo ze znam‚nkov‚ho bitu
         dec       bh                       ; ‡¡ta‡ bit– k rotaci
         jnz       Fnc0065                  ; dal¨¡ bit k rotaci
         mov       [si-1],dh                ; lev  polovina znaku
         mov       [si+ChrBuf2-ChrBuf-1],dl ; prav  polovina znaku
         loop      Fnc0064
         dec       word ptr ss:[bp+6]       ; sn¡‘en¡ pozice znaku na displeji
         mov       [Whide],cl               ; p©¡znak dvojn sobn‚ho znaku = 0

         pop       si
         pop       cx

; ------ rozdˆlen¡ znaku

Fnc0066: test      ah,bit6                  ; je rozdˆlovac¡ znam‚nko ?
         jz        Fnc007                   ; nen¡ rozdˆlovac¡ znam‚nko

         push      cx
         push      si                       ; adresa znaku v bufferu

         mov       al,1
Fnc0067: or        ds:[si],al               ; ozna‡en¡ rozdˆlen¡ znaku
         inc       si
         loop      Fnc0067                  ; dal¨¡ linka znaku

         pop       si
         pop       cx

; ------ p©¡prava adresy na displeji

Fnc007:  mov       di,[bp+4]                ; © dek znaku
         shl       di,1                     ; DI = po‡et linek * 2
         mov       di,[di+adrrow]           ; p©e‡ten¡ adresy linky
         add       di,[bp+6]                ; pozice znaku na © dku
         mov       es,ds:[AdrVRAM]          ; adresa videopamˆti
         mov       bh,[InvMsk]              ; maska pro v˜stup (pro inverzi)

; ------ rozli¨en¡, zda je videom¢d EGA/VGA

         push      cx
         test      ah,bit5                  ; p©¡znak inverzn¡ho znaku
         jz        Fnc0071
         not       bh                       ; inverzn¡ znak
Fnc0071: cmp       byte ptr ds:[TypCard],2
         jbe       Fnc008                   ; je CGA nebo HGC

; ------ zobrazen¡ znaku v m¢du EGA/VGA

Fnc0072: lodsb                              ; p©e‡ten¡ bajtu k v˜stupu DS:SI
         xor       al,bh                    ; filtrace barvy (inverze)
         stosb                              ; ulo‘en¡ sud‚ho bajtu do VRAM ES:DI
         add       di,80-1                  ; dal¨¡ © dek (zv˜¨en¡ o 80)
         loop      Fnc0072                  ; dal¨¡ dva bajty znaku

; ------ test, zda n sleduje druh  polovina ¨irok‚ho znaku

Fnc0074: pop       cx

         xor       ah,80h
         jns       Fnc0075                  ; je ¨irok‚ p¡smo-p©enos prav‚ ‡ sti
         ret

Fnc0075: inc       word ptr [BP+6]          ; zv˜¨en¡ pozice
         mov       si,offset ChrBuf2        ; adresa prav‚ poloviny znaku
         jmp       short Fnc0066

; ------ zobrazen¡ znaku v m¢du CGA

Fnc008:  jb        Fnc009                   ; je HGC
         shr       cx,1                     ; po‡et slov k p©enosu
Fnc0082: lodsb                              ; p©e‡ten¡ bajtu k v˜stupu DS:SI
         xor       al,bh                    ; filtrace barvy (inverze)
         stosb                              ; ulo‘en¡ sud‚ho bajtu do VRAM ES:DI
         add       di,2000h-1               ; dal¨¡ © dek
         lodsb                              ; p©e‡ten¡ druh‚ho bajtu k v˜stupu
         xor       al,bh                    ; filtrace barvy
         stosb                              ; ulo‘en¡ lich‚ho bajtu do VRAM
         add       di,80 - 2000h - 1        ; dal¨¡ © dek
         loop      Fnc0082                  ; dal¨¡ dva bajty znaku
         jmp       short Fnc0074

; ------ HGC

Fnc009:  lodsb                              ; p©e‡ten¡ bajtu k v˜stupu DS:SI
         xor       al,bh                    ; filtrace barvy (inverze)
         stosb                              ; ulo‘en¡ sud‚ho bajtu do VRAM ES:DI
         dec       cx                       ; sn¡‘en¡ ‡¡ta‡e bajt–
         jz        Fnc0074
         add       di,2000h-1               ; dal¨¡ © dek
         jns       Fnc009
         add       di,90 - 4*2000h          ; adresa dal¨¡ho © dku
         jmp       short Fnc009             ; dal¨¡ dva bajty znaku

Fnc00    ENDP

; -----------------------------------------------------------------------------
;        kurz¡va 6 linek
; -----------------------------------------------------------------------------

Kurziv6  PROC      NEAR

         shr       byte ptr [si],1          ; >> 1
         shr       byte ptr [si+1],1        ; >> 1
                                            ;    0
                                            ;    0
         shl       byte ptr [si+4],1        ; << 1
         shl       byte ptr [si+5],1        ; << 1
         ret

Kurziv6  ENDP

; -----------------------------------------------------------------------------
;        kurz¡va 8 linek
; -----------------------------------------------------------------------------

Kurziv8  PROC      NEAR

         shr       byte ptr [si],cl         ; >> 2
         shr       byte ptr [si+1],1        ; >> 1
         shr       byte ptr [si+2],1        ; >> 1
                                            ;    0
                                            ;    0
         shl       byte ptr [si+5],1        ; << 1
         shl       byte ptr [si+6],1        ; << 1
         shl       byte ptr [si+7],cl       ; << 2
         ret

Kurziv8  ENDP

; -----------------------------------------------------------------------------
;        kurz¡va 14 linek
; -----------------------------------------------------------------------------

Kurziv14 PROC      NEAR

         shr       byte ptr [si],cl         ; >> 2
         shr       byte ptr [si+1],cl       ; >> 2
         shr       byte ptr [si+2],cl       ; >> 2
         shr       byte ptr [si+3],1        ; >> 1
         shr       byte ptr [si+4],1        ; >> 1
         shr       byte ptr [si+5],1        ; >> 1
                                            ;    0
                                            ;    0
         shl       byte ptr [si+8],1        ; >> 1
         shl       byte ptr [si+9],1        ; << 1
         shl       byte ptr [si+10],1       ; << 1
         shl       byte ptr [si+11],cl      ; << 2
         shl       byte ptr [si+12],cl      ; << 2
         shl       byte ptr [si+13],cl      ; << 2
         ret

Kurziv14 ENDP

; -----------------------------------------------------------------------------
;        kurz¡va 19 linek
; -----------------------------------------------------------------------------

Kurziv19 PROC      NEAR

         shr       byte ptr [si],cl         ; >> 2
         shr       byte ptr [si+1],cl       ; >> 2
         shr       byte ptr [si+2],cl       ; >> 2
         shr       byte ptr [si+3],cl       ; >> 2
         shr       byte ptr [si+4],1        ; >> 1
         shr       byte ptr [si+5],1        ; >> 1
         shr       byte ptr [si+6],1        ; >> 1
         shr       byte ptr [si+7],1        ; >> 1
                                            ;    0
                                            ;    0
                                            ;    0
                                            ;    0
         shl       byte ptr [si+12],1       ; << 1
         shl       byte ptr [si+13],1       ; << 1
         shl       byte ptr [si+14],1       ; << 1
         shl       byte ptr [si+15],1       ; << 1
         shl       byte ptr [si+16],cl      ; << 2
         shl       byte ptr [si+17],cl      ; << 2
         shl       byte ptr [si+18],cl      ; << 2
         ret

Kurziv19 ENDP

; *****************************************************************************
;
;                         Funkce 01: Kurzor a maz n¡
;
; -----------------------------------------------------------------------------
; VSTUP: SS:[BP+4] typ operace
;                       0 = norm ln¡ okno
;                       1 = inverzn¡ okno
;                       2 = kurzor
;        SS:[BP+6] koncov  linka
;        SS:[BP+8] koncov  pozice
;        SS:[BP+10] po‡ te‡n¡ linka
;        SS:[BP+12] po‡ te‡n¡ pozice
; *****************************************************************************
;þ
Fnc01    PROC      NEAR

; ------ po‡ te‡n¡ a koncov˜ © dek

         mov       ax,ss:[bp+10]            ; po‡ te‡n¡ © dek
         mov       bx,ss:[bp+6]             ; koncov˜ © dek
         cmp       ax,bx
         jbe       Fnc011
         cmp       byte ptr ss:[bp+4],2
         jne       Fnc011
         dec       ax
         mov       bx,ax
Fnc011:  sub       bx,ax
         inc       bx                       ; v˜¨ka okna (© dk–)

; ------ adresa © dku na obrazovce

         mov       di,80                    ; po‡et znak– na © dek
         cmp       byte ptr ds:[Mode],1     ; je textov˜ m¢d ?
         je        Fnc0112                  ; je textov˜ m¢d
         cmp       byte ptr ds:[TypCard],3  ; je m¢d EGA/VGA ?
         jae       Fnc0112                  ; je m¢d EGA/VGA
         mov       di,40                    ; p©¡rustek adresy pro CGA a HGC
Fnc0112: mul       di                       ; po‡ te‡n¡ adresa
         xchg      ax,di                    ; po‡ te‡n¡ adresa
         mov       es,ds:[AdrVRAM]          ; adresa videopamˆti

; ------ po‡ te‡n¡ pozice

         mov       ax,ss:[bp+12]            ; po‡ te‡n¡ pozice
         dec       ax                       ; korekce pozice
         jns       Fnc012                   ; pozice je OK
         xor       ax,ax                    ; omezen¡ po‡ te‡n¡ pozice

; ------ ¨¡©ka okna

Fnc012:  mov       cx,ss:[bp+8]             ; koncov  pozice
         cmp       cx,ds:[Pozic]            ; je pozice OK ?
         jbe       Fnc013                   ; pozice je OK
         mov       cx,ds:[Pozic]            ; omezen¡ koncov‚ pozice
Fnc013:  sub       cx,ax                    ; ¨¡©ka okna
         jbe       Fnc0149                  ; nen¡ okno
         add       di,ax                    ; po‡ te‡n¡ adresa okna

; ------ test, zda je textov˜ m¢d

         cld
         cmp       byte ptr ds:[Mode],1     ; je textov˜ m¢d ?
         jne       Fnc015                   ; nen¡ textov˜ m¢d

; ------ norm ln¡ a inverzn¡ okno

         shl       di,1
         cmp       byte ptr ss:[bp+4],1     ; parametr
         ja        Fnc0143                  ; je kurzor
         mov       ah,ds:[Normal]           ; je norm ln¡ text
         jb        Fnc0141                  ; je inverzn¡ text
         mov       ah,ds:[Invert]           ; inverzn¡ text
Fnc0141: mov       al," "
Fnc0142: push      di
         push      cx
         rep       stosw
         pop       cx
         pop       di

         add       di,ds:[Pozic]
         add       di,ds:[Pozic]
         dec       bx
         jnz       Fnc0142
         jmp       short Fnc0149

; ------ zobrazen¡ kurzoru

Fnc0143: push      di
         push      cx                       ; ‡¡ta‡ znak– na © dek

Fnc0144: inc       di
         mov       al,es:[di]               ; nastaven˜ atribut
         not       al
         cmp       byte ptr ds:[TypCard],1  ; je MDA ?
         jne       Fnc0145
         not       al
         ror       al,1
         ror       al,1
         ror       al,1
         ror       al,1
Fnc0145: stosb
         loop      Fnc0144

         pop       cx
         pop       di

         add       di,ds:[Pozic]
         add       di,ds:[Pozic]
         dec       bx                       ; ‡¡ta‡ © dk–
         jnz       Fnc0143                  ; dal¨¡ © dek
Fnc0149: jmp       short Fnc019

; ------ test, zda je m¢d EGA/VGA


Fnc015:  cmp       byte ptr ds:[TypCard],2  ; je karta EGA/VGA ?
         ja        Fnc0152                  ; je karta EGA/VGA
         je        Fnc0151                  ; je karta CGA
         jmp       Fnc015H                  ; je karta HGC
Fnc0151: jmp       Fnc015C                  ; CGA

; ------ nastaven¡ registr– pro m¢d EGA

Fnc0152: mov       dx,03CEh                 ; adresov˜ registr grafiky EGA
         mov       al,1                     ; nastaven¡ registru ‡¡slo 1
         out       dx,al
         inc       dx                       ; adresa dat registru
         mov       al,0fh                   ; nastaven¡ bit– pro set/reset
         out       dx,al
         dec       dx                       ; adresov˜ registr grafiky EGA
         mov       al,0
         out       dx,al                    ; nastaven¡ registru ‡¡slo 0
         inc       dx                       ; adresa dat registru
         mov       ah,[InvMsk]              ; maska znak– pro v˜stup
         cmp       byte ptr [bp+4],1        ; parametr pro z pis okna
         jnc       fnc016                   ; je vymaz n¡ okna
         not       ah                       ; negace masky pro v˜stup
         jmp       short fnc017

Fnc016:  mov       al,0ffh                  ; maz n¡ v¨ech rovin
         jne       fnc018
fnc017:  mov       al,[Planes]              ; roviny pro v˜stup znaku
         or        ah,ah                    ; negovan  maska znak– pro v˜stup
         jnz       fnc018                   ; je negace zpˆt
         mov       al,[PlanNg]              ; roviny pro negaci znaku (kurzor)
fnc018:  out       dx,al                    ; v˜bˆr rovin pro v˜stup

         push      ds
         mov       es,ds:[AdrVRAM]
         mov       ds,ds:[AdrVRAM]          ; nastaven¡ segmentov˜ch registr–
         cld                                ; smˆr p©enosu dat vzh–ru
         inc       al                       ; roviny pro z pis
         jnz       fnc01A                   ; nejsou v¨echny roviny (tj. maz n¡)
         dec       dx                       ; adresov˜ registr grafiky EGA
         mov       al,3
         out       dx,al                    ; nastaven¡ registru 3
         inc       dx                       ; datov˜ registr
         mov       al,18h                   ; z pisov˜ m¢d XOR
         out       dx,al

fnc0182: mov       si,di
         push      cx                       ; ¨¡©ka okna k vymaz n¡
         push      di                       ; ukazatel okna
         repnz     movsb                    ; vymaz n¡ bajt– obrazovky
         pop       di
         pop       cx
         add       di,80                    ; dal¨¡ linka
         dec       bx                       ; ‡¡ta‡ linek okna
         jnz       fnc0182                  ; dal¨¡ linka okna
         jmp       short fnc01B             ; konec

fnc01A:  push      cx
         push      di
         repnz     stosb                    ; ulo‘en¡ negovac¡ho bajtu
         pop       di
         pop       cx
         add       di,80                    ; dal¨¡ linka
         dec       bx                       ; ‡¡ta‡ linek
         jnz       fnc01A                   ; dal¨¡ linka k negaci

fnc01B:  pop       ds
         call      iniega                   ; inicializace displeje
fnc019:  ret

; ------ CGA

Fnc015C: cmp       byte ptr [bp+4],1        ; parametr pro z pis okna
         jbe       fnc015C5                 ; je vymaz n¡ okna

fnc015C2:push      cx
         push      di
fnc015C3:not       byte ptr es:[di]         ; invereze kurzoru
         inc       di
         loop      fnc015C3
         pop       di
         pop       cx
         sub       di,1fb0h
         jns       fnc015C4
         add       di,3fb0h
fnc015C4:dec       bx
         jnz       fnc015C2
         jmp       fnc015C9


fnc015C5:mov       al,ds:[invmsk]
         jnz       fnc015C6
         not       al
fnc015C6:push      cx
         push      di
         repnz     stosb                    ; ulo‘en¡ negovac¡ho bajtu
         pop       di
         pop       cx
         sub       di,1fb0h                 ; dal¨¡ linka
         jns       fnc015C7                 ; dal¨¡ linka k negaci
         add       di,3fb0h
fnc015C7:dec       bx
         jnz       fnc015C6
fnc015C9:ret

; ------ HGC

Fnc015H: mov       ax,[bp+10]               ; po‡ te‡n¡ linka
         mov       di,ax                    ; DI <- po‡ te‡n¡ linka
         mov       dx,ax                    ; DX <- po‡ te‡n¡ linka
         and       dx,3                     ; ‡¡slo podlinky
         ror       dx,1
         ror       dx,1
         ror       dx,1                     ; adresa linky 0,2000h,4000h,6000h
         shr       ax,1
         shr       ax,1                     ; ‡¡slo © dku
         mov       cl,90                    ; po‡et bajt– na © dek
         mul       cl                       ; adresa © dku
         add       ax,dx                    ; po‡ te‡n¡ adresa linky

         mov       dx,[bp+6]                ; koncov  linka
         sub       dx,di                    ; v˜¨ka okna - 1
         inc       dx                       ; v˜¨ka okna
         mov       di,ax                    ; DI <- po‡ te‡n¡ adresa linky

         mov       ax,[bp+12]               ; po‡ te‡n¡ pozice
         or        ax,ax                    ; je pozice OK ?
         jg        fnc015h1                 ; pozice je OK
         mov       ax,1                     ; omezen¡ po‡ te‡n¡ pozice

fnc015h1:mov       cx,[bp+8]                ; koncov  pozice
         cmp       cx,90                    ; kontrola ¨¡©ky okna
         jng       fnc015h2
         mov       cx,90                    ; omezen¡ na max. po‡et znak– 90

fnc015h2:dec       ax
         sub       cx,ax                    ; ¨¡©ka okna
         jng       fnc015h24                ; nen¡ ‘ dn‚ okno

         add       di,ax                    ; po‡ te‡n¡ adresa okna
         mov       es,ds:[AdrVRAM]          ; adresa videopamˆti
         cld
         cmp       byte ptr [bp+4],1
         jna       fnc015h23                ; je norm ln¡ nebo inverzn¡ okno

; ------ zobrazen¡ kurzoru HGC

fnc015h20:push      cx
         push      di
fnc015h21:not       byte ptr es:[di]
         inc       di
         loop      fnc015h21
         pop       di
         pop       cx
         add       di,2000h
         jns       fnc015h22
         sub       di,7fa6h
fnc015h22:dec       dx
         jnz       fnc015h20
         jmp       short fnc015h24

; ------ zobrazen¡ norm ln¡ho nebo inverzn¡ho okna HGC

fnc015h23:mov       al,[invmsk]
         jnz       fnc015h25
         not       al
fnc015h25:push      cx
         push      di
         repnz     stosb
         pop       di
         pop       cx
         add       di,2000h
         jns       fnc015h26
         sub       di,7fa6h
fnc015h26:dec       dx
         jnz       fnc015h25
fnc015h24:ret

Fnc01    ENDP

; *****************************************************************************
;
;                    Funkce 02: rolov n¡ okna nahoru
;
; *****************************************************************************
;þ
                                            ; FUNKCE 02 - rolov n¡ okna nahoru

                                            ; SS:BP = ukazatel parametr–
                                            ;    + 4 = kone‡n˜ © dek rolov n¡
                                            ;    + 6 = ¨¡©ka okna (pozic)
                                            ;    + 8 = po‡ te‡n¡ © dek k rol.
                                            ;    +10 = po‡ te‡n¡ pozice okna

fnc02:   cmp       byte ptr ds:[Mode],1
         jne       Fnc020                   ; nen¡ textov˜ m¢d

         mov       ax,[bp+4]                ; kone‡n˜ © dek k rolov n¡
         sub       ax,[bp+8]                ; po‡ te‡n¡ © dek k rol.-po‡et © dk–
         jz        fnc0219                  ; nen¡ ‘ dn˜ © dek - konec
         mov       bx,ax                    ; po‡et © dk– k rolov n¡
         mov       ax,[bp+8]                ; po‡ te‡n¡ © dek k rolov n¡
         mov       cx,ds:[Pozic]
         add       cx,cx
         mul       cx                       ; po‡ te‡n¡ adresa po‡ t. © dku
         mov       si,ax                    ; po‡ te‡n¡ adresa po‡ t. © dku
         mov       di,ax                    ; adresa n sleduj¡c¡ho © dku
         add       si,ds:[Pozic]
         add       si,ds:[Pozic]
         mov       ax,[bp+10]               ; po‡ te‡n¡ pozice okna
         dec       ax
         add       di,ax                    ; po‡ te‡n¡ adresa
         add       di,ax
         add       si,ax
         add       si,ax
         mov       cx,[bp+6]                ; kone‡n  pozice
         sub       cx,ax                    ; po‡et pozic okna k rolov n¡
         add       cx,cx
         push      es
         push      ds
;         add       si,word ptr ds:[AdrVRAM]
;         add       di,word ptr ds:[AdrVRAM]
         mov       ax,ds:[AdrVRAM] ; segment displeje
         mov       es,ax
         mov       ds,ax
         cld                                ; p©enos nahoru
fnc0212: push      si
         push      di
         push      cx
         repnz     movsb
         pop       cx
         pop       di
         pop       si
         add       di,cs:[Pozic]
         add       di,cs:[Pozic]
         add       si,cs:[Pozic]
         add       si,cs:[Pozic]
         dec       bx
         jnz       fnc0212
         pop       ds
         pop       es
fnc0219: ret


fnc020:  cmp       byte ptr ds:[TypCard],2
         jbe       fnc023

         mov       ax,[bp+4]                ; kone‡n˜ © dek k rolov n¡
         mov       si,[bp+8]                ; po‡ te‡n¡ © dek k rolov n¡
         sub       ax,si                    ; po‡et © dk– k rolov n¡
         jz        fnc022                   ; nen¡ ‘ dn˜ © dek k rolov n¡
         mul       byte ptr [vyska]         ; vyn soben¡ po‡tem linek na © dek
         mov       bx,ax                    ; celkov˜ po‡et linek k rolov n¡
         mov       dx,03ceh                 ; adresov˜ registr grafiky EGA
         mov       al,8
         out       dx,al                    ; nastaven¡ registru 08
         inc       dx                       ; datov˜ registr grafiky EGA
         mov       al,0
         out       dx,al                    ; nastaven¡ masky pro v¨echny roviny
         shl       si,1                     ; po‡ te‡n¡ © dek k rolov n¡
         add       si,offset adrrow         ; tabulka adres © dk– na displeji
         mov       di,[si]                  ; p©e‡ten¡ adresy © dku
         inc       si
         inc       si
         mov       si,[si]                  ; adresa n sleduj¡c¡ho © dku
         mov       ax,[bp+10]               ; po‡ te‡n¡ pozice okna k rolov n¡
         add       di,ax                    ; koncov‚ adresy okna
         add       si,ax
         mov       cx,[bp+6]                ; ¨¡©ka okna (pozic)
         sub       cx,ax
         inc       cx                       ; - kone‡n  pozice okna
         push      ds
         mov       ax,0a000h                ; segment pamˆti EGA
         mov       es,ax                    ; nastaven¡ segment– pamˆti EGA
         mov       ds,ax
         cld                                ; smˆr p©enosu dat nahoru
fnc021:  push      si
         push      di
         push      cx                       ; po‡et bajt– k p©enosu
         repnz     movsb                    ; p©enos jedn‚ linky okna
         pop       cx
         pop       di
         pop       si
         add       di,80                    ; dal¨¡ linka
         add       si,80
         dec       bx                       ; ‡¡ta‡ linek
         jnz       fnc021
         pop       ds
         call      iniega                   ; inicializace displeje EGA
fnc022:  ret

fnc023:  jb        fnc024                   ; je MDA

         mov       ax,[bp+4]                ; kone‡n˜ © dek k rolov n¡
         mov       si,[bp+8]                ; po‡ te‡n¡ © dek k rolov n¡
         sub       ax,si                    ; po‡et © dk– k rolov n¡
         jbe       fnc0232                  ; nen¡ ‘ dn˜ © dek k rolov n¡
         mul       byte ptr [vyska]         ; vyn soben¡ po‡tem linek na © dek
         shr       ax,1
         shl       si,1                     ; po‡ te‡n¡ © dek k rolov n¡
         add       si,offset adrrow         ; tabulka adres © dk– na displeji
         mov       di,[si]                  ; p©e‡ten¡ adresy © dku
         inc       si
         inc       si
         mov       si,[si]                  ; adresa n sleduj¡c¡ho © dku
         mov       bx,[bp+10]               ; po‡ te‡n¡ pozice okna k rolov n¡
         add       si,bx
         add       di,bx                    ; koncov‚ adresy okna
         mov       cx,[bp+6]                ; ¨¡©ka okna (pozic)
         sub       cx,bx
         inc       cx                       ; - kone‡n  pozice okna
         mov       bx,0b800h                ; segment pamˆti EGA
         mov       es,bx                    ; nastaven¡ segment– pamˆti EGA
         mov       ds,bx
         cld                                ; smˆr p©enosu dat nahoru
fnc0231: push      si
         push      di
         push      cx                       ; po‡et bajt– k p©enosu
         repnz     movsb                    ; p©enos jedn‚ linky okna
         pop       cx
         pop       di
         pop       si
         add       di,2000h                 ; dal¨¡ linka
         add       si,2000h
         push      si
         push      di
         push      cx                       ; po‡et bajt– k p©enosu
         repnz     movsb                    ; p©enos jedn‚ linky okna
         pop       cx
         pop       di
         pop       si
         sub       di,1fb0h
         sub       si,1fb0h
         dec       ax                       ; ‡¡ta‡ linek
         jnz       fnc0231
fnc0232: ret

fnc024:  mov       ax,[bp+4]                ; kone‡n˜ © dek k rolov n¡
         mov       si,[bp+8]                ; po‡ te‡n¡ © dek k rolov n¡
         sub       ax,si                    ; po‡et © dk– k rolov n¡
         jz        fnc0242                  ; nen¡ ‘ dn˜ © dek k rolov n¡
         mul       byte ptr [vyska]         ; vyn soben¡ po‡tem linek na © dek
         mov       bx,ax                    ; celkov˜ po‡et linek k rolov n¡
         shl       si,1
         add       si,offset adrrow         ; tabulka adres © dk– na displeji
         mov       di,[si]                  ; p©e‡ten¡ adresy © dku
         inc       si
         inc       si
         mov       si,[si]                  ; adresa n sleduj¡c¡ho © dku
         mov       ax,[bp+10]               ; po‡ te‡n¡ pozice okna k rolov n¡
         add       di,ax                    ; koncov‚ adresy okna
         add       si,ax
         mov       cx,[bp+6]                ; ¨¡©ka okna (pozic)
         sub       cx,ax
         inc       cx                       ; - kone‡n  pozice okna
         mov       ax,0b000h                ; segment pamˆti HGC
         mov       es,ax                    ; nastaven¡ segment– pamˆti EGA
         mov       ds,ax
         cld                                ; smˆr p©enosu dat nahoru
fnc0241: push      si
         push      di
         push      cx                       ; po‡et bajt– k p©enosu
         repnz     movsb                    ; p©enos jedn‚ linky okna
         pop       cx
         pop       di
         pop       si
         add       di,2000h                 ; dal¨¡ linka
         jns       fnc02417
         sub       di,7fa6h
fnc02417:add       si,2000h
         jns       fnc02418
         sub       si,7fa6h
fnc02418:
         dec       bx                       ; ‡¡ta‡ linek
         jnz       fnc0241
fnc0242: ret

; *****************************************************************************
;
;                    Funkce 03: rolov n¡ okna dol–
;
; *****************************************************************************
;þ
                                            ; FUNKCE 03 - rolov n¡ okna dol–

                                            ; SS:BP = ukazatel parametr–
                                            ;    + 4 = kone‡n˜ © dek rolov n¡
                                            ;    + 6 = ¨¡©ka okna (pozic)
                                            ;    + 8 = po‡ te‡n¡ © dek k rol.
                                            ;    +10 = po‡ te‡n¡ pozice okna


fnc03:   cmp       byte ptr ds:[Mode],1
         jne       Fnc032                   ; nen¡ textov˜ m¢d

         mov       ax,[bp+4]                ; kone‡n˜ © dek k rolov n¡
         sub       ax,[bp+8]                ; po‡ te‡n¡ © dek k rol.-po‡et © dk–
         jz        fnc0319                   ; nen¡ ‘ dn˜ © dek - konec
         mov       bx,ax                    ; po‡et © dk– k rolov n¡
         mov       ax,[bp+4]                ; kone‡n˜ © dek k rolov n¡
         mov       cx,ds:[Pozic]
         add       cx,cx
         mul       cx                       ; po‡ te‡n¡ adresa kone‡n‚ho © dku
         mov       si,ax                    ; po‡ te‡n¡ adresa kone‡n‚ho © dku
         mov       di,ax                    ; adresa n sleduj¡c¡ho © dku
         add       di,ds:[Pozic]
         add       di,ds:[Pozic]
         mov       ax,[bp+10]               ; po‡ te‡n¡ pozice okna
         dec       ax
         add       di,ax                    ; po‡ te‡n¡ adresa
         add       di,ax
         add       si,ax
         add       si,ax
         mov       cx,[bp+6]                ; kone‡n  pozice
         sub       cx,ax                    ; po‡et pozic okna k rolov n¡
         add       cx,cx
         push      es
         push      ds
         mov       ax,word ptr ds:[AdrVRAM] ; segment displeje
         mov       es,ax
         mov       ds,ax
         cld                                ; p©enos nahoru
fnc0312:
         sub       di,cs:[Pozic]
         sub       di,cs:[Pozic]
         sub       si,cs:[Pozic]
         sub       si,cs:[Pozic]
         push      si
         push      di
         push      cx
         repnz     movsb
         pop       cx
         pop       di
         pop       si
         dec       bx
         jnz       fnc0312
         pop       ds
         pop       es
fnc0319:  ret


fnc032:  cmp       byte ptr ds:[TypCard],2
         jbe       fnc033

         mov       ax,[bp+4]                ; kone‡n˜ © dek k rolov n¡
         mov       di,ax
         sub       ax,[bp+8]                ; po‡ te‡n¡ © dek k rolov n¡
         jz        fnc0322                   ; je nulov˜ po‡et © dk– k rolov n¡
         mul       byte ptr [vyska]         ; vypo‡ten¡ po‡tu linek k rolov n¡
         mov       bx,ax                    ; po‡et linek k rolov n¡
         mov       dx,03ceh                 ; adresov˜ registr grafiky EGA
         mov       al,8
         out       dx,al                    ; nastaven¡ registru 08
         inc       dx
         mov       al,0
         out       dx,al                    ; nastaven¡ bitov‚ masky v¨ech rovin
         shl       di,1                     ; kone‡n˜ © dek k rolov n¡ * 2
         add       di,offset adrrow         ; tabulka adres © dk– na displeji
         mov       si,[di]                  ; adresa kone‡n‚ho © dku k rolov n¡
         inc       di
         inc       di
         mov       di,[di]                  ; adresa po‡ te‡n¡ho © dku k rol.
         mov       ax,[bp+10]               ; po‡ te‡n¡ pozice okna
         add       di,ax                    ; adresa prav‚ho doln¡ho rohu
         add       si,ax                    ; adresa prav‚ho horn¡ho rohu
         mov       cx,[bp+6]                ; ¨¡©ka okna (pozic)
         sub       cx,ax
         inc       cx                       ; - kone‡n  pozice okna
         push      ds
         mov       ax,0a000h                ; po‡ te‡n¡ adresa pamˆti EGA
         mov       es,ax                    ; nastaven¡ segmentu pro EGA
         mov       ds,ax
         cld                                ; smˆr p©enosu dat nahoru
fnc0321:  sub       di,80
         sub       si,80
         push      si
         push      di
         push      cx
         repnz     movsb                    ; p©enos jedn‚ linky
         pop       cx
         pop       di
         pop       si
         dec       bx
         jnz       fnc0321                   ; dal¨¡ linka k p©enosu
         pop       ds
         call      iniega                   ; inicializace displeje EGA
fnc0322:  ret

fnc033: jb        fnc034                   ; je MDA

         mov       ax,[bp+4]                ; kone‡n˜ © dek k rolov n¡
         mov       di,ax
         sub       ax,[bp+8]                ; po‡et © dk– k rolov n¡
         jbe       fnc0332                   ; nen¡ ‘ dn˜ © dek k rolov n¡
         mul       byte ptr [vyska]         ; vyn soben¡ po‡tem linek na © dek
         shr       ax,1
         shl       di,1                     ; po‡ te‡n¡ © dek k rolov n¡
         add       di,offset adrrow         ; tabulka adres © dk– na displeji
         mov       si,[di]                  ; p©e‡ten¡ adresy © dku
         inc       di
         inc       di
         mov       di,[di]                  ; adresa n sleduj¡c¡ho © dku
         mov       bx,[bp+10]               ; po‡ te‡n¡ pozice okna k rolov n¡
         add       di,bx                    ; koncov‚ adresy okna
         add       si,bx
         mov       cx,[bp+6]                ; ¨¡©ka okna (pozic)
         sub       cx,bx
         inc       cx                       ; - kone‡n  pozice okna
         mov       bx,0b800h                ; segment pamˆti EGA
         mov       es,bx                    ; nastaven¡ segment– pamˆti EGA
         mov       ds,bx
         cld                                ; smˆr p©enosu dat nahoru
fnc0331:
         add       di,1fb0h
         add       si,1fb0h
         push      si
         push      di
         push      cx                       ; po‡et bajt– k p©enosu
         repnz     movsb                    ; p©enos jedn‚ linky okna
         pop       cx
         pop       di
         pop       si
         sub       di,2000h                 ; dal¨¡ linka
         sub       si,2000h
         push      si
         push      di
         push      cx                       ; po‡et bajt– k p©enosu
         repnz     movsb                    ; p©enos jedn‚ linky okna
         pop       cx
         pop       di
         pop       si
         dec       ax                       ; ‡¡ta‡ linek
         jnz       fnc0331
fnc0332:  ret

fnc034:   mov       ax,[bp+4]                ; kone‡n˜ © dek k rolov n¡
         mov       di,ax
         sub       ax,[bp+8]                ; po‡ te‡n¡ © dek k rolov n¡
         jna       fnc0342                   ; je nulov˜ po‡et © dk– k rolov n¡
         mul       byte ptr [vyska]         ; vypo‡ten¡ po‡tu linek k rolov n¡
         mov       bx,ax                    ; po‡et linek k rolov n¡
         shl       di,1                     ; kone‡n˜ © dek k rolov n¡ * 2
         add       di,offset adrrow         ; tabulka adres © dk– na displeji
         mov       si,[di]                  ; adresa kone‡n‚ho © dku k rolov n¡
         inc       di
         inc       di
         mov       di,[di]                  ; adresa po‡ te‡n¡ho © dku k rol.
         mov       ax,[bp+10]               ; po‡ te‡n¡ pozice okna
         add       di,ax                    ; adresa prav‚ho doln¡ho rohu
         add       si,ax                    ; adresa prav‚ho horn¡ho rohu
         mov       cx,[bp+6]                ; ¨¡©ka okna (pozic)
         sub       cx,ax
         inc       cx                       ; - kone‡n  pozice okna
         mov       ax,0b000h                ; po‡ te‡n¡ adresa pamˆti EGA
         mov       es,ax                    ; nastaven¡ segmentu pro EGA
         mov       ds,ax
         cld                                ; smˆr p©enosu dat nahoru
fnc0341:  sub       di,2000h
         jns       fnc03415
         add       di,7fa6h
fnc03415: sub       si,2000h
         jns       fnc03416
         add       si,7fa6h
fnc03416: push      si
         push      di
         push      cx
         repnz     movsb                    ; p©enos jedn‚ linky
         pop       cx
         pop       di
         pop       si
         dec       bx
         jnz       fnc0341                   ; dal¨¡ linka k p©enosu
fnc0342:  ret

; *****************************************************************************
;
;                        Funkce 04: prav˜ okraj (‡ ra)
;
; *****************************************************************************
;þ
                                            ; FUNKCE 04 - prav˜ okraj (‡ ra)

                                            ; SS:BP = ukazatel parametr–
                                            ;    + 4 = k¢d ‡ ry
                                            ;    + 6 = kone‡n˜ © dek
                                            ;    + 8 = po‡ te‡n¡ © dek
                                            ;    +10 = pozice ‡ ry

fnc04:   cmp       byte ptr ds:[Mode],1
         jne       Fnc042                   ; nen¡ textov˜ m¢d

         cld
         mov       si,[bp+10]               ; pozice ‡ ry
         dec       si
         jl        fnc0412                   ; z porn  pozice
         cmp       si,ds:[Pozic]
         jae       fnc0412
         mov       ax,[bp+6]                ; kone‡n˜ © dek
         mov       di,[bp+8]                ; po‡ te‡n¡ © dek
         sub       ax,di
         inc       ax                       ; po‡et © dk–
         mov       cx,ax                    ; po‡et © dk–
         mov       ax,ds:[Pozic]
         mul       di
         mov       di,ax
         add       di,si
         add       di,di
         push      es
         mov       ax,word ptr ds:[AdrVRAM]
         mov       es,ax
         mov       dl,ds:[cara]             ; barva okraje
         mov       dh,ds:[normal]           ; barva norm ln¡ho textu
fnc0411:  mov       al,es:[di+1]

         xor       al,dl
         xor       al,dh
;         cmp       al,dl
;         mov       al,dl
;         jnz       fnc0413
;         mov       al,dh
fnc0413:  mov       es:[di+1],al
         add       di,ds:[Pozic]
         add       di,ds:[Pozic]
         loop      fnc0411
         pop       es
fnc0412: ret


Fnc042:  cmp       byte ptr ds:[TypCard],2
         jbe       fnc043

         mov       si,[bp+10]               ; pozice ‡ ry
         or        si,si
         jng       fnc0422                   ; z porn  pozice
         cmp       si,word ptr 80           ; kontrola p©ekro‡en¡ 80 znak–
         ja        fnc0422                   ; p©ekro‡en po‡et znak– 80
         mov       ax,[bp+6]                ; kone‡n˜ © dek
         mov       di,[bp+8]                ; po‡ te‡n¡ © dek
         sub       ax,di
         inc       ax                       ; po‡et © dk– okna
         mul       byte ptr [vyska]         ; po‡et linek na © dek
         mov       cx,ax                    ; ‡¡ta‡ linek
         shl       di,1                     ; po‡ te‡n¡ © dek * 2
         mov       di,[di+adrrow]           ; adresa © dku (v bajtech)
         add       di,si                    ; adresa za‡ tku okna (v bajtech)
         mov       dx,03ceh                 ; adresov˜ registr grafiky EGA
         mov       al,1
         out       dx,al                    ; nastaven¡ registru 01
         inc       dx                       ; datov˜ registr grafiky EGA
         mov       al,0                     ; nastaven¡ bod–
         out       dx,al
         dec       dx                       ; adresov˜ registr grafiky EGA
         mov       al,3
         out       dx,al                    ; nastaven¡ registru 03
         inc       dx                       ; datov˜ registr grafiky EGA
         mov       al,18h                   ; nastaven¡ re‘imu pro z pis XOR
         out       dx,al
         mov       al,[bp+04]               ; znak ‡ ry
         push      ds
         mov       dx,0a000h                ; adresa pamˆti EGA
         mov       ds,dx                    ; datov˜ segment na pamˆŸ EGA
fnc0421:  mov       ah,[di]                  ; p©e‡ten¡ bajtu
         mov       [di],al                  ; z pis znaku ‡ ry
         add       di,80
         loop      fnc0421                   ; dal¨¡ linka
         pop       ds
         call      iniega                   ; inicializace displeje EGA
fnc0422:  ret

fnc043:  jb        fnc044                   ; je MDA

         mov       si,[bp+10]               ; pozice ‡ ry
         or        si,si
         jng       fnc0432                   ; z porn  pozice
         cmp       si,word ptr 80           ; kontrola p©ekro‡en¡ 80 znak–
         ja        fnc0432                   ; p©ekro‡en po‡et znak– 80
         mov       ax,[bp+6]                ; kone‡n˜ © dek
         mov       di,[bp+8]                ; po‡ te‡n¡ © dek
         sub       ax,di
         inc       ax                       ; po‡et © dk– okna
         mul       byte ptr [vyska]         ; po‡et linek na © dek
         mov       cx,ax                    ; ‡¡ta‡ linek
         shr       cx,1
         shl       di,1                     ; po‡ te‡n¡ © dek * 2
         add       si,[di+adrrow]           ; adresa © dku (v bajtech)
         mov       al,[bp+04]               ; znak ‡ ry
         mov       dx,0b800h                ; adresa pamˆti CGA
         mov       ds,dx                    ; datov˜ segment na pamˆŸ CGA
fnc0431:  xor       ds:[si],al
         add       si,2000h
         xor       ds:[si],al
         sub       si,1fb0h
         loop      fnc0431                   ; dal¨¡ linka
fnc0432:  ret

fnc044:   mov       si,[bp+10]               ; pozice ‡ ry
         or        si,si
         jng       fnc0442                   ; z porn  pozice
         cmp       si,word ptr 90           ; kontrola p©ekro‡en¡ 90 znak–
         ja        fnc0442                   ; p©ekro‡en po‡et znak– 90
         mov       ax,[bp+6]                ; kone‡n˜ © dek
         mov       di,[bp+8]                ; po‡ te‡n¡ © dek
         sub       ax,di
         inc       ax                       ; po‡et © dk– okna
         mul       byte ptr [vyska]         ; po‡et linek na © dek
         mov       cx,ax                    ; ‡¡ta‡ linek
         shl       di,1                     ; po‡ te‡n¡ © dek * 2
         mov       di,[di+adrrow]           ; adresa © dku (v bajtech)
         add       di,si                    ; adresa za‡ tku okna (v bajtech)
         mov       ax,0b000h                ; adresa pamˆti HGC
         mov       ds,ax                    ; datov˜ segment na pamˆŸ HGC
fnc0441:  mov       al,[bp+4]                ; p©e‡ten¡ bajtu
         xor       [di],al                  ; z pis znaku ‡ ry
         add       di,2000h
         jns       fnc04415
         sub       di,7fa6h
fnc04415: loop      fnc0441                   ; dal¨¡ linka
fnc0442:  ret

; *****************************************************************************
;
;                    Funkce 05: inicializace displeje
;
; *****************************************************************************
;þ
; -----------------------------------------------------------------------------
;        Funkce 5: Inicializace parametr–
; -----------------------------------------------------------------------------
; VSTUP: SS:[BP+4] (1) barva podkladu
;        SS:[BP+6] (1) barva p¡sma
;        SS:[BP+8] (1) re‘im obrazovky
;                         1=textov˜ m¢d
;                         2=norm ln¡ re‘im
;                         3=hust˜ re‘im (PREVIEW)
; VSTUP: AX=po‡et pozic na © dek
;         BX=po‡et linek na © dek
;         CX=‡¡slo posledn¡ho © dku (=po‡et © dk– - 1)
;         ES:SI=jm‚no karty (3 znaky ASCII)
; -----------------------------------------------------------------------------

Fnc05    PROC      NEAR

         mov       al,[bp+8]                ; re‘im obrazovky (© dkov n¡)
         mov       ds:[Mode],al             ; re‘im obrazovky (1,2,3)

; ------ detekce typu videokarty

         push      bp
         call      DetCard                  ; detekce typu videokarty
         pop       bp

; ------ inicializace videom¢du

         mov       al,ds:[AktVMod]          ; videom¢d pro grafick˜ re‘im
         cmp       byte ptr ds:[Mode],1     ; je textov˜ videom¢d ?
         jne       Fnc051                   ; nen¡ textov˜ videom¢d
         mov       al,ds:[TxtVMod]          ; videom¢d pro textov˜ re‘im
Fnc051:  mov       ah,0
         call      Int10                    ; nastaven¡ videom¢du

; ------ inicializace pro grafick˜ videom¢d HGC

         cmp       byte ptr ds:[Mode],1     ; je textov˜ videom¢d ?
         je        Fnc052                   ; je textov˜ videom¢d
         cmp       byte ptr ds:[TypCard],1  ; je karta HGC ?
         jne       Fnc052                   ; nen¡ karta HGC
         call      InitHGC                  ; inicializace grafick‚ho m¢du HGC

         mov       ax,0b000h
         mov       es,ax
         xor       ax,ax
         xor       di,di
         mov       cx,8000h/2
         rep       stosw

; ------ p©¡prava adresy videopamˆti

Fnc052:  mov       word ptr ds:[AdrVRAM],0b000h ; adresa karty HGC
         cmp       byte ptr ds:[TypCard],2  ; je karta CGA ?
         jb        Fnc053                   ; je karta HGC
         mov       word ptr ds:[AdrVRAM],0b800h ; textov‚ videom¢dy a m¢d CGA
         je        Fnc053                   ; je karta CGA
         cmp       byte ptr ds:[Mode],1     ; je textov˜ videom¢d ?
         je        Fnc053                   ; je textov˜ videom¢d
         mov       word ptr ds:[AdrVRAM],0a000h ; adresa pro EGA/VGA graf. m¢d

; ------ p©¡prava barev displeje

Fnc053:  mov       al,[bp+6]                ; barva p¡sma
         mov       bl,[bp+4]                ; barva podkladu
         call      InitCols                 ; inicializace barev displeje

; ------ p©¡prava po‡tu © dk– a v˜¨ky znaku

         call      InitRows                 ; inicializace po‡tu © dk–

; ------ vypnut¡ kurzoru v textov‚m m¢du

         cmp       byte ptr ds:[Mode],1     ; je textov˜ videom¢d ?
         jne       Fnc056                   ; nen¡ textov˜ videom¢d
         mov       dx,25*HI
         mov       ah,2
         mov       bh,0
         call      Int10                    ; nastaven¡ kurzoru za roh

; ------ vymaz n¡ displeje

Fnc056:

;         call      InitClr                  ; inicializa‡n¡ vymaz n¡ displeje
;                                        ;* vymaz n¡ spodn¡ch linek
;        push      di
;        mov       di,476*80
;        mov       cx,0a000h                ; videopamˆŸ EGA
;        mov       es,cx                    ; nastaven¡ segmentu ES
;        mov       al,[invmsk]              ; maska pro v˜stup (pro inverzi)
;        mov       cx,4*80
;        cld
;        rep       stosb                    ; vymaz n¡ linek
;        pop       di
;
;        pop       cx                       ;

; ------ adresa textu karty

Fnc058:  mov       al,ds:[TypCard]          ; typ karty
         mov       ah,0
         shl       ax,1
         shl       ax,1
         add       ax,offset Karta          ; adresa textu karty
         xchg      ax,si                    ; SI <- adresa textu karty

; ------ parametry displeje

Fnc059:  mov       ax,ds:[Pozic]            ; po‡et pozic na © dek
         mov       bh,0
         mov       bl,ds:[Vyska]            ; po‡et linek na © dek
         mov       cx,ds:[Radku]            ; po‡et © dk– displeje celkem
         dec       cx                       ; ‡¡slo posledn¡ho © dku
         push      cs
         pop       es                       ; ES <- segment ovlada‡e
         ret

Fnc05    ENDP

; -----------------------------------------------------------------------------
;        p©¡prava parametr– © dk–
; -----------------------------------------------------------------------------

InitRows PROC      NEAR

; ------ po‡et © dk– displeje celkem

         mov       al,ds:[TypCard]          ; typ karty (1 a‘ 4)
         mov       ah,al
         shl       al,1
         add       al,ah                    ; offset karty
         add       al,ds:[Mode]             ; offset v tabulce
         mov       ah,0
         xchg      ax,bx                    ; BX <- offset v tabulce
         mov       al,ds:[bx+TabRows-3-1]   ; po‡et © dk– displeje
         mov       byte ptr ds:[Radku],al   ; po‡et © dk– displeje

; ------ po‡et linek na © dek

         mov       al,ds:[bx+TabLines-3-1]  ; po‡et linek na © dek
         mov       [Vyska],al               ; po‡et linek na © dek

; ------ po‡et pozic na © dek

         mov       al,80                    ; po‡et znak– na © dek
         cmp       byte ptr ds:[TypCard],1  ; je karta HGC ?
         jne       InitRws2                 ; nen¡ karta HGC
         cmp       byte ptr ds:[Mode],1     ; je textov˜ re‘im MDA ?
         je        InitRws2                 ; je textov˜ re‘im MDA
         mov       al,90                    ; jinak 90 znak– na © dek
InitRws2:mov       byte ptr ds:[Pozic],al   ; po‡et pozic na © dek

; ------ inicializace adres © dk–

         cld
         push      cs
         pop       es
         mov       cx,ds:[Radku]            ; po‡et © dk– displeje
         inc       cx                       ; © dek + 1
         mov       di,offset AdrRow         ; tabulka adres © dk– na displeji

         xor       ax,ax                    ; po‡ te‡n¡ adresa
         mov       dx,160                   ; po‡et bajt– na © dek pro text. m¢d
         cmp       byte ptr ds:[Mode],1     ; je to textov˜ m¢d ?
         je        InitRws4                 ; je to textov˜ m¢d

         mov       al,80                    ; po‡et bajt– na linku EGA/VGA
         mul       byte ptr ds:[Vyska]      ; po‡et bajt– na © dek pro EGA/VGA
         xchg      ax,dx                    ; DX <- p©¡rustek adresy
         cmp       byte ptr ds:[TypCard],3  ; je karta EGA/VGA ?
         jae       initRws3                 ; je karta EGA/VGA
         shr       dx,1                     ; pro CGA je polovi‡n¡ po‡et
         cmp       byte ptr ds:[TypCard],2  ; je to karta CGA ?
         jb        InitRws5                 ; je to karta HGC

; ------ adresy © dk– pro textov‚ m¢dy a pro m¢dy CGA, EGA a VGA

InitRws3:mov       ax,-1                    ; ukazatel adresy © dku
InitRws4:stosw                              ; ulo‘en¡ adresy © dku
         add       ax,dx                    ; zv˜¨en¡ adresy © dku
         loop      InitRws4
         jmp       short InitRws8

; ------ adresy © dk– pro grafick˜ m¢d HGC

InitRws5:mov       ax,-1
         mov       dx,90                    ; po‡et bajt– na © dek
         mov       bl,ds:[Vyska]            ; v˜¨ka © dku
InitRws6:mov       al,dh                    ; ukazatel ‡¡sla © dku
         mul       bl                       ; ‡¡slo linky absolutnˆ
         mov       si,ax                    ; SI <- absolutn¡ ‡¡slo linky
         and       si,3                     ; ‡¡slo linky relativnˆ v © dku
         ror       si,1
         ror       si,1
         ror       si,1                     ; adresa 0, 2000h, 4000h
         shr       ax,1
         shr       ax,1                     ; ‡¡slo © dku
         mul       dl                       ; po‡ te‡n¡ adresa © dku
         add       ax,si                    ; p©i‡ten¡ korekce pro podlinku
         dec       ax                       ; p©ednastaven¡ - 1
         stosw                              ; ulo‘en¡ adresy linky
         inc       dh                       ; zv˜¨en¡ ukazatele ‡¡sla linky
         loop      InitRws6                 ; dal¨¡ © dek

; ------ adresa fontu

InitRws8:mov       al,ds:[Vyska]            ; v˜¨ka © dku
         mov       ah,6                     ; v˜¨ka zmen¨en‚ho fontu
         mov       bx,offset Fnt6x8         ; zmen¨en˜ font 6x8
         mov       si,offset Fnt6x8         ; hlavn¡ font
         mov       word ptr ds:[Fnc005K+1],offset(Kurziv6-Fnc005K-3)
         cmp       al,6
         jbe       InitRws9
         mov       si,offset Fnt8x8
         mov       word ptr ds:[Fnc005K+1],offset(Kurziv8-Fnc005K-3)
         cmp       al,8
         jbe       InitRws9
         mov       ah,8                     ; v˜¨ka zmen¨en‚ho fontu
         mov       bx,si                    ; zmen¨en˜ font 8x8
         mov       si,offset Fnt14x8        ; 14x8
         mov       word ptr ds:[Fnc005K+1],offset(Kurziv14-Fnc005K-3)
         cmp       al,14
         jbe       InitRws9
         mov       si,offset Fnt19x8        ; 19x8
         mov       word ptr ds:[Fnc005K+1],offset(Kurziv19-Fnc005K-3)

InitRws9:mov       ds:[AdrLFont],bx         ; zmen¨en˜ font
         mov       ds:[AdrFont],si          ; hlavn¡ font
         mov       ds:[VyskaL],ah           ; po‡et bajt– na zmen¨en˜ font
         ret

InitRows ENDP

; ------ po‡et © dk– displeje

TabRows  db        25,25,43                 ; MDA
         db        25,25,33                 ; CGA
         db        25,25,43                 ; EGA
         db        25,25,60                 ; VGA

; ------ po‡ty linek na © dek

TabLines db        1,14,8                   ; MDA
         db        1,8,6                    ; CGA
         db        1,14,8                   ; EGA
         db        1,19,8                   ; VGA

; -----------------------------------------------------------------------------
;        inicializace barev displeje
; -----------------------------------------------------------------------------
; VSTUP: AL=barva p¡sma
;        BL=barva podkladu
; -----------------------------------------------------------------------------

InitCols PROC      NEAR

; ------ inicializace pro textov˜ m¢d

         cmp       byte ptr ds:[Mode],1     ; je textov˜ m¢d ?
         jne       InitCls3                 ; nen¡ textov˜ m¢d

; ------ barvy pro MDA re‘im

         cmp       byte ptr ds:[TypCard],1  ; je karta HGC ?
         jne       InitCls1                 ; nen¡ karta HGC

         mov       byte ptr ds:[Normal],7
         mov       byte ptr ds:[Cara],70h
         mov       byte ptr ds:[Invert],0f0h
         mov       byte ptr ds:[Zvyraz],0fh
         mov       byte ptr ds:[Sikme],70h
         mov       byte ptr ds:[Podtrz],70h
         mov       byte ptr ds:[Jine],70h

;         cmp       al,7
;         mov       al,7
;         je        IniCls12
;         mov       al,0
;         jb        IniCls12
;         mov       al,15
;
;IniCls12:cmp       bl,7
;         mov       bl,7
;         je        InitCls1
;         mov       bl,0
;         jb        InitCls1
;         mov       bl,15
         jmp       short InitCls8

InitCls1:mov       cl,4
         shl       bl,cl                    ; p©¡prava barvy podkladu
         and       al,0fh                   ; barva p¡sma
         or        al,bl                    ; zadan  barva textu
         mov       bl,al                    ; £schova barvy textu
         mov       [Normal],al              ; barva norm ln¡ho textu

         push      ax
         xor       al,77h                   ; negace barvy
         mov       [Cara],al                ; barva ‡ ry okraje
         pop       ax

         ror       al,cl
         mov       [Invert],al              ; barva invertovan‚ho textu

;         cmp       byte ptr ds:[TypCard],1  ; je karta HGC ?
;         jne       InitCls2                 ; nen¡ karta HGC
;
;         or        al,8
;         mov       ds:[Zvyraz],al
;         mov       ds:[Sikme],al
;         mov       ds:[Podtrz],al
;         jmp       short IniCls24
;
;InitCls2:
         and       bl,0f0h                  ; barva podkladu
         mov       al,bl
         or        al,14                    ; ‘lut  barva
         mov       [Zvyraz],al              ; zv˜raznˆn‚ p¡smo
         mov       al,bl
         or        al,10                    ; zelen  barva
         mov       [Sikme],al               ; ¨ikm‚ p¡smo
         mov       al,bl
         or        al,13                    ; fialov‚ p¡smo
         mov       [Podtrz],al              ; podtr‘en‚ p¡smo
         mov       al,bl
         or        al,15                    ; b¡l‚ p¡smo
         mov       [Jine],al                ; jin‚ p¡smo
         shr       bl,cl                    ; barva podkladu
         jmp       short InitCls8

; ------ inicializace pro grafick˜ re‘im EGA/VGA

InitCls3:cmp       byte ptr ds:[TypCard],2  ; je karta EGA nebo VGA ?
         jbe       InitCls5                 ; nen¡ karta EGA/VGA
         mov       bh,bl                    ; barva podkladu
         mov       ah,0                     ; maska pro inverzi - nedˆl  se
         or        bh,al                    ; p©id n¡ rovin barvy p¡sma
         cmp       bh,bl                    ; je p¡smo podmno‘inou pozad¡ ?
         jne       InitCls4                 ; nen¡ podmno‘inou pozad¡
         mov       ah,-1                    ; maska pro inverzi - dˆl  se
         xchg      al,bl                    ; z mˆna pozad¡ a pop©ed¡
InitCls4:or        al,bl                    ; spole‡n‚ barvy pro p¡smo
         mov       [PlanNg],al              ; roviny pro negaci znaku (kurzor)
         mov       [Planes],bl              ; roviny EGA pro v˜stup znaku
         mov       [InvMsk],ah              ; maska znak– pro inverzi
         call      IniEga                   ; inicializace displeje EGA/VGA
IniCls44:jmp       short InitCls9

; ------ inicializace pro grafick˜ re‘im CGA a HGC

InitCls5:mov       byte ptr ds:[InvMsk],0ffh ; prov d¡ se inverze p¡sma
         cmp       al,bl                    ; je barva p¡sma vy¨¨¡ ne‘ pozad¡ ?
         jbe       InitCls8                 ; prov d¡ se inverze
         mov       byte ptr ds:[InvMsk],0   ; inverze p¡sma se neprov d¡
         mov       bl,al                    ; jinak plat¡ barva p¡sma

; ------ nastaven¡ barvy okol¡

InitCls8:;cmp       byte ptr ds:[TypCard],1  ; je karta HGC ?
         ;je        InitCls9                 ; okol¡ se pro HGC nenastavuje
         mov       ah,0bh
         mov       bh,0
         call      Int10                    ; nastaven¡ barvy okol¡

         cmp       byte ptr ds:[Mode],1     ; je textov˜ re‘im ?
         jne       InitCls9                 ; nen¡ textov˜ re‘im

IniCls82:mov       ax,1003h
         mov       bl,0                     ; p©¡znak - intezivn¡ podklad
         call      Int10                    ; nastaven¡ atributu s jasem

         xor       ax,ax
         mov       es,ax
         mov       dx,es:[463h]             ; port CRT
         add       dl,4                     ; ©¡dic¡ port
         mov       al,bit0 + bit3
         out       dx,al                    ; vypnut¡ blik n¡

InitCls9:ret

InitCols ENDP

; -----------------------------------------------------------------------------
;        Inicializace displeje EGA v grafick‚m m¢du
; -----------------------------------------------------------------------------

IniEga   PROC      NEAR

         cmp       byte ptr ds:[TypCard],3  ; je karta EGA/VGA ?
         jb        IniEga9                  ; nen¡ karta EGA/VGA

         cli
         mov       dx,03ceh                 ; adresov˜ registr EGA
         mov       al,0
         out       dx,al                    ; nastaven¡ registru 0
         inc       dx                       ; datov˜ registr
         mov       al,[planes]              ; roviny EGA pro v˜stup
         mov       bl,al
         out       dx,al                    ; nastaven¡ rovin EGA pro v˜stup
         dec       dx                       ; adresov˜ registr EGA
         mov       al,1
         out       dx,al                    ; nastaven¡ registru 1
         inc       dx                       ; datov˜ registr
         mov       al,[planng]              ; roviny pro negaci znaku
         xor       al,bl
         not       al
         out       dx,al                    ; nastaven¡ rovin EGA pro negaci
         dec       dx                       ; adresov˜ registr EGA
         mov       al,3                     ; registr 3
         out       dx,al                    ; nastaven¡ registru 3
         inc       dx                       ; datov˜ registr EGA
         mov       al,0
         out       dx,al                    ; nastaven¡ re‘imu z pisu bez zmˆny
         dec       dx                       ; adresov˜ registr
         mov       al,8                     ; registr 8
         out       dx,al                    ; nastaven¡ registru 8
         inc       dx                       ; datov˜ registr
         mov       al,0ffh
         out       dx,al                    ; nastaven¡ masky bit– v¨ech rovin
         sti

IniEga9: ret

IniEga   ENDP

; -----------------------------------------------------------------------------
;        Detekce videokarty
; -----------------------------------------------------------------------------

DetCard  PROC      NEAR

; ------ test, zda ji‘ probˆhla detekce

         cmp       byte ptr ds:[TypCard],0  ; byl ji‘ typ karty detekov n ?
         je        DetCard1                 ; typ karty je¨tˆ nebyl detekov n
         ret

; ------ £schova videom¢du

DetCard1:mov       ah,0fh
         call      Int10                    ; poskytnut¡ aktivn¡ho videom¢du
         mov       ds:[OldVMod],al          ; £schova videom¢du

; ------ test, zda je videokarta EGA/VGA

         mov       ah,12h
         mov       bx,0ff10h
         call      Int10                    ; test, zda je karta EGA/VGA
         cmp       bh,1
         ja        DetCard3                 ; nen¡ karta EGA/VGA
         cmp       bl,5
         ja        DetCard3                 ; nen¡ karta EGA/VGA

; ------ test, zda je videokarta VGA

         mov       byte ptr ds:[TypCard],3  ; p©ednastaven¡ - karta EGA
         mov       ax,1a00h
         call      Int10                    ; dotaz na instalaci videokarty
         cmp       al,1ah                   ; je funkce obsluhovan  ?
         jne       DetCard4                 ; funkce nen¡ obsluhovan 
         cmp       bl,7
         je        DetCard2                 ; je karta VGA
         cmp       bl,8
         je        DetCard2                 ; je karta VGA
         cmp       bl,11
         je        DetCard2                 ; je karta VGA
         cmp       bl,12
         jne       DetCard4                 ; nen¡ karta VGA
DetCard2:mov       byte ptr ds:[TypCard],4  ; je karta VGA
         jmp       short DetCard4

; ------ test, zda je videokarta HGC

DetCard3:mov       byte ptr ds:[TypCard],1  ; je karta HGC
         cmp       byte ptr ds:[OldVMod],7  ; je videom¢d 7 (=HGC) ?
         je        DetCard4                 ; je karta HGC
         mov       byte ptr ds:[TypCard],2  ; jinak je karta CGA

; ------ p©¡prava ‡¡sla videom¢du

DetCard4:mov       bl,ds:[TypCard]          ; typ videokarty
         mov       bh,0
         dec       bx
         mov       al,ds:[bx+TabTVMod]      ; videom¢d pro textov˜ re‘im
         mov       ds:[TxtVMod],al          ; textov˜ videom¢d
         mov       al,ds:[bx+TabGVMod]      ; videom¢d pro grafick˜ re‘im
         mov       ds:[AktVMod],al          ; grafick˜ videom¢d
         ret

DetCard  ENDP

TabTVMod db        7,3,3,3                  ; videom¢dy pro textov˜ re‘im
TabGVMod db        7,6,16,18                ; videom¢dy pro grafick˜ re‘im

; -----------------------------------------------------------------------------
;        inicializace grafick‚ho re‘imu karty HGC
; -----------------------------------------------------------------------------

InitHGC  PROC      NEAR

         cli

; ------ nastaven¡ registru m¢du karty HGC

         mov       dx,3b8h
         mov       al,2
         out       dx,al                    ; grafick˜ m¢d

; ------ inicializace registr– HGC

         mov       ah,0
         mov       si,offset HGCTabR        ; tabulka registr– karty HGC
         mov       dx,3b4h                  ; adresa portu karty HGC
         cld
InitHGC2:mov       al,ah                    ; ‡¡slo registru
         out       dx,al                    ; nastaven¡ ‡¡sla registru
         inc       dx
         lodsb                              ; volba ‡¡sla registru
         out       dx,al                    ; nastaven¡ registru
         dec       dx
         inc       ah                       ; zv˜¨en¡ ukazatele registr–
         cmp       ah,16
         jb        InitHGC2                 ; dal¨¡ registr

; ------ povolen¡ druh‚ str nky videopamˆti (od adresy B800h)

         mov       dx,3bfh
         mov       al,1
         out       dx,al

; ------ zapnut¡ videosign lu

         mov       dx,03b8h
         mov       al,1010b                 ; grafick˜ m¢d, videosign l zapnut
         out       dx,al
         sti
         ret

InitHgc  ENDP

; ------ registry pro inicializaci grafick‚ho re‘imu karty HGC

HGCTabR  db        53                       ; 0: horizont lnˆ celkem
         db        45                       ; 1: horizont lnˆ zobrazeno
         db        46                       ; 2: pozice HSYNC
         db        7                        ; 3: ¨¡©ka HSYNC
         db        91                       ; 4: vertik lnˆ celkem
         db        2                        ; 5: vertik ln¡ korekce
         db        88                       ; 6: vertik lnˆ zobrazeno
         db        88                       ; 7: vertik ln¡ pozice
         db        2                        ; 8: m¢d prokl d n¡
         db        3                        ; 9: po‡et linek na © dek
         db        0                        ; 10: po‡ tek kurzoru
         db        0                        ; 11: konec kurzoru
         db        0                        ; 12: adresa po‡ tku HIGH
         db        0                        ; 13: adresa po‡ tku LOW
         db        0                        ; 14: kurzor HIGH
         db        0                        ; 15: kurzor LOW

; -----------------------------------------------------------------------------
;        obsluha INT 10h s £schovou registr–
; -----------------------------------------------------------------------------

Int10    PROC      NEAR

         pushf
         push      si
         push      di
         push      bp
         push      ds
         push      es

         int       10h

         pop       es
         pop       ds
         pop       bp
         pop       di
         pop       si
         popf
         ret

Int10    ENDP

; *****************************************************************************
;
;                   Funkce 06: n vrat textov‚ho videom¢du
;
; *****************************************************************************
;þ
                                            ; FUNKCE 06 - nastaven¡ text. m¢du
fnc06:   mov       al,ds:[OldVMod]
         mov       ah,0
         call      Int10                    ; nastaven¡ textov‚ho m¢du 80x25
         ret

                                            ; slovn¡k 1
slov1    db        'ANA ANI ABE ALTR EINI DEST GEI INI INS '
         db        'NACHT UNIV ZEITEN ZEITU ',0ffh

                                            ; slovn¡k 2
slov2    db        'AB AN AUF AUS NACH UN VER VOR ',0ffh

                                            ; slovn¡k 3
slov3    db        'ALT ALP BETT BERG DES EIN ERD ERNST FETT FEST '
         db        'GELD HAUS KREUZ LANG LEIT MACHT MAUS MENSCH NORD '
         db        'NŽCHST NETZ OBER OST REIT RšCK SCHUL SPIEL SšD '
         db        'TEST TIEF VOLL WACHS WASCH WEIT WETT WEG WEST ',0ffh

                                            ; slovn¡k 4
slov4    db        'DURCH LETZTEND SPORT TRANS ',0ffh

                                            ; slovn¡k 5
slov5    db        'DORT HOCH ZEIT šBER ',0ffh

                                            ; slovn¡k 6
slov6    db        'CHA CURS DAR EMPF ENT HIN HER KOMP MIT SYN ',0ffh

                                            ; slovn¡k 7
slov7    db        'ABL ABR ALLEIN ANEI ANER ANORD ANS AT BEEH BEEI '
         db        'BEEN BEERD BEIRR BEOB BEUN BEUR ER GE IN KOOR '
         db        'OKT UM UR ZUGABE ',0ffh

                                            ; slovn¡k 8
slov8    db        'AI AU AY EI EU EY ŽU ',0ffh

                                            ; slovn¡k 9
slov9    db        'EA EŽ E™ Eš II IŽ I™ Iš OA OE UE UI UU ',0ffh

                                            ; slovn¡k 10
slov10   db        'BL BR CH DR FL FR GL GR KL KN KR PFL PH PF '
         db        'PL PR SCHL SCHM SCHN SCHR SCHW STR SPR SCH '
         db        'SP ST TH TR ZW ',0ffh

                                            ; slovn¡k 11
slov11   db        'BS B CHTS CHS CHT CKT CH CK D FF FS F GS G '
         db        'HRS HL HM HN HR HT H KT K LBST LSCH LCH LFS '
         db        'LKS LTS LB LD LF LG LK LL LP LS LT L '
         db        'MPF MTS MM MP MT M NSCH NGS NDS NST ND NN NF '
         db        'NG NK NS NT NZ N PF PP PT P '
         db        'RSCH RCH RKT RST RB RD RF RG RK RM RN RS RT '
         db        'RZ R SCH ST S TSCH TZT TS TT TZ T XT Z '
         db        'á ',0ffh

                                            ; slovn¡k 12
slov12   db        'EITS IONS LLGE NSGE UNGS ',0ffh

                                            ; slovn¡k 13
slov13   db        'HAUS HEIT LICH LADE LAND LER LIG LOS PHA PREIS '
         db        'PUNKT RAND RAUM RECHT RECHN REICH RIG ROUT '
         db        'RUF ',0ffh

                                            ; slovn¡k 14
slov14   db        'AUS AUF AUGE AUTO AMT ART AB ARZT ARBEIT AKT '
         db        'ALGO ANALY ANDER ANF ANGAB ANGEB ANL ANOR '
         db        'ANSA ANTEIL ANWA ANWEN ANZAHL ANZEIG ANTW '
         db        'APFEL AFRI AMERI ADR APP ARGU EBENE ECK EDITO '
         db        'EIN EFF EURO ELEM ENERG ENTW EIMER ERDA '
         db        'ERGEBN ERFA ERLEB ERFO ERHAL ERKENN ETW EXP '
         db        'INDU INF INH INS IMP OBER ORDN ORDEN OPER ORGA '
         db        'ORIENT ORT UHR UMBRU UNFALL UNTER UMSATZ '
         db        'URLAUB URSACH URTEIL ŽRZT ŽNDER ŽMT ™FFN '
         db        '™KO šB ',0ffh

b0aa4    db        0                        ; povolen  d‚lka © dku
b0aa5    dw        0                        ; adresa p©evodn¡ tabulky znak–
w0aa7    dw        0
                                            ; test, zda jde o oddˆlovac¡ znak
                                            ; vstup:  DS:[BX+DI] = ukaz. textu
                                            ;         ES:[CS:[B0AA5]] p©ev.tab.
                                            ; v˜stup: ZF = 0 je oddˆlovac¡ znak

tst04:   mov       ah,4
         jmp       short tst401

                                            ; test, zda jde o ©¡d¡c¡ znak
                                            ; vstup:  DS:[BX+DI] = ukaz. textu
                                            ;         ES:[CS:[B0AA5]] p©ev.tab.
                                            ; v˜stup: ZF = 0 je ©¡d¡c¡ znak

tst40:   mov       ah,40h
tst401:  mov       al,[bx+di]               ; znak z textu k otestov n¡
         push      bx
         mov       bx,cs:[b0aa5]            ; adresa tabulky oddˆlovac¡ch znak–
         db        26h
         xlat                               ; transformace podle tab. ES:BX
         test      ah,al
         pop       bx
         ret

                                            ; nalezen¡ za‡ tku slova v textu
                                            ; vstup:  DS:[BX+DI] = ukaz.textu

endwrd:  dec       bx                       ; ukazatel textu
endwrd1: inc       bx
         call      tst40                    ; test, zda jde o ©¡d¡c¡ znak
         jnz       endwrd2                  ; je ©¡d¡c¡ znak - konec
         test      al,4                     ; jde o oddˆlovac¡ znak slova ?
         jnz       endwrd1                  ; je oddˆlovac¡ znak slova - dal¨¡
endwrd2: ret

                                            ; nalezen¡ slova ve slovn¡ku

                                            ; vstup:  CS:[SI] = ukazatel tabulky
                                            ;         DS:[BX+DI] = ukaz. textu
                                            ; v˜stup: AX = d‚lka shodn‚ho slova
                                            ;         (AX = 0,ZF = 0 nenalezeno)

srcwrd:  mov       ah,32                    ; oddˆlovac¡ znak slov (mezera)
srcwrd1: mov       al,cs:[si]               ; prvn¡ znak slova z tabulky
         cmp       al,[bx+di]               ; porovn n¡ se znakem v textu
         ja        srcwrd5                  ; znak v tabulce vˆt¨¡ - nenalezeno
         jc        srcwrd3                  ; znak v tabulce men¨¡ - dal¨¡ slovo
         push      bx                       ; (shoda prvn¡ho znaku) ukaz. textu
srcwrd2: inc       bx                       ; ukazatel zkouman‚ho textu
         inc       si                       ; ukazatel slovn¡ku
         mov       al,cs:[si]               ; dal¨¡ znak ze slovn¡ku
         cmp       al,ah                    ; je mezera (konec slova) ?
         jz        srcwrd4                  ; je mezera = konec slova
         cmp       al,[bx+di]               ; porovn n¡ dal¨¡ho znaku slova
         jz        srcwrd2                  ; shoda - dal¨¡ znak
         pop       bx                       ; (nen¡ shoda) ukazatel textu
srcwrd3: inc       si                       ; nalezen¡ dal¨¡ho slova v tabulce
         cmp       ah,cs:[si]               ; je oddˆlovac¡ mezera ?
         jnz       srcwrd3                  ; je¨tˆ ne - dal¨¡ znak v tabulce
         inc       si
         jmp       short srcwrd1            ; test dal¨¡ho slova
srcwrd4: mov       ax,bx                    ; slova se shoduj¡
         pop       bx                       ; adresa slova ve slovn¡ku
         sub       ax,bx                    ; d‚lka nalezen‚ho slova
         ret
srcwrd5: xor       ax,ax                    ; slovo nenalezeno - konec (d‚lka 0)
         ret


lab0af5: push      bx                       ; adresa textu
         add       bx,ax                    ; adresa konce slabiky (rozdˆlit)
         push      bx                       ; adresa konce slabiky
         call      endwrd                   ; nalezen¡ konce slova v textu
         mov       ax,bx                    ; konec slova v textu
         pop       bx                       ; adresa konce slabiky
         jz        lab0af51                 ; je ji‘ konec © dku
         sub       ax,bx                    ; po‡et znak– do konec slova
         cmp       al,1                     ; d‚lka zbyl‚ ‡ sti slova
         jna       lab0af52                 ; nen¡ ji‘ ‘ dn˜ znak
         push      ax                       ; d‚lka zbyl‚ ‡ sti slova
         mov       si,offset slov10         ; slovn¡k 10
         call      srcwrd                   ; nalezen¡ slova ve slovn¡ku
         pop       si                       ; d‚lka zbyl‚ ‡ sti slova
         cmp       ax,si                    ; nalezeno stejnˆ dlouh‚ slovo ?
         jz        lab0af52                 ; je cel‚ slovo
         xor       ax,ax                    
lab0af51:pop       bx                       
         ret                                
lab0af52:pop       si                       
lab0af53:cmp       bl,cs:[b0aa4]             ; povolen  d‚lka © dku
         jnc       lab0af55                  ; p©ekro‡en okraj © dku
         inc       dx                       
         cmp       dx,bx                    
         mov       dx,bx                    
         jz        lab0af54                 
         mov       cs:[w0aa7],bx
lab0af54:ret                                
lab0af55:jmp       fnc097

                                            ;
lab0b2f: mov       si,offset slov1          ; slovn¡k 1 - nelze rozdˆlit
         call      srcwrd                   ; nalezen¡ slova ve slovn¡ku
         jnz       lab0b2f5                 ; slovo nalezeno - konec
lab0b2f1:mov       si,offset slov2          ; slovn¡k 2 - lze rozdˆlit
         call      srcwrd                   ; nalezen¡ slova ve slovn¡ku
         jz        lab0b2f2                 ; slovo nenalezeno - slovn¡k 3
         call      lab0af5                 
         jnz       lab0b2f1                
lab0b2f2:mov       si,offset slov3          ; slovn¡k 3
         call      srcwrd                   ; nalezen¡ slova ve slovn¡ku
         jz        lab0b2f3                
         mov       si,di                   
         add       si,ax                   
         cmp       byte ptr [bx+si],"E"     ; je znak "E" ?
         jnz       lab0b2f4                
lab0b2f3:mov       si,offset slov4          ; slovn¡k 4
         call      srcwrd                   ; nalezen¡ slova ve slovn¡ku
         mov       ax,5                    
         jnz       lab0b2f4                
         mov       si,offset slov5          ; slovn¡k 5
         call      srcwrd                   ; nalezen¡ slova ve slovn¡ku
         mov       ax,4                    
         jnz       lab0b2f4                
         mov       si,offset slov6          ; slovn¡k 6
         call      srcwrd                   ; nalezen¡ slova ve slovn¡ku
         mov       ax,3                    
         jnz       lab0b2f4                
         mov       si,offset slov7          ; slovn¡k 7
         call      srcwrd                   ; nalezen¡ slova ve slovn¡ku
         mov       ax,2                    
         jz        lab0b2f5                
lab0b2f4:call      lab0af5                 
lab0b2f5:ret

lab0b85: mov       ax,bx                   
         sub       ax,cx                   
         cmp       ax,2                    
         xchg      bx,cx                   
         jc        lab0b853                
         jz        lab0b851                
         mov       si,offset slov8          ; slovn¡k 8
         call      srcwrd                   ; nalezen¡ slova ve slovn¡ku
         jz        lab0b851                
         inc       bx                      
         jmp       short lab0b852          
lab0b851:mov       si,offset slov9          ; slovn¡k 9
         call      srcwrd                   ; nalezen¡ slova ve slovn¡ku
         jz        lab0b853                
         cmp       byte ptr [bx+di-1],"Q"   ; je znak "Q" ?
         jz        lab0b853                
lab0b852:inc       bx                      
         call      lab0af53                
lab0b853:xchg      bx,cx                   
         ret       

lab0bb2: cmp       bx,cx
         jz        lab0bb27
         push      bx
         push      dx
         mov       dx,bx
         dec       dx
         mov       bx,cx
         dec       bx
lab0bb21:inc       bx
         cmp       bx,dx
         jz        lab0bb22
         mov       si,offset slov10         ; slovn¡k 10
         call      srcwrd                   ; nalezen¡ slova ve slovn¡ku
         jz        lab0bb21
         add       ax,bx
         dec       ax
         cmp       ax,dx
         jnz       lab0bb21
         cmp       byte ptr [bx+di-1],"C"   ; byl p©edchoz¡ znak "C" ?
         jz        lab0bb21
lab0bb22:xchg      bx,cx
         mov       si,offset slov11         ; slovn¡k 11
         call      srcwrd                   ; nalezen¡ slova ve slovn¡ku
         add       bx,ax
         xchg      bx,cx
         cmp       bx,cx
         jnc       lab0bb26
         mov       dx,bx
lab0bb23:mov       si,offset slov12         ; slovn¡k 12
         push      bx
         sub       bx,4
         call      srcwrd                   ; nalezen¡ slova ve slovn¡ku
         pop       bx
         jz        lab0bb24
         cmp       byte ptr cs:[w0aa7],0
         jnz       lab0bb26
lab0bb24:call      tst40                    ; test, zda jde o ©¡d¡c¡ znak
         mov       si,offset slov14         ; slovn¡k 14
         jnz       lab0bb25
         mov       si,offset slov13         ; slovn¡k 13
lab0bb25:call      srcwrd                   ; nalezen¡ slova ve slovn¡ku
         jnz       lab0bb26
         inc       bx
         cmp       bx,cx
         jna       lab0bb23
         mov       bx,dx
lab0bb26:pop       dx
         call      lab0af53
         pop       bx
lab0bb27:ret

; *****************************************************************************
;
;                        Funkce 09: rozdˆlov n¡ slov
;
; *****************************************************************************
;þ
                                            ; FUNKCE 09 - rozdˆlov n¡ slov
                                            ; vstup:  DS = datov˜ segment textu
                                            ;         CX = pozice v © dku

fnc09:   mov       [w0aa7],0
         mov       [b0aa4],al               ; povolen  d‚lka © dku
         mov       [b0aa5],si               ; adresa transforma‡n¡ tabulky znak–
         pop       dx                       ; n vratov  adresa programu
         pop       ds                       ; datov˜ segment textu
         push      ds                       ; datov˜ segment textu
         push      dx                       ; n vratov  adresa programu
         push      bp                       
         mov       bp,sp                    
fnc091:  mov       bx,cx                    ; aktu ln¡ pozice v © dku
         dec       bx                       
fnc092:  inc       bx                       ; dal¨¡ znak
         cmp       bl,cs:[b0aa4]            ; povolen  d‚lka © dku
         jnc       fnc097                   ; p©ekro‡en okraj © dku - konec
         call      tst04                    ; test, zda jde o oddˆlovac¡ znak
         jz        fnc092                   ; nenalezeno - dal¨¡
         mov       cx,bx                    ; nov  pozice rozdˆlen¡ slova
         dec       bx                       
fnc093:  inc       bx                       
         call      tst04                    ; test, zda jde o oddˆlovac¡ znak
         jnz       fnc093                   ; nalezeno - dal¨¡ pokus
         xchg      bx,cx                    
         mov       ax,cx                    
         sub       ax,bx                    
         cmp       ax,4                     
         jc        fnc091                   
         mov       dx,bx                    
         call      lab0b2f                  
         call      endwrd                   ; nalezen¡ za‡ tku slova v textu
         jz        fnc091                   
         push      cx                       
fnc094:  mov       cx,bx                    
         dec       bx                       
fnc095:  inc       bx                       
         call      tst40                    ; test, zda jde o ©¡d¡c¡ znak
         jnz       fnc095                   
         call      lab0b85                  
         mov       cx,bx                    
         call      endwrd                   ; nalezen¡ za‡ tku slova v textu
         jz        fnc096                   
         call      lab0bb2                  
         jmp       short fnc094             
fnc096:  pop       cx                       
         jmp       short fnc091             
fnc097:  mov       ax,cs:[w0aa7]            
         mov       sp,bp                    
         pop       bp                       
         ret                                


                                            ; tabulka pro p©ek¢dov n¡ kl ves

tabkey   db        0c1h,1eh                 ; ALT A
         db        0c2h,30h                 ; ALT B
         db        0c3h,2eh                 ; ALT C
         db        0c4h,20h                 ; ALT D
         db        0c5h,12h                 ; ALT E
         db        0c6h,21h                 ; ALT F
         db        0c7h,22h                 ; ALT G
         db        0c8h,23h                 ; ALT H
         db        0c9h,17h                 ; ALT I
         db        0cah,24h                 ; ALT J
         db        0cbh,25h                 ; ALT K
         db        0cch,26h                 ; ALT L
         db        0cdh,32h                 ; ALT M
         db        0ceh,31h                 ; ALT N
         db        0cfh,18h                 ; ALT O
         db        0d0h,19h                 ; ALT P
         db        0d1h,10h                 ; ALT Q
         db        0d2h,13h                 ; ALT R
         db        0d3h,1fh                 ; ALT S
         db        0d4h,14h                 ; ALT T
         db        0d5h,16h                 ; ALT U
         db        0d6h,2fh                 ; ALT V
         db        0d7h,11h                 ; ALT W
         db        0d8h,2dh                 ; ALT X
         db        0d9h,15h                 ; ALT Y
         db        0dah,2ch                 ; ALT Z
         db        0f0h,81h                 ; ALT 0
         db        0f1h,78h                 ; ALT 1
         db        0f2h,79h                 ; ALT 2
         db        0f3h,7ah                 ; ALT 3
         db        0f4h,7bh                 ; ALT 4
         db        0f5h,7ch                 ; ALT 5
         db        0f6h,7dh                 ; ALT 6
         db        0f7h,7eh                 ; ALT 7
         db        0f8h,7fh                 ; ALT 8
         db        0f9h,80h                 ; ALT 9
         db        1bh,3bh                  ; F1
         db        1ch,3ch                  ; F2
         db        1dh,3dh                  ; F3
         db        1eh,3eh                  ; F4
         db        1fh,3fh                  ; F5
         db        20h,40h                  ; F6
         db        21h,41h                  ; F7
         db        22h,42h                  ; F8
         db        23h,43h                  ; F9
         db        24h,44h                  ; F10
         db        5bh,54h                  ; SHIFT F1
         db        5ch,55h                  ; SHIFT F2
         db        5dh,56h                  ; SHIFT F3
         db        5eh,57h                  ; SHIFT F4
         db        5fh,58h                  ; SHIFT F5
         db        60h,59h                  ; SHIFT F6
         db        61h,5ah                  ; SHIFT F7
         db        62h,5bh                  ; SHIFT F8
         db        63h,5ch                  ; SHIFT F9
         db        64h,5dh                  ; SHIFT F10
         db        9bh,5eh                  ; CTRL F1
         db        9ch,5fh                  ; CTRL F2
         db        9dh,60h                  ; CTRL F3
         db        9eh,61h                  ; CTRL F4
         db        9fh,62h                  ; CTRL F5
         db        0a0h,63h                 ; CTRL F6
         db        0a1h,64h                 ; CTRL F7
         db        0a2h,65h                 ; CTRL F8
         db        0a3h,66h                 ; CTRL F9
         db        0a4h,67h                 ; CTRL F10
         db        0dbh,68h                 ; ALT F1
         db        0dch,69h                 ; ALT F2
         db        0ddh,6ah                 ; ALT F3
         db        0deh,6bh                 ; ALT F4
         db        0dfh,6ch                 ; ALT F5
         db        0e0h,6dh                 ; ALT F6
         db        0e1h,6eh                 ; ALT F7
         db        0e2h,6fh                 ; ALT F8
         db        0e3h,70h                 ; ALT F9
         db        0e4h,71h                 ; ALT F10
         db        0a5h,73h                 ; CTRL <-
         db        0a6h,74h                 ; CTRL ->
         db        0a7h,77h                 ; CTRL HOME
         db        0a8h,75h                 ; CTRL END
         db        0a9h,84h                 ; CTRL PGUP
         db        0aah,76h                 ; CTRL PGDN
         db        6bh,0fh                  ; SHIFT TAB
         db        0,0                      


tabshf   db        6                        ; d‚lka jedn‚ polo‘ky
         db        0,'     '                ; 00h
         db        5,'SHIFT'                ; 40h
         db        4,'CTRL '                ; 80h
         db        3,'ALT  '                ; C0h
         db        0ffh                     

tabhome  db        5                        ; d‚lka jedn‚ polo‘ky
         db        2,'<-  '                 ; 25h
         db        2,'->  '                 ; 26h
         db        4,'HOME'                 ; 27h
         db        3,'END '                 ; 28h
         db        4,'PGUP'                 ; 29h
         db        4,'PGDN'                 ; 2Ah
         db        3,'TAB '                 ; 2Bh
         db        0ffh                     

tabfn    db        4                        ; d‚lka jedn‚ polo‘ky
         db        2,'F1 '                  ; 1Bh
         db        2,'F2 '                  ; 1Ch
         db        2,'F3 '                  ; 1Dh
         db        2,'F4 '                  ; 1Eh
         db        2,'F5 '                  ; 1Fh
         db        2,'F6 '                  ; 20h
         db        2,'F7 '                  ; 21h
         db        2,'F8 '                  ; 22h
         db        2,'F9 '                  ; 23h
         db        3,'F10'                  ; 24h
         db        0ffh                     

                                            ; p©evod textu na velk  p¡smena

                                            ; vstup:  ES:SI = ukazatel na text
                                            ;         BL = nejmen¨¡ znak
                                            ; v˜stup: ES:[BX+SI] = za‡ tek textu
                                            ;         CX = po‡et znak– textu
                                            ;         CF=0,ZF=0 je konec textu
                                            ;         CF=0,ZF=1 nen¡ je¨tˆ konec

upper:   xor       cx,cx                    ; vynulov n¡ ‡¡ta‡e znak–
upper1:  cmp       bl,es:[si]               ; porovn n¡ znaku s BL
         ja        upper6                   ; znak je men¨¡ ne‘ BL - konec
         cmp       byte ptr es:[bx+si]," "  ; je mezera ?
         jnz       upper2                   ; nen¡ - konec hled n¡
         inc       bx                       ; dal¨¡ pozice
         jmp       short upper1             ; dal¨¡ znak
upper2:  push      bx                       
upper3:  cmp       bl,es:[si]               ; kontrola koncov‚ho znaku
         ja        upper5                   ; znak je men¨¡ ne‘ BL - konec
         mov       al,es:[bx+si]            ; p©e‡ten¡ znaku k p©ek¢dov n¡
         cmp       al," "                   ; je mezera ?
         jz        upper5                   ; je mezera - konec
         cmp       al,"|"                   ; "|"
         jz        upper5                   ; je znak "|" - konec
         cmp       al,"a"                   ; "a"
         jc        upper4                   ; je znak men¨¡ ne‘ "a"
         and       al,0dfh                  ; p©evod na velk‚ p¡smeno
         mov       es:[bx+si],al            ; ulo‘en¡ p©eveden‚ho znaku
upper4:  inc       bx                       ; dal¨¡ znak
         inc       cx                       ; zv˜¨en¡ ‡¡ta‡e nalezen˜ch znak–
         jmp       upper3                   ; p©evod dal¨¡ho znaku na velk˜
upper5:  pop       bx                       
upper6:  ret                                

                                            ; nalezen¡ textov‚ polo‘ky v tabulce

                                            ; vstup:  ES:[BX+SI] ukazatel textu
                                            ;         DS:DI = ukazatel tabulky
                                            ;         CX = d‚lka hled. textu
                                            ; v˜stup: ES:[BX+SI] nov  pozice
                                            ;         DL = po©ad. ‡¡slo polo‘ky
                                            ;         AX = d‚lka polo‘ky tabulky

srctab:  mov       al,[di]                  ; p©e‡ten¡ d‚lky jedn‚ polo‘ky
         mov       ah,0                     
         inc       di                       ; ukazatel tabulky - prvn¡ polo‘ka
         xor       dl,dl                    ; ‡¡ta‡ polo‘ek
srctab1: inc       dx                       ; zv˜¨en¡ ‡¡ta‡e polo‘ek
         cmp       cl,[di]                  ; porovn n¡ d‚lky textu
         jnz       srctab4                  ; nesouhlas¡ - dal¨¡ polo‘ka
         push      ax                       ; d‚lka jedn‚ polo‘ky
         push      bx                       ; ukazatel textu
         push      cx                       ; d‚lka textu
         push      di                       ; ukazatel tabulky
         inc       di                       ; prvn¡ znak polo‘ky
srctab2: mov       al,es:[bx+si]            ; zkouman˜ znak
         cmp       al,[di]                  ; porovn n¡ se znakem z tabulky
         jnz       srctab3                  ; nesouhlas¡ - dal¨¡ polo‘ka
         inc       bx                       ; zv˜¨en¡ ukazatele textu
         inc       di                       ; zv˜¨en¡ ukazatele tabulky
         loop      srctab2                  ; dal¨¡ znak k testu
         pop       di                       ; ukazatel tabulky - nalezeno
         pop       cx                       ; d‚lka textu
         pop       ax                       ; zru¨en¡ p–vodn¡ pozice textu BX
         pop       ax                       ; d‚lka jedn‚ polo‘ky
         or        dl,dl                    ; po©adov‚ ‡¡slo polo‘ky
         ret                                
srctab3: pop       di                       ; ukazatel tabulky
         pop       cx                       ; d‚lka textu
         pop       bx                       ; ukazatel zkouman‚ho textu
         pop       ax                       ; d‚lka jedn‚ polo‘ky
srctab4: add       di,ax                    ; adresa dal¨¡ polo‘ky
         cmp       byte ptr [di],0ffh       ; je koncov˜ znak tabulky ?
         jnz       srctab1                  ; nen¡ je¨tˆ konec
         xor       dl,dl                    ; nenalezen - ‡¡slo polo‘ky = 0
         ret                                

; *****************************************************************************
;
;                     Funkce 10: dek¢dov n¡ kl vesy
;
; *****************************************************************************
;þ
                                            ; FUNKCE 10 - dek¢dov n¡ kl vesy

                                            ; vstup:  ES:SI = ukazatel textu
                                            ; v˜stup: ES:SI = nov  pozice
                                            ;         AX = k¢d kl vesy

fnc10:   mov       bx,1                     ; koncov˜ znak = 00
         call      upper                    ; p©evod textu na velk  p¡smena
         jcxz      fnc106                   ; je ji‘ konec textu
         mov       di,offset tabshf         ; tabulka kl ves SHIFT,CTRL,ALT
         call      srctab                   ; nalezen¡ textov‚ polo‘ky v tabulce
         mov       dh,dl                    ; po©adov‚ ‡¡slo polo‘ky - £schova
         jz        fnc103                   ; text nebyl nalezen
         dec       dh                       ; po©ad. ‡¡slo polo‘ky - 1
         call      upper                    ; p©evod textu na velk  p¡smena
         jcxz      fnc106                   ; je ji‘ konec textu
         cmp       cx,1                     ; je pouze jeden znak ?
         jnz       fnc102                   ; nen¡ pr vˆ jeden znak
         mov       al,es:[bx+si]            ; znak k p©ek¢dov n¡
         cmp       al,"@"                   ; = "@"
         jc        fnc101                   ; men¨¡ ne‘ "@"
         cmp       al,"Z"                   ; = "Z"
         ja        fnc106                   ; vˆt¨¡ ne‘ "Z"
         sub       al,40h                   ; p©evod na ©¡d¡c¡ znak
fnc101:  mov       dl,al                    ; p©ek¢dovan˜ znak
         jmp       short fnc104             
fnc102:  mov       di,offset tabhome        ; tabulka ©¡d¡c¡ch kl ves HOME,...
         call      srctab                   ; nalezen¡ textov‚ polo‘ky v tabulce
         jz        fnc103                   ; polo‘ka nenalezena
         add       dl,24h                   ; korekce
         jmp       short fnc104
fnc103:  mov       di,offset tabfn          ; tabulka funk‡n¡ch kl ves Fn
         call      srctab                   ; nalezen¡ textov‚ polo‘ky v tabulce
         jz        fnc106                   ; nenalezena
         add       dl,1ah                   ; korekce
fnc104:  ror       dh,1                     
         ror       dh,1                     
         or        dl,dh                    ; kl vesy SHIFT,.. jako bity 6 a 7
         mov       di,offset tabkey         ; tabulka pro dek¢dov n¡ k¢d– kl ves
fnc105:  mov       al,[di]                  ; k¢d kl vesy
         inc       di                       
         inc       di                       
         or        al,al                    ; je posledn¡ k¢d ?
         jz        fnc106                   ; k¢d kl vesy nenalezen
         cmp       al,dl                    ; porovn n¡ s hledan˜m k¢dem
         jnz       fnc105                   ; dal¨¡ pokus
         dec       di                       
         mov       al,[di]                  ; p©ek¢dov n¡ k¢du kl vesy
         mov       ah,0                     
         ret                                
fnc106:  xor       ax,ax                    ; k¢d kl vesy nenalezen
         ret                                

                                            ; v˜pis k¢du kl vesy

                                            ; vstup:  CS:SI tabulka kl ves
                                            ;         DL = po©ad. ‡¡slo kl vesy
                                            ;         DS:[BX+DI] = v˜st. buffer
                                            ;         CH = 0
                                            ; v˜stup: DS:[BX+DI] = nov  pozice

outkey:  push      ax                       
         mov       al,cs:[si]               ; d‚lka jedn‚ polo‘ky v tabulce
         mul       dl                       ; rel. adresa polo‘ky v tabulce
         add       si,ax                    ; adresa polo‘ky v tabulce
         inc       si                       
         mov       cl,cs:[si]               ; d‚lka textu v polo‘ce
         jcxz      outkey2                  
outkey1: inc       si                       ; znak polo‘ky textu kl vesy
         inc       bx                       ; ukazatel v˜stupn¡ho textu
         mov       al,cs:[si]               ; p©e‡ten¡ znaku polo‘ky
         mov       [bx+di],al               ; ulo‘en¡ do bufferu
         loop      outkey1                  ; dal¨¡ znak
         inc       bx                       ; n sleduj¡c¡ pozice
         mov       byte ptr [bx+di]," "     ; oddˆlovac¡ mezera
outkey2: pop       ax                       
         ret                                

adrtab   dw        offset tabkey            ; ukazatel dek¢dovac¡ tabulky kl ves

; *****************************************************************************
;
;                   Funkce 11: v˜pis k¢du kl vesy
;
; *****************************************************************************
;þ
                                            ; FUNKCE 11 - v˜pis k¢du kl vesy

                                            ; vstup:  DS:[BX+DI] = v˜st. buffer
                                            ;         AX = k¢d kl vesy
                                            ; v˜stup: DS:[BX+DI] = nov  adresa

fnc11:   mov       dx,ax                    ; k¢d kl vesy
         xor       cx,cx                    ; ‡¡ta‡ znak–
         mov       bx,[adrtab]              ; ukazatel dek¢dovac¡ tabulky kl ves
         inc       bx                       ; dal¨¡ kl vesa
fnc111:  mov       al,[bx]                  ; k¢d kl vesy
         or        al,al                    ; je ukon‡ovac¡ znak 00 ?
         jnz       fnc112                   ; nen¡ je¨tˆ konec
         mov       [adrtab],offset tabkey   ; nastav. za‡. dek¢d. tabulky kl ves
         mov       es,dx
         mov       es:[di],ch               ;
         ret       
fnc112:  push      si                       ; ukazatel tabulky kl ves
fnc113:  mov       ah,es:[si]               ; k¢d kl vesy
         or        ah,ah
         jz        fnc114                   ; je ji‘ posledn¡
         cmp       al,ah                    ; porovn n¡ s hledanou kl vesou
         jz        fnc115                   ; je nalezen k¢d kl vesy
         inc       si                       ; dal¨¡ kl vesa
         mov       cl,es:[si]               ;
         inc       cx
         add       si,cx                    ; adresa n sleduj¡c¡ho textu
         jmp       short fnc113             ; nov˜ pokus
fnc114:  pop       si
         inc       bx
         inc       bx
         jmp       short fnc111
fnc115:  inc       bx
         mov       [adrtab],bx              ; nov  adresa ukazatele tabulky
         mov       al,[bx-2]                ; k¢d kl vesy
         inc       si
         pop       bx
         push      si
         push      dx
         pop       ds
         xor       bx,bx
         mov       dl,al
         and       dl,0c0h
         rol       dl,1
         rol       dl,1
         mov       si,offset tabshf         ; tabulka kl ves SHIFT,CTRL,ALT
         call      outkey                   ; v˜pis k¢du kl vesy
         mov       dl,al
         and       dl,3fh
         cmp       dl,1ah
         ja        fnc116
         add       dl,40h
         inc       bx
         mov       [bx+di],dl
         jmp       short fnc119
fnc116:  cmp       dl,24h
         ja        fnc117
         sub       dl,1bh
         mov       si,offset tabfn          ; tabulka funk‡n¡ch kl ves
         call      outkey                   ; v˜pis k¢du kl vesy
         jmp       short fnc119
fnc117:  cmp       dl,2bh                   ; znak "+"
         ja        fnc118
         sub       dl,25h
         mov       si,offset tabhome        ; tabulka ©¡d¡c¡ch kl ves
         call      outkey                   ; v˜pis k¢du kl vesy
         jmp       short fnc119
fnc118:  inc       bx
         mov       [bx+di],dl
fnc119:  cmp       bx,9
         ja        fnc11a
         inc       bx
         mov       byte ptr [bx+di]," "     ; oddˆlovac¡ znak mezery
         jmp       short fnc119
fnc11a:  inc       bx
         mov       byte ptr [bx+di],"|"     ; znak "|"
         pop       si
         mov       cl,es:[si]
fnc11b:  inc       si
         mov       al,es:[si]               ; p©e‡ten¡ znaku k vyti¨tˆn¡
         inc       bx
         cmp       al," "
         jnc       fnc11c                   ; nen¡ ©¡d¡c¡ znak
         mov       byte ptr [bx+di],"^"     ; ulo‘en¡ znaku "^" (znak s CTRL-)
         inc       bx                       ; zv˜¨en¡ ukazatele
         or        al,40h                   ; p©evod znaku na tisknuteln˜ znak
fnc11c:  mov       [bx+di],al               ; ulo‘en¡ znaku
         loop      fnc11b                   ; p©evod dal¨¡ho znaku
         inc       bx
         mov       byte ptr [bx+di],"|"     ; znak "|"
         mov       [di],bl
         ret       

; *****************************************************************************
;
;                   Funkce 07: poskytnut¡ adresy definice okna
;
; *****************************************************************************
;þ
; -----------------------------------------------------------------------------
;        Poskytnut¡ adresy definice okna menu
; -----------------------------------------------------------------------------
; VSTUP: SS:[BP+4] (2) ‡¡slo po‘adovan‚ho okna
; VSTUP: ES:SI=adresa definice okna
; -----------------------------------------------------------------------------

Fnc07    PROC      NEAR

         mov       bx,[bp+4]
         shl       bx,1
         mov       si,[bx+TabHlp]
         push      ds
         pop       es
         ret

Fnc07    ENDP

INCLUDE  SCREENWN.ASM                       ; definice oken

; *****************************************************************************
;
;                   Funkce 08: poskytnut¡ adresy textu hl ¨en¡
;
; *****************************************************************************
;þ
; -----------------------------------------------------------------------------
;        Poskytnut¡ adresy tabulky chybov˜ch hl ¨en¡
; -----------------------------------------------------------------------------
; VSTUP: ES:SI=adresa tabulky chybov˜ch hl ¨en¡
; -----------------------------------------------------------------------------

Fnc08    PROC      NEAR

         mov       si,offset TabErr         ; tabulka hl ¨en¡
         push      ds                       ; nastaven¡ ES = DS
         pop       es
         ret

Fnc08    ENDP

TabErr   LABEL     BYTE
         db        1,'Soubor nebyl nalezen',0
         db        2,'€ten¡ souboru nen¡ mo‘n‚',0
         db        3,'Z pis do souboru nen¡ mo‘n˜',0
         db        4,'Po‘adovan˜ adres © nenalezen',0
         db        5,'Nedostatek m¡sta v pamˆti',0
         db        6,'P©ekro‡en maxim ln¡ po‡et © dk–',0
         db        7,'" nebyl nalezen',0
         db        8,'ž dek je moc dlouh˜',0
         db        9,'Nepovolen‚ zad n¡',0
         db        10,'Nen¡ ozna‡en ‘ dn˜ blok',0
         db        11,'PamˆŸ makrokl ves je pln ',0
         db        12,'Tisk rna nen¡ v provozu',0
         db        13,'Chybn˜ form t dat',0
         db        15,'Soubor s t¡mto jm‚nem ji‘ existuje',0
         db        16,'Zdrojov˜ a c¡lov˜ soubor jsou identick‚',0
         db        17,'Prostor @ nen¡ dosud vytistˆn.',0
         db        18,'Sloupcov˜ blok nem–‘e b˜t zaps n do souboru',0
         db        19,'Nen¡ ozna‡en ‘ dn˜ sloupcov˜ blok',0
         db        20,'Kurzor se mus¡ nach zet vpravo od prav‚ho okraje',0
         db        21,'Mus¡ b˜t zad n soubor EXE nebo COM',0
         db        50,'Nepovolen˜ matematick˜ v˜raz',0
         db        51,'P©ete‡en¡ desetinn‚ te‡ky',0
         db        52,'Dˆlen¡ nulou',0
         db        53,'Chybn˜ argument',0
         db        54,'Chyba argumentu',0
         db        99,'Intern¡ chyba',0
         db        100,'Disketov  mechanika nepracuje',0
         db        153,'Neo‡ek van˜ konec souboru',0
         db        196,'P©ete‡en¡ ‡¡sla Integer',0
         db        240,'Disk je pln˜',0
         db        241,'Nepovolen‚ jm‚no souboru',0
         db        0,'Bl¡‘e neur‡iteln  chyba',0

; *****************************************************************************
;
;                                Fonty
;
; *****************************************************************************
;þ
; Pro grafick˜ m¢d CGA a HGC mus¡ b˜t sud˜ po‡et linek na znak !

INCLUDE  SCREEN06.ASM                       ; fonty 8x6
INCLUDE  SCREEN08.ASM                       ; fonty 8x8
INCLUDE  SCREEN14.ASM                       ; fonty 8x14
INCLUDE  SCREEN19.ASM                       ; fonty 8x19

Code     ENDS
         END       Screen
