
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
; P©enosov  rychlost 115200 Baud
; 8 datov˜ch bit–, bez parity, 1 START bit, 1 STOP bit
; 86.80555556 us na bajt
; 230 bajt– celkem na sektor (19965 us)
; -----------------------------------------------------------------------------
; 10*19: u‘ite‡n  190, celkem 220   (46080 B)
; 10*18: u‘ite‡n  180, celkem 209   (43520 B)
;  9*21: u‘ite‡n  189, celkem 220
;  9*20: u‘ite‡n  180, celkem 210   (43520 B)
;  8*23: u‘ite‡n  184, celkem 216
;  8*22: u‘ite‡n  176, celkem 207   (42496 B)
;  7*26: u‘ite‡n  182, celkem 216
;  7*25: u‘ite‡n  175, celkem 208   (42240 B)
;  6*30: u‘ite‡n  180, celkem 217
;  5*35: u‘ite‡n  175, celkem 216
;  5*34: u‘ite‡n  170, celkem 210 ! (40960 B)
;  4*43: u‘ite‡n  172, celkem 220
;  4*42: u‘ite‡n  168, celkem 215
;  3*54: u‘ite‡n  162, celkem 220
;  3*53: u‘ite‡n  159, celkem 216
;  3*52: u‘ite‡n  156, celkem 212
;
; Struktura sektoru:
;    Sektor je organizov n do matice 11 © dk– po 20 bajtech. Posledn¡ bajt
;    v ka‘d‚m © dku je kontroln¡ a je to XOR p©edch zej¡c¡ch 19 bajt–.
;    Posledn¡ 11. © dek je kontroln¡ a je to XOR p©edch zej¡c¡ch 10 © dk–.
;    Platn  data jsou 10*19=190 bajt–, z toho prvn¡ch 10 bajt– je z hlav¡,
;    skute‡n˜ch dat je 180 bajt–.
;
;           0: (4) absolutn¡ ‡as za‡ tku nahr vky (po‡et sekund od 1.1.1980)
;           4: (2) celkov  d‚lka nahr vky v sekund ch/2 (max. 36 hodin)
;                        (0FFFFh = 36 hodin 24 minut 30 sekund a v¡ce)
;           6: (2) aktu ln¡ ‡¡slo bloku 0 a‘ 16383
;                  b14: 0=datov˜ sektor (0 a‘ 255), 1=korek‡n¡ sektor (0 a‘ 32)
;                  b15: 0=datov˜ blok, 1=adres ©ov˜ blok
;           8: (1) aktu ln¡ ‡¡slo sektoru v bloku 0 a‘ 255
;                      korek‡n¡ sektor  33 -> z vˆre‡n˜ synchroniza‡n¡ sektor
;                      korek‡n¡ sektor 255 -> po‡ te‡n¡ synchroniza‡n¡ sektor
;           9: (1) informace o dˆlen¡ blok– do skupin
;                  b0  a‘ b3 : po‡et datov˜ch blok– ve skupinˆ 1 a‘ 16
;                  b4  a‘ b7 : oddˆlovac¡ mezera mezi skupinami (2 a‘ 32 sekund)
;
;   - Po‡ te‡n¡ synchroniza‡n¡ sektor obsahuje na pozici 10: offset popisova‡e
;        prvn¡ho souboru v bloku (0ffffh = neza‡¡n  ‘ dn˜ popisova‡ souboru).
;   - Pro synchroniza‡n¡ sektory jsou n sleduj¡c¡ bajty nastaveny na 0FFh.
;     (tj. nekontroluje se kontroln¡ sou‡et, sektor se ovˆ©uje v¡cen sobnˆ)
;          10: (9) zbytek prvn¡ho © dku - data
;          19: (1) kontroln¡ sou‡et XOR prvn¡ho © dku
;
;          20: (180) ostatn¡ data (9 © dk– x (19 bajt– + 1 kontroln¡))
;
;         200: (20) kontroln¡ opravn˜ © dek XOR pro data i z hlav¡
;
;         220: (2) kontroln¡ sou‡et cel‚ho sektoru CRC (i se z hlav¡m)
;
;         222: (1) £vodn¡ synchronizace 0ffh
;         223: (2) sn¡mkov  synchronizace 0ffh
;         225: (3) z vˆre‡n  synchronizace 0ffh
;         228: (1) START bajt 02h
;         229: (1) START bajt 0e5h
; -----------------------------------------------------------------------------
; Struktura bloku:
;          256 sektor– data (v matici 16*16)
;           16 kontroln¡ sektory © dk– XOR
;           16 kontroln¡ sektory sloupc– XOR
;            1 kontroln¡ sektor celkov˜ XOR
;   celkem 289 sektor– na blok
; -----------------------------------------------------------------------------
; €asov n¡:
;    1 sektor = 230 bajt– * 86.80555556 us = 19965.277779 us,  u‘ite‡n˜ch 180 B
;         efektivn¡ rychlost 9015.65217 B/sekundu, vy‘aduje pamˆŸ 224 Bajt–
;    1 blok = 289 sektor– * 19.965277779 ms = 5.76996527 s, u‘ite‡n˜ch 256 sekt.
;         rychlost 7986.18325 B/sekundu, vy‘aduje pamˆŸ 64736 Bajt–
;    1 blok p©enese 46080 B, vy‘aduje 64736 B
;      najednou se p©enesou 4 bloky, vy‘aduj¡ 262144 Bajt– pamˆti (184320 B)
;    ‡as pro 4 bloky: 23.07986108 sek., mezera asi 7 sekund, celkem 30 sekund
;    efektivn¡ rychlost = 6144 B/sek. = 368.640 KB/min = 22.118 MB/hod.
; ----------------
;; 160 bajt– na sektor: rychlost 8000 B/sek., 1 blok = 40960 B
;; 1 blok = 8*32 = 256 dat. sektor– + 41 kontroln¡ = 297 sektor– (5.94 s)
;;    4 bloky = 23.76 s + 7 s mezera = 31 sek. = 5285 B/s = 317 KB/min. = 19 MB/h
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

MAXF     EQU       86                       ; maxim ln¡ d‚lka jm‚na souboru
MAXFILES EQU       2978                     ; maxim ln¡ po‡et soubor– v adres ©i
SOUBDIS  EQU       12                       ; po‡et zobrazen˜ch soubor– v oknˆ

; ------ definice polo‘ky seznamu (vypou¨t¡ se polo‘ky "." a "..")
; Polo‘ky jednoho adres ©e jsou na‡teny pohromadˆ. Pro ka‘d˜ podadres ©
; v adres ©i n sleduje za adres ©em dal¨¡ podadres © (s hlub¨¡ strukturou).
; Ka‘d˜ podadres © je ukon‡en bajtem 0 (offset dal¨¡ polo‘ky = 0).

FileOffs EQU       0                        ; (1) offset dal¨¡ polo‘ky
                                            ;      0=konec adres ©e
FileAtr  EQU       1                        ; (1) atributy polo‘ky
                                            ;      b0: 1=R/O
                                            ;      b1: 1=HID
                                            ;      b2: 1=SYS
                                            ;      b3: 1=soubor p©ijat s chybou
                                            ;      b4: 1=DIR
                                            ;      b5: 1=ARC
                                            ;      b6: 1=£schova ozna‡en¡
                                            ;      b7: 1=polo‘ka ozna‡ena
FileTime EQU       2                        ; (2) ‡as polo‘ky
                                            ;      b0 a‘ b4: sekunda/2 (0 a‘ 62)
                                            ;      b5 a‘ b10: minuta (0 a‘ 63)
                                            ;      b11 a‘ b15: hodina (0 a‘ 31)
FileDate EQU       4                        ; (2) datum polo‘ky
                                            ;      b0 a‘ b4: den (0 a‘ 31)
                                            ;      b5 a‘ b8: mˆs¡c (0 a‘ 15)
                                            ;      b9 a‘ b15: rok-1980 (a‘ 2107)
FileSize EQU       6                        ; (4) velikost polo‘ky (bajt–)
FileName EQU       10                       ; (x) jm‚no polo‘ky ASCII
                                            ;      (v popisova‡i souboru
                                            ;       pln  cesta; "\"=ROOT)

; ------ definice seznamu soubor–
;þ
SeznPar  EQU       0                        ; (1) parametry seznamu
                                            ;      b0: 0=disk, 1=streamer
                                            ;      b1: 0=lev‚, 1=prav‚ okno
                                            ;      b2: 1=okno je aktivn¡
SeznPar2 EQU       1                        ; (1) parametry seznamu 2
                                            ;      b0: 1=t©¡dˆn¡ podle jm‚na
                                            ;      b1: 1=t©¡dˆn¡ podle p©¡pony
                                            ;      b2: 1=t©¡dˆn¡ podle velikosti
                                            ;      b3: 1=t©¡dˆn¡ podle data+‡asu
                                            ;      b4: 1=adres © se net©¡d¡
                                            ;      b5: 1=sestupn‚ t©¡dˆn¡ adres.
SeznSeg  EQU       2                        ; (2) segment seznamu soubor–
SeznEnd  EQU       4                        ; (2) segment konce seznamu soubor–
SeznNum  EQU       6                        ; (2) celkov˜ po‡et polo‘ek v sezn.

SeznSelN EQU       8                        ; (2) po‡et ozna‡en˜ch soubor–
SeznSelS EQU       10                       ; (4) sou‡et ozna‡en˜ch soubor–
SeznSelR EQU       14                       ; (4) - po‘adovan  kapacita na disku
                                            ;..(2) - d‚lka z znamu v sekund ch/2

SeznDSeg EQU       18                       ; (2) segment za‡ tku adres ©e
SeznDNum EQU       20                       ; (2) po‡et polo‘ek adres ©e
SeznDAkt EQU       22                       ; (2) aktivn¡ zobraz. polo‘ka adres.
SeznDTop EQU       24                       ; (2) po‡ te‡n¡ zobr. polo‘ka adres.

SeznFFil EQU       26                       ; (2) po‡et soubor– v adres ©i
SeznFSum EQU       28                       ; (4) zabran  kapacita soubory

SeznLabN EQU       32                       ; (2) d‚lka n vˆ¨t¡
SeznDirN EQU       34                       ; (2) d‚lka adres ©e
SeznLab  EQU       36                       ; (12) n vˆ¨t¡
SeznDir  EQU       36+12                    ; (MAXF) aktivn¡ adres © seznamu
SeznInd  EQU       SEZNDIR+MAXF             ; (2*MAXFILES) indexy soubor– adres.
SeznSum  EQU       SEZNIND+2*MAXFILES       ; celkov  d‚lka popisova‡e seznamu

AktDir   EQU       offset DatDefL + SeznDir ; adres © disku
AktDirN  EQU       offset DatDefL + SeznDirN ; d‚lka adres ©e
AktLab   EQU       offset DatDefL + SeznLab ; jm‚no disku
AktLabN  EQU       offset DatDefL + SeznLabN ; d‚lka jm‚na disku

; ------ atributy soubor–

RO       EQU       b0                       ; R/O
HID      EQU       b1                       ; HID
SYS      EQU       b2                       ; SYS
VOL      EQU       b3                       ; VOL
DIR      EQU       b4                       ; DIR
ARC      EQU       b5                       ; ARC

; ------ konstanty

b0       EQU       1
b1       EQU       2
b2       EQU       4
b3       EQU       8
b4       EQU       10h
b5       EQU       20h
b6       EQU       40h
b7       EQU       80h
b8       EQU       100h
b9       EQU       200h
b10      EQU       400h
b11      EQU       800h
b12      EQU       1000h
b13      EQU       2000h
b14      EQU       4000h
b15      EQU       8000h

HI       EQU       256

; ------ parametry

UVSYNCH  EQU       60                       ; £vodn¡ synchroniza‡n¡ sektory
ZAVSYNCH EQU       30                       ; z vˆre‡n‚ synchroniza‡n¡ sektory
MINSYNCH EQU       12                       ; minim ln¡ po‡et synchroniz.sektor–


Code     SEGMENT
         ASSUME    cs:Code,ds:Code
         ORG       100h

; ------ p©edefinov n¡ ukazatele z sobn¡ku

Start:   cmp       sp,offset Zasob          ; je z sobn¡k OK ?
         jb        Start1                   ; nedostatek pamˆti
         mov       sp,offset Zasob          ; p©edefinov n¡ z sobn¡ku

; ------ zmen¨en¡ bloku programu na minimum

         mov       bx,(offset(Zasob-Start)+10fh)/16 ; velikost programu
         mov       ah,4ah
         int       21h                      ; zmen¨en¡ bloku programu
         jnc       Start2

; ------ chyba - nedostatek pamˆti

Start1:  mov       dx,offset MemTxt
         mov       ah,9
         int       21h
         int       20h

; ------ vytvo©en¡ bufferu soubor–

Start2:  mov       bx,(289*180+15)/16       ; velikost bufferu soubor–
         mov       ah,48h
         int       21h                      ; vytvo©en¡ bufferu soubor–
         jc        Start1                   ; chyba - nedostatek pamˆti
         mov       ds:[BuffSeg],ax          ; adresa bufferu soubor–

; ------ vytvo©en¡ bufferu sektor–

         mov       bx,-1
         mov       ah,48h
         int       21h
         jnc       Start1
         mov       ah,48h
         int       21h                      ; vytvo©en¡ maxim ln¡ho bufferu
         jc        Start1                   ; nedostatek pamˆti
         cmp       bx,(289+30)*(224/16)     ; minim ln¡ velikost bufferu
         jb        Start1                   ; nedostatek pamˆti

; ------ v˜po‡et parametr– bufferu sektor–

         mov       ds:[SektBSeg],ax         ; adresa za‡ tku bufferu sektor–
         mov       ds:[DatSegm],ax          ; za‡ tek datov‚ho segmentu
         mov       ds:[DatFre],ax           ; za‡ tek voln‚ho m¡sta v segmentu
         add       ax,bx                    ; adresa konce bufferu sektor–
         mov       ds:[SektESeg],ax         ; adresa konce bufferu sektor–
         mov       ds:[DatMax],ax           ; maxim ln¡ konec bufferu
         xchg      ax,bx                    ; AX <- velikost bufferu sektor–
         xor       dx,dx                    ; DX <- 0
         mov       bx,224/16                ; velikost jednoho sektoru
         div       bx                       ; v˜po‡et po‡tu sektor–
         mov       ds:[SektMax],ax          ; maxim ln¡ po‡et sektor–
         sub       ds:[SektESeg],dx         ; oprava konce bufferu sektor–
         sub       ax,20                    ; p r sektor– rezerva
         xor       dx,dx                    ; DX <- 0
         mov       bx,289+3                 ; po‡et sektor– na blok (+ synch.)
         div       bx                       ; v˜po‡et po‡tu blok–
         mov       ds:[MaxDBlok],ax         ; maxim ln¡ po‡et datov˜ch blok–

; ------ instalace INT 24h

         mov       dx,offset Int24
         mov       ax,2524h
         int       21h

; ------ instalace INT 23h

         mov       dx,offset Int23
         mov       ax,2523h
         int       21h

; ------ £schova p©¡znaku BREAK

         mov       ax,3300h
         int       21h                      ; poskytnut¡ BREAK
         mov       ds:[OldBreak],dl         ; £schova p©¡znaku BREAK

; ------ vypnut¡ p©¡znaku BREAK

         mov       ax,3301h
         mov       dl,0
         int       21h                      ; vypnut¡ BREAK

; ------ inicializace videom¢du

         mov       ah,0fh
         call      Int10P
         cmp       al,7
         je        Start3
         cmp       al,2
         je        Start3
         mov       al,3
Start3:  mov       ah,0
         call      Int10P                   ; nastaven¡ videom¢du
         call      AktPDisp                 ; nastaven¡ parametr– displeje

; ------ nulov n¡ seznam–

         mov       bx,offset DatDefL        ; seznam lev‚ho okna
         mov       byte ptr ds:[bx+SeznPar],b2 ; lev‚ okno, disk, aktivn¡
         call      InitSezn                 ; inicializace seznamu lev‚ho okna
         mov       al,ds:[DatParmL]
         mov       ds:[bx+SeznPar2],al      ; t©¡dˆn¡ lev‚ho okna

         mov       bx,offset DatDefR        ; seznam prav‚ho okna
         mov       byte ptr ds:[bx+SeznPar],b0+b1 ; prav‚, streamer, neaktivn¡
         call      InitSezn                 ; inicializace seznamu prav‚ho okna
         mov       al,ds:[DatParmR]
         mov       ds:[bx+SeznPar2],al      ; t©¡dˆn¡ prav‚ho okna

         mov       word ptr ds:[AdrHlp],offset TextHlp0 ; nen¡ n povˆda
         call      DispAll                  ; zobrazen¡ obrazovky
         call      KurzOff                  ; vypnut¡ kurzoru

; ------ inicializace parametr– disku

         call      AktDPar                  ; inicializace parametr– akt. disku
         call      DispAll                  ; zobrazen¡ obrazovky

; ------ na‡ten¡ obsahu aktivn¡ho disku
;þ
         mov       byte ptr cs:[ParBreak],0 ; nulov n¡ p©¡znaku p©eru¨en¡
         mov       bx,offset DatDefL        ; definice seznamu disku
         call      ReadDisk                 ; na‡ten¡ obsahu disku seznamu BX

; ------ obsluha kl vesnice

Start32: mov       word ptr ds:[AdrHlp],offset TextHlp0 ; nen¡ n povˆda
         call      DispAll                  ; zobrazen¡ obrazovky

         mov       bx,offset DatDefL        ; lev‚ okno
         test      byte ptr ds:[DatParm],b0
         jz        Start4                   ; je lev‚ okno
         mov       bx,offset DatDefR        ; prav‚ okno

Start4:  call      InpKey                   ; vstup znaku z kl vesnice

         cmp       dl," "
         jb        Start422
         mov       dh,0
Start422:

         mov       ax,ds:[bx+SeznDAkt]      ; aktivn¡ polo‘ka
         call      JumpDX

         dw        4800h,WUp                ; kurzor nahoru
         dw        5000h,WDown              ; kurzor dol–
         dw        4700h,WHome              ; prvn¡ © dek
         dw        4f00h,WEnd               ; posledn¡ © dek
         dw        4900h,WPgUp              ; str nka nahoru
         dw        5100h,WPgDwn             ; str nka dol–
         dw        4d00h,WRght              ; vpravo
         dw        4b00h,WLeft              ; vlevo
         dw        0f09h,WTab               ; tabel tor
         dw        5200h,WIns               ; Insert
         dw        "*",WAIns                ; *
         dw        1c0dh,WEnter             ; Enter

         dw        11bh,Konec               ; konec programu
         dw        0,Start4

; -----------------------------------------------------------------------------
;        konec programu ESC
; -----------------------------------------------------------------------------

Konec:   mov       ah,0fh
         call      Int10P                   ; poskytnut¡ videom¢du
         mov       ah,0
         call      Int10P                   ; inicializace videom¢du

; ------ n vrat p©¡znaku BREAK

         mov       dl,ds:[OldBreak]
         mov       ax,3301h
         int       21h

         int       20h

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                           Editace oken
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

;þ

; -----------------------------------------------------------------------------
;        posun kurzoru dol–
; -----------------------------------------------------------------------------

WDown:   inc       ax                       ; zv˜¨en¡ ‡¡sla polo‘ky
         cmp       ax,ds:[bx+SeznDNum]      ; je maxim ln¡ polo‘ka ?
         jae       WDown8                   ; je posledn¡ polo‘ka
WDown6:  mov       ds:[bx+SeznDAkt],ax      ; nov  aktivn¡ polo‘ka
WDown7:  call      DispWin
WDown8:  jmp       Start4

; -----------------------------------------------------------------------------
;        posun kurzoru nahoru
; -----------------------------------------------------------------------------

WUp:     or        ax,ax                    ; je prvn¡ polo‘ka ?
         jz        WDown8                   ; je prvn¡ polo‘ka
         dec       ax                       ; posun aktivn¡ polo‘ky
         jmp       short WDown6

; -----------------------------------------------------------------------------
;        prvn¡ © dek
; -----------------------------------------------------------------------------

WHome:   xor       ax,ax
         jmp       short WDown6

; -----------------------------------------------------------------------------
;        posledn¡ © dek
; -----------------------------------------------------------------------------

WEnd:    mov       ax,ds:[bx+SeznDNum]      ; po‡et polo‘ek
         or        ax,ax
         jz        WDown8
         dec       ax
         mov       ds:[bx+SeznDAkt],ax
         jmp       short WDown6

; -----------------------------------------------------------------------------
;        posun o str nku nahoru
; -----------------------------------------------------------------------------

WPgUp:   mov       cx,ds:[bx+SeznDTop]      ; prvn¡ polo‘ka
         or        cx,cx
         jz        WHome                    ; je zobrazen za‡ tek
         cmp       cx,SOUBDIS-1
         jb        WPgUp2
         mov       cx,SOUBDIS-1
WPgUp2:  sub       ds:[bx+SeznDTop],cx
         sub       ax,cx
         jmp       short WDown6

; -----------------------------------------------------------------------------
;        posun o str nku dol–
; -----------------------------------------------------------------------------

WPgDwn:  mov       cx,ds:[bx+SeznDNum]      ; po‡et polo‘ek
         sub       cx,SOUBDIS               ; prvn¡ polo‘ka
         jbe       WEnd                     ; je mal˜ po‡et polo‘ek
         sub       cx,ds:[bx+SeznDTop]      ; posunut¡ po‡ tku
         jbe       WEnd
         cmp       cx,SOUBDIS-1
         jb        WPgDwn2
         mov       cx,SOUBDIS-1
WPgDwn2: add       ds:[bx+SeznDTop],cx
         add       ax,cx
WDown66: jmp       short WDown6

; -----------------------------------------------------------------------------
;        ozna‡en¡ jedn‚ polo‘ky
; -----------------------------------------------------------------------------

WIns:    mov       si,ax                    ; aktivn¡ polo‘ka
         cmp       si,ds:[bx+SeznDNum]
         jae       WDown8                   ; neplatn  polo‘ka
         shl       si,1
         mov       si,ds:[bx+si+SeznInd]    ; adresa polo‘ky
         push      es
         mov       es,ds:[bx+SeznDSeg]
         cmp       si,-1
         je        WIns3                    ; neplatn  polo‘ka
         call      InvPol                   ; inverze ozna‡en¡ polo‘ky ES:SI
WIns3:   pop       es
         inc       ax                       ; zv˜¨en¡ ‡¡sla polo‘ky
         cmp       ax,ds:[bx+SeznDNum]      ; je maxim ln¡ polo‘ka ?
         jb        WDown6
         dec       ax
         jmp       short WDown66

; -----------------------------------------------------------------------------
;        zmˆna ozna‡en¡ v¨ech polo‘ek
; -----------------------------------------------------------------------------

WAIns:   mov       cx,ds:[bx+SeznDNum]
         jcxz      WDown8                   ; nen¡ ‘ dn  polo‘ka
         lea       di,[bx+SeznInd]          ; indexov  tabulka
         push      es
         mov       es,ds:[bx+SeznDSeg]
WAIns2:  mov       si,ds:[di]               ; adresa polo‘ky
         cmp       si,-1
         je        WAIns3                   ; polo‘ka ".."
         call      InvPol                   ; inverze polo‘ky
WAIns3:  inc       di
         inc       di
         loop      WAIns2
         pop       es
         jmp       short WDown66

; -----------------------------------------------------------------------------
;        kurzor vlevo - aktivn¡ je disk
; -----------------------------------------------------------------------------

WLeft:   mov       bx,offset DatDefL
         and       byte ptr ds:[DatParm],not b0 ; p©¡znak aktivn¡ho lev‚ho okna
         and       byte ptr ds:[DatDefR+SeznPar],not b2
         or        byte ptr ds:[bx+SeznPar],b2
WLeft2:  jmp       Start32

; -----------------------------------------------------------------------------
;        kurzor vpravo - aktivn¡ je streamer
; -----------------------------------------------------------------------------

WRght:   mov       bx,offset DatDefR
         or        byte ptr ds:[DatParm],b0 ; p©¡znak aktivn¡ho prav‚ho okna
         and       byte ptr ds:[DatDefL+SeznPar],not b2
         or        byte ptr ds:[bx+SeznPar],b2
         jmp       short WLeft2

; -----------------------------------------------------------------------------
;        tabel tor - p©epnut¡ okna
; -----------------------------------------------------------------------------

WTab:    test      byte ptr ds:[DatParm],b0
         jz        WRght
         jmp       short WLeft

; -----------------------------------------------------------------------------
;        Enter - zmˆna adres ©e
; -----------------------------------------------------------------------------

WEnter:  mov       si,ax                    ; aktivn¡ polo‘ka
         cmp       si,ds:[bx+SeznDNum]
         jb        WEnter2                  ; polo‘ka je OK
WDown88: jmp       WDown8                   ; neplatn  polo‘ka
WEnter2: shl       si,1
         mov       si,ds:[bx+si+SeznInd]    ; adresa polo‘ky

; ------ posun n¡‘e o podadres ©

         mov       byte ptr ds:[DTAName],0  ; zru¨en¡ jm‚na k vyhled n¡
         cmp       si,-1
         jne       WEnter4                  ; neplatn  polo‘ka

         lea       si,[bx+SeznDir]          ; aktivn¡ adres © seznamu
         mov       cx,ds:[bx+SeznDirN]      ; d‚lka aktivn¡ho seznamu
         xor       dx,dx                    ; ‡¡ta‡ d‚lky jm‚na adres ©e
         add       si,cx
WEnter3: dec       si
         dec       cx
         inc       dx                       ; ‡¡ta‡ d‚lky jm‚na adres ©e
         cmp       byte ptr ds:[si],"\"
         jne       WEnter3

         push      cx
         push      si
         push      ds
         pop       es
         mov       cx,dx                    ; d‚lka jm‚na adres ©e
         mov       di,offset DTAName - 1    ; jm‚no souboru - 1
         cld
         rep       movsb                    ; p©enos jm‚na souboru
         mov       al,0
         stosb                              ; koncov  0
         pop       si
         pop       cx

         cmp       byte ptr ds:[si-1],":"
         jne       WEnter32
         inc       si
         inc       cx
WEnter32:mov       byte ptr ds:[si],0
         mov       ds:[bx+SeznDirN],cx      ; nov  d‚lka
         jmp       short WEnter6

; ------ posun v˜¨e o podadres ©

WEnter4: mov       es,ds:[bx+SeznDSeg]      ; segment adres ©e
         test      byte ptr es:[si+FileAtr],DIR
         jz        WDown88                  ; nen¡ adres © - neplat¡

         lea       di,[bx+SeznDir]          ; aktivn¡ adres © seznamu
         mov       cx,ds:[bx+SeznDirN]
         add       di,cx
         cmp       byte ptr ds:[di-1],"\"
         je        WEnter42
         mov       byte ptr ds:[di],"\"
         inc       di
         inc       word ptr ds:[bx+SeznDirN]

WEnter42:push      ds
         push      es

         push      ds
         push      es
         pop       ds
         pop       es

         mov       cl,ds:[si]
         mov       ch,0
         sub       cx,FileName
         add       es:[bx+SeznDirN],cx      ; zv˜¨en¡ d‚lky adres ©e
         add       si,FileName
         cld
         rep       movsb
         mov       al,0
         stosb

         pop       es
         pop       ds

WEnter6:
         mov       si,offset AktDir
         call      SrcPath                  ; nalezen¡ aktivn¡ho adres ©e
         mov       si,di
         call      SelFile
         call      SortFile                 ; set©¡dˆn¡ obsahu adres ©e

         mov       si,offset DTAName        ; jm‚no adres ©e k p©ednastaven¡
         cmp       byte ptr ds:[si],0       ; je adres © k vyhled n¡ ?
         je        WEnter7                  ; nen¡ adres © k vyhled n¡

         call      SrcPol                   ; vyhled n¡ polo‘ky DS:SI
         jc        WEnter7                  ; polo‘ka nenalezena
         mov       ds:[bx+SeznDAkt],ax      ; nov  aktivn¡ polo‘ka

WEnter7: jmp       WDown7




;         cmp       byte ptr ds:[80h],2
;         jne       Start000
;         jmp       Starr
;
;Start000:
;         mov       ax,ds:[SektBSeg]
;         mov       ds:[SektSeg],ax
;
;;         mov       ax,ds:[BuffSeg]
;;         mov       es,ax
;;         xor       di,di
;;         mov       cx,289*180/2
;;         cld
;;         mov       ax,-1
;;         rep       stosw
;
;         call      RSoub
;
;         call      KodBlok                  ; v˜po‡et kontroln¡ch sektor–
;
;         call      KodSYNU                  ; k¢dov n¡ £vodn¡ho synchr. sektoru
;
;         mov       byte ptr ds:[AktSekt],0  ; aktu ln¡ ‡¡slo sektoru
;         and       byte ptr ds:[AktBlok+1],not (b14/HI) ; datov˜ sektor
;         mov       cx,289
;         mov       ax,ds:[BuffSeg]
;         mov       es,ax
;         xor       si,si
;Start001:call      KodSekt
;         inc       byte ptr ds:[AktSekt]
;         jnz       Start002
;         or        byte ptr ds:[AktBlok+1],b14/HI
;
;Start002:add       si,180
;         loop      Start001
;
;         call      KodSYNZ                  ; k¢dov n¡ z vˆre‡n‚ho synch.sektoru
;
;         mov       byte ptr ds:[CitByte],0
;         mov       word ptr ds:[CitSekt],0
;
;         mov       ax,ds:[SektBSeg]
;         mov       ds:[SektSeg],ax
;
;         mov       word ptr ds:[SektUkaz],0
;         mov       word ptr ds:[CitSynch],UVSYNCH ; po‡et synchroniz. sektor–
;
;         cli
;         call      InitVys
;         jnc       Start01
;         jmp       Start12
;
;Start01: mov       al,0ffh
;         mov       dx,ds:[AdrPort]
;         out       dx,al
;         add       dx,5
;         in        al,dx
;         sti
;
;Start02:
;
;Start11:  sti
;         call      Kurz
;
;;         push      ds
;;         xor       ax,ax
;;         mov       ds,ax
;;         mov       ax,ds:[41ch]
;;         cmp       ax,ds:[41ah]
;;         pop       ds
;         cmp       byte ptr ds:[KeyBreak],0
;         jne       start13                  ; p©eru¨en¡ ESC
;
;         cmp       word ptr ds:[CitSekt],289+ZAVSYNCH
;         jb        Start11
;
;start13:
;         call      DeInit                   ; odinstalov n¡ portu COM
;
;
;Start132:
;         mov       ah,1
;         int       16h
;         jz        start12
;Start133:mov       ah,0
;         int       16h
;start12:



; -----------------------------------------------------------------------------
;
; -----------------------------------------------------------------------------

Kurz     PROC      NEAR

         push      ax
         push      es

         mov       ax,0b800h
         mov       es,ax
         inc       byte ptr es:[0]
         jnz       Kurz2
         inc       byte ptr es:[2]
Kurz2:   jnz       Kurz3
         inc       byte ptr es:[4]

Kurz3:
         pop       es
         pop       ax
         ret

Kurz     ENDP

; -----------------------------------------------------------------------------
;        p©¡jem
; -----------------------------------------------------------------------------
;þ
Starr:
         mov       al,ds:[82h]
         and       al,3
         mov       ds:[Param],al

         mov       ax,ds:[SektBSeg]
         mov       ds:[SektSeg],ax

         cli
         call      InitPri
         jnc       Starr01
         jmp       Starr2

Starr01: mov       dx,ds:[AdrPort]
         in        al,dx
         add       dx,5
         in        al,dx

         mov       byte ptr ds:[CitByte],-4
         mov       word ptr ds:[CitSekt],0
         mov       word ptr ds:[SektUkaz],0
         mov       word ptr ds:[CitSynch],MINSYNCH ; po‡et synchroniz. sektor–

         sti

         mov       es,ds:[SektSeg]
         xor       di,di

Starr02:

Starr1:  sti
         call      Kurz
         cmp       byte ptr ds:[KeyBreak],0
         jne       starr3                   ; p©eru¨en¡ ESC

         cmp       word ptr ds:[CitSekt],289
         jb        Starr1

Starr3:  call      DeInit

; ------ inicializace mapy vadn˜ch sektor–

         push      ds
         pop       es
         mov       di,offset ErrMap
         mov       cx,289
         mov       al,-1
         cld
         rep       stosb                    ; vymaz n¡ mapy vadn˜ch sektor–
         mov       word ptr ds:[ErrCit],289 ; po‡et vadn˜ch sektor–

; ------ dek¢dov n¡ sektor–

         mov       ax,ds:[SektBSeg]
         mov       ds:[SektSeg],ax
         mov       cx,289                   ; po‡et sektor–

Starr31: xor       ax,ax
         call      KorSekt                  ; korekce sektoru
         sbb       ax,ax                    ; -1=vadn˜ sektor, 0=OK
         cmp       bx,289                   ; je ‡¡slo sektoru OK ?
         jae       Starr314                 ; chybn‚ ‡¡slo sektoru
         cmp       byte ptr ds:[ErrMap+bx],0 ; je sektor ji‘ nastaven ?
         je        Starr314                 ; je ji‘ nastaven
         mov       byte ptr ds:[ErrMap+bx],al ; p©¡znak vadn‚ho sektoru
         inc       ax                       ; 1=OK, 0=vadn˜
         sub       word ptr ds:[ErrCit],ax  ; sn¡‘en¡ ‡¡ta‡e vadn˜ch sektor–

         sub       word ptr ds:[SektSeg],224/16 ; sn¡‘en¡ adresy sektoru
         call      DekSekt                  ; dek¢dov n¡ sektoru
Starr314:loop      Starr31                  ; dal¨¡ sektor

         call      KorBlok                  ; korekce blok– (oprava chyb)
         mov       ax,ds:[ErrCit]
         add       ds:[CitChyb],ax

         call      WSoub

         mov       ah,0fh
         call      Int10P                   ; poskytnut¡ videom¢du
         mov       ah,0
         call      Int10P                   ; inicializace videom¢du

;         mov       dx,offset UvTxt
;         mov       ah,9
;         int       21h


         mov       dx,offset KorTxt1
         mov       ah,9
         int       21h

         mov       ax,ds:[CitKor1]
         call      DispNum

         mov       dx,offset KorTxt2
         mov       ah,9
         int       21h

         mov       ax,ds:[CitKor2]
         call      DispNum

         mov       dx,offset KorTxt3
         mov       ah,9
         int       21h

         mov       ax,ds:[CitChyb]
         call      DispNum

         mov       dx,offset KorTxt4
         mov       ah,9
         int       21h

         mov       ah,1
         int       16h
         jz        starr2
         mov       ah,0
         int       16h
Starr2:
         mov       ax,4c00h
         int       21h

; -----------------------------------------------------------------------------
;        obsluha INT 24h a INT 23h
; -----------------------------------------------------------------------------

Int24    PROC      FAR

         mov       al,0
Int23:   iret

Int24    ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ ‡¡sla AX
; -----------------------------------------------------------------------------

DispNum  PROC      NEAR

         push      ax
         push      bx
         push      cx
         push      dx

         xor       cx,cx
         mov       bx,10
DispNum1:xor       dx,dx
         div       bx
         push      dx
         inc       cx
         or        ax,ax
         jnz       DispNum1

DispNum2:pop       dx
         mov       ah,2
         add       dl,"0"
         int       21h
         loop      DispNum2

         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

DispNum  ENDP

; -----------------------------------------------------------------------------
; -----------------------------------------------------------------------------

RSoub    PROC      NEAR

         mov       dx,offset Soubor
         mov       ax,3d00h
         int       21h
         jc        RSoub9
         mov       bx,ax

         push      ds
         mov       ax,ds:[BuffSeg]
         mov       ds,ax
         xor       dx,dx
         mov       cx,256*180
         mov       ah,3fh
         int       21h
         pop       ds

         mov       ah,3eh
         int       21h
RSoub9:  ret

RSoub    ENDP

; -----------------------------------------------------------------------------
; -----------------------------------------------------------------------------

WSoub    PROC      NEAR

         mov       dx,offset Soubor2
         mov       ah,3ch
         xor       cx,cx
         int       21h
         jc        WSoub9
         mov       bx,ax

         push      ds
         mov       ax,ds:[BuffSeg]
         mov       ds,ax
         xor       dx,dx
         mov       cx,256*180
         mov       ah,40h
         int       21h
         pop       ds

         mov       ah,3eh
         int       21h
WSoub9:  ret

WSoub    ENDP



; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                           Zpracov n¡ dat
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;þ
; -----------------------------------------------------------------------------
;        zak¢dov n¡ datov‚ho bloku (sestaven¡ kontroln¡ch sektor–)
; -----------------------------------------------------------------------------

KodBlok  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      ds
         push      es

; ------ p©¡prava registr–

         mov       es,ds:[BuffSeg]          ; segment bufferu soubor–
         push      es
         pop       ds
         mov       di,256*180               ; ukl dac¡ adresa sektor–
         xor       cx,cx
         cld

; ------ v˜po‡et kontroln¡ch sektor– © dk–

         xor       si,si                    ; za‡ tek matice
         mov       bx,16                    ; po‡et © dk– sektor– k v˜po‡tu
KodBlok1:mov       dx,180                   ; po‡et bajt– v sektoru
KodBlok2:mov       cl,16                    ; po‡et sektor–
         mov       al,0
KodBlok3:xor       al,ds:[si]
         add       si,180                   ; bajt v dal¨¡m sektoru
         loop      KodBlok3
         stosb                              ; kontroln¡ bajt sektor– v © dku
         sub       si,16*180-1              ; adresa dal¨¡ho bajtu
         dec       dx                       ; ‡¡ta‡ bajt–
         jnz       KodBlok2                 ; dal¨¡ bajt v sektoru
         add       si,16*180-180            ; adresa dal¨¡ho © dku
         dec       bx                       ; ‡¡ta‡ © dk–
         jnz       KodBlok1                 ; v˜po‡et dal¨¡ho © dku

; ------ v˜po‡et kontroln¡ch sektor– sloupc–

         mov       dx,16*180                ; po‡et sloupc–
         xor       si,si                    ; po‡ te‡n¡ adresa matice
KodBlok4:mov       cl,16                    ; po‡et sektor– ve sloupci
         mov       al,0
KodBlok5:xor       al,ds:[si]
         add       si,16*180                ; bajt sektoru v dal¨¡m © dku
         loop      KodBlok5                 ; dal¨¡ © dek
         stosb
         sub       si,16*16*180-1           ; dal¨¡ sloupec bajt–
         dec       dx                       ; ‡¡ta‡ sloupc–
         jnz       KodBlok4                 ; dal¨¡ sloupec

; ------ koncov˜ kontroln¡ sou‡et

         mov       si,(256+16)*180          ; © dek kontroln¡ch sektor– sloupc–
         mov       dx,180                   ; po‡et bajt– v sektoru
KodBlok6:mov       cl,16                    ; po‡et sektor–
         mov       al,0
KodBlok7:xor       al,ds:[si]
         add       si,180
         loop      KodBlok7                 ; dal¨¡ sektor v © dku
         stosb
         sub       si,16*180-1              ; adresa dal¨¡ho bajtu v sektoru
         dec       dx
         jnz       KodBlok6                 ; dal¨¡ bajt v sektoru

; ------ n vrat registr–

         pop       es
         pop       ds
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

KodBlok  ENDP

; -----------------------------------------------------------------------------
;        proveden¡ korekce datov‚ho bloku
; -----------------------------------------------------------------------------

KorBlok  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      ds
         push      es

; ------ p©¡prava registr–

         test      byte ptr ds:[Param],b1   ; prov d¡ se korekce 2 ?
         jz        KorBlk11                 ; neprov d¡ se korekce 2

         mov       es,ds:[BuffSeg]          ; adresa bufferu soubor–
         xor       cx,cx
         cld
KorBlok1:cmp       word ptr ds:[ErrCit],0   ; jsou nˆjak‚ chyby ?
         jne       KorBlok2                 ; jsou nˆjak‚ chyby
KorBlk11:jmp       KorBlok9                 ; nejsou chyby

; ------ nalezen¡ © dku s jednou chybou

KorBlok2:mov       di,offset ErrMap+256     ; kontroln¡ sektory © dk–
         mov       si,offset ErrMap         ; mapa vadn˜ch sektor–
         mov       dl,16                    ; DL <- po‡et © dk–
KorBlk21:mov       cl,16                    ; po‡et pozic v © dku
         mov       dh,0                     ; ‡¡ta‡ chyb
KorBlk22:call      TestBlk                  ; test bloku DS:SI, je-li vadn˜
         loop      KorBlk22
         xchg      si,di
         call      TestBlk
         xchg      si,di
         cmp       dh,1                     ; je 1 chyba ?
         je        KorBlok3                 ; je 1 chyba
         dec       dl                       ; ‡¡ta‡ © dk–
         jnz       KorBlk21                 ; dal¨¡ © dek

; ------ test po‡tu chyb v kontroln¡m © dku sloupc–

         mov       si,offset ErrMap+256+16  ; kontroln¡ © dek sloupc–
         mov       cl,17                    ; po‡et sektor– ke kontrole
         mov       dh,0                     ; ‡¡ta‡ chyb
KorBlk25:call      TestBlk                  ; test bloku
         loop      KorBlk25
         mov       di,si                    ; kontroln¡ bajt + 1
         dec       si                       ; korekce na © dek+16
         cmp       dh,1                     ; je 1 chyba v kontroln¡m © dku ?
         je        KorBlok3                 ; je 1 chyba

; ------ test po‡tu chyb v kontroln¡m © dku © dk–

         mov       si,offset ErrMap+256
         mov       cl,16                    ; po‡et sektor– ke kontrole
         mov       dh,0                     ; ‡¡ta‡ chyb
KorBlk26:call      TestBlk                  ; test bloku
         loop      KorBlk26
         xchg      si,di                    ; DI <- adresa konce © dku
         mov       si,offset ErrMap+256+16+16 ; celkov˜ kontroln¡ sektor
         call      TestBlk
         xchg      si,di
         cmp       dh,1                     ; je 1 chyba ?
         jne       KorBLok4                 ; nen¡ 1 chyba-prohled v n¡ sloupc–

; ------ vodorovn  korekce sektoru (BX=vadn˜ sektor, SI=© dek+16, DI=kontrola+1)

KorBlok3:mov       byte ptr ds:[bx],0       ; zru¨en¡ p©¡znaku vadn‚ho sektoru
         sub       bx,offset ErrMap         ; ‡¡slo sektoru k opravˆ
         sub       si,offset ErrMap+16      ; ‡¡slo © dku k opravˆ
         sub       di,offset ErrMap+1       ; ‡¡slo kontroln¡ho sektoru

; ------ v˜po‡et adres sektor–

         mov       ax,180                   ; po‡et bajt– na sektor
         mul       bx
         xchg      ax,bx                    ; BX <- adresa sektoru k opravˆ
         mov       ax,180                   ; po‡et bajt– na sektor
         mul       si
         xchg      ax,si                    ; SI <- adresa © dku sektor–
         mov       ax,180                   ; po‡et bajt– na sektor
         mul       di
         xchg      ax,di                    ; DI <- adresa kontroln¡ho sektoru

; ------ proveden¡ horizont ln¡ korekce sektoru

         push      ds
         push      es
         pop       ds                       ; DS <- adresa bufferu sektor–
         mov       dx,180                   ; po‡et bajt– ke korekci
KorBlk32:mov       cl,16                    ; po‡et sektor– v © dku
         mov       al,0                     ; st©ada‡ kontroln¡ho sou‡tu
KorBlk34:xor       al,ds:[si]
         add       si,180
         loop      KorBlk34
         xor       al,ds:[di]               ; kontroln¡ sektor
         xor       ds:[bx],al               ; proveden¡ korekce
         sub       si,16*180-1              ; ukazatel sektor– v © dku
         inc       di                       ; ukazatel kontroln¡ho sektoru
         inc       bx                       ; ukazatel opravovan‚ho sektoru
         dec       dx                       ; ‡¡ta‡ bajt– v sektoru
         jnz       KorBlk32                 ; dal¨¡ bajt
         pop       ds
         dec       word ptr ds:[ErrCit]     ; sn¡‘en¡ ‡¡ta‡e chyb
         inc       word ptr ds:[CitKor2]    ; ‡¡ta‡ korekc¡ 2
         jmp       KorBlok1                 ; dal¨¡ opravy



; ------ nalezen¡ sloupce s jednou chybou

KorBlok4:mov       di,offset ErrMap+256+16  ; kontroln¡ sektory sloupc–
         mov       si,offset ErrMap         ; mapa vadn˜ch sektor–
         mov       dl,16                    ; DL <- po‡et sloupc–
KorBlk41:mov       cl,16                    ; po‡et © dk– ve sloupci
         mov       dh,0                     ; ‡¡ta‡ chyb
KorBlk42:call      TestBlk                  ; test bloku DS:SI, je-li vadn˜
         add       si,16-1                  ; adresa dal¨¡ho © dku
         loop      KorBlk42
         sub       si,16*16-1               ; za‡ tek dal¨¡ho sloupce
         xchg      si,di
         call      TestBlk
         xchg      si,di
         cmp       dh,1                     ; je 1 chyba ?
         je        KorBlok5                 ; je 1 chyba
         dec       dl                       ; ‡¡ta‡ © dk–
         jnz       KorBlk41                 ; dal¨¡ © dek
         jmp       KorBlok9                 ; opravu nelze prov‚st

; ------ vertik. korekce sektoru (BX=vadn˜ sektor, SI=sloupec+1, DI=kontrola+1)

KorBlok5:mov       byte ptr ds:[bx],0       ; zru¨en¡ p©¡znaku vadn‚ho sektoru
         sub       bx,offset ErrMap         ; ‡¡slo sektoru k opravˆ
         sub       si,offset ErrMap+1       ; ‡¡slo sloupce k opravˆ
         sub       di,offset ErrMap+1       ; ‡¡slo kontroln¡ho sektoru

; ------ v˜po‡et adres sektor–

         mov       ax,180                   ; po‡et bajt– na sektor
         mul       bx
         xchg      ax,bx                    ; BX <- adresa sektoru k opravˆ
         mov       ax,180                   ; po‡et bajt– na sektor
         mul       si
         xchg      ax,si                    ; SI <- adresa sloupce sektor–
         mov       ax,180                   ; po‡et bajt– na sektor
         mul       di
         xchg      ax,di                    ; DI <- adresa kontroln¡ho sektoru

; ------ proveden¡ vertik ln¡ korekce sektoru

         push      ds
         push      es
         pop       ds                       ; DS <- adresa bufferu sektor–
         mov       dx,180                   ; po‡et bajt– ke korekci
KorBlk52:mov       cl,16                    ; po‡et sektor– ve sloupci
         mov       al,0                     ; st©ada‡ kontroln¡ho sou‡tu
KorBlk54:xor       al,ds:[si]
         add       si,16*180
         loop      KorBlk54
         xor       al,ds:[di]               ; kontroln¡ sektor
         xor       ds:[bx],al               ; proveden¡ korekce
         sub       si,16*16*180-1           ; ukazatel sektor– ve sloupci
         inc       di                       ; ukazatel kontroln¡ho sektoru
         inc       bx                       ; ukazatel opravovan‚ho sektoru
         dec       dx                       ; ‡¡ta‡ bajt– v sektoru
         jnz       KorBlk52                 ; dal¨¡ bajt
         pop       ds
         dec       word ptr ds:[ErrCit]     ; sn¡‘en¡ ‡¡ta‡e chyb
         inc       word ptr ds:[CitKor2]    ; ‡¡ta‡ korekc¡ 2
         jmp       KorBlok1                 ; dal¨¡ opravy

; ------ n vrat registr–

KorBlok9:pop       es
         pop       ds
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

KorBlok  ENDP

; -----------------------------------------------------------------------------
;     test bloku DS:SI, je-li vadn˜ (DH=‡¡ta‡ vadn˜ch blok–, BX=£schova SI)
; -----------------------------------------------------------------------------

TestBlk  PROC      NEAR

         cmp       byte ptr ds:[si],0
         je        TestBlk1                 ; nen¡ vadn˜
         mov       bx,si                    ; £schova adresy sektoru
         inc       dh                       ; ‡¡ta‡ chyb
TestBlk1:inc       si
         ret

TestBlk  ENDP

; -----------------------------------------------------------------------------
;        zak¢dov n¡ sektoru ES:SI (a p©id n¡ k seznamu sektor–)
; -----------------------------------------------------------------------------

KodSekt  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      ds
         push      es

; ------ ukl dac¡ adresa sektoru

         push      si
         push      es
         mov       es,ds:[SektSeg]          ; segment sektoru
         add       word ptr ds:[SektSeg],224/16 ; posun ukazatele sektor–

; ------ p©enesen¡ z hlav¡ sektoru

         xor       di,di
         mov       si,offset Zahlavi        ; z hlav¡ sektoru
         mov       cx,10/2                  ; d‚lka z hlav¡ sektoru
         cld
         rep       movsw                    ; p©enesen¡ z hlav¡ sektoru

; ------ p©enesen¡ dat sektoru

         pop       ds                       ; DS <- segment dat sektoru
         pop       si
         mov       cl,9/2                   ; zbytek dat v prvn¡m © dku
         rep       movsw                    ; p©enos zbytku prvn¡ho © dku
         movsb
         mov       dx,9                     ; po‡et zbyl˜ch © dk– dat
KodSekt1:inc       di                       ; p©esko‡en¡ bajtu sou‡tu XOR
         mov       cl,19/2
         rep       movsw                    ; p©enesen¡ jednoho © dku dat
         movsb
         dec       dx                       ; ‡¡ta‡ © dk– dat
         jnz       KodSekt1                 ; p©enesen¡ dal¨¡ho © dku dat

; ------ v˜po‡et kontroln¡ho bajtu © dk– dat XOR

         push      es
         pop       ds                       ; DS <- adresa sektoru
         xor       di,di
         mov       dx,10                    ; po‡et © dk– dat se z hlav¡m
KodSekt2:mov       cl,19                    ; d‚lka jednoho © kdu
         mov       al,0                     ; v˜choz¡ hodnota sou‡tu
KodSekt3:xor       al,ds:[di]
         inc       di
         loop      KodSekt3                 ; dal¨¡ bajt © dku
         stosb                              ; ulo‘en¡ kontroln¡ho bajtu XOR
         dec       dx                       ; ‡¡ta‡ © dk–
         jnz       KodSekt2                 ; v˜po‡et dal¨¡ho © dku dat

; ------ v˜po‡et kontroln¡ho opravn‚ho © dku XOR

         xor       si,si
         mov       dx,20                    ; po‡et sloupc– dat
KodSekt4:mov       cl,10                    ; po‡et © dk– dat
         mov       al,0                     ; v˜zhoz¡ hodnota kontroln¡ho sou‡tu
KodSekt5:xor       al,ds:[si]
         add       si,20                    ; adresa dal¨¡ho © dku dat
         loop      KodSekt5                 ; bajt v dal¨¡m © dku sloupce
         stosb                              ; ulo‘en¡ kontroln¡ho bajtu XOR
         sub       si,10*20-1               ; adresa za‡ tku dal¨¡ho soupce
         dec       dx                       ; ‡¡ta‡ sloupc– dat
         jnz       KodSekt4                 ; v˜po‡et dal¨¡ho sloupce dat

; ------ v˜po‡et kontroln¡ho sou‡tu dat CRC

         xor       si,si                    ; SI <- za‡ tek dat sektoru
         mov       cx,220                   ; velikost bloku dat
         mov       dx,-1
         call      CheckSum                 ; v˜po‡et kontroln¡ho sou‡tu dat
         mov       ds:[di],dx               ; ulo‘en¡ kontroln¡ho sou‡tu dat
         mov       word ptr ds:[di+2],-1    ; synchroniza‡n¡ bajty

; ------ n vrat registr–

         pop       es
         pop       ds
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

KodSekt  ENDP

; -----------------------------------------------------------------------------
;        zak¢dov n¡ £vodn¡ho synchroniza‡n¡ho sektoru
; -----------------------------------------------------------------------------

KodSYNU  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      si
         push      di
         push      es

; ------ ukl dac¡ adresa sektoru

         mov       es,ds:[SektSeg]          ; segment sektoru
         add       word ptr ds:[SektSeg],224/16 ; posun ukazatele sektor–
         xor       di,di

; ------ p©enesen¡ z hlav¡ sektoru

         mov       si,offset Zahlavi        ; z hlav¡ sektoru
         mov       cx,10/2                  ; d‚lka z hlav¡ sektoru
         cld
         rep       movsw                    ; p©enesen¡ z hlav¡ sektoru

; ------ korekce polo‘ek v z hlav¡

         mov       byte ptr es:[di-2],255   ; ozna‡en¡ synchroniza‡n¡ho sektoru
         or        byte ptr es:[di-3],b14/HI ; p©¡znak korek‡n¡ho sektoru
         mov       ax,ds:[FileOff]          ; offset prvn¡ho souboru
         stosw

; ------ vymaz n¡ zbytku sektoru

         mov       ax,-1
         mov       cx,(224-12)/2            ; d‚lka zbytku dat ve slovech
         rep       stosw                    ; vymaz n¡ zbytku sektoru

; ------ n vrat registr–

         pop       es
         pop       di
         pop       si
         pop       cx
         pop       ax
         ret

KodSYNU  ENDP

; -----------------------------------------------------------------------------
;        zak¢dov n¡ z vˆre‡n‚ho synchroniza‡n¡ho sektoru
; -----------------------------------------------------------------------------

KodSYNZ  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      si
         push      di
         push      es

; ------ ukl dac¡ adresa sektoru

         mov       es,ds:[SektSeg]          ; segment sektoru
         add       word ptr ds:[SektSeg],224/16 ; posun ukazatele sektor–
         xor       di,di

; ------ p©enesen¡ z hlav¡ sektoru

         mov       si,offset Zahlavi        ; z hlav¡ sektoru
         mov       cx,10/2                  ; d‚lka z hlav¡ sektoru
         cld
         rep       movsw                    ; p©enesen¡ z hlav¡ sektoru

; ------ korekce polo‘ek v z hlav¡

         mov       byte ptr es:[di-2],33    ; ozna‡en¡ synchroniza‡n¡ho sektoru
         or        byte ptr es:[di-3],b14/HI ; p©¡znak korek‡n¡ho sektoru

; ------ vymaz n¡ zbytku sektoru

         mov       ax,-1
         mov       cx,(224-10)/2            ; d‚lka zbytku dat ve slovech
         rep       stosw                    ; vymaz n¡ zbytku sektoru

; ------ n vrat registr–

         pop       es
         pop       di
         pop       si
         pop       cx
         pop       ax
         ret

KodSYNZ  ENDP

; -----------------------------------------------------------------------------
;        korekce jednoho sektoru (CY=chyba, BX=‡¡slo sektoru)
; -----------------------------------------------------------------------------

KorSekt  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx
         push      si
         push      di
         push      ds

; ------ p©¡prava registr–

         mov       ax,ds:[SektSeg]          ; segment sektoru
         add       word ptr ds:[SektSeg],224/16 ; posun ukazatele sektor–
         mov       ds,ax

; ------ kontrola © dk– sektoru

         cld
         mov       dx,11                    ; po‡et © dk– dat
         xor       cx,cx
         xor       bx,bx                    ; BX <- 0 ‡¡ta‡ vadn˜ch © dk–
         xor       si,si
KorSekt2:mov       cl,20                    ; po‡et bajt– na © dek
         mov       ah,0
KorSekt3:lodsb
         xor       ah,al
         loop      KorSekt3
         jz        KorSekt4                 ; © dek je OK
         inc       bx                       ; ‡¡ta‡ vadn˜ch © dk–
         mov       di,si                    ; adresa konce © dku
KorSekt4:dec       dx                       ; ‡¡ta‡ © dk–
         jnz       KorSekt2                 ; kontrola dal¨¡ho © dku

; ------ test, zda je pr vˆ 1 © dek k opravˆ

         cmp       bl,1                     ; je vadn˜ 1 © dek ?
         ja        KorSekt8                 ; je v¡c vadn˜ch © dk–
         jb        KorSekt7                 ; nen¡ ‘ dn˜ vadn˜ © dek

; ------ korekce vadn‚ho © dku

         test      byte ptr cs:[Param],b0   ; prov d¡ se korekce 1 ?
         jz        KorSekt7                 ; neprov d¡ se korekce 1

         sub       di,20                    ; adresa za‡ tku © dku
         xor       si,si                    ; za‡ tek dat
         mov       dx,20                    ; po‡et sloupc– v matici
KorSekt5:mov       cl,11                    ; po‡et © dk–
         mov       ah,0
KorSekt6:lodsb
         add       si,20-1                  ; adresa dal¨¡ho © dku
         xor       ah,al
         loop      KorSekt6
         xor       ds:[di],ah               ; korekce vadn‚ho bajtu
         inc       di
         sub       si,(11*20)-1
         dec       dx
         jnz       KorSekt5
         inc       word ptr cs:[CitKor1]

; ------ ovˆ©en¡ kontroln¡ho sou‡tu

KorSekt7:mov       dx,-1
         xor       si,si
         mov       cx,220                   ; velikost bloku dat
         call      CheckSum                 ; v˜po‡et kontroln¡ho sou‡tu dat
         cmp       ds:[220],dx              ; kontrola kontroln¡ho sou‡tu dat
         je        KorSekt9

; ------ chyba

KorSekt8:stc                                ; p©¡znak chyby

; ------ ‡¡slo sektoru

KorSekt9:pushf
         mov       bh,0
         mov       bl,ds:[8]                ; aktu ln¡ ‡¡slo sektoru
         test      byte ptr ds:[7],b14/HI   ; je to korek‡n¡ sektor ?
         jz        KorSektA                 ; nen¡ to korek‡n¡ sektor
         mov       bh,1                     ; ‡¡slo korek‡n¡ho sektoru
KorSektA:popf

; ------ n vrat registr–

         pop       ds
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       ax
         ret

KorSekt  ENDP

; -----------------------------------------------------------------------------
;        dek¢dov n¡ sektoru do bufferu souboru
; -----------------------------------------------------------------------------

DekSekt  PROC      NEAR

; ------ £schova registr–

         push      cx
         push      dx
         push      si
         push      di
         push      ds
         push      es

; ------ adresa sektoru

         mov       es,ds:[BuffSeg]          ; segment bufferu soubor–
         mov       cx,ds:[SektSeg]
         add       word ptr ds:[SektSeg],224/16
         mov       ds,cx

; ------ adresa v bufferu soubor–

         mov       ah,0
         mov       al,ds:[8]                ; aktu ln¡ ‡¡slo sektoru
         test      byte ptr ds:[7],b14/HI   ; je to korek‡n¡ sektor ?
         jz        DekSekt1                 ; nen¡ to korek‡n¡ sektor
         mov       ah,1                     ; ‡¡slo korek‡n¡ho sektoru
DekSekt1:cmp       ax,289
         jae       DekSekt8                 ; neplatn‚ ‡¡slo sektoru
         mov       cx,180                   ; d‚lka dat
         mul       cx                       ; offset sektoru
         xchg      ax,di                    ; ukl dac¡ adresa sektoru v bufferu

; ------ p©enos dat sektoru

         cld
         mov       si,10
         mov       cx,9/2
         rep       movsw
         movsb

         mov       dx,9
DekSekt2:inc       si
         mov       cl,19/2
         rep       movsw
         movsb
         dec       dx
         jnz       DekSekt2

; ------ n vrat registr–

DekSekt8:pop       es
         pop       ds
         pop       di
         pop       si
         pop       dx
         pop       cx
         ret

DekSekt  ENDP

; -----------------------------------------------------------------------------
;        Kontroln¡ sou‡et CRC bloku dat
; -----------------------------------------------------------------------------
; VSTUP: DS:SI=za‡ tek bloku dat
;        CX=po‡et bajt–
;        DX=vstupn¡ hodnota kontroln¡ho sou‡tu
; VSTUP:DX=v˜stupn¡ hodnota kontroln¡ho sou‡tu
; -----------------------------------------------------------------------------

CheckSum PROC      NEAR

         push      ax
         push      cx
         push      di

         jcxz      CheckSm2                 ; nen¡ ‘ dn˜ bajt
         mov       di,cx                    ; po‡et bajt– pro kontroln¡ sou‡et
         cld                                ; smˆr nahoru

CheckSm1:lodsb                              ; a = fgetc(fp);
         xor       al,dh                    ; d = a ^= d;
         mov       dh,al
         mov       cl,4
         shr       al,cl                    ; a >>= 4;
         xor       al,dh                    ; d = a ^= d;
         mov       dh,al
         mov       cl,3
         ror       al,cl                    ; a = (a >> 3) | (a << 5);
         mov       ch,al                    ; f = a;
         and       al,1fh                   ; a &= 0x1f;
         xor       al,dl                    ; e = a ^= e;
         mov       dl,al
         mov       al,ch                    ; a = f;
         ror       al,1                     ; a = (a >> 1) | (a << 7);
         and       al,0f0h                  ; a &= 0xf0;
         xor       al,dl                    ; e = a ^= e;
         mov       dl,al
         mov       al,ch                    ; a = f & 0xe0;
         and       al,0e0h
         xor       al,dh                    ; a ^= d;
         mov       dh,dl                    ; d = e;
         mov       dl,al                    ; e = a;

         dec       di
         jnz       CheckSm1                 ; dal¨¡ bajt

CheckSm2:pop       di
         pop       cx
         pop       ax
         ret

CheckSum ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                        Obsluha vys¡l n¡ a p©¡jmu dat
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;þ
; -----------------------------------------------------------------------------
;        inicializace portu na vys¡l n¡
; -----------------------------------------------------------------------------

InitVys  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      es

; ------ adresa portu

         push      ds
         mov       bx,ds:[Port]             ; ‡¡slo portu COMn
         shl       bx,1
         xor       ax,ax
         mov       ds,ax                    ; DS <- 0
         mov       dx,ds:[bx+400h]          ; adresa portu
         pop       ds
         or        dx,dx
         jnz       InitVys1
         stc
         jmp       InitVys8                 ; neplatn  adresa portu COM
InitVys1:mov       ds:[AdrPort],dx          ; adresa portu COM

; ------ zru¨en¡ po‘adavk– od portu

         cli
         in        al,dx                    ; zru¨en¡ p©ij¡mac¡ho bufferu
         inc       dx                       ; 1: registr povolen¡ p©eru¨en¡
         xor       al,al                    ; AL <- 0
         out       dx,al                    ; z kaz v¨ech p©eru¨en¡
         add       dx,3                     ; 4: ©¡dic¡ registr modemu
         out       dx,al                    ; z kaz p©eru¨en¡ a sign l– modemu
         inc       dx                       ; 5: stavov˜ registr linky
         in        al,dx                    ; zru¨en¡ po‘adavk– od linky
         inc       dx                       ; 6: stavov˜ registr modemu
         in        al,dx                    ; zru¨en¡ po‘adavk– modemu

; ------ £schova adresy INT 08h

         mov       ax,3508h
         int       21h
         mov       word ptr ds:[Old08],bx
         mov       word ptr ds:[Old08+2],es

; ------ £schova adresy INT 09h

         mov       ax,3509h
         int       21h
         mov       word ptr ds:[Old09],bx
         mov       word ptr ds:[Old09+2],es

; ------ instalace obsluhy INT 08h

         mov       dx,offset Int08
         mov       ax,2508h
         int       21h

; ------ instalace obsluhy INT 09h

         mov       dx,offset Int09
         mov       ax,2509h
         int       21h

; ------ £schova p–vodn¡ho p©eru¨en¡ COM

         call      GetICom                  ; poskytnut¡ ‡¡sla p©eru¨en¡ COM
         push      ax
         mov       ah,35h
         int       21h
         mov       word ptr ds:[OldComm],bx
         mov       word ptr ds:[OldComm+2],es
         pop       ax

; ------ instalace obsluhy p©eru¨en¡ COM

         push      ax
         mov       ah,25h
         mov       dx,offset IntVys         ; adresa obsluhy vys¡la‡e
         int       21h                      ; instalace obsluhy vys¡la‡e
         pop       ax

; ------ inicializace portu COM

         mov       dx,ds:[AdrPort]          ; adresa portu COM
         add       dx,3                     ; 3: ©¡dic¡ registr linky
         mov       al,10000011b
         out       dx,al                    ; DLAB, 8 bit– bez parity, 1 STOP b.
         inc       dx                       ; 4: ©¡dic¡ registr modemu
         mov       al,1011b                 ; OUT2 a RTS+DTR
         out       dx,al                    ; ©¡dic¡ registr modemu
         sub       dx,4                     ; 0: registr dˆli‡ky LOW
         mov       al,1
         out       dx,al                    ; dˆlic¡ pomˆr 1
         inc       dx                       ; 1: registr dˆli‡ky HIGH
         mov       al,0
         out       dx,al
         inc       dx
         inc       dx                       ; 3: ©¡dic¡ registr linky
         mov       al,00000011b             ; 8 bit– bez parity, 1 STOP bit
         out       dx,al
         dec       dx                       ; 2:
         dec       dx                       ; 1:
         mov       al,0010b                 ; p©eru¨en¡ po vysl n¡ znaku
         out       dx,al

; ------ nastaven¡ registru p©eru¨en¡

         in        al,[21h]
         mov       ds:[OldInt],al           ; £schova masky p©eru¨en¡
         mov       al,ah
         out       [21h],al                 ; povoleno p©eru¨en¡
         clc

; ------ n vrat registr–

InitVys8:pop       es
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

InitVys  ENDP

; -----------------------------------------------------------------------------
;        inicializace portu na p©¡jem
; -----------------------------------------------------------------------------

InitPri  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      es

; ------ adresa portu

         push      ds
         mov       bx,ds:[Port]             ; ‡¡slo portu COMn
         shl       bx,1
         xor       ax,ax
         mov       ds,ax                    ; DS <- 0
         mov       dx,ds:[bx+400h]          ; adresa portu
         pop       ds
         or        dx,dx
         jnz       InitPri1
         stc
         jmp       InitPri8                 ; neplatn  adresa portu COM
InitPri1:mov       ds:[AdrPort],dx          ; adresa portu COM

; ------ zru¨en¡ po‘adavk– od portu

         cli
         in        al,dx                    ; zru¨en¡ p©ij¡mac¡ho bufferu
         inc       dx                       ; 1: registr povolen¡ p©eru¨en¡
         xor       al,al                    ; AL <- 0
         out       dx,al                    ; z kaz v¨ech p©eru¨en¡
         add       dx,3                     ; 4: ©¡dic¡ registr modemu
         out       dx,al                    ; z kaz p©eru¨en¡ a sign l– modemu
         inc       dx                       ; 5: stavov˜ registr linky
         in        al,dx                    ; zru¨en¡ po‘adavk– od linky
         inc       dx                       ; 6: stavov˜ registr modemu
         in        al,dx                    ; zru¨en¡ po‘adavk– modemu

; ------ £schova adresy INT 08h

         mov       ax,3508h
         int       21h
         mov       word ptr ds:[Old08],bx
         mov       word ptr ds:[Old08+2],es

; ------ £schova adresy INT 09h

         mov       ax,3509h
         int       21h
         mov       word ptr ds:[Old09],bx
         mov       word ptr ds:[Old09+2],es

; ------ instalace obsluhy INT 08h

         mov       dx,offset Int08
         mov       ax,2508h
         int       21h

; ------ instalace obsluhy INT 09h

         mov       dx,offset Int09
         mov       ax,2509h
         int       21h

; ------ £schova p–vodn¡ho p©eru¨en¡ COM

         call      GetICom                  ; poskytnut¡ ‡¡sla p©eru¨en¡ COM
         push      ax
         mov       ah,35h
         int       21h
         mov       word ptr ds:[OldComm],bx
         mov       word ptr ds:[OldComm+2],es
         pop       ax

; ------ instalace obsluhy p©eru¨en¡ COM

         push      ax
         mov       ah,25h
         mov       dx,offset IntPri         ; adresa obsluhy p©ij¡ma‡e
         int       21h                      ; instalace obsluhy p©ij¡ma‡e
         pop       ax

; ------ inicializace portu COM

         mov       dx,ds:[AdrPort]          ; adresa portu COM
         add       dx,3                     ; 3: ©¡dic¡ registr linky
         mov       al,10000011b
         out       dx,al                    ; DLAB, 8 bit– bez parity, 1 STOP b.
         inc       dx                       ; 4: ©¡dic¡ registr modemu
         mov       al,1010b                 ; OUT2 a RTS
         out       dx,al                    ; ©¡dic¡ registr modemu
         sub       dx,4                     ; 0: registr dˆli‡ky LOW
         mov       al,1
         out       dx,al                    ; dˆlic¡ pomˆr 1
         inc       dx                       ; 1: registr dˆli‡ky HIGH
         mov       al,0
         out       dx,al
         inc       dx
         inc       dx                       ; 3: ©¡dic¡ registr linky
         mov       al,00000011b             ; 8 bit– bez parity, 1 STOP bit
         out       dx,al
         dec       dx                       ; 2:
         dec       dx                       ; 1:
         mov       al,0101b                 ; p©eru¨en¡ p©i p©¡jmu znaku
         out       dx,al

; ------ nastaven¡ registru p©eru¨en¡

         in        al,[21h]
         mov       ds:[OldInt],al           ; £schova masky p©eru¨en¡
         mov       al,ah
         out       [21h],al                 ; povoleno p©eru¨en¡
         clc

; ------ n vrat registr–

InitPri8:pop       es
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

InitPri  ENDP

; -----------------------------------------------------------------------------
;        poskytnut¡ ‡¡sla p©eru¨en¡ (-> AL) a masky p©eru¨en¡ COM (-> AH)
; -----------------------------------------------------------------------------

GetICom  PROC      NEAR

         mov       ax,(NOT b4+b1+b0)*HI + 0ch ; COM1 a COM3
         test      byte ptr ds:[Port],b0      ; je COM1 nebo COM3 ?
         jz        GetICom2                   ; je COM1 nebo COM3
         mov       ax,(NOT b3+b1+b0)*HI + 0bh ; COM2 a COM4
GetICom2:ret

GetICom  ENDP

; -----------------------------------------------------------------------------
;        p©eru¨en¡ p©i vys¡l n¡ sektoru
; -----------------------------------------------------------------------------

IntVys   PROC      FAR

; ------ £schova registr–

         push      ax
         push      bx
         push      dx
         push      ds
         push      es

; ------ p©¡prava registr–

         push      cs
         pop       ds

; ------ prvn¡ ‡ten¡ stavov‚ho registru p©eru¨en¡

         mov       dx,ds:[AdrPort]          ; adresa portu COM
         mov       es,ds:[SektSeg]
         inc       dx
         inc       dx
         in        al,dx                    ; stavov˜ registr p©eru¨en¡
         jmp       short IntVys2

; ------ test, zda je dal¨¡ p©eru¨en¡ od portu

IntVys1: mov       dx,ds:[AdrPort]          ; adresa portu COM
         inc       dx
         inc       dx
         in        al,dx                    ; stavov˜ registr p©eru¨en¡
         test      al,1                     ; je dal¨¡ p©eru¨en¡ ?
         jz        IntVys2
         jmp       IntVys9                  ; nen¡ dal¨¡ p©eru¨en¡

; ------ test, zda je p©eru¨en¡ p©i vys¡l n¡

IntVys2: cmp       al,2                     ; p©eru¨en¡ p©i vys¡l n¡ ?
         jne       IntVys8                  ; nen¡ p©eru¨en¡ p©i vys¡l n¡

         dec       dx
         dec       dx

; ------ zv˜¨en¡ ‡¡ta‡e bajt– sektoru

         mov       ah,ds:[CitByte]          ; ‡¡ta‡ bajt– sektoru
         inc       ah
         mov       ds:[CitByte],ah          ; nov˜ ‡¡ta‡ bajt– sektoru

; ------ test, zda jsou data sektoru

         cmp       ah,224+1                 ; jsou data sektoru ?
         jae       IntVys3                  ; nejsou data

; ------ vysl n¡ datov‚ho bajtu

         mov       bx,ds:[SektUkaz]         ; ukazatel dat sektoru
         mov       al,es:[bx]               ; bajt sektoru k vysl n¡
         inc       bx
         mov       ds:[SektUkaz],bx         ; nov˜ ukazatel dat sektoru
         jmp       short IntVys7

; ------ za‡ tek synchronizace

IntVys3: jne       IntVys4                  ; nen¡ za‡ tek synchronizace
         add       dx,4
         mov       al,1000b
         out       dx,al
         sub       dx,4

; ------ konec synchronizace

IntVys4: cmp       ah,226+1
         jne       IntVys5
         add       dx,4
         mov       al,1011b
         out       dx,al
         sub       dx,4

; ------ prvn¡ startovac¡ bajt

IntVys5: cmp       ah,228+1
         jb        IntVys6
         mov       al,2                     ; prvn¡ startovac¡ bajt
         je        IntVys7

; ------ druh˜ startovac¡ bajt

         cmp       byte ptr es:[8],33       ; je z vˆre‡n˜ synchr. sektor ?
         jne       IntVys52
         test      byte ptr es:[7],b14/HI   ; korek‡n¡ sektor ?
         jnz       IntVys57                 ; je z vˆre‡n˜ sychr. sektor

IntVys52:mov       word ptr ds:[SektUkaz],0
         cmp       word ptr ds:[CitSynch],0 ; je synchroniza‡n¡ sektor ?
         je        IntVys55                 ; nen¡ synchroniza‡n¡ sektor
         dec       word ptr ds:[CitSynch]   ; sn¡‘en¡ ‡¡ta‡e sektor–
         jmp       short IntVys56
IntVys55:add       word ptr ds:[SektSeg],224/16
         mov       es,ds:[SektSeg]
IntVys57:inc       word ptr ds:[CitSekt]    ; zv˜¨en¡ ‡¡ta‡e sektor–
IntVys56:mov       byte ptr ds:[CitByte],0  ; inicializace ‡¡ta‡e
         mov       al,0e5h                  ; druh˜ startovac¡ bajt
         jmp       short IntVys7

; ------ vysl n¡ datov‚ho bajtu

IntVys6: mov       al,0ffh
IntVys7: out       dx,al
         inc       dx
         inc       dx

; ------ zru¨en¡ stavov‚ho registru linky

IntVys8: add       dx,3                     ; 5: stavov˜ registr linky
         in        al,dx                    ; zru¨en¡ po‘adavku od reg. linky
         jmp       IntVys1            ; dal¨¡ test

; ------ n vrat registr–

IntVys9: mov       al,20h
         out       [20h],al                 ; uvolnˆn¡ ©adi‡e p©eru¨en¡
         pop       es
         pop       ds
         pop       dx
         pop       bx
         pop       ax
         iret

IntVys   ENDP

; -----------------------------------------------------------------------------
;        p©eru¨en¡ p©i p©¡jmu sektoru
; -----------------------------------------------------------------------------

IntPri   PROC      FAR

; ------ £schova registr–

         push      ax
         push      bx
         push      dx
         push      ds
         push      es

; ------ p©¡prava registr–

         push      cs
         pop       ds

; ------ prvn¡ ‡ten¡ stavov‚ho registru p©eru¨en¡

         mov       dx,ds:[AdrPort]          ; adresa portu COM
         mov       es,ds:[SektSeg]
         inc       dx
         inc       dx
         in        al,dx                    ; stavov˜ registr p©eru¨en¡
         jmp       short IntPri2

; ------ test, zda je dal¨¡ p©eru¨en¡ od portu

IntPri1: mov       dx,ds:[AdrPort]          ; adresa portu COM
         inc       dx
         inc       dx
         in        al,dx                    ; stavov˜ registr p©eru¨en¡
         test      al,1                     ; je dal¨¡ p©eru¨en¡ ?
         jz        IntPri2
         jmp       IntPri9                  ; nen¡ dal¨¡ p©eru¨en¡

; ------ test, zda je p©eru¨en¡ p©i p©¡jmu

IntPri2: cmp       al,4                     ; p©eru¨en¡ p©i p©¡jmu ?
         jne       IntPri8                  ; nen¡ p©eru¨en¡ p©i p©¡jmu

; ------ p©¡jem datov‚ho bajtu

         dec       dx
         dec       dx
         in        al,dx                    ; p©¡jem bajtu
         inc       dx
         inc       dx

; ------ ‡¡ta‡ bajt– sektoru

         mov       ah,ds:[CitByte]          ; ‡¡ta‡ bajt– sektoru
         cmp       ah,222                   ; je platn˜ bajt ?
         jae       IntPri3                  ; nen¡ platn˜ bajt

; ------ p©¡jem platn‚ho bajtu sektoru

IntPri25:mov       bx,ds:[SektUkaz]         ; ukazatel dat sektoru
         mov       es:[bx],al               ; ulo‘en¡ p©ijat‚ho bajtu
         inc       bx
         mov       ds:[SektUkaz],bx         ; nov˜ ukazatel dat sektoru
         jmp       short IntPri6

; ------ p©¡jem startovac¡ho bajtu

IntPri3: cmp       ax,0fe02h                ; prvn¡ startovac¡ bajt ?
         je        IntPri6                  ; prvn¡ startovac¡ bajt
         cmp       ax,0ffe5h                ; druh˜ startovac¡ bajt ?
         je        IntPri6                  ; druh˜ startovac¡ bajt

; ------ 2 synchroniza‡n¡ bajty

         cmp       al,0ffh                  ; synchronizace ?
         jne       IntPri7                  ; nen¡ synchronizace
         cmp       ah,-2                    ; je posledn¡ bajt ?
         je        IntPri61                 ; je posledn¡ bajt - nezv˜¨¡ se

; ------ zv˜¨en¡ ‡¡ta‡e bajt– sektoru

IntPri6: inc       ah
IntPri61:mov       ds:[CitByte],ah          ; nov˜ ‡¡ta‡ bajt– sektoru
         cmp       ah,222                   ; jsou ji‘ v¨echny bajty sektoru ?
         jne       IntPri8                  ; nejsou je¨tˆ v¨echny bajty sektoru
         cmp       byte ptr ds:[CitSynch],0 ; ‡ek  se na synchroniza‡n¡ sektor ?
         je        IntPri62                 ; ne‡ek  se na synchronizaci

         cmp       byte ptr es:[8],255      ; je sektor 255 ?
         jne       IntPri66                 ; nen¡ sektor 255
         test      byte ptr es:[7],b14/HI   ; je synchroniza‡n¡ sektor ?
         jz        IntPri66                 ; nen¡ synchroniza‡n¡ sektor
         dec       byte ptr ds:[CitSynch]   ; sn¡‘en¡ ‡¡ta‡e synch. sektor–
         jmp       short IntPri66

IntPri62:cmp       byte ptr es:[8],255      ; je sektor 255 ?
         jne       IntPri64                 ; nen¡ sektor 255
         test      byte ptr es:[7],b14/HI   ; je synchroniza‡n¡ sektor ?
         jnz       IntPri66                 ; je synchroniza‡n¡ sektor

IntPri64:inc       word ptr ds:[CitSekt]    ; zv˜¨en¡ ‡¡ta‡e sektor–
         add       word ptr ds:[SektSeg],224/16
         mov       es,ds:[SektSeg]
IntPri66:mov       word ptr ds:[SektUkaz],0 ; oprava ukazatele sektoru
IntPri7: mov       byte ptr ds:[CitByte],-4 ; ‡¡ta‡ startovac¡ch bajt–

; ------ zru¨en¡ stavov‚ho registru linky

IntPri8: add       dx,3                     ; 5: stavov˜ registr linky
         in        al,dx                    ; zru¨en¡ po‘adavku od reg. linky
         jmp       IntPri1                  ; dal¨¡ test

; ------ n vrat registr–

IntPri9: mov       al,20h
         out       [20h],al                 ; uvolnˆn¡ ©adi‡e p©eru¨en¡
         pop       es
         pop       ds
         pop       dx
         pop       bx
         pop       ax
         iret

IntPri   ENDP

; -----------------------------------------------------------------------------
;        odinstalov n¡ port–
; -----------------------------------------------------------------------------

DeInit   PROC      NEAR

; ------ £schova registr–

         push      ax
         push      dx

; ------ n vrat masky p©eru¨en¡

         cli
         mov       al,ds:[OldInt]           ; p–vodn¡ maska p©eru¨en¡
         out       [21h],al                 ; n vrat masky p©eru¨en¡

; ------ z kaz p©eru¨en¡ od portu

         mov       dx,ds:[AdrPort]          ; 0: adresa portu
         in        al,dx                    ; zru¨en¡ p©ijat‚ho znaku
         inc       dx                       ; 1: ©¡dic¡ registr p©eru¨en¡
         xor       al,al                    ; v¨echna p©eru¨en¡ zak z na
         out       dx,al                    ; z kaz v¨ech p©eru¨en¡
         inc       dx                       ; 2: stavov˜ registr p©eru¨en¡
         in        al,dx                    ; zru¨en¡ stavov‚ho registru p©eru¨.
         inc       dx                       ; 3: ©¡dic¡ registr linky
         inc       dx                       ; 4: ©¡dic¡ registr modemu
         xor       al,al                    ; v¨echny sign ly vypnuty
         out       dx,al                    ; vypnut¡ v¨ech sign l– modemu
         inc       dx                       ; 5: stavov˜ registr linky
         in        al,dx                    ; zru¨en¡ stavov‚ho registru linky
         inc       dx                       ; 6: stavov˜ registr modemu
         in        al,dx                    ; zru¨en¡ stavov‚ho registru modemu

; ------ uvolnˆn¡ ©adi‡e p©eru¨en¡

         mov       al,20h
         out       [20h],al

; ------ n vrat obsluhy p©eru¨en¡ od portu COM

         call      GetICom                  ; poskytnut¡ p©eru¨en¡ COM
         mov       ah,25h
         push      ds
         lds       dx,ds:[OldComm]          ; p–vodn¡ adresa INT COM
         int       21h                      ; n vrat p–vodn¡ adresy portu
         pop       ds

; ------ n vrat obsluhy p©eru¨en¡ INT 08h

         push      ds
         lds       dx,ds:[Old08]            ; p–vodn¡ adresa INT 08h
         mov       ax,2508h
         int       21h
         pop       ds

; ------ n vrat obsluhy p©eru¨en¡ INT 09h

         push      ds
         lds       dx,ds:[Old09]            ; p–vodn¡ adresa INT 09h
         mov       ax,2509h
         int       21h
         pop       ds
         sti

; ------ inicializace hodin

         call      InitClk                  ; n vrat nastaven¡ hodin

; ------ n vrat registr–

         pop       dx
         pop       ax
         ret

DeInit   ENDP

; -----------------------------------------------------------------------------
;        inicializace nastaven¡ hodin (AT)
; -----------------------------------------------------------------------------

InitClk  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      es

; ------ ‡ten¡ hodin syst‚mov‚ho ‡asu (AT)

         mov       dl,8ah                   ; p©ednastaven¡ na nesmysl
         mov       ah,2
         int       1ah                      ; ‡ten¡ hodin
         cmp       dl,1                     ; p©ete‡en¡ p©es p–lnoc ?
         ja        InitClk8                 ; hodiny neplat¡

; ------ n vrat p©¡znaku p©ete‡en¡ p©es p–lnoc

         jne       InitClk2                 ; nen¡ p©ete‡en¡ p©es p–lnoc
         xor       ax,ax
         mov       es,ax
         mov       byte ptr es:[470h],dl    ; n vrat p©¡znaku p©ete‡en¡ p–lnoci

; ------ konverze z BCD form tu na bin rn¡

InitClk2:mov       al,ch                    ; hodina
         call      BcdBin                   ; konverze na bin rn¡ tvar
         mov       ch,al
         mov       al,cl                    ; minuta
         call      BcdBin                   ; konverze na bin rn¡ tvar
         mov       cl,al
         mov       al,dh                    ; sekunda
         call      BcdBin                   ; konverze na bin rn¡ tvar
         mov       dh,al

; ------ nastaven¡ ‡asu

         xor       dl,dl                    ; setina sekundy
         mov       ah,2dh
         int       21h                      ; nastaven¡ syst‚mov‚ho ‡asu

; ------ n vrat registr–

InitClk8:pop       es
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

InitClk  ENDP

; -----------------------------------------------------------------------------
;        Konverze ‡¡sla z BCD tvaru na bin rn¡
; -----------------------------------------------------------------------------

BcdBin   PROC      NEAR

         push      cx
         mov       ch,al
         and       ch,0fh
         and       al,0f0h
         mov       cl,4
         shr       al,cl
         mov       cl,10
         mul       cl
         add       al,ch
         pop       cx
         ret

BcdBin   ENDP

; -----------------------------------------------------------------------------
;        obsluha hodin INT 08h
; -----------------------------------------------------------------------------

Int08    PROC      FAR

         push      ax
         mov       al,20h
         out       [20h],al
         sti
         inc       word ptr cs:[Citac08]    ; posun ‡¡ta‡e hodin
         pop       ax
         iret

Int08    ENDP

; -----------------------------------------------------------------------------
;        obsluha kl vesnice INT 09h
; -----------------------------------------------------------------------------

Int09    PROC      FAR

; ------ £schova registr–

         push      ax

; ------ uvolnˆn¡ ©adi‡e p©eru¨en¡

         mov       al,20h
         out       [20h],al                 ; uvolnˆn¡ ©adi‡e p©eru¨en¡
         sti                                ; povolen¡ p©eru¨en¡

         push      cx

; ------ uzam‡en¡ kl vesnice

         call      Cekej09                  ; ‡ek n¡ na p©ipravenost kl vesnice
         mov       al,0adh
         out       [64h],al                 ; vysl n¡ povelu-uzam‡en¡ kl vesnice

; ------ ‡ten¡ k¢du kl vesy

         call      Cekej09                  ; ‡ek n¡ na p©ipravenost kl vesnice
         in        al,[60h]

; ------ test, zda je p©eru¨en¡ ESC

         cmp       al,1                     ; je stisk kl vesy ESC ?
         jne       Int099                   ; nen¡ kl vesa ESC
         mov       byte ptr cs:[KeyBreak],1 ; p©¡znak p©eru¨en¡ kl vesou BREAK

; ------ uvolnˆn¡ kl vesnice

Int099:  call      Cekej09                  ; ‡ek n¡ na p©ipravenost kl vesnice
         mov       al,0aeh
         out       [64h],al                 ; vysl n¡ povelu-uvolnˆn¡ kl vesnice

; ------ n vrat registr–

         pop       cx
         pop       ax
         iret

Int09    ENDP

; -----------------------------------------------------------------------------
;        ‡ek n¡ na p©ipravenost kl vesnice
; -----------------------------------------------------------------------------

Cekej09  PROC      NEAR

         mov       cx,5000
Cekej091:in        al,[64h]
         test      al,b1
         loopnz    Cekej091
         ret

Cekej09  ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                            Obsluha zobrazen¡
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;þ
; -----------------------------------------------------------------------------
;        zobrazen¡ cel‚ obrazovky
; -----------------------------------------------------------------------------

DispAll  PROC      NEAR

         push      bx
         mov       bx,offset DatDefL        ; definice lev‚ho okna
         call      DispWin                  ; zobrazen¡ lev‚ho okna

         mov       bx,offset DatDefR        ; definice prav‚ho okna
         call      DispWin                  ; zobrazen¡ prav‚ho okna

         call      DispLHlp                 ; zobrazen¡ okna mal‚ n povˆdy

         pop       bx
         ret

DispAll  ENDP

; -----------------------------------------------------------------------------
;        normalizace kurzoru okna BX
; -----------------------------------------------------------------------------

NormWin  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx

; ------ p©¡prava registr–

         mov       ax,cs:[bx+SeznDAkt]      ; aktivn¡ polo‘ka
         mov       cx,cs:[bx+SeznDTop]      ; po‡ te‡n¡ polo‘ka
         mov       dx,cs:[bx+SeznDNum]      ; po‡et polo‘ek

; ------ kontrola aktivn¡ polo‘ky

         or        dx,dx
         jz        NormWin2                 ; nen¡ ‘ dn  polo‘ka
         cmp       ax,dx
         jb        NormWin2                 ; kurzor je OK
         mov       ax,dx
         dec       ax                       ; omezen¡ na posledn¡ polo‘ku
         mov       cs:[bx+SeznDAkt],ax      ; nov  aktivn¡ polo‘ka

; ------ kontrola podte‡en¡ po‡ tku

NormWin2:cmp       ax,cx                    ; je podte‡en¡ pod po‡ tek ?
         ja        NormWin3                 ; nen¡ podte‡en¡ pod po‡ tek
         mov       cx,ax                    ; omezen¡ po‡ tku zdola
         jcxz      NormWin3                 ; je prvn¡ © dek
         dec       cx

; ------ v˜po‡et maxim ln¡ho prvn¡ho © dku

NormWin3:or        dx,dx                    ; je nˆjak˜ © dek ?
         jz        NormWin5                 ; nen¡ ‘ dn˜ © dek
         dec       dx                       ; maxim ln¡ © dek
         cmp       ax,dx                    ; je maxim ln¡ © dek ?
         mov       dx,SOUBDIS               ; po‡et © dk– na str nku
         jae       NormWin4                 ; je posledn¡ © dek
         dec       dx                       ; je¨tˆ jeden © dek rezerva
NormWin4:dec       dx                       ; po‡et © dk– - 1
         sub       ax,dx                    ; maxim ln¡ prvn¡ © dek
         jae       NormWin6
NormWin5:xor       ax,ax                    ; omezen¡ max. prvn¡ho © dku

; ------ kontrola p©ete‡en¡ kurzoru za konec

NormWin6:cmp       ax,cx                    ; je p©ete‡en¡ za konec ?
         jbe       NormWin7                 ; kurzor je OK
         mov       cx,ax                    ; omezen¡ prvn¡ho © dku

; ------ ulo‘en¡ registr–

NormWin7:mov       cs:[bx+SeznDTop],cx      ; nov  prvn¡ polo‘ka

; ------ n vrat registr–

         pop       dx
         pop       cx
         pop       ax
         ret

NormWin  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ okna podle definice BX
; -----------------------------------------------------------------------------

DispWin  PROC      NEAR

         push      dx

         call      NormWin                  ; normalizace okna

; ------ pozice okna na obrazovce

         xor       dx,dx
         test      byte ptr cs:[bx+SeznPar],b1 ; je to prav‚ okno ?
         jz        DispWin1                 ; je to lev‚ okno
         mov       dl,40                    ; pozice prav‚ho okna

; ------ zobrazen¡ okna

DispWin1:call      DispTop                  ; zobrazen¡ horn¡ linky okna
         inc       dh
         call      DispCelk                 ; zobrazen¡ celkov‚ kapacity disku
         inc       dh
         call      DispAdr                  ; zobrazen¡ informace o adres ©i
         inc       dh
         call      DispPth                  ; zobrazen¡ aktivn¡ho adres ©e
         inc       dh
         call      DispFile                 ; zobrazen¡ soubor– adres ©e
         call      DispLin                  ; zobrazen¡ st©edn¡ oddˆlovac¡ ‡ ry
         inc       dh
         call      DispSel                  ; zobrazen¡ ozna‡en˜ch soubor–
         inc       dh
         call      DispReq                  ; zobrazen¡ po‘adovan‚ kapacity
         inc       dh
         call      DispEnd                  ; zobrazen¡ koncov‚ho © dku okna

         pop       dx
         ret

DispWin  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ horn¡ linky okna (pozice DX, definice BX)
; -----------------------------------------------------------------------------

DispTop  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx
         push      si
         push      di
         push      ds
         push      es

; ------ p©¡prava registr–

         sub       sp,40
         push      cs
         pop       ds
         push      ss
         pop       es

; ------ inicializace linky

         cld
         mov       di,sp                    ; ukl dac¡ adresa do z sobn¡ku
         mov       al,"É"                   ; lev˜ horn¡ roh
         stosb
         mov       al,"Í"
         mov       cx,38
         rep       stosb                    ; horn¡ linka
         mov       al,"»"
         stosb                              ; prav˜ horn¡ roh

; ------ d‚lka textu n zvu disku

         lea       si,[bx+SeznLab]          ; adresa textu n vˆ¨t¡
         mov       cx,cs:[bx+SeznLabN]      ; d‚lka n vˆ¨t¡ disku
         jcxz      DispTop6                 ; nen¡ nic k zobrazen¡
         cmp       cx,36                    ; maxim ln¡ d‚lka n vˆ¨t¡
         jb        DispTop2                 ; d‚lka je OK
         mov       cx,36                    ; omezen¡ d‚lky n vˆ¨t¡
DispTop2:mov       ax,38                    ; d‚lka © dku - 2
         sub       ax,cx                    ; zbytek na okraje
         shr       ax,1                     ; zbytek na lev˜ okraj
         add       ax,sp                    ; adresa po‡ tku textu
         xchg      ax,di                    ; DI <- ukl dac¡ adresa
         mov       al," "                   ; lev  mezera
         stosb                              ; ulo‘en¡ lev‚ mezery
         rep       movsb                    ; p©enos n vˆ¨t¡ do bufferu
         stosb                              ; ulo‘en¡ prav‚ mezery

; ------ zobrazen¡ linky

DispTop6:mov       si,sp                    ; buffer v z sobn¡ku
         mov       cl,40                    ; d‚lka textu
         mov       ah,cs:[Color1]           ; bˆ‘n  barva okna
         call      DispTxt                  ; zobrazen¡ textu

; ------ n vrat registr–

         add       sp,40
         pop       es
         pop       ds
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       ax
         ret

DispTop  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ druh‚ linky okna (pozice DX, definice BX)
; -----------------------------------------------------------------------------

DispCelk PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx
         push      si
         push      di
         push      ds
         push      es

; ------ p©¡prava registr–

         sub       sp,2*50
         push      cs
         pop       ds
         push      ss
         pop       es

; ------ dek¢dov n¡ voln‚ kapacity disku

         mov       di,sp                    ; buffer v z sobn¡ku

         test      byte ptr ds:[bx+SeznPar],b0 ; je to disk ?
         jnz       DispCel2                 ; nen¡ to disk

         push      dx

         mov       ax,word ptr ds:[DiskFree] ; voln  kapacita disku LOW
         mov       dx,word ptr ds:[DiskFree+2] ; voln  kapacita disku HIGH
         mov       cl,0                     ; d‚lka pole
         mov       ch,ds:[Color2]           ; vysv¡cen  barva textu
         call      DekNum                   ; dek¢dov n¡ ‡¡sla do z sobn¡ku

; ------ dek¢dov n¡ textu "B voln‚ z"

         mov       ah,ds:[Color1]           ; bˆ‘n  barva
         mov       si,offset TextFre1       ; text "B voln‚ z"
         call      DekTxt                   ; dek¢dov n¡ textu

; ------ dek¢dov n¡ celkov‚ kapacity disku

         mov       ax,word ptr ds:[DiskSumm] ; celkov  kapacita LOW
         mov       dx,word ptr ds:[DiskSumm+2] ; celkov  kapacita HIGH
         call      DekNum                   ; dek¢dov n¡ ‡¡sla

         pop       dx

DispCel2:


; ------ d‚lka textu

DispCel5:mov       cx,di                    ; konec textu
         sub       cx,sp                    ; d‚lka textu * 2
         shr       cx,1                     ; d‚lka textu (po‡et znak–)
         cmp       cx,38
         jb        DispCel6
         mov       cx,38                    ; omezen¡ d‚lky textu

; ------ zobrazen¡ lev‚ho okraje

DispCel6:push      cx                       ; d‚lka textu k zobrazen¡
         mov       ah,ds:[Color1]           ; bˆ‘n  barva textu
         mov       al,"º"                   ; lev˜ okraj
         call      Disp1Chr                 ; zobrazen¡ lev‚ho okraje
         sub       cx,38                    ; zbytek na okraje
         neg       cx
         shr       cx,1                     ; lev˜ okraj
         mov       al," "
         call      DispChr                  ; zobrazen¡ lev‚ho okraje
         pop       cx

; ------ zobrazen¡ textu

         mov       si,sp                    ; buffer v z sobn¡ku
         call      DispStr                  ; zobrazen¡ textu

; ------ zobrazen¡ prav‚ho okraje

         sub       cx,38                    ; zbytek na okraje
         neg       cx
         inc       cx                       ; zaokrouhlen¡ nahoru
         shr       cx,1                     ; prav˜ okraj
         call      DispChr                  ; zobrazen¡ prav‚ho okraje
         mov       al,"º"
         call      Disp1Chr                 ; zobrazen¡ prav‚ho okraje

; ------ n vrat registr–

         add       sp,2*50
         pop       es
         pop       ds
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       ax
         ret

DispCelk ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ t©et¡ linky okna (pozice DX, definice BX)
; -----------------------------------------------------------------------------

DispAdr PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx
         push      si
         push      di
         push      ds
         push      es

; ------ p©¡prava registr–

         sub       sp,2*50
         push      cs
         pop       ds
         push      ss
         pop       es

; ------ dek¢dov n¡ po‡tu soubor– v adres ©i

         mov       di,sp                    ; buffer v z sobn¡ku

         test      byte ptr ds:[bx+SeznPar],b0 ; je to disk ?
         jnz       DispAdr2                 ; nen¡ to disk

         push      dx

         mov       ax,ds:[bx+SeznFFil]      ; po‡et soubor– v adres ©i
         xor       dx,dx
         mov       cl,0                     ; d‚lka pole
         mov       ch,ds:[Color2]           ; vysv¡cen  barva textu
         call      DekNum                   ; dek¢dov n¡ ‡¡sla do z sobn¡ku

; ------ dek¢dov n¡ textu " souboru zabira "

         mov       ah,ds:[Color1]           ; bˆ‘n  barva
         mov       si,offset TextFil1       ; text " souboru zabira "
         call      DekTxt                   ; dek¢dov n¡ textu

; ------ dek¢dov n¡ kapacity disku zabran‚ soubory

         mov       ax,ds:[bx+SeznFSum]      ; zabran  kapacita LOW
         mov       dx,ds:[bx+SeznFSum+2]    ; zabran  kapacita HIGH
         call      DekNum                   ; dek¢dov n¡ ‡¡sla

; ------ dek¢dov n¡ textu " B"

         mov       ah,ds:[Color1]           ; bˆ‘n  barva textu
         mov       si,offset TextFil2       ; text " B"
         call      DekTxt                   ; dek¢dov n¡ textu

         pop       dx

DispAdr2:
         jmp       DispCel5                 ; zobrazen¡ textu

DispAdr  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ st©edn¡ linky okna (pozice DX, definice BX)
; -----------------------------------------------------------------------------

DispPth  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx
         push      si
         push      di
         push      ds
         push      es

; ------ p©¡prava registr–

         sub       sp,2*50                  ; buffer v z sobn¡ku
         push      cs
         pop       ds
         push      ss
         pop       es

; ------ inicializace linky

         cld
         mov       di,sp                    ; ukl dac¡ adresa do z sobn¡ku
         mov       ah,ds:[Color1]           ; bˆ‘n  barva textu
         mov       al,"Ì"                   ; lev˜ okraj
         stosw
         mov       al,"Í"
         mov       cx,38
         rep       stosw                    ; st©edn¡ linka
         mov       al,"¹"
         stosw                              ; prav˜ okraj

; ------ barva textu cesty

         test      byte ptr ds:[bx+SeznPar],b2 ; je okno aktivn¡ ?
         jz        DispPth1                 ; okno nen¡ aktivn¡
         mov       ah,ds:[Color3]           ; barva kurzoru

; ------ d‚lka textu adres ©e

DispPth1:mov       di,sp                    ; za‡ tek bufferu v z sobn¡ku
         inc       di
         inc       di                       ; po‡ te‡n¡ adresa k dek¢dov n¡
         lea       si,[bx+SeznDir]          ; adresa textu adres ©e
         mov       cx,ds:[bx+SeznDirN]      ; d‚lka textu adres ©e
         cmp       cx,36                    ; maxim ln¡ d‚lka cesty
         jbe       DispPth2                 ; d‚lka je OK

; ------ dek¢dov n¡ po‡ tku

         mov       al," "
         stosw                              ; £vodn¡ mezera
         lodsb
         stosw

; ------ pokud nebyl ROOT, dek¢dov n¡ disku

         cmp       al,"\"
         je        DispPt11                 ; disk se nezobraz¡
         lodsb
         stosw                              ; znak ":"
         lodsb
         stosw                              ; znak "\"
         add       si,cx                    ; konec textu cesty
         sub       si,3
         mov       cx,36-6                  ; omezen  d‚lka textu s diskem
         jmp       short DispPt12

; ------ omezen¡ d‚lky textu

DispPt11:add       si,cx                    ; konec textu cesty
         dec       si
         mov       cx,36-4                  ; omezen  d‚lka textu bez disku
DispPt12:sub       si,cx                    ; za‡ tek textu
         mov       al,"."
         stosw
         stosw
         stosw                              ; £vodn¡ te‡ky
         jmp       short DispPth3           ; dek¢dov n¡ textu cesty

; ------ vyst©edˆn¡ textu

DispPth2:mov       di,38                    ; d‚lka © dku - 2
         sub       di,cx                    ; zbytek na okraje
         and       di,not 1                 ; zarovn n¡ na slovo
         add       di,sp                    ; adresa po‡ tku textu
         mov       al," "                   ; lev  mezera
         stosw                              ; ulo‘en¡ lev‚ mezery

; ------ dek¢dov n¡ textu

DispPth3:lodsb
         stosw
         loop      DispPth3

; ------ z vˆre‡n  mezera

         mov       al," "
         stosw

; ------ zobrazen¡ linky

         mov       si,sp                    ; buffer v z sobn¡ku
         mov       cl,40                    ; d‚lka textu
         call      DispStr                  ; zobrazen¡ textu

; ------ n vrat registr–

         add       sp,2*50
         pop       es
         pop       ds
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       ax
         ret

DispPth  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ soubor– v adres ©ov‚m oknˆ (pozice DX, seznam BX)
; -----------------------------------------------------------------------------
; lok ln¡ promˆnn‚: SS:[BP-2] (2) ‡¡ta‡ aktivn¡ polo‘ky
;                   SS:[BP-4] (2) ukazatel ‡¡sla polo‘ky
; -----------------------------------------------------------------------------

DispFile PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      si
         push      di
         push      bp
         push      ds
         push      es
         mov       bp,sp

; ------ p©¡prava bufferu v z sobn¡ku

         sub       sp,2*40 + 4              ; vytvo©en¡ bufferu v z sobn¡ku

; ------ p©¡prava lok ln¡ch promˆnn˜ch

         mov       ax,cs:[bx+SeznDAkt]      ; polo‘ka s kurzorem
         sub       ax,cs:[bx+SeznDTop]      ; offset od prvn¡ polo‘ky
         inc       ax                       ; p©ednastaven¡ ‡¡ta‡e kurzoru
         mov       ss:[bp-2],ax             ; ‡¡ta‡ aktivn¡ polo‘ky (kurzoru)
         mov       ax,cs:[bx+SeznDTop]      ; prvn¡ zobrazen  polo‘ka
         mov       ss:[bp-4],ax             ; ukazatel ‡¡sla polo‘ky
         push      ss
         pop       es                       ; ES <- segmentu bufferu v z sobn¡ku
         mov       ds,cs:[bx+SeznDSeg]      ; segment za‡ tku adres ©e

; ------ inicializa‡n¡ dek¢dov n¡ okraj– © dku

DispFil1:cld
         mov       di,sp                    ; buffer v z sobn¡ku
         mov       ah,cs:[Color1]           ; bˆ‘n  barva textu
         mov       al,"º"                   ; znak okraje
         stosw                              ; ulo‘en¡ lev‚ho okraje
         mov       es:[di+2*38],ax          ; ulo‘en¡ prav‚ho okraje

; ------ dek¢dov n¡ kurzoru

         mov       al,cs:[Color2]           ; vysv¡cen  polo‘ka
         dec       word ptr ss:[bp-2]       ; sn¡‘en¡ ‡¡ta‡e aktivn¡ polo‘ky
         jnz       DispFil2                 ; nen¡ to © dek s kurzorem
         mov       byte ptr es:[di-2],"°"   ; znak kurzoru
         mov       byte ptr es:[di+2*38],"°"; znak kurzoru
         test      byte ptr cs:[bx+SeznPar],b2 ; je okno aktivn¡ ?
         jz        DispFil2                 ; nen¡ to aktivn¡ okno
         mov       si,ss:[bp-4]             ; ukazatel ‡¡sla polo‘ky
         cmp       si,cs:[bx+SeznDNum]      ; je platn‚ ‡¡slo polo‘ky ?
         jae       DispFil2                 ; nen¡ platn‚ ‡¡slo polo‘ky
         mov       ah,cs:[Color3]           ; barva kurzoru
         mov       al,cs:[Color4]           ; vysv¡cen˜ kurzor

; ------ rozli¨en¡ vysv¡cen‚ polo‘ky

DispFil2:mov       si,ss:[bp-4]             ; ukazatel ‡¡sla polo‘ky
         cmp       si,cs:[bx+SeznDNum]      ; je platn‚ ‡¡slo polo‘ky ?
         jae       DispFil3                 ; nen¡ platn‚ ‡¡slo polo‘ky
         shl       si,1
         mov       si,cs:[si+bx+SeznInd]    ; offset polo‘ky
         cmp       si,-1                    ; je to ".." ?
         je        DispFil3                 ; je to ".."
         test      byte ptr ds:[si+FileAtr],b7 ; je to ozna‡en  polo‘ka ?
         jz        DispFil3                 ; nen¡ ozna‡en  polo‘ka
         mov       ah,al                    ; barva ozna‡en‚ polo‘ky
DispFil3:push      ss
         pop       es                       ; ES <- segment bufferu v z sobn¡ku

; ------ p©¡prava neplatn‚ polo‘ky

         push      di
         mov       al," "
         mov       cx,12
         rep       stosw
         mov       al,"³"
         stosw
         mov       al," "
         mov       cl,10
         rep       stosw
         mov       al,"³"
         stosw
         mov       al," "
         mov       cl,8
         rep       stosw
         mov       al,"³"
         stosw
         mov       al," "
         mov       cl,5
         rep       stosw
         pop       di

; ------ adresa polo‘ky

DispFil4:mov       si,ss:[bp-4]             ; ‡¡slo polo‘ky
         cmp       si,cs:[bx+SeznDNum]      ; je to platn  polo‘ka ?
         jae       DispFil7                 ; nen¡ to platn  polo‘ka
         shl       si,1
         mov       si,cs:[bx+si+SeznInd]    ; offset polo‘ky
         cmp       si,-1                    ; je to ".." ?
         jne       DispFl42

; ------ dek¢dov n¡ polo‘ky ".."

         mov       al,"."
         stosw
         stosw
         add       di,11*2
         mov       si,offset DskTxtD        ; text "<NAD--ADR>"
         push      ds
         push      cs
         pop       ds
         call      DekTxt                   ; dek¢dov n¡ textu
         pop       ds
         jmp       short DispFil7

; ------ dek¢dov n¡ souboru

DispFl42:call      DekAFil                  ; dek¢dov n¡ jm‚na souboru
         mov       al,"³"
         test      byte ptr ds:[si+FileAtr],b7 ; je to ozna‡en  polo‘ka ?
         jz        DispFil5                 ; nen¡ to ozna‡en  polo‘ka
         mov       al,"²"                   ; indik tor ozna‡en‚ polo‘ky
DispFil5:stosw
         call      DekASiz                  ; dek¢dov n¡ velikosti souboru
         mov       al,"³"
         stosw
         call      DekADat                  ; dek¢dov n¡ data souboru
         mov       al,"³"
         stosw
         call      DekATim                  ; dek¢dov n¡ ‡asu souboru

; ------ zobrazen¡ jednoho © dku

DispFil7:inc       word ptr ss:[bp-4]       ; zv˜¨en¡ ukazatele polo‘ky
         mov       si,sp                    ; buffer v z sobn¡ku
         mov       cl,40                    ; d‚lka textu © dku
         push      dx
         call      DispStr                  ; zobrazen¡ jednoho © dku
         pop       dx

; ------ p©¡prava k zobrazen¡ dal¨¡ho © dku

         inc       dh                       ; zv˜¨en¡ ukazatele © dku
         cmp       dh,SOUBDIS+4
         jae       DispFil9
         jmp       DispFil1                 ; zobrazen¡ dal¨¡ho © dku

; ------ n vrat registr–

DispFil9:mov       sp,bp
         pop       es
         pop       ds
         pop       bp
         pop       di
         pop       si
         pop       cx
         pop       ax
         ret

DispFile ENDP

; -----------------------------------------------------------------------------
;        dek¢dov n¡ jm‚na souboru DS:SI do bufferu ES:DI, barva AH
; -----------------------------------------------------------------------------

DekAFil  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si

; ------ p©¡prava k dek¢dov n¡ jm‚na souboru

         cld
         mov       bl,ds:[si+FileAtr]       ; atributy souboru
         mov       bh,bl                    ; atributy souboru
         mov       ch,0
         mov       cl,ds:[si+FileOffs]      ; offset dal¨¡ polo‘ky
         sub       cl,FileName              ; d‚lka jm‚na souboru
         add       si,FileName              ; jm‚no souboru

; ------ dek¢dov n¡ jm‚na ".."

         mov       dx,8                     ; d‚lka jm‚na souboru
         cmp       cl,2
         jne       DekAFil1
         cmp       word ptr ds:[si],".."
         jne       DekAFil1
         mov       al,"."
         stosw
         stosw                              ; ulo‘en¡ textu ".."
         xor       cx,cx
         dec       dx
         dec       dx

; ------ dek¢dov n¡ jm‚na souboru

DekAFil1:mov       al," "                   ; n hradn¡ znak mezery
         jcxz      DekAFil2                 ; konec jm‚na
         cmp       byte ptr ds:[si],"."     ; je p©¡pona ?
         je        DekAFil2                 ; bude p©¡pona
         lodsb                              ; na‡ten¡ znaku jm‚na souboru
         dec       cx                       ; sn¡‘en¡ ‡¡ta‡e znak–
         test      bl,DIR + HID + SYS       ; bude velk‚ p¡smeno ?
         jnz       DekAFil2                 ; bude velk‚ p¡smeno
         cmp       al,"A"
         jb        DekAFil2                 ; nen¡ velk‚ p¡smeno
         cmp       al,"Z"
         ja        DekAFil2                 ; nen¡ velk‚ p¡smeno
         add       al,32                    ; korekce na mal‚ p¡smeno
DekAFil2:stosw                              ; ulo‘en¡ znaku jm‚na souboru
         and       bl,not HID + SYS         ; zru¨en¡ atribut– HID a SYS
         dec       dx                       ; ‡¡ta‡ znak– k ulo‘en¡
         jnz       DekAFil1                 ; dek¢dov n¡ dal¨¡ho znaku

; ------ oddˆlovac¡ znak mezi jm‚nem a p©¡ponou

         mov       al,"°"                   ; ozna‡en¡ soubor– HID a SYS
         test      bh,HID + SYS             ; je soubor HID nebo SYS ?
         jnz       DekAFil3                 ; soubor je HID nebo SYS
         mov       al,"*"                   ; ozna‡en¡ soubor– R/O
         test      bh,RO                    ; je soubor R/O ?
         jnz       DekAFil3                 ; soubor je R/O
         mov       al," "                   ; jinak mezera pro bˆ‘n˜ soubor
DekAFil3:stosw

; ------ zru¨en¡ oddˆlovac¡ te‡ky p©ed p©¡ponou

         mov       dx,3                     ; d‚lka p©¡pony jm‚na souboru
         jcxz      DekAFil4                 ; nen¡ dal¨¡ znak
         cmp       byte ptr ds:[si],"."
         jne       DekAFil4
         inc       si
         dec       cx

; ------ dek¢dov n¡ p©¡pony jm‚na souboru

DekAFil4:mov       al," "                   ; n hradn¡ znak mezery
         jcxz      DekAFil5                 ; nen¡ dal¨¡ znak
         lodsb                              ; na‡ten¡ znaku p©¡pony jm‚na soub.
         dec       cx                       ; sn¡‘en¡ ‡¡ta‡e znak–
         test      bl,DIR                   ; bude velk‚ p¡smeno ?
         jnz       DekAFil5                 ; bude velk‚ p¡smeno
         cmp       al,"A"
         jb        DekAFil5                 ; nen¡ velk‚ p¡smeno
         cmp       al,"Z"
         ja        DekAFil5                 ; nen¡ velk‚ p¡smeno
         add       al,32                    ; korekce na mal‚ p¡smeno
DekAFil5:stosw                              ; ulo‘en¡ znaku p©¡pony jm‚na soub.
         dec       dx                       ; ‡¡ta‡ znak– k ulo‘en¡
         jnz       DekAFil4                 ; dek¢dov n¡ dal¨¡ho znaku

; ------ n vrat registr–

         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

DekAFil  ENDP

; -----------------------------------------------------------------------------
;        dek¢dov n¡ velikosti souboru DS:SI do bufferu ES:DI, barva AH
; -----------------------------------------------------------------------------

DekASiz  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      dx
         push      si

; ------ test, zda je to adres ©

         test      byte ptr ds:[si+FileAtr],DIR ; je to adres © ?
         jz        DekASiz3                 ; nen¡ to adres © - je to soubor

; ------ dek¢dov n¡ identifika‡n¡ho textu adres ©e

;         cmp       word ptr ds:[si+FileName],".." ; je to nadadres © ?
;         mov       si,offset DskTxtD        ; text "<NAD--ADR>"
;         je        DekASiz2                 ; je to nadadres ©
         mov       si,offset DskTxtC        ; text "<POD--ADR>"
DekASiz2:push      ds
         push      cs
         pop       ds
         call      DekTxt                   ; dek¢dov n¡ textu
         pop       ds
         jmp       short DekASiz9

; ------ dek¢dov n¡ velikosti souboru (‡¡slo)

DekASiz3:mov       ch,ah                    ; barva textu
         mov       cl,10                    ; ¨¡©ka pole (pozic)
         mov       ax,ds:[si+FileSize]      ; velikost souboru LOW
         mov       dx,ds:[si+FileSize+2]    ; velikost souboru HIGH
         call      Dek10Num                 ; dek¢dov n¡ velikosti s omezen¡m

; ------ n vrat registr–

DekASiz9:pop       si
         pop       dx
         pop       bx
         pop       ax
         ret

DekASiz  ENDP

; -----------------------------------------------------------------------------
;        dek¢dov n¡ data souboru DS:SI do bufferu ES:DI, barva AH
; -----------------------------------------------------------------------------

DekADat  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx

; ------ dek¢dov n¡ dne

         cld
         mov       al,ds:[si+FileDate]      ; datum LOW
         and       al,31                    ; den
         mov       ch," "                   ; dopl¤uj¡c¡ znak
         call      DekADT                   ; dek¢dov n¡ dvoj‡¡sl¡
         mov       al,"."
         stosw                              ; oddˆlovac¡ znak data

; ------ dek¢dov n¡ mˆs¡ce

         mov       dx,ds:[si+FileDate]      ; datum souboru
         mov       cl,5                     ; po‡et rotac¡
         shr       dx,cl                    ; rotace mˆs¡ce
         mov       al,dl                    ; mˆs¡c
         and       al,15                    ; mˆs¡c
         call      DekADT                   ; dek¢dov n¡ dvoj‡¡sl¡
         mov       al,"."
         stosw                              ; oddˆlovac¡ znak data

; ------ dek¢dov n¡ roku

         mov       al,ds:[si+FileDate+1]    ; datum souboru HIGH
         shr       al,1                     ; rok
         add       al,80                    ; p©i‡ten¡ z kladu bez stolet¡
         cmp       al,100                   ; je posledn¡ dvoj‡¡sl¡ ?
         jb        DekADat2                 ; ‡¡slo je OK
         sub       al,100                   ; ode‡ten¡ stolet¡
         cmp       al,100                   ; je ji‘ posledn¡ dvoj‡¡sl¡ ?
         jb        DekADat2                 ; ‡¡slo je OK
         sub       al,100                   ; ode‡ten¡ stolet¡
DekADat2:call      DekADT                   ; dek¢dov n¡ dvoj‡¡sl¡ roku

; ------ n vrat registr–

         pop       dx
         pop       cx
         pop       ax
         ret

DekADat  ENDP

; -----------------------------------------------------------------------------
;        dek¢dov n¡ ‡asu souboru DS:SI do bufferu ES:DI, barva AH
; -----------------------------------------------------------------------------

DekATim  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx

; ------ dek¢dov n¡ hodiny

         cld
         mov       ch," "                   ; n hradn¡ znak
         mov       al,ds:[si+FileTime+1]    ; ‡as souboru
         mov       cl,11-8                  ; po‡et rotac¡
         shr       al,cl                    ; hodina
         call      DekADT                   ; dek¢dov n¡ £daje hodiny

; ------ oddˆlovac¡ znak ‡asu

         mov       al,":"                   ; oddˆlovac¡ znak ‡asu
         stosw                              ; ulo‘en¡ oddˆlovac¡ho znaku ‡asu

; ------ dek¢dov n¡ minuty

         mov       dx,ds:[si+FileTime]      ; ‡as souboru
         mov       cl,5                     ; po‡et rotac¡
         shr       dx,cl                    ; minuta
         mov       al,dl                    ; minuta
         and       al,3fh                   ; minuta
         call      DekADT                   ; dek¢dov n¡ £daje minut

; ------ n vrat registr–

         pop       dx
         pop       cx
         pop       ax
         ret

DekATim  ENDP

; -----------------------------------------------------------------------------
;        dek¢dov n¡ dvoj‡¡sl¡ s doplnˆn¡m znaku
; -----------------------------------------------------------------------------
; VSTUP: AL=‡¡slo
;        AH=barva textu
;        CH=znak k doplnˆn¡ (nam¡sto prvn¡ "0")
;        ES:DI=ukl dac¡ adresa (mus¡ b˜t smˆr CLD)
; VSTUP:ES:DI=nov  ukl dac¡ adresa
;        CH=znak k doplnˆn¡ "0"
; -----------------------------------------------------------------------------
; ni‡¡ registry AL a DX
; -----------------------------------------------------------------------------

DekADT   PROC      NEAR

         push      ax
         aam                                ; rozdˆlen¡ AL na 2 ‡¡slice
         add       ax,"00"                  ; korekce na ‡¡slice ASCII
         xchg      ax,dx                    ; £schova ‡¡sla do DX
         pop       ax
         mov       al,dh                    ; vy¨¨¡ ‡¡slice
         cmp       al,"0"                   ; je prvn¡ znak 0 ?
         jne       DekADT1                  ; nen¡ 0
         mov       al,ch                    ; n hradn¡ prvn¡ znak
DekADT1: stosw                              ; ulo‘en¡ prvn¡ ‡¡slice
         mov       al,dl                    ; druh  ‡¡slice
         stosw                              ; ulo‘en¡ druh‚ ‡¡slice
         mov       ch,"0"                   ; dopl¤uj¡c¡ znak "0"
         ret

DekADT   ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ horn¡ linky okna (pozice DX, definice BX)
; -----------------------------------------------------------------------------

DispLin  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx
         push      si
         push      di
         push      ds
         push      es

; ------ p©¡prava registr–

         sub       sp,2*40
         push      cs
         pop       ds
         push      ss
         pop       es

; ------ inicializace linky

         cld
         mov       di,sp                    ; ukl dac¡ adresa do z sobn¡ku
         mov       ah,ds:[Color1]
         mov       al,"Ì"                   ; lev˜ horn¡ roh
         stosw
         mov       al,"Í"
         mov       cx,38
         rep       stosw                    ; horn¡ linka
         mov       al,"¹"
         stosw                              ; prav˜ horn¡ roh

; ------ zobrazen¡ linky

DispLin6:mov       si,sp                    ; buffer v z sobn¡ku
         mov       cl,40                    ; d‚lka textu
         call      DispStr                  ; zobrazen¡ textu

; ------ n vrat registr–

         add       sp,2*40
         pop       es
         pop       ds
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       ax
         ret

DispLin  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ ozna‡en˜ch soubor– (pozice DX, definice BX)
; -----------------------------------------------------------------------------

DispSel  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx
         push      si
         push      di
         push      ds
         push      es

; ------ p©¡prava registr–

         sub       sp,2*50
         push      cs
         pop       ds
         push      ss
         pop       es

; ------ dek¢dov n¡ textu "oznaceno "

         mov       di,sp                    ; buffer v z sobn¡ku

         push      dx
         mov       ah,ds:[Color1]           ; bˆ‘n  barva
         mov       si,offset TextSel1       ; text "ozna‡eno "
         call      DekTxt                   ; dek¢dov n¡ textu

; ------ dek¢dov n¡ po‡tu ozna‡en˜ch soubor–

         mov       ax,ds:[bx+SeznSelN]      ; po‡et ozna‡en˜ch soubor–
         xor       dx,dx
         mov       cl,0                     ; d‚lka pole
         mov       ch,ds:[Color2]           ; vysv¡cen  barva
         call      DekNum                   ; dek¢dov n¡ ‡¡sla

; ------ dek¢dov n¡ textu " souboru / "

         mov       ah,ds:[Color1]           ; bˆ‘n  barva
         mov       si,offset TextSel2       ; text " souboru / "
         call      DekTxt                   ; dek¢dov n¡ textu

; ------ dek¢dov n¡ sou‡tu ozna‡en˜ch soubor–

         mov       ax,ds:[bx+SeznSelS]      ; ozna‡en  kapacita LOW
         mov       dx,ds:[bx+SeznSelS+2]    ; ozna‡en  kapacita HIGH
         call      DekNum                   ; dek¢dov n¡ ‡¡sla

; ------ dek¢dov n¡ textu " B"

         mov       ah,ds:[Color1]           ; bˆ‘n  barva textu
         mov       si,offset TextFil2       ; text " B"
         call      DekTxt                   ; dek¢dov n¡ textu

         pop       dx

         jmp       DispCel5                 ; zobrazen¡ textu

DispSel  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ po‘adovan‚ kapacity (pozice DX, definice BX)
; -----------------------------------------------------------------------------

DispReq  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx
         push      si
         push      di
         push      ds
         push      es

; ------ p©¡prava registr–

         sub       sp,2*50
         push      cs
         pop       ds
         push      ss
         pop       es

         mov       di,sp                    ; buffer v z sobn¡ku
         push      dx

; ------ rozli¨en¡, zda je streamer

         test      byte ptr ds:[bx+SeznPar],b0 ; je to streamer ?
         jz        DispReq2                 ; nen¡ streamer, je to disk

; ------ dek¢dov n¡ textu "po‘adovan  kapacita "

         mov       ah,ds:[Color1]           ; bˆ‘n  barva
         mov       si,offset TextReq1       ; text "po‘adovan  kapacita "
         call      DekTxt                   ; dek¢dov n¡ textu

; ------ dek¢dov n¡ po‘adovan‚ kapacity

         mov       ax,ds:[bx+SeznSelR]      ; po‘adovan  kapacita LOW
         mov       dx,ds:[bx+SeznSelR+2]    ; po‘adovan  kapacita HIGH
         mov       ch,ds:[Color2]           ; vysv¡cen  barva
         mov       cl,0                     ; d‚lka pole
         call      DekNum                   ; dek¢dov n¡ ‡¡sla

; ------ dek¢dov n¡ textu " B"

         mov       ah,ds:[Color1]           ; bˆ‘n  barva textu
         mov       si,offset TextFil2       ; text " B"
         call      DekTxt                   ; dek¢dov n¡ textu

DispReq1:pop       dx

         jmp       DispCel5                 ; zobrazen¡ textu

; ------ dek¢dov n¡ textu "d‚lka z znamu "

DispReq2:mov       ah,ds:[Color1]           ; bˆ‘n  barva
         mov       si,offset TextReq2       ; text "d‚lka z znamu "
         call      DekTxt                   ; dek¢dov n¡ textu

; ------ v˜po‡et po‘adovan‚ho ‡asu

         mov       ax,ds:[bx+SeznSelR]      ; po‘adovan˜ ‡as
         xor       dx,dx
         mov       cx,30                    ; po‡et sekund / 2
         div       cx                       ; v˜po‡et po‡tu sekund/2
         shl       dx,1                     ; p©epo‡et na sekundy
         mov       cl,60
         div       cl                       ; v˜po‡et po‡tu hodin a minut
                                            ; AL=hodiny, AH=minuty, DL=sekundy

; ------ dek¢dov n¡ po‡tu hodin

         mov       ch,ds:[Color2]           ; zv˜raznˆn  barva
         mov       cl,0                     ; odsazen¡
         or        al,al                    ; jsou hodiny ?
         jz        DispReq3                 ; nejsou hodiny

         push      ax
         push      dx
         mov       ah,0
         xor       dx,dx                    ; DX <- 0
         call      DekNum                   ; dek¢dov n¡ po‡tu hodin
         mov       ah,ds:[Color1]           ; bˆ‘n  barva
         mov       si,offset TextReq3       ; text " hod, "
         call      DekTxt                   ; dek¢dov n¡ textu
         pop       dx
         pop       ax

; ------ dek¢dov n¡ po‡tu minut

DispReq3:push      dx
         xor       dx,dx                    ; DX <- 0
         mov       al,ah                    ; po‡et minut
         mov       ah,0
         call      DekNum                   ; dek¢dov n¡ po‡tu minut
         mov       ah,ds:[Color1]           ; bˆ‘n  barva
         mov       si,offset TextReq4       ; text " min, "
         call      DekTxt                   ; dek¢dov n¡ textu
         pop       dx

; ------ dek¢dov n¡ po‡tu sekund

         mov       ax,dx                    ; AX <- po‡et sekund
         xor       dx,dx                    ; DX <- 0
         call      DekNum                   ; dek¢dov n¡ po‡tu sekund
         mov       ah,ds:[Color1]           ; bˆ‘n  barva
         mov       si,offset TextReq5       ; text " sek"
         call      DekTxt                   ; dek¢dov n¡ tetu
         jmp       short DispReq1

DispReq  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ koncov‚ linky okna (pozice DX, definice BX)
; -----------------------------------------------------------------------------

DispEnd  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx
         push      si
         push      di
         push      ds
         push      es

; ------ p©¡prava registr–

         sub       sp,2*40
         push      cs
         pop       ds
         push      ss
         pop       es

; ------ inicializace linky

         cld
         mov       di,sp                    ; ukl dac¡ adresa do z sobn¡ku
         mov       ah,ds:[Color1]
         mov       al,"È"                   ; lev˜ doln¡ roh
         stosw
         mov       al,"Í"
         mov       cx,38
         rep       stosw                    ; doln¡ linka
         mov       al,"¼"
         stosw                              ; prav˜ doln¡ roh

; ------ zobrazen¡ linky

DispEnd6:mov       si,sp                    ; buffer v z sobn¡ku
         mov       cl,40                    ; d‚lka textu
         call      DispStr                  ; zobrazen¡ textu

; ------ n vrat registr–

         add       sp,2*40
         pop       es
         pop       ds
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       ax
         ret

DispEnd  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ okna mal‚ n povˆdy
; -----------------------------------------------------------------------------

DispLHlp PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      ds
         push      es

; ------ p©¡prava registr–

         sub       sp,2*80
         push      ss
         pop       es
         push      cs
         pop       ds

; ------ zobrazen¡ horn¡ linky

         mov       dx,20*HI
         mov       ah,ds:[ColorH1]          ; bˆ‘n  barva n povˆdy
         mov       al,"Ú"
         call      Disp1Chr                 ; lev˜ horn¡ roh
         mov       al,"Ä"
         mov       cl,78
         call      DispChr                  ; horn¡ linka
         mov       al,"¿"
         call      Disp1Chr

; ------ p©¡prava k zobrazen¡ vnit©n¡ch © dk–

         mov       dh,21                    ; po‡ te‡n¡ © dek
         mov       si,ds:[AdrHlp]           ; adresa textu n povˆdy

; ------ vymaz n¡ © dku v bufferu

DispLHl2:mov       ah,ds:[ColorH1]          ; bˆ‘n  barva n povˆdy
         mov       dl,ds:[ColorH2]          ; vysv¡cen  barva n povˆdy
         mov       di,sp                    ; buffer v z sobn¡ku
         cld
         mov       al,"³"                   ; lev˜ okraj
         stosw                              ; ulo‘en¡ lev‚ho okraje
         mov       al," "                   ; vnit©n¡ mezera
         mov       cx,78
         rep       stosw                    ; ulo‘en¡ vnit©n¡ mezery
         mov       al,"³"                   ; prav˜ okraj
         stosw                              ; ulo‘en¡ prav‚ho okraje

; ------ d‚lka © dku textu

         cld
         lodsb                              ; bajt d‚lky
         mov       cl,al                    ; d‚lka textu
         mov       ch,0
         jcxz      DispLHl6                 ; je pr zdn˜ © dek

; ------ ur‡en¡ ukl dac¡ adresy textu

         mov       di,cx                    ; d‚lka textu
         sub       di,80
         neg       di
         and       di,not 1
         add       di,sp                    ; ukl dac¡ adresa

; ------ dek¢dov n¡ textu © dku

DispLHl3:lodsb                              ; bajt textu
         cmp       al,0                     ; je p©ep¡na‡ barvy ?
         jne       DispLHl4                 ; nen¡ p©ep¡na‡ barvy
         xchg      ah,dl                    ; zmˆna barvy
         jmp       short DispLHl3           ; dal¨¡ znak
DispLHl4:stosw                              ; ulo‘en¡ znaku do bufferu
         loop      DispLHl3                 ; dal¨¡ znak

; ------ zobrazen¡ textu © dku

DispLHl6:push      si
         mov       si,sp                    ; buffer © dku
         inc       si
         inc       si                       ; p©esko‡en¡ slova v z sobn¡ku
         mov       cl,80                    ; d‚lka © dku
         mov       dl,0                     ; po‡ te‡n¡ pozice
         call      DispStr                  ; zobrazen¡ textu © dku
         pop       si

; ------ p©¡prava k zobrazen¡ dal¨¡ho © dku

         inc       dh                       ; zv˜¨en¡ ukazatele © dku
         cmp       dh,24
         jne       DispLHl2                 ; dal¨¡ © dek

; ------ zobrazen¡ doln¡ linky

         mov       dl,0
         mov       ah,ds:[ColorH1]          ; bˆ‘n  barva n povˆdy
         mov       al,"À"
         call      Disp1Chr                 ; lev˜ doln¡ roh
         mov       al,"Ä"
         mov       cl,2
         call      DispChr                  ; lev˜ okraj
         mov       si,offset LicTxt1
         mov       cl,22
         call      DispTxt
         mov       cl,30
         call      DispChr
         mov       si,offset LicTxt2
         mov       cl,22
         call      DispTxt
         mov       cl,2
         call      DispChr
         mov       al,"Ù"
         call      Disp1Chr

; ------ n vrat registr–

         add       sp,2*80
         pop       es
         pop       ds
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

DispLHlp ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                                Obsluha seznam–
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;þ
; -----------------------------------------------------------------------------
;        inicializace seznamu BX
; -----------------------------------------------------------------------------

InitSezn PROC      NEAR

         push      ax
         push      cx
         push      si
         push      di
         push      es

         push      ds
         pop       es

         mov       ax,ds:[DatFre]           ; za‡ tek voln‚ho m¡sta
         mov       ds:[bx+SeznSeg],ax       ; adresa seznamu soubor–
         mov       ds:[bx+SeznDSeg],ax      ; adresa za‡ tku adres ©e
         mov       ds:[bx+SeznEnd],ax       ; adresa konce seznamu

         xor       ax,ax
         mov       ds:[bx+SeznNum],ax       ; po‡et polo‘ek v seznamu
         mov       ds:[bx+SeznDNum],ax      ; po‡et polo‘ek adres ©e
         mov       ds:[bx+SeznDAkt],ax      ; aktivn¡ polo‘ka adres ©e
         mov       ds:[bx+SeznDTop],ax      ; po‡ te‡n¡ polo‘ka adres ©e

         mov       ds:[bx+SeznFFil],ax      ; po‡et soubor– v adres ©i
         mov       ds:[bx+SeznFSum],ax      ; zabran  kapacita LOW
         mov       ds:[bx+SeznFSum+2],ax    ; zabran  kapacita HIGH

         mov       ds:[bx+SeznSelN],ax      ; po‡et ozna‡en˜ch soubor–
         mov       ds:[bx+SeznSelS],ax      ; ozna‡en  kapacita LOW
         mov       ds:[bx+SeznSelS+2],ax    ; ozna‡en  kapacita HIGH
         mov       ds:[bx+SeznSelR],ax      ; po‘adovan  kapacita LOW
         mov       ds:[bx+SeznSelR+2],ax    ; po‘adovan  kapacita HIGH

         mov       word ptr ds:[bx+SeznDirN],1 ; d‚lka textu adres ©e
         mov       byte ptr ds:[bx+SeznDir],"\" ; ozna‡en¡ adres ©e ROOT

         test      byte ptr ds:[bx+SeznPar],b0 ; je to disk ?
         jz        InitSzn2                 ; je to streamer

         mov       si,offset TextStr
         mov       cx,8
         mov       ds:[bx+SeznLabN],cx      ; d‚lka n vˆ¨t¡
         cld
         lea       di,[bx+SeznLab]
         rep       movsb

InitSzn2:;mov       byte ptr ds:[bx+SeznPar2],b0 ; t©¡dˆn¡ podle jm‚na

         pop       es
         pop       di
         pop       si
         pop       cx
         pop       ax
         ret

InitSezn ENDP

; -----------------------------------------------------------------------------
;        v˜bˆr soubor– z adres ©e ES:SI seznamu BX
; -----------------------------------------------------------------------------

SelFile  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         push      ds
         push      es

; ------ inicializace ukazatel–

         xor       ax,ax
         mov       ds:[bx+SeznFFil],ax      ; po‡et soubor– v adres ©i
         mov       ds:[bx+SeznFSum],ax      ; zabran  kapacita LOW
         mov       ds:[bx+SeznFSum+2],ax    ; zabran  kapacita HIGH
         mov       ds:[bx+SeznDNum],ax      ; po‡et polo‘ek adres ©e
         mov       ds:[bx+SeznDAkt],ax      ; aktivn¡ polo‘ka
         mov       ds:[bx+SeznDTop],ax      ; prvn¡ zobrazen  polo‘ka
         mov       ds:[bx+SeznDSeg],es      ; segment adres ©e

; ------ p©¡prava registr–

         push      es
         pop       ds                       ; DS <- segment adres ©e
         push      cs
         pop       es                       ; ES <- CS
         lea       di,[bx+SeznInd]          ; indexov  tabulka soubor–
         mov       bp,bx                    ; ukazatel parametr–
         mov       cx,word ptr cs:[BlokByte] ; po‡et bajt– na blok LOW
         mov       bx,word ptr cs:[BlokByte+2] ; po‡et bajt– na blok HIGH
         cld

; ------ inicializa‡n¡ polo‘ka ".." pro podadres ©e

         mov       ax,ds
         cmp       ax,cs:[bp+SeznSeg]       ; je to ROOT ?
         je        SelFile2                 ; nen¡ to ROOT
         mov       ax,-1
         stosw                              ; p©¡znak ROOT
         inc       word ptr cs:[bp+SeznDNum] ; ‡¡ta‡ soubor–

; ------ test, zda je ji‘ maximum soubor– v adres ©i

SelFile2:cmp       word ptr cs:[bp+SeznDNum],MAXFILES
         jae       SelFile6

; ------ test, zda je ji‘ konec adres ©e

         cmp       byte ptr ds:[si],0       ; konec adres ©e
         je        SelFile6                 ; konec adres ©e

; ------ ‡¡t n¡ platn˜ch soubor–

         test      byte ptr ds:[si+FileAtr],DIR ; je to adres © ?
         jnz       SelFile3                 ; je to adres ©
         inc       word ptr cs:[bp+SeznFFil] ; zv˜¨en¡ ‡¡ta‡e soubor–

; ------ v˜po‡et zabran‚ kapacity souborem

         mov       ax,ds:[si+FileSize]
         mov       dx,ds:[si+FileSize+2]
         add       ax,cx
         adc       dx,bx
         sub       ax,1
         sbb       dx,0
         call      Vydel
         call      Nasob
         add       word ptr cs:[bp+SeznFSum],ax
         adc       word ptr cs:[bp+SeznFSum+2],dx

; ------ ulo‘en¡ adresy polo‘ky

SelFile3:mov       ax,si
         stosw
         inc       word ptr cs:[bp+SeznDNum]     ; ‡¡ta‡ polo‘ek v adres ©i

; ------ adresa dal¨¡ polo‘ky

         lodsb
         dec       si
         mov       ah,0
         add       si,ax
         jmp       short SelFile2           ; dal¨¡ polo‘ka

; ------ n vrat registr–

SelFile6:pop       es
         pop       ds
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

SelFile  ENDP

; -----------------------------------------------------------------------------
;        set©¡dˆn¡ adres ©e seznamu BX
; -----------------------------------------------------------------------------

SortFile PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         push      ds
         push      es

; ------ p©¡prava registr–

         cld
         mov       dl,ds:[bx+SeznPar2]      ; t©¡dˆn¡ okna
         test      dl,b4                    ; t©¡d¡ se adres © ?
         jnz       SortFl99                 ; adres © se net©¡d¡

         mov       es,ds:[bx+SeznDSeg]      ; segment za‡ tku adres ©e
         mov       cx,ds:[bx+SeznDNum]      ; po‡et polo‘ek adres ©e
         add       bx,SeznInd               ; tabulka index–

; ------ p©esko‡en¡ prvn¡ polo‘ky ".."

         cmp       word ptr ds:[bx],-1      ; je prvn¡ polo‘ka ".." ?
         jne       SortFil1                 ; neza‡¡n  polo‘kou ".."
         inc       bx
         inc       bx                       ; p©esko‡en¡ prvn¡ polo‘ky
         dec       cx

; ------ kontrola minim ln¡ho mno‘stv¡ polo‘ek

SortFil1:push      es
         pop       ds                       ; DS <- segment soubor–
         cmp       cx,2                     ; minim lnˆ 2 polo‘ky
         jge       SortFl12                 ; po‡et polo‘ek OK
SortFl99:jmp       SortFil9                 ; m lo polo‘ek k set©¡dˆn¡
SortFl12:dec       cx                       ; po‡et p r– polo‘ek k set©¡dˆn¡
         mov       bp,cx                    ; £schova po‡tu p r– polo‘ek

; ------ adresa 2 polo‘ek k porovn n¡

SortFil2:mov       si,cs:[bx]               ; prvn¡ polo‘ka
         mov       di,cs:[bx+2]             ; druh  polo‘ka

; ------ test po©ad¡ adres ©/soubor

         mov       al,ds:[si+FileAtr]       ; atributy prvn¡ho souboru
         mov       ah,ds:[di+FileAtr]       ; atributy druh‚ho
         and       ax,DIR*HI + DIR          ; atributy adres ©–
         cmp       al,ah                    ; je po©ad¡ OK ?
         ja        SortFl82                 ; po©ad¡ je OK
         jb        SortFl72                 ; nutn  z mˆna polo‘ek

; ------ porovn n¡ velikosti

         test      dl,b2                    ; je t©¡dˆn¡ podle velikosti ?
         jz        SortFil3                 ; nen¡ t©¡dˆn¡ podle velikosti
         mov       ax,ds:[di+FileSize+2]    ; velikost druh‚ polo‘ky HIGH
         cmp       ax,ds:[si+FileSize+2]    ; velikost prvn¡ polo‘ky HIGH
         jne       SortFil5                 ; velikost HIGH nen¡ shodn 
         mov       ax,ds:[di+FileSize]      ; velikost druh‚ polo‘ky LOW
         cmp       ax,ds:[si+FileSize]      ; velikost prvn¡ polo‘ky LOW
         jmp       short SortFil5

; ------ porovn n¡ data a ‡asu

SortFil3:test      dl,b3                    ; je t©¡dˆn¡ podle data a ‡asu ?
         jz        SortFil4                 ; nen¡ t©¡dˆn¡ podle data a ‡asu
         mov       ax,ds:[di+FileDate]      ; datum druh‚ polo‘ky
         cmp       ax,ds:[si+FileDate]      ; datum prvn¡ polo‘ky
         jne       SortFil5                 ; datum nen¡ shodn‚
         mov       ax,ds:[di+FileTime]      ; ‡as druh‚ polo‘ky
         cmp       ax,ds:[si+FileTime]      ; ‡as prvn¡ polo‘ky
         jmp       short SortFil5

; ------ t©¡dˆn¡ podle p©¡pony

SortFil4:test      dl,b1                    ; je t©¡dˆn¡ podle p©¡pony ?
         jz        SortFil6                 ; nen¡ t©¡dˆn¡ podle p©¡pony
         call      SortExt                  ; porovn n¡ podle p©¡pony

; ------ vˆtven¡ polo‘ek

SortFil5:jb        SortFil8                 ; OK
         ja        SortFil7                 ; z mˆna

; ------ porovn n¡ jmen

SortFil6:call      SortName                 ; porovn n¡ podle jm‚na
         jbe       SortFil8

; ------ z mˆna polo‘ek

SortFil7:test      dl,b5                    ; je sestupn‚ t©¡dˆn¡ ?
         jnz       SortFl82                 ; je sestupn‚ t©¡dˆn¡
SortFl72:mov       ax,cs:[bx]
         xchg      ax,cs:[bx+2]
         mov       cs:[bx],ax
         cmp       cx,bp                    ; je ji‘ po‡ tek adres ©e ?
         jae       SortFl82                 ; je ji‘ po‡ tek adres ©e
         dec       bx                       ; posun na p©edchoz¡ p r polo‘ek
         dec       bx
         inc       cx                       ; p©ednastaven¡ ‡¡ta‡e
         jmp       short SortFil2           ; test p©edchoz¡ho p ru polo‘ek

; ------ polo‘ky OK - dal¨¡ p r polo‘ek

SortFil8:test      dl,b5                    ; je sestupn‚ t©¡dˆn¡ ?
         jnz       SortFl72                 ; je sestupn‚ t©¡dˆn¡
SortFl82:inc       bx
         inc       bx                       ; adresa dal¨¡ho p ru polo‘ek
         loop      SortFil2                 ; test dal¨¡ho p ru polo‘ek

; ------ n vrat registr–

SortFil9:pop       es
         pop       ds
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

SortFile ENDP

; -----------------------------------------------------------------------------
;        porovn n¡ polo‘ek DS:SI a ES:DI podle p©¡pony (CLD, ni‡¡ AX)
; -----------------------------------------------------------------------------

SortExt  PROC      NEAR

; ------ £schova registr–

         push      si
         push      di
         push      cx

; ------ p©¡prava registr–

         mov       cl,ds:[si]               ; d‚lka jm‚na polo‘ky 1
         mov       ch,ds:[di]               ; d‚lka jm‚na polo‘ky 2
         sub       cx,FileName*HI + FileName ; d‚lka jm‚na
         add       si,FileName
         add       di,FileName

; ------ nalezen¡ p©¡pony polo‘ky 1

SortExt1:or        cl,cl
         jz        SortExt2                 ; je konec jm‚na 1
         lodsb
         dec       cx
         cmp       al,"."
         jne       SortExt1

; ------ nalezen¡ p©¡pony polo‘ky 2

SortExt2:or        ch,ch
         jz        SortExt3                 ; je konec jm‚na 2
         mov       al,ds:[di]
         inc       di
         dec       ch
         cmp       al,"."
         jne       SortExt2

; ------ test, zda je konec jmen (ZY=polo‘ky shodn‚)

SortExt3:mov       al,cl
         or        al,ch
         jz        SortExt9                 ; konec jmen - p©¡pony shodn‚

; ------ je-li konec polo‘ky 1, je polo‘ka 1 men¨¡ ne‘ polo‘ka 2

         or        cl,cl
         jz        SortExt4                 ; je konec polo‘ky 1

; ------ je-li konec polo‘ky 2, je polo‘ka 2 men¨¡ ne‘ polo‘ka 1

         or        ch,ch
         jnz       SortExt5                 ; nen¡ konec polo‘ky 2
SortExt4:cmp       cl,ch                    ; nastaven¡ p©¡znaku NC nebo CY
         jmp       short SortExt9           ; polo‘ka 2 krat¨¡ ne‘ polo‘ka 1

; ------ porovn n¡ 1 znaku polo‘ek

SortExt5:dec       ch
         dec       cl
         cmpsb                              ; porovn n¡ jednoho znaku
         je        SortExt3                 ; shoda - dal¨¡ znak

; ------ n vrat registr–

SortExt9:pop       cx
         pop       di
         pop       si
         ret

SortExt  ENDP

; -----------------------------------------------------------------------------
;        porovn n¡ polo‘ek DS:SI a ES:DI podle jm‚na (ni‡¡ SI a DI)
; -----------------------------------------------------------------------------

SortName PROC      NEAR

; ------ £schova registr–

         push      cx

; ------ p©¡prava registr–

         mov       cl,ds:[si]               ; d‚lka jm‚na polo‘ky 1
         mov       ch,ds:[di]               ; d‚lka jm‚na polo‘ky 2
         sub       cx,FileName*HI + FileName ; d‚lka jm‚n
         add       si,FileName
         add       di,FileName

; ------ test, zda je konec jmen (ZY=polo‘ky shodn‚)

SortNam1:mov       al,cl
         or        al,ch
         jz        SortNam9                 ; konec jmen - polo‘ky shodn‚

; ------ je-li konec polo‘ky 1, je polo‘ka 1 men¨¡ ne‘ polo‘ka 2

         or        cl,cl
         jz        SortNam2                 ; je konec polo‘ky 1

; ------ je-li konec polo‘ky 2, je polo‘ka 2 men¨¡ ne‘ polo‘ka 1

         or        ch,ch
         jnz       SortNam3                 ; nen¡ konec polo‘ky 2
SortNam2:cmp       cl,ch                    ; nastaven¡ p©¡znaku NC
         jmp       short SortNam9           ; polo‘ka 2 krat¨¡ ne‘ polo‘ka 1

; ------ porovn n¡ 1 znaku polo‘ek

SortNam3:dec       ch
         dec       cl
         cmpsb                              ; porovn n¡ jednoho znaku
         je        SortNam1                 ; shoda - dal¨¡ znak

; ------ pokud byla te‡ka polo‘ky 1, je polo‘ka 1 men¨¡ ne‘ polo‘ka 2

         mov       al,"."
         pushf
         cmp       ds:[si-1],al             ; byla te‡ka polo‘ky 1 ?
         jne       SortNam4                 ; nebyla te‡ka
         popf                               ; zru¨en¡ F
         cmp       al,"."+1                 ; nastaven¡ p©¡znaku CY
         jmp       short SortNam9

; ------ pokud byla te‡ka polo‘ky 2, je polo‘ka 2 men¨¡ ne‘ polo‘ka 1

SortNam4:cmp       ds:[di-1],al             ; byla te‡ka polo‘ky 2 ?
         jne       SortNam5                 ; nebyla te‡ka
         popf
         cmp       al,"."-1                 ; nastaven¡ p©¡znaku NC
         pushf
SortNam5:popf

; ------ n vrat registr–

SortNam9:pop       cx
         ret

SortName ENDP

; -----------------------------------------------------------------------------
;    nalezen¡ cesty ASCIIZ DS:SI v seznamu BX -> ES:DI adresa adres ©e, CY=nen¡
; -----------------------------------------------------------------------------

SrcPath  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si

; ------ vypu¨tˆn¡ za‡ tku cesty

         cld
         cmp       byte ptr ds:[si],0
         je        SrcPath0
         cmp       byte ptr ds:[si+1],":"
         jne       SrcPath0
         lodsw

; ------ p©¡prava registr–

SrcPath0:mov       es,ds:[bx+SeznSeg]       ; segment seznamu soubor–
         xor       di,di                    ; po‡ te‡n¡ adresa

; ------ vypu¨tˆn¡ oddˆlova‡e adres ©e

SrcPath1:cmp       byte ptr ds:[si],"\"
         jne       SrcPath2
         inc       si

; ------ test, zda je konec cesty

SrcPath2:cmp       byte ptr ds:[si],0       ; je konec cesty ?
         je        SrcPath9                 ; je konec cesty - adres © nalezen

; ------ nalezen¡ podadres ©e

         xor       cx,cx                    ; ‡¡ta‡ adres ©–
SrcPath3:cmp       byte ptr es:[di],0       ; konec adres ©e ?
         stc                                ; p©¡znak chyby
         je        SrcPath9                 ; je konec adres ©e - nenalezeno
         test      byte ptr es:[di+FileAtr],DIR ; je to adres © ?
         jz        SrcPath5                 ; nen¡ to adres ©

; ------ porovn n¡ jednoho adres ©e

         push      si

         push      cx
         push      di

         mov       ch,0
         mov       cl,es:[di+FileOffs]      ; offset dal¨¡ polo‘ky
         sub       cl,FileName              ; d‚lka jm‚na adres ©e
         add       di,FileName              ; jm‚no adres ©e
         repe      cmpsb                    ; porovn n¡ jm‚na adres ©e

         pop       di
         pop       cx

         jne       SrcPath4                 ; nen¡ shodn‚ jm‚no

         cmp       byte ptr ds:[si],0
         je        SrcPath6                 ; je konec jm‚na adres ©e
         cmp       byte ptr ds:[si],"\"
         je        SrcPath6                 ; je konec jm‚na adres ©e

SrcPath4:pop       si
         inc       cx                       ; zv˜¨en¡ ‡¡ta‡e adres ©–

; ------ nalezen¡ dal¨¡ polo‘ky

SrcPath5:mov       al,es:[di+FileOffs]      ; offset dal¨¡ polo‘ky
         mov       ah,0
         add       di,ax                    ; adresa dal¨¡ polo‘ky
         call      NormESDI                 ; normalizace adresy
         jmp       short SrcPath3           ; test dal¨¡ polo‘ky

; ------ adres © nalezen - ukazatel v cestˆ se posune

SrcPath6:pop       ax                       ; zru¨en¡ adresy ze z sobn¡ku

; ------ nalezen¡ nov‚ho adres ©e

SrcPath7:push      cx
         call      SrcEAdr                  ; posun na konec adres ©e
         pop       cx
         call      SrcPth                   ; p©esko‡en¡ CX adres ©–
         jmp       short SrcPath1           ; nalezen¡ dal¨¡ho podadres ©e

; ------ n vrat registr–

SrcPath9:pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

SrcPath  ENDP

; -----------------------------------------------------------------------------
;        posun o CX podadres ©– od adresy ES:DI
; -----------------------------------------------------------------------------

SrcPth   PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         jcxz      SrcPth9                  ; nen¡ podadres ©

; ------ posun o CX podadres ©–

SrcPth1: push      cx
         call      SrcEAdr                  ; posun na konec adres ©e
         call      SrcPth                   ; posun o podadres ©e
         pop       cx
         loop      SrcPth1

; ------ n vrat registr–

SrcPth9: pop       cx
         pop       ax
         ret

SrcPth   ENDP

; -----------------------------------------------------------------------------
;        nalezen¡ konce adres ©e ES:DI -> CX=po‡et podadres ©–
; -----------------------------------------------------------------------------

SrcEAdr  PROC      NEAR

; ------ £schova registr–

         push      ax
         xor       cx,cx                    ; CX <- 0 ‡¡ta‡ adres ©–

; ------ test, zda je konec adres ©e

SrcEAdr1:mov       al,es:[di]               ; offset dal¨¡ho podadres ©e
         mov       ah,0
         or        ax,ax
         jz        SrcEAdr3                 ; je konec adres ©e

; ------ test, zda je to adres ©

         test      byte ptr es:[di+FileAtr],DIR ; je to adres © ?
         jz        SrcEAdr2                 ; nen¡ to adres ©
         inc       cx                       ; zv˜¨en¡ ‡¡ta‡e adres ©–

; ------ adresa dal¨¡ polo‘ky

SrcEAdr2:add       di,ax                    ; adresa dal¨¡ polo‘ky
         call      NormESDI                 ; normalizace adresy
         jmp       short SrcEAdr1           ; test dal¨¡ polo‘ky

; ------ n vrat registr–

SrcEAdr3:inc       di                       ; p©esko‡en¡ koncov‚ 0
         pop       ax
         ret

SrcEAdr  ENDP

; -----------------------------------------------------------------------------
;        inverze ozna‡en¡ polo‘ky ES:SI seznamu DS:BX
; -----------------------------------------------------------------------------

InvPol   PROC      NEAR

         test      byte ptr es:[si+FileAtr],b7 ; je polo‘ka ozna‡en  ?
         jnz       ResPol                   ; polo‘ka je ozna‡en 
                                            ; jinak n sleduje ozna‡en¡ polo‘ky

InvPol   ENDP

; -----------------------------------------------------------------------------
;        ozna‡en¡ polo‘ky ES:SI seznamu DS:BX
; -----------------------------------------------------------------------------

SelPol   PROC      NEAR

; ------ £schova registr–

         push      ax

; ------ test, zda je polo‘ka ji‘ ozna‡en 

         test      byte ptr es:[si+FileAtr],b7 ; je ji‘ ozna‡en  ?
         stc
         jnz       SelPol7                  ; polo‘ka je ji‘ ozna‡en 

; ------ ozna‡en¡ polo‘ky

         or        byte ptr es:[si+FileAtr],b7 ; ozna‡en¡ polo‘ky

; ------ ukazatele ozna‡en¡ polo‘ek

         test      byte ptr es:[si+FileAtr],DIR ; je to adres © ?
         jnz       SelPol7                  ; je to adres ©
         inc       word ptr ds:[bx+SeznSelN] ; zv˜¨en¡ ‡¡ta‡e ozna‡en˜ch soubor–
         mov       ax,es:[si+FileSize]      ; velikost souboru LOW
         add       word ptr ds:[bx+SeznSelS],ax ; ‡¡ta‡ ozna‡en‚ kapacity
         mov       ax,es:[si+FileSize+2]    ; velikost souboru HIGH
         adc       word ptr ds:[bx+SeznSelS+2],ax ; ‡¡ta‡ kapacity HIGH
         clc

; ------ n vrat registr–

SelPol7: pop       ax
         ret

SelPol   ENDP

; -----------------------------------------------------------------------------
;        odzna‡en¡ polo‘ky ES:SI seznamu DS:BX
; -----------------------------------------------------------------------------

ResPol   PROC      NEAR

; ------ £schova registr–

         push      ax

; ------ test, zda je polo‘ka ji‘ odzna‡en 

         test      byte ptr es:[si+FileAtr],b7 ; je ji‘ ozna‡en  ?
         stc
         jz        ResPol7                  ; polo‘ka nen¡ ozna‡en 

; ------ odzna‡en¡ polo‘ky

         and       byte ptr es:[si+FileAtr],not b7 ; odzna‡en¡ polo‘ky

; ------ ukazatele ozna‡en¡ polo‘ek

         test      byte ptr es:[si+FileAtr],DIR ; je to adres © ?
         jnz       ResPol7                  ; je to adres ©
         dec       word ptr ds:[bx+SeznSelN] ; sn¡‘en¡ ‡¡ta‡e ozna‡en˜ch soubor–
         mov       ax,es:[si+FileSize]      ; velikost souboru LOW
         sub       word ptr ds:[bx+SeznSelS],ax ; ‡¡ta‡ ozna‡en‚ kapacity
         mov       ax,es:[si+FileSize+2]    ; velikost souboru HIGH
         sbb       word ptr ds:[bx+SeznSelS+2],ax ; ‡¡ta‡ kapacity HIGH
         clc

; ------ n vrat registr–

ResPol7: pop       ax
         ret

ResPol   ENDP

; -----------------------------------------------------------------------------
;        vyhled n¡ polo‘ky ASCIIZ DS:SI v seznamu BX -> AX (CY=nenalezena)
; -----------------------------------------------------------------------------

SrcPol   PROC      NEAR

; ------ £schova registr–

         push      bx
         push      cx
         push      dx
         push      di
         push      es

; ------ p©¡prava registr–

         mov       es,ds:[bx+SeznDSeg]      ; segment adres ©e
         mov       dx,ds:[bx+SeznDNum]      ; po‡et polo‘ek v adres ©i
         xor       ax,ax                    ; ukazatel ‡¡sla polo‘ky
         or        dx,dx                    ; je nˆjak  polo‘ka ?
         stc
         jz        SrcPol9                  ; nen¡ ‘ dn  polo‘ka
         add       bx,SeznInd               ; tabulka index–
         mov       ch,0
         cld

; ------ porovn n¡ jedn‚ polo‘ky

SrcPol2: mov       di,ds:[bx]               ; adresa polo‘ky
         cmp       di,-1
         je        SrcPol5                  ; neplatn  polo‘ka

         push      si
         mov       cl,es:[di]               ; offset dal¨¡ polo‘ky
         sub       cl,FileName              ; d‚lka jm‚na
         add       di,FileName              ; jm‚no souboru
         repe      cmpsb                    ; porovn n¡ polo‘ek
         jne       SrcPol3                  ; nesouhlas¡
         cmp       byte ptr ds:[si],0       ; mus¡ kon‡it 0
SrcPol3: pop       si
         je        SrcPol9                  ; polo‘ka nalezena OK

SrcPol5: inc       bx
         inc       bx
         inc       ax                       ; zv˜¨en¡ ukazatele ‡¡sla polo‘ky
         dec       dx
         jnz       SrcPol2                  ; dal¨¡ polo‘ka
         stc                                ; chyba - nenalezeno

; ------ n vrat registr–

SrcPol9: pop       es
         pop       di
         pop       dx
         pop       cx
         pop       bx
         ret

SrcPol   ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                            Obsluha disku
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;þ
; -----------------------------------------------------------------------------
;        inicializace parametr– aktivn¡ho disku
; -----------------------------------------------------------------------------

AktDPar  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      es

; ------ stanoven¡ aktivn¡ho disku

         mov       word ptr ds:[AktLabN],0  ; nen¡ n vˆ¨t¡ disku

         mov       ah,19h
         int       21h
         mov       ds:[AktDisk],al          ; aktivn¡ disk
         inc       ax                       ; korekce ‡¡sla disku
         mov       dl,al                    ; ‡¡slo disku

; ------ na‡ten¡ aktivn¡ho adres ©e

         mov       si,AktDir+3
         add       al,"A"-1                 ; jm‚no disku
         mov       ah,":"
         mov       ds:[si-3],ax
         mov       word ptr ds:[si-1],"\"   ; ozna‡en¡ ROOT
         mov       ah,47h
         int       21h
         jc        AktDPr99

; ------ stanoven¡ d‚lky textu adres ©e

         push      ds
         pop       es
         mov       di,AktDir
         mov       cx,MAXF
         cld
         xor       ax,ax
         repne     scasb                    ; nalezen¡ konce textu
         sub       cx,MAXF-1
         neg       cx
         mov       ds:[AktDirN],cx          ; d‚lka textu adres ©e

; ------ poskytnut¡ parametr– disku

         mov       ah,36h
         int       21h                      ; poskytnut¡ parametr– disku

; ------ p©¡prava po‡tu blok– obsazen‚ kapacity pro indik tor ‡ten¡ disku

         mov       ds:[DiskSumC],dx         ; celkov˜ po‡et blok–
         sub       ds:[DiskSumC],bx         ; v˜po‡et obsazen‚ kapacity
         cmp       word ptr ds:[DiskSumC],40
         jae       AktDPar2
         mov       word ptr ds:[DiskSumC],40

; ------ po‡et bajt– na blok

AktDPar2:push      dx                       ; celkov˜ po‡et blok–
         mov       ds:[BlokSekt],ax         ; po‡et sektor– na alok. blok
         mov       ds:[SektByte],cx         ; po‡et bajt– na sektor
         mul       cx                       ; po‡et bajt– na aloka‡n¡ blok
         mov       word ptr ds:[BlokByte],ax ; po‡et bajt– na blok LOW
         mov       word ptr ds:[BlokByte+2],dx; po‡et bajt– na blok HIGH

; ------ voln  kapacita disku

         xchg      ax,cx                    ; CX <- bajt– na blok LOW
         xchg      ax,bx                    ; AX <- po‡et voln˜ch alok. blok–
         mov       bx,dx                    ; BX <- bajt– na blok HIGH
         xor       dx,dx                    ; DX <- 0
         call      Nasob                    ; vyn soben¡ DX:AX * BX:CX -> DX:AX
         mov       word ptr ds:[DiskFree],ax ; voln  kapacita LOW
         mov       word ptr ds:[DiskFree+2],dx ; voln  kapacita HIGH
         pop       ax                       ; celkov˜ po‡et blok–

; ------ celkov  kapacita disku

         xor       dx,dx                    ; DX <- 0
         call      Nasob                    ; vyn soben¡ DX:AX * BX:CX -> DX:AX
         mov       word ptr ds:[DiskSumm],ax ; celkov  kapacita LOW
         mov       word ptr ds:[DiskSumm+2],dx ; celkov  kapacita HIGH

; ------ nastaven¡ adresy DTA

         mov       dx,offset DTA
         mov       ah,1ah
         int       21h                      ; nastaven¡ adresy DTA

; ------ test p©eru¨en¡ operace

         call      TestBrk                  ; test p©eru¨en¡
AktDPr99:jc        AktDPar9                 ; p©eru¨en¡ operace

; ------ nalezen¡ n vˆ¨t¡ disku

         mov       al,ds:[AktDisk]
         add       al,"A"
         mov       ds:[AllRoot],al          ; aktivn¡ disk
         mov       dx,offset AllRoot
         mov       cx,VOL                   ; nalezne se n vˆ¨t¡
         mov       ah,4eh
         int       21h                      ; nalezen¡ n vˆ¨t¡ disku
         jc        AktDPar8                 ; nen¡ n vˆ¨t¡ disku

; ------ vymaz n¡ bufferu n vˆ¨t¡

         mov       di,AktLab                ; buffer n vˆ¨t¡ disku
         mov       cx,12
         mov       al," "
         cld
         rep       stosb                    ; vymaz n¡ bufferu n vˆ¨t¡

; ------ p©enesen¡ textu n vˆ¨t¡

         mov       di,AktLab                ; buffer n vˆ¨t¡ disku
         mov       cx,8                     ; maxim ln¡ d‚lka jm‚na disku
         mov       si,offset DTAName        ; jm‚no n vˆ¨t¡
AktDPar3:lodsb
         cmp       al,0
         je        AktDPar5                 ; konec textu n vˆ¨t¡
         cmp       al,"."
         jne       AktDPar4                 ; nen¡ p©¡pona
         mov       di,AktLab+8
         mov       cl,3
         jmp       short AktDPar3
AktDPar4:jcxz      AktDPar3                 ; p©ekro‡en¡ d‚lky jm‚na
         stosb
         dec       cx
         jmp       short AktDPar3
AktDPar5:sub       di,AktLab                ; d‚lka n vˆ¨t¡ disku
         mov       ds:[AktLabN],di          ; d‚lka n vˆ¨t¡ disku

; ------ n vrat registr–

AktDPar8:clc
AktDPar9:pop       es
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

AktDPar  ENDP

; -----------------------------------------------------------------------------
;        na‡ten¡ obsahu aktivn¡ho disku (definice seznamu BX)
; -----------------------------------------------------------------------------

ReadDisk PROC      NEAR

; ------ £schova registr–

         push      si
         push      di
         push      es

; ------ p©¡prava specifikace soubor–

         cld
         push      ds
         pop       es
         mov       si,AktDir                ; aktivn¡ adres ©
         mov       di,offset FilePath       ; specifikace soubor–
         movsw                              ; p©enos "C:"
         movsb                              ; p©enos "\"
         mov       word ptr ds:[FilePthN],3 ; d‚lka textu adres ©e

; ------ p©¡prava ukl dac¡ adresy do bufferu

         mov       es,ds:[bx+SeznSeg]       ; adresa seznamu
         xor       di,di                    ; offset ukl dac¡ adresy
         mov       word ptr ds:[SrcIndxE],0 ; konce bufferu index–
         mov       word ptr ds:[DiskSumR],0 ; ‡¡ta‡ na‡ten˜ch blok– disku

; ------ na‡ten¡ obsahu adres ©e

         call      ReadDir                  ; na‡ten¡ obsahu adres ©e

; ------ posun ukazatel–

         mov       di,es                    ; segment ukl dac¡ adresy
         inc       di                       ; zaokrouhlen¡
         mov       ds:[bx+SeznEnd],di       ; konec seznamu
         mov       ds:[DatFre],di           ; nov˜ konec dat

         mov       si,offset AktDir
         call      SrcPath                  ; nalezen¡ aktivn¡ho adres ©e
         mov       si,di
         call      SelFile
         call      SortFile                 ; set©¡dˆn¡ obsahu adres ©e

; ------ n vrat registr–

         pop       es
         pop       di
         pop       si
         ret

ReadDisk ENDP

; -----------------------------------------------------------------------------
;        na‡ten¡ obsahu adres ©e (ES:DI=ukl dac¡ adresa, BX=seznam)
; -----------------------------------------------------------------------------

ReadDir  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si

; ------ zobrazen¡ hl ¨en¡ o na‡¡t n¡ struktury disku

         mov       word ptr ds:[AdrHlp],offset TextHlp1
         mov       al,ds:[AktDisk]          ; aktivn¡ disk
         add       al,"A"                   ; korekce na ozna‡en¡ disku
         mov       ds:[TextHl11],al         ; ozna‡en¡ disku
         push      di
         push      es

         mov       di,offset TextHl12
         cld
         push      cs
         pop       es
         mov       al,"°"
         mov       cx,20
         rep       stosb

         mov       ax,ds:[DiskSumR]         ; na‡ten˜ po‡et blok–
         mov       cx,21                    ; d‚lka indik toru+1
         mul       cx
         div       word ptr ds:[DiskSumC]   ; v˜po‡et d‚lky indik toru
         cmp       ax,20
         jb        ReadDr12
         mov       ax,20
ReadDr12:xchg      ax,cx
         mov       di,offset TextHl12
         mov       al,"²"
         rep       stosb

         pop       es
         pop       di
         call      DispLHlp

; ------ £schova za‡ tku adres ©e do bufferu index–

         mov       si,ds:[SrcIndxE]         ; konec seznamu
         mov       word ptr ds:[si+SrcIndx],di ; £schova za‡ tku adres ©e
         mov       word ptr ds:[si+SrcIndx+2],es
         add       word ptr ds:[SrcIndxE],4 ; zv˜¨en¡ ukazatele

; ------ p©id n¡ textu "*.*"

         push      es
         push      ds
         pop       es
         mov       si,offset All            ; text "*.*"
         mov       cl,3                     ; d‚lka
         call      AddGFil                  ; p©id n¡ textu "*.*"
         pop       es
         jc        ReadDir5                 ; p©ete‡en¡

; ------ nastaven¡ adresy DTA

         mov       dx,offset DTA
         mov       ah,1ah
         int       21h                      ; nastaven¡ adresy DTA

; ------ test p©eru¨en¡ operace

         call      TestBrk                  ; test p©eru¨en¡
         jc        ReadDir5                 ; p©eru¨en¡ operace

; ------ nalezen¡ prvn¡ho souboru

         mov       dx,offset FilePath
         mov       cx,RO+DIR+ARC            ; atributy souboru
         mov       ah,4eh
ReadDir2:int       21h                      ; nalezen¡ souboru
         jc        ReadDir5                 ; nen¡ dal¨¡ soubor - konec

; ------ kontrola, zda je platn  polo‘ka

         cmp       word ptr ds:[DTAName],"."
         je        ReadDir4                 ; nen¡ platn  polo‘ka
         cmp       word ptr ds:[DTAName],".."
         je        ReadDir4                 ; nen¡ platn  polo‘ka

; ------ kontrola, zda je buffer pln˜ (rezerva pro 1 polo‘ku a ukon‡ovac¡ bajty)

         mov       ax,es                    ; ukl dac¡ adresa
         add       ax,5                     ; rezerva 80 bajt–
         cmp       ax,ds:[DatMax]           ; je konec bufferu ?
         jae       ReadDir5                 ; buffer je pln˜

; ------ p©enesen¡ za‡ tku

         cld
         inc       word ptr ds:[bx+SeznNum] ; zv˜¨en¡ ‡¡ta‡e polo‘ek celkem
         inc       di                       ; rezerva m¡sta pro offset
         mov       si,offset DTAAtrib       ; za‡ tek polo‘ky
         lodsb
         and       al,RO+HID+SYS+DIR+ARC    ; atributy polo‘ky
         stosb                              ; ulo‘en¡ atribut–
         movsw                              ; ‡as
         movsw                              ; datum

         lodsw
         stosw                              ; velikost LOW
         xchg      ax,dx
         lodsw
         stosw                              ; velikost HIGH
         xchg      ax,dx

; ------ zv˜¨en¡ ‡¡ta‡e blok–

         mov       cx,word ptr cs:[BlokByte] ; po‡et bajt– na blok
         dec       cx
         add       ax,cx
         adc       dx,0
         inc       cx

         cmp       dx,cx
         jae       ReadDr24
         div       cx
         add       cs:[DiskSumR],ax         ; zv˜¨en¡ ‡¡ta‡e blok–
ReadDr24:

; ------ p©enesen¡ jm‚na souboru

         mov       cx,FILENAME-1            ; velikost polo‘ky bez jm‚na
ReadDir3:inc       cx                       ; zv˜¨en¡ ‡¡ta‡e d‚lky polo‘ky
         lodsb
         stosb                              ; znak jm‚na
         cmp       al,0
         jne       ReadDir3                 ; p©enesen¡ textu po bajt 0
         dec       di                       ; n vrat ukazatele
         push      di
         sub       di,cx                    ; n vrat za‡ tku jm‚na souboru
         mov       es:[di+FileOffs],cl      ; offset dal¨¡ polo‘ky
         pop       di
         call      NormESDI                 ; normalizace adresy ES:DI

; ------ dal¨¡ polo‘ka adres ©e

ReadDir4:mov       ah,4fh
         jmp       short ReadDir2

; ------ ulo‘en¡ koncov‚ho bajtu "0" do seznamu (=konec adres ©e)

ReadDir5:mov       al,0                     ; ukon‡ovac¡ bajt "0"
         cld
         stosb
         call      NormESDI                 ; normalizace adresy ES:DI

; ------ odstranˆn¡ textu "*.*" ze jm‚na souboru

         call      SubGFil                  ; odstranˆn¡ textu "*.*"

; ------ nalezen¡ adres ©e v seznamu

ReadDir6:push      es
         mov       si,ds:[SrcIndxE]         ; konec seznamu
         mov       es,word ptr ds:[si+SrcIndx+2-4]
         mov       si,word ptr ds:[si+SrcIndx-4] ; adresa ukazatele v adres ©i
         cmp       byte ptr es:[si],0       ; je to konec adres ©e ?
         je        ReadDir8                 ; je to konec adres ©e
         test      byte ptr es:[si+FileAtr],DIR ; je to adres © ?
         jz        ReadDir7                 ; nen¡ to adres ©

; ------ p©id n¡ textu adres ©e

         mov       cl,es:[si+FileOffs]      ; offset dal¨¡ polo‘ky
         sub       cl,FileName              ; d‚lka textu jm‚na souboru
         add       si,offset FileName       ; jm‚no adres ©e
         call      AddGFil                  ; p©id n¡ textu ke jm‚nu adres ©e
         jc        ReadDir7                 ; p©ete‡en¡ hloubky

; ------ na‡ten¡ zadan‚ho adres ©e

         pop       es
         call      ReadDir                  ; na‡ten¡ adres ©e
         push      es

; ------ odstranˆn¡ textu adres ©e

         call      SubGFil                  ; odstranˆn¡ jm‚na adres ©e

; ------ dal¨¡ polo‘ka adres ©e

ReadDir7:mov       si,ds:[SrcIndxE]         ; konec seznamu
         mov       es,word ptr ds:[si+SrcIndx+2-4]
         mov       si,word ptr ds:[si+SrcIndx-4] ; adresa ukazatele v adres ©i
         mov       al,es:[si+FileOffs]      ; offset dal¨¡ polo‘ky
         mov       ah,0
         add       si,ax                    ; adresa dal¨¡ polo‘ky
         call      NormESSI                 ; normalizace adresy ES:SI
         xchg      ax,si                    ; AX <- nov˜ offset polo‘ky
         mov       si,ds:[SrcIndxE]         ; konec seznamu
         mov       word ptr ds:[si+SrcIndx-4],ax ; adresa dal¨¡ polo‘ky
         mov       word ptr ds:[si+SrcIndx+2-4],es
         pop       es
         jmp       short ReadDir6           ; test dal¨¡ polo‘ky

; ------ nen¡ dal¨¡ polo‘ka podadres ©e

ReadDir8:pop       es
         sub       word ptr ds:[SrcIndxE],4 ; sn¡‘en¡ ukazatele

; ------ n vrat registr–

ReadDir9:pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

ReadDir  ENDP

; -----------------------------------------------------------------------------
;        p©id n¡ textu k cestˆ (ES:SI=text, CL=d‚lka -> CY=p©ete‡en¡)
; -----------------------------------------------------------------------------

AddGFil  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      si
         push      di

; ------ kontrola, zda bude p©ete‡en¡ bufferu

         mov       ch,0
         mov       ax,cx                    ; d‚lka textu
         add       ax,cs:[FilePthN]         ; nov  d‚lka textu cesty
         cmp       ax,MAXF-1                ; maxim ln¡ d‚lka bufferu
         cmc
         jc        AddGFil9                 ; p©ete‡en¡ bufferu

; ------ zaji¨tˆn¡ koncov‚ho znaku "\"

         mov       di,offset FilePath       ; buffer cesty
         add       di,cs:[FilePthN]         ; konec cesty
         mov       al,"\"
         cmp       cs:[di-1],al             ; kon‡¡ znakem "\" ?
         je        AddGFil3                 ; kon‡¡ znakem "\" - OK
         mov       cs:[di],al               ; oddˆlovac¡ znak
         inc       di                       ; zv˜¨en¡ ukazatele na dal¨¡ znak

; ------ p©enesen¡ textu

AddGFil3:push      ds
         push      es

         push      cs
         push      es
         pop       ds                       ; DS <- ES
         pop       es                       ; ES <- DS
         cld
         rep       movsb                    ; p©enesen¡ textu

         pop       es
         pop       ds

; ------ koncov  0, d‚lka textu

         mov       byte ptr cs:[di],0       ; koncov  0
         sub       di,offset FilePath       ; nov  d‚lka textu
         mov       cs:[FilePthN],di         ; nov  d‚lka textu
         clc                                ; p©¡znak operace OK

; ------ n vrat registr–

AddGFil9:pop       di
         pop       si
         pop       cx
         pop       ax
         ret

AddGFil  ENDP

; -----------------------------------------------------------------------------
;        odstranˆn¡ posledn¡ho souboru z cesty
; -----------------------------------------------------------------------------

SubGFil  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      si

; ------ konec textu v bufferu

         mov       si,offset FilePath       ; cesta
         add       si,cs:[FilePthN]         ; konec textu cesty

; ------ nalezen¡ posledn¡ho znaku "\"

         mov       al,"\"                   ; hledan˜ znak "\"
SubGFil1:dec       si                       ; sn¡‘en¡ ukazatele v bufferu
         cmp       cs:[si],al               ; je konec cesty ?
         jne       SubGFil1                 ; nen¡ - dal¨¡ znak

; ------ ozna‡en¡ konce cesty

         cmp       byte ptr cs:[si-1],":"   ; je to ROOT ?
         jne       SubGFil2                 ; nen¡ to ROOT
         inc       si                       ; zv˜¨en¡ ukazatele - ponech  se "\"
SubGFil2:mov       byte ptr cs:[si],0       ; koncov  0

; ------ nov  d‚lka cesty

         sub       si,offset FilePath       ; d‚lka textu cesty
         mov       cs:[FilePthN],si         ; nov  d‚lka cesty

; ------ n vrat registr–

         pop       si
         pop       cx
         pop       ax
         ret

SubGFil  ENDP


; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                       Univerz ln¡ obsluha displeje
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;þ
; -----------------------------------------------------------------------------
;        vypnut¡ kurzoru
; -----------------------------------------------------------------------------

KurzOff  PROC      NEAR

         push      dx

         mov       dx,25*HI                 ; prvn¡ © dek "za rohem"
         call      SetKurz                  ; nastaven¡ pozice kurzoru

         pop       dx
         ret

KurzOff  ENDP

; -----------------------------------------------------------------------------
;        nastaven¡ pozice kurzoru na DX
; -----------------------------------------------------------------------------

SetKurz  PROC      NEAR

         push      ax
         push      bx

         mov       bh,0                     ; aktivn¡ videostr nka
         mov       ah,2                     ; funkce - nastaven¡ pozice kurzoru
         call      Int10P                   ; nastaven¡ pozice kurzoru

         pop       bx
         pop       ax
         ret

SetKurz  ENDP

; -----------------------------------------------------------------------------
;        Z pis 1 znaku AX na obrazovku na pozici DX (DL <- nov  pozice)
; -----------------------------------------------------------------------------

Disp1Chr PROC      NEAR

         push      cx
         mov       cl,1                     ; 1 znak k z pisu
         call      DispChr                  ; z pis 1 znaku na displej
         pop       cx
         ret

Disp1Chr ENDP

; -----------------------------------------------------------------------------
;        z pis CL znak– AX na obrazovku na pozici DX (DL <- nov  pozice)
; -----------------------------------------------------------------------------

DispChr  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      di
         push      es

         xchg      ax,bx                    ; £schova znaku k z pisu
         mov       ch,0                     ; CX = po‡et znak–

; ------ v˜po‡et adresy ve videopamˆti

         mov       es,cs:[SegmVRAM]         ; adresa VRAM
         mov       al,80                    ; po‡et znak– na © dek
         mul       dh                       ; p©epo‡et © dk– na pozice
         mov       dh,0                     ; DX=pozice
         add       ax,dx                    ; p©i‡ten¡ pozice
         shl       ax,1                     ; * 2 = offset ve videostr nce
         xchg      ax,di                    ; DI <- adresa ve videopamˆti

; ------ p©¡prava dat; zji¨tˆn¡, zda se kontroluje snˆ‘en¡

         cld                                ; smˆr posunu ukazatele nahoru
         cmp       byte ptr cs:[ChckSnow],0 ; prov d¡ se kontrola snˆ‘en¡ ?
         je        DispChr6                 ; neprov d¡ se kontrola snˆ‘en¡
         jcxz      DispChr7                 ; nen¡ ‘ dn˜ znak k p©enosu

; ------ p©enos dat s obsluhou snˆ‘en¡

         sti                                ; p©eru¨en¡ povoleno
         mov       dx,cs:[PortCRT]          ; stavov˜ registr ©adi‡e displeje
DispChr2:in        al,dx                    ; ‡ten¡ stavu video©adi‡e 6845
         test      al,8                     ; prob¡h  vertik ln¡ impuls ?
         jnz       DispChr5                 ; prob¡h  vertik ln¡ impuls
DispChr3:in        al,dx                    ; ‡ten¡ stavu video©adi‡e 6845
         rcr       al,1                     ; test horizont ln¡ho zpˆtn‚ho bˆhu
         jc        DispChr3                 ; je horiz. zpˆtn˜ bˆh - ‡ek n¡
DispChr4:in        al,dx                    ; ‡ten¡ stavu video©adi‡e 6845
         rcr       al,1                     ; test horiz. zpˆtn‚ho bˆhu
         jnc       DispChr4                 ; ‡ek n¡ na horiz. zpˆtn˜ bˆh
DispChr5:xchg      ax,bx                    ; znak k zobrazen¡
         stosw                              ; z pis jednoho znaku
         xchg      ax,bx
         loop      DispChr2                 ; z pis dal¨¡ho znaku

; ------ z pis znaku bez kontroly snˆ‘en¡

DispChr6:xchg      ax,bx                    ; AX <- znak k zobrazen¡
         rep       stosw                    ; z pis znaku bez kontroly snˆ‘en¡

; ------ n vrat registr–, konec

DispChr7:pop       es
         pop       di
         pop       dx
         pop       cx
         pop       bx
         pop       ax

         add       dl,cl                    ; zv˜¨en¡ pozice
         ret

DispChr  ENDP

; -----------------------------------------------------------------------------
;        Zobrazen¡ st¡nu CL pozic od pozice DX (DL <- nov  pozice)
; -----------------------------------------------------------------------------

DispStin PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx
         push      di
         push      es
         mov       ch,0                     ; CX = po‡et znak–

; ------ v˜po‡et adresy ve videopamˆti

         mov       es,cs:[SegmVRAM]         ; adresa VRAM
         mov       al,80                    ; po‡et znak– na © dek
         mul       dh                       ; p©epo‡et © dk– na pozice
         mov       dh,0                     ; DX=pozice
         add       ax,dx                    ; p©i‡ten¡ pozice
         shl       ax,1                     ; * 2 = offset ve videostr nce
         xchg      ax,di                    ; DI <- adresa ve videopamˆti

; ------ zobrazen¡ st¡nu

         jcxz      DispStn7
         mov       al,7
         inc       di
         cld
DispStn6:stosb
         inc       di
         loop      DispStn6

; ------ n vrat registr–, konec

DispStn7:pop       es
         pop       di
         pop       dx
         pop       cx
         pop       ax

         add       dl,cl                    ; zv˜¨en¡ pozice
         ret

DispStin ENDP

; -----------------------------------------------------------------------------
;     Zobrazen¡ textu ES:SI o d‚lce CL a barvˆ AH od pozice DX (DL<-nov  pozice)
; -----------------------------------------------------------------------------

DispTxt  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx
         push      si
         push      di
         push      ds
         push      es
         mov       ch,0                     ; CX = po‡et znak–

; ------ v˜po‡et adresy ve videopamˆti

         push      ax                       ; AH=barva textu
         mov       al,80                    ; po‡et znak– na © dek
         mul       dh                       ; p©epo‡et © dk– na pozice
         mov       dh,0                     ; DX=pozice
         add       ax,dx                    ; p©i‡ten¡ pozice
         shl       ax,1                     ; * 2 = offset ve videostr nce
         xchg      ax,di                    ; DI <- adresa ve videopamˆti
         pop       ax                       ; AH=barva textu

; ------ p©¡prava dat; zji¨tˆn¡, zda se kontroluje snˆ‘en¡

         push      es                       ; segment zdrojov‚ho textu
         pop       ds                       ; segment zdrojov‚ adresy
         mov       es,cs:[SegmVRAM]         ; adresa VRAM

         cld                                ; smˆr posunu ukazatele nahoru
         cmp       byte ptr cs:[ChckSnow],0 ; prov d¡ se kontrola snˆ‘en¡ ?
         jcxz      DispTxt7                 ; nen¡ ‘ dn˜ znak k p©enosu
         je        DispTxt6                 ; neprov d¡ se kontrola snˆ‘en¡

; ------ p©enos dat s obsluhou snˆ‘en¡

         sti                                ; p©eru¨en¡ povoleno
         mov       dx,cs:[PortCRT]          ; stavov˜ registr ©adi‡e displeje
DispTxt2:in        al,dx                    ; ‡ten¡ stavu video©adi‡e 6845
         test      al,8                     ; prob¡h  vertik ln¡ impuls ?
         jnz       DispTxt5                 ; prob¡h  vertik ln¡ impuls
DispTxt3:in        al,dx                    ; ‡ten¡ stavu video©adi‡e 6845
         rcr       al,1                     ; test horizont ln¡ho zpˆtn‚ho bˆhu
         jc        DispTxt3                 ; je horiz. zpˆtn˜ bˆh - ‡ek n¡
DispTxt4:in        al,dx                    ; ‡ten¡ stavu video©adi‡e 6845
         rcr       al,1                     ; test horiz. zpˆtn‚ho bˆhu
         jnc       DispTxt4                 ; ‡ek n¡ na horiz. zpˆtn˜ bˆh
DispTxt5:lodsb                              ; na‡ten¡ znaku k zobrazen¡
         stosw                              ; p©enos jednoho znaku
         loop      DispTxt2                 ; p©enos dal¨¡ho znaku
         jmp       short DispTxt7

; ------ p©enos dat bez kontroly snˆ‘en¡

DispTxt6:lodsb                              ; znak k zobrazen¡
         stosw                              ; ulo‘en¡ znaku
         loop      DispTxt6                 ; p©enos dat bez kontroly snˆ‘en¡

; ------ n vrat registr–, konec

DispTxt7:pop       es
         pop       ds
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       ax

         add       dl,cl                    ; zv˜¨en¡ pozice
         ret

DispTxt  ENDP

; -----------------------------------------------------------------------------
;        Zobrazen¡ ©etˆzce ES:SI o d‚lce CL od pozice DX (DL <- nov  pozice)
; -----------------------------------------------------------------------------

DispStr  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx
         push      si
         push      di
         push      ds
         push      es
         mov       ch,0                     ; CX = po‡et znak–

; ------ v˜po‡et adresy ve videopamˆti

         mov       al,80                    ; po‡et znak– na © dek
         mul       dh                       ; p©epo‡et © dk– na pozice
         mov       dh,0                     ; DX=pozice
         add       ax,dx                    ; p©i‡ten¡ pozice
         shl       ax,1                     ; * 2 = offset ve videostr nce
         xchg      ax,di                    ; adresa ve videopamˆti

; ------ p©¡prava dat; zji¨tˆn¡, zda se kontroluje snˆ‘en¡

         push      es                       ; £schova segmentu zdrojov‚ho textu
         pop       ds                       ; segment zdrojov‚ adresy
         mov       es,cs:[SegmVRAM]         ; adresa VRAM

         cld                                ; smˆr posunu ukazatele nahoru
         cmp       byte ptr cs:[ChckSnow],0 ; prov d¡ se kontrola snˆ‘en¡ ?
         je        DispStr6                 ; neprov d¡ se kontrola snˆ‘en¡
         jcxz      DispStr7                 ; nen¡ ‘ dn˜ znak k p©enosu

; ------ p©enos dat s obsluhou snˆ‘en¡

         sti                                ; povolen¡ p©eru¨en¡
         mov       dx,cs:[PortCRT]          ; stavov˜ registr ©adi‡e displeje
DispStr2:in        al,dx                    ; ‡ten¡ stavu video©adi‡e 6845
         test      al,8                     ; prob¡h  vertik ln¡ impuls ?
         jnz       DispStr5                 ; prob¡h  vertik ln¡ impuls
DispStr3:in        al,dx                    ; ‡ten¡ stavu video©adi‡e 6845
         rcr       al,1                     ; test horizont ln¡ho zpˆtn‚ho bˆhu
         jc        DispStr3                 ; je horiz. zpˆtn˜ bˆh - ‡ek n¡
DispStr4:in        al,dx                    ; ‡ten¡ stavu video©adi‡e 6845
         rcr       al,1                     ; test horiz. zpˆtn‚ho bˆhu
         jnc       DispStr4                 ; ‡ek n¡ na horiz. zpˆtn˜ bˆh
DispStr5:movsw                              ; p©enos jednoho znaku
         loop      DispStr2                 ; p©enos dal¨¡ho znaku

; ------ p©enos dat bez kontroly snˆ‘en¡

DispStr6:rep       movsw                    ; p©enos dat bez kontroly snˆ‘en¡

; ------ n vrat registr–, konec

DispStr7:pop       es
         pop       ds
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       ax

         add       dl,cl                    ; zv˜¨en¡ pozice
         ret

DispStr  ENDP

; -----------------------------------------------------------------------------
;        Atualizace parametr– displeje
; -----------------------------------------------------------------------------

AktPDisp PROC    NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      ds

; ------ p©¡prava registr–

         xor       bx,bx
         mov       ds,bx                    ; DS <- 0

; ------ aktu ln¡ videom¢d

         mov       ah,0fh
         call      Int10P                   ; aktu ln¡ videom¢d
         and       al,7fh                   ; zru¨en¡ p©¡znaku nemaz n¡ displeje

; ------ segment videopamˆti a p©ednastaven¡ p©¡znaku obsluhy snˆ‘en¡

         mov       byte ptr cs:[ChckSnow],0 ; zru¨en¡ p©¡znaku obsluhy snˆ‘en¡
         mov       byte ptr cs:[SegmVRAM+1],0B0h ; segment videopamˆti pro MDA
         cmp       al,7                     ; je videom¢d MDA ?
         je        AktPDis1                 ; je videom¢d MDA
         inc       byte ptr cs:[ChckSnow]   ; nastav. p©¡znaku obsluhy snˆ‘en¡
         mov       byte ptr cs:[SegmVRAM+1],0B8h ; segment videopamˆti pro CGA

; ------ test videokarty EGA/VGA

AktPDis1:mov       ah,12h                   ; slu‘ba EGA/VGA - informace o EGA
         mov       bx,4510h                 ; poskytnut¡ informac¡ o EGA
         call      Int10P                   ; poskytnut¡ informac¡ o EGA/VGA
         mov       byte ptr cs:[EgaVga],0   ; zru¨en¡ p©¡znaku karty EGA/VGA
         cmp       bh,1                     ; kontrola navr cen‚ho m¢du displeje
         ja        AktPDis2                 ; nen¡ to karta EGA/VGA
         cmp       bl,5                     ; maxim lnˆ povolen 1 MB pamˆti
         ja        AktPDis2                 ; nen¡ to karta EGA/VGA
         inc       byte ptr cs:[EgaVga]     ; p©¡znak karty EGA/VGA

; ------ up©esnˆn¡ p©¡znaku kontroly snˆ‘en¡

         mov       al,ds:[487h]             ; p©¡znak kontroly snˆ‘en¡
         shr       al,1
         shr       al,1
         and       al,1                     ; p©¡znak obsluhy snˆ‘en¡
         mov       cs:[ChckSnow],al         ; nastaven¡ p©¡znaku kontroly snˆ‘.

; ------ zji¨tˆn¡ b zov‚ ©¡dic¡ adresy displeje CRT

AktPDis2:mov       ax,ds:[463h]             ; b zov  adresa portu ©adi‡e CRT
         add       ax,6
         mov       cs:[PortCRT],ax          ; stavov˜ registr ©adi‡e displeje

; ------ n vrat registr–

         pop       ds
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

AktPDisp ENDP

; -----------------------------------------------------------------------------
;        obsluha INT 10h s £schovou registr–
; -----------------------------------------------------------------------------

Int10P   PROC      NEAR

         pushf
         push      si
         push      di
         push      bp
         push      ds
         push      es

         int       10h

         pop       es
         pop       ds
         pop       bp
         pop       di
         pop       si
         popf
         ret

Int10P   ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                             R–zn‚ procedury
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;þ
; -----------------------------------------------------------------------------
;        dek¢dov n¡ textu DS:SI do bufferu ES:DI, AH=barva (1. bajt = d‚lka)
; -----------------------------------------------------------------------------

DekTxt   PROC      NEAR

         push      ax
         push      cx

         cld
         lodsb
         mov       cl,al                    ; d‚lka textu
         mov       ch,0
         jcxz      DekTxt4

DekTxt2: lodsb
         stosw
         loop      DekTxt2

DekTxt4: pop       cx
         pop       ax
         ret

DekTxt   ENDP

; -----------------------------------------------------------------------------
;        dek¢dov n¡ ‡¡sla DX:AX do ES:DI (CL=¨¡©ka, CH=barva) s omezen¡m d‚lky
; -----------------------------------------------------------------------------

Dek10Num PROC      NEAR

; ------ test, zda je ‡¡slo 100.000.000 a vˆt¨¡

         cmp       dx,1525
         jne       Dek10Nm2
         cmp       ax,57600
Dek10Nm2:jb        DekNum                   ; velikost ‡¡sla je OK

; ------ zkr cen¡ d‚lky pole

         cmp       cl,9
         jb        Dek10Nm3
         dec       cl

; ------ vydˆlen¡ ‡¡sla 1000x

Dek10Nm3:push      bx
         push      cx
         mov       cx,1000
         xor       bx,bx
         call      Vydel                    ; vydˆlen¡ DX:AX/1000
         pop       cx
         pop       bx

; ------ dek¢dov n¡ ‡¡sla DX:AX

         call      DekNum

; ------ ulo‘en¡ koncov‚ho ozna‡en¡ "K"

         push      ax
         mov       ah,ch
         mov       al,"K"
         cld
         stosw
         pop       ax
         ret

Dek10Num ENDP

; -----------------------------------------------------------------------------
;   dek¢dov n¡ ‡¡sla DX:AX do bufferu ES:DI, CL=¨¡©ka pole, CH=barva (0=nen¡)
; -----------------------------------------------------------------------------

DekNum   PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      bp

; ------ p©¡prava registr–

         xchg      ax,bp                    ; BP <- ‡¡slo LOW
         mov       si,10                    ; SI = dˆlitel
         mov       bh,ch                    ; BH <- barva
         mov       ch,0                     ; CH = ‡¡ta‡ ‡¡slic, CL = ¨¡©ka
         mov       bl,"."                   ; BL = znak mezery, BH = barva

; ------ v˜po‡et jedn‚ ‡¡slice

DekNum1: xchg      ax,dx                    ; AX <- ‡¡slo HIGH
         xor       dx,dx                    ; DX <- 0
         div       si                       ; vydˆlen¡ ‡¡sla HIGH
         xchg      ax,bp                    ; AX <- n vrat LOW, BP<-£schova HIGH
         div       si                       ; vydˆlen¡ ‡¡sla LOW

; ------ oddˆlova‡ © d–

         cmp       ch,3
         je        DekNum2                  ; jsou tis¡ce
         cmp       ch,7
         jne       DekNum3                  ; nejsou miliony
DekNum2: push      bx                       ; ulo‘en¡ oddˆlovac¡ te‡ky
         inc       ch                       ; zv˜¨en¡ ‡¡ta‡e ‡¡slic v z sobn¡ku

; ------ ulo‘en¡ ‡¡sla

DekNum3: add       dl,"0"                   ; korekce na znak ASCII
         mov       dh,bh                    ; barva
         push      dx                       ; ulo‘en¡ ‡¡sla do z sobn¡ku
         inc       ch                       ; zv˜¨en¡ ‡¡ta‡e ‡¡slic v z sobn¡ku
         mov       dx,bp                    ; DX <- n vrat ‡¡sla HIGH
         xchg      ax,bp                    ; BP <- ‡¡slo LOW, (AX <- HIGH)

; ------ test, zda bude dal¨¡ ‡¡slice

         or        ax,bp                    ; je ‡¡slo ji‘ = 0 ?
         jnz       DekNum1                  ; dek¢dov n¡ dal¨¡ ‡¡slice

; ------ rozli¨en¡, zda se ukl d  barva

         or        bh,bh                    ; ukl d  se barva ?
         jnz       DekNum5                  ; barva se ukl d 

; ------ vymaz n¡ bufferu pro ulo‘en¡ ‡¡sla (bez barvy)

         mov       si,di                    ; £schova za‡ tku bufferu
         push      cx
         mov       ch,0
         mov       al," "                   ; mezera k vymaz n¡ bufferu
         rep       stosb                    ; vymaz n¡ bufferu pro ‡¡slo
         pop       ax
         mov       cl,ah                    ; CX = po‡et znak– v z sobn¡ku

; ------ adresa v bufferu k ulo‘en¡ ‡¡sla

         sub       di,cx                    ; adresa k ulo‘en¡ ‡¡sla
         cmp       di,si                    ; je pod p–vodn¡m po‡ tkem ?
         ja        DekNum4                  ; po‡ tek je OK
         mov       di,si                    ; omezen¡ na p–vodn¡ po‡ tek

; ------ ulo‘en¡ ‡¡sla do bufferu (bez barvy)

DekNum4: pop       ax
         stosb
         loop      DekNum4                  ; dal¨¡ znak
         jmp       short DekNum9

; ------ vymaz n¡ bufferu pro ulo‘en¡ ‡¡sla (s barvou)

DekNum5: mov       si,di                    ; £schova za‡ tku bufferu
         push      cx
         mov       ch,0
         mov       ah,bh                    ; barva
         mov       al," "                   ; mezera k vymaz n¡ bufferu
         rep       stosw                    ; vymaz n¡ bufferu pro ‡¡slo
         pop       ax
         mov       cl,ah                    ; CX = po‡et znak– v z sobn¡ku

; ------ adresa v bufferu k ulo‘en¡ ‡¡sla

         sub       di,cx
         sub       di,cx                    ; adresa k ulo‘en¡ ‡¡sla
         cmp       di,si                    ; je pod p–vodn¡m po‡ tkem ?
         ja        DekNum6                  ; po‡ tek je OK
         mov       di,si                    ; omezen¡ na p–vodn¡ po‡ tek

; ------ ulo‘en¡ ‡¡sla do bufferu (s barvou)

DekNum6: pop       ax
         stosw
         loop      DekNum6                  ; dal¨¡ znak

; ------ n vrat registr–

DekNum9: pop       bp
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

DekNum   ENDP

; -----------------------------------------------------------------------------
;        p©evod znaku AL na velk‚ p¡smeno
; -----------------------------------------------------------------------------

UpCase   PROC      NEAR

         cmp       al,"a"
         jb        UpCase2
         cmp       al,"z"
         ja        UpCase2
         sub       al,32
UpCase2: ret

UpCase   ENDP

; -----------------------------------------------------------------------------
;                    skok na obsluhu podle DX
; -----------------------------------------------------------------------------
; Procedura se vol  instrukc¡ CALL JumpDX, za kterou n sleduje tabulka
; skok–. Procedura zmˆn¡ svou n vratovou adresu na adresu podle nalezen‚
; polo‘ky v tabulce (nebo podle polo‘ky pro nenalezenou hodnotu).
; -----------------------------------------------------------------------------
; VSTUP: v z sobn¡ku slovo (n vratov  adresa NEAR) = za‡ tek tabulky skok–
;             stuktura tabulky: 1 slovo testovan  hodnota DX
;                               1 slovo adresa NEAR obsluhy
;             konec tabulky: 1 slovo = 0 (p©¡znak konce tabulky)
;                            1 slovo adresa NEAR obsluhy p©i nenalezen¡ k¢du
; -----------------------------------------------------------------------------

JumpDX   PROC      NEAR

; ------ £schova registr–

                                            ; SS:[BP+6] = IP
         push      si                       ; SS:[BP+4] = SI
         push      bp                       ; SS:[BP+2] = BP
         push      ds                       ; SS:[BP+0] = DS
         mov       bp,sp

; ------ nalezen¡ hodnoty v tabulce

         push      cs
         pop       ds
         mov       si,ss:[bp+6]             ; offset adresy tabulky
JumpDX1: cmp       word ptr ds:[si],0       ; je konec tabulky ?
         je        JumpDX2                  ; konec tabulky - nenalezeno
         cmp       word ptr ds:[si],dx      ; je to hledan  hodnota ?
         je        JumpDX2                  ; hodnota nalezena
         add       si,4                     ; adresa dal¨¡ polo‘ky
         jmp       short JumpDX1            ; test dal¨¡ polo‘ky

; ------ nastaven¡ n vratov‚ adresy podle tabulky

JumpDX2: mov       si,ds:[si+2]             ; adresa skoku
         mov       ss:[bp+6],si             ; nov  n vratov  adresa

; ------ n vrat registr–

         pop       ds
         pop       bp
         pop       si
         ret

JumpDX   ENDP

; -----------------------------------------------------------------------------
;                                 Cekej
;                        ‡ek n¡ po zadanou dobu
; -----------------------------------------------------------------------------
; VSTUP: CX=po‡et impuls– 1/18 s
; -----------------------------------------------------------------------------

Cekej    PROC      NEAR

         push      ax
         push      cx
         push      ds

         xor       ax,ax
         mov       ds,ax
         jcxz      Cekej3
         sti

Cekej1:  mov       ax,ds:[46ch]
Cekej2:  cmp       ax,ds:[46ch]
         je        Cekej2
         loop      Cekej1

Cekej3:  pop       ds
         pop       cx
         pop       ax
         ret

Cekej    ENDP

; -----------------------------------------------------------------------------
;        test p©eru¨en¡ operace (CY=p©eru¨en¡ operace)
; -----------------------------------------------------------------------------

TestBrk  PROC      NEAR

         push      ax

         cmp       byte ptr cs:[ParBreak],0 ; je p©eru¨en¡ operace ?
         jne       TestBrk2                 ; je p©eru¨en¡ operace

         call      TestKey                  ; test, zda je nˆjak  kl vesa
         jc        TestBrk2                 ; nen¡ ‘ dn  kl vesa

         cmp       ax,11bh
         je        TestBrk1                 ; je kl vesa ESC
         or        ax,ax
         jnz       TestBrk3                 ; nen¡ Ctrl-Break

TestBrk1:push      bx
         call      InpKey                   ; vstup znaku z kl vesnice
         pop       bx
         or        byte ptr cs:[ParBreak],b0 ; p©¡znak p©eru¨en¡

TestBrk2:cmc
TestBrk3:pop       ax
         ret

TestBrk  ENDP

; -----------------------------------------------------------------------------
;        vstup znaku z kl vesnice do DX (CY=p©eru¨en¡ ESC, BREAK)
; -----------------------------------------------------------------------------

InpKey   PROC      NEAR

         push      ax

; ------ ‡ek n¡ na stisk kl vesy

InpKey1: call      TestKey
         jc        InpKey1                  ; nen¡ kl vesa

; ------ vstup znaku z kl vesnice

         mov       ah,0
         int       16h                      ; vstup znaku z kl vesnice

; ------ n hrada Ctrl-Break kl vesou ESC

         or        ax,ax                    ; je Ctrl-Break ?
         jnz       InpKey2                  ; nen¡ Ctrl-Break
         mov       ax,11bh                  ; n hrada ESC

; ------ test, zda je ESC

InpKey2: cmp       ax,11bh                  ; je ESC ?
         clc
         jne       InpKey3                  ; nen¡ ESC
         stc                                ; p©¡znak kl vesy ESC

InpKey3: xchg      ax,dx                    ; DX <- k¢d kl vesy
         pop       ax
         ret

InpKey   ENDP

; -----------------------------------------------------------------------------
;        test stavu kl vesnice (AX=kl vesa, CY=nen¡ znak z kl vesnice)
; -----------------------------------------------------------------------------

TestKey  PROC      NEAR

         push      ds
         xor       ax,ax
         mov       ds,ax
         and       byte ptr ds:[418h],not 80h
         pop       ds

         mov       ah,1
         int       16h                      ; test stavu kl vesnice
         clc                                ; p©¡znak - je kl vesa
         jnz       TestKey2                 ; je kl vesa
         stc                                ; p©¡znak - nen¡ kl vesa
TestKey2:ret

TestKey  ENDP

; -----------------------------------------------------------------------------
;        normalizace adresy ES:DI
; -----------------------------------------------------------------------------

NormESDI PROC      NEAR

         push      ax
         push      di

         mov       ax,es
         shr       di,1
         shr       di,1
         shr       di,1
         shr       di,1
         add       ax,di
         mov       es,ax

         pop       di
         pop       ax
         and       di,0fh
         ret

NormESDI ENDP

; -----------------------------------------------------------------------------
;        normalizace adresy ES:SI
; -----------------------------------------------------------------------------

NormESSI PROC      NEAR

         push      ax
         push      si

         mov       ax,es
         shr       si,1
         shr       si,1
         shr       si,1
         shr       si,1
         add       ax,si
         mov       es,ax

         pop       si
         pop       ax
         and       si,0fh
         ret

NormESSI ENDP

; -----------------------------------------------------------------------------
;        vyn soben¡ ‡¡sla DX:AX ‡¡slem BX:CX -> DX:AX sou‡in (CY=chyba, -> -1)
; -----------------------------------------------------------------------------

Nasob    PROC      NEAR

; ------ £schova registr–

         push      si
         push      di

; ------ AX*CX

         push      dx
         push      ax
         mul       cx                       ; AX * CX
         xchg      ax,si                    ; SI <- st©ada‡ LOW
         mov       di,dx                    ; DI <- st©ada‡ HIGH
         pop       ax

; ------ AX*BX

         mul       bx                       ; AX * BX
         add       di,ax
         adc       dx,0                     ; p©enos
         pop       ax                       ; DX
         jc        Nasob3                   ; p©ete‡en¡
         jnz       Nasob3                   ; p©ete‡en¡

; ------ DX*CX

         push      ax
         mul       cx                       ; DX * CX
         add       di,ax
         adc       dx,0
         pop       ax
         jc        Nasob3                   ; p©ete‡en¡
         jnz       Nasob3                   ; p©ete‡en¡

; ------ DX*BX

         mul       bx
         or        ax,dx
         jz        Nasob4                   ; nen¡ p©ete‡en¡

; ------ p©ete‡en¡ ‡¡sla

Nasob3:  mov       si,-1
         mov       di,si
         stc                                ; p©¡znak chyby

; ------ v˜sledek operace

Nasob4:  xchg      ax,si
         mov       dx,di

; ------ n vrat registr–

         pop       di
         pop       si
         ret

Nasob    ENDP

; -----------------------------------------------------------------------------
;        vydˆlen¡ ‡¡sla DX:AX ‡¡slem BX:CX -> DX:AX pod¡l
; -----------------------------------------------------------------------------

Vydel    PROC      NEAR

; ------ £schova registr–

         push      si

; ------ test, zda je dˆlen¡ velk˜m ‡¡slem

         or        bx,bx                    ; je dˆlen¡ velk˜m ‡¡slem ?
         jnz       Vydel2                   ; je dˆlen¡ velk˜m ‡¡slem

; ------ test, zda je dˆlen¡ nulou

         jcxz      Vydel8                   ; chyba operace - dˆlen¡ nulou

; ------ dˆlen¡ DX:AX ‡¡slem CX

         xchg      ax,si                    ; SI <- dˆlenec LOW (AX)
         xchg      ax,dx                    ; AX <- dˆlenec HIGH (DX)
         xor       dx,dx                    ; DX <- 0
         div       cx                       ; dˆlen¡ DX/CX
         xchg      ax,si                    ; SI <- nov‚ HIGH, AX <- star‚ LOW
         div       cx                       ; dˆlen¡ AX/CX
         xchg      dx,si                    ; DX <- nov‚ HIGH, SI <- zbytek
         clc                                ; operace OK
         jmp       short Vydel9

; ------ dˆlen¡ ode‡¡t n¡m velk‚ho ‡¡sla

Vydel2:  xchg      ax,si                    ; SI <- ‡¡slo LOW
         mov       ax,-1                    ; AX <- st©ada‡ pod¡lu
Vydel3:  inc       ax                       ; st©ada‡ pod¡lu
         sub       si,cx                    ; ode‡ten¡ dˆlitele
         sbb       dx,bx
         jnc       Vydel3                   ; nen¡ p©ete‡en¡
         xor       dx,dx
         jmp       short Vydel9

; ------ chyba operace - dˆlen¡ nulou

Vydel8:  mov       ax,-1
         mov       dx,ax
         stc                                ; p©¡znak chyby operace

; ------ n vrat registr–

Vydel9:  pop       si
         ret

Vydel    ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                                     data
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;þ

OldBreak db        1                        ; p–vodn¡ stav BREAK
ParBreak db        0                        ; p©¡znak p©eru¨en¡ operace

; ------ parametry pro displej

         EVEN
SegmVRAM dw        0b000h                   ; segment videopamˆti
PortCRT  dw        3dah                     ; b zov  adresa portu ©adi‡e CRT
EgaVga   db        0                        ; p©¡znak, ‘e je karta EGA/VGA
ChckSnow db        0                        ; p©¡znak, ‘e se kontroluje snˆ‘en¡

; ------ parametry pro komunika‡n¡ port

Port     dw        0                        ; ‡¡slo datov‚ho portu
AdrPort  dw        3f8h                     ; adresa portu COM
OldComm  dd        0                        ; p–vodn¡ adresa komunika‡n¡ho portu
Old08    dd        0                        ; p–vodn¡ adresa INT 08h
Old09    dd        0                        ; p–vodn¡ adresa INT 09h
OldInt   db        0                        ; p–vodn¡ registr p©eru¨en¡

; ------ ukazatel sektor–

VysDBlok dw        0                        ; po‡et vys¡lan˜ch datov˜ch blok–
PriDBlok dw        0                        ; po‡et p©ij¡man˜ch datov˜ch blok–
MaxDBlok dw        0                        ; maxim ln¡ po‡et datov˜ch blok–
SektBSeg dw        0                        ; adresa za‡ tku bufferu sektor–
SektESeg dw        0                        ; adresa konce bufferu sektor–
SektMax  dw        0                        ; maxim ln¡ po‡et sektor– v bufferu
SektSeg  dw        0                        ; segment aktu ln¡ho bufferu sektor–
SektUkaz dw        0                        ; ukazatel dat v aktu ln¡m sektoru
CitSekt  dw        0                        ; ‡¡ta‡ po‡tu sektor–

CitSynch dw        0                        ; ‡¡ta‡ synchroniza‡n¡ch sektor–
CitByte  db        0                        ; ‡¡ta‡ bajt– sektoru

Soubor   db        'POKUS1',0
Soubor2  db        'POKUS2',0

KeyBreak db        0                        ; p©¡znak p©eru¨en¡ z kl vesnice
Citac08  dw        0                        ; ‡¡ta‡ hodin INT 08h

; ------ z hlav¡ sektoru (10 bajt–)

         EVEN
Zahlavi  label     byte
TimeBeg  dd        0                        ; absolutn¡ ‡as za‡ tku nahr vky
TimeDel  dw        0                        ; d‚lka cel‚ nahr vky (sek.)
AktBlok  dw        0                        ; aktu ln¡ ‡¡slo bloku
AktSekt  db        0                        ; ‡¡slo aktu ln¡ho sektoru
InfBlok  db        0                        ; informace o organizaci blok–

FileOff  dw        -1                       ; offset prvn¡ho souboru v bloku

; ------ datov˜ buffer soubor– (velikost pro 1 datov˜ blok = 289*180 B)

BuffSeg  dw        0                        ; segment bufferu soubor–
BuffNum  dw        0                        ; po‡et bajt– v bufferu soubor–

; ------ mapa vadn˜ch sektor–

ErrMap   db        289 dup(-1)              ; 0=sektor je OK, -1=vadn˜ sektor
ErrCit   dw        289                      ; ‡¡ta‡ vadn˜ch sektor–

Param    db        0                        ; parametry
                                            ;  bit 0: 1=prov d¡ se korekce 1
                                            ;  bit 1: 1=prov d¡ se korekce 2
                                            ;  bit 2: 1=neulo‘en prac. soubor 1
                                            ;  bit 3: 1=neulo‘en prac. soubor 2

; ------ pracovn¡ soubory na disku

SoubCnf  db    'STREAMER.CNF',MAXF-12 dup(0) ; konfigura‡n¡ soubor STREAMER.CNF
SoubFil1 db    'STREAMER.$$1',MAXF-12 dup(0) ; seznam soubor– disku STREAMER.$$1
SoubFil2 db    'STREAMER.$$2',MAXF-12 dup(0) ; seznam soubor– strm. STREAMER.$$2
SoubFil3 db    'STREAMER.$$3',MAXF-12 dup(0) ; uschovan˜ p©ij. blok STREAMER.$$3

; ------ konfigurace programu



; ------ texty

KorTxt1  db        13,10,'Oprava 1 ..... $'
KorTxt2  db        13,10,'Oprava 2 ..... $'
KorTxt3  db        13,10,'Chyby ........ $'
KorTxt4  db        13,10,13,10,'$'

CitKor1  dw        0                        ; ‡¡ta‡ korekc¡ 1
CitKor2  dw        0                        ; ‡¡ta‡ korekc¡ 2
CitChyb  dw        0                        ; ‡¡ta‡ neopraviteln˜ch chyb

MemTxt   db        'CHYBA - nedostatek pameti ke spusteni programu !',13,10,'$'

; ------ parametry disku

AktDisk  db        0                        ; aktivn¡ disk
BlokSekt dw        0                        ; po‡et sektor– na aloka‡n¡ blok
SektByte dw        0                        ; po‡et bajt– na sektor
BlokByte dd        0                        ; po‡et bajt– na aloka‡n¡ blok
DiskFree dd        0                        ; voln  kapacita disku
DiskSumm dd        0                        ; celkov  kapacita disku
DiskSumC dw        0                        ; obsazen  kapacita disku (blok–)
                                            ;  (min. 40 - pro indikaci ‡ten¡)
DiskSumR dw        0                        ; ‡¡ta‡ na‡ten˜ch blok– disku

; ------ buffer jm‚na souboru

FilePthN dw        0                        ; d‚lka textu adres ©e + soubor
FilePath db        MAXF+2 dup(0)            ; buffer adres ©e + soubor

AllRoot  db        'C:\'
All      db        '*.*',0
TextStr  db        'STREAMER'

AdrHlp   dw        offset TextHlp0          ; adresa textu n povˆdy

SrcIndxE dw        0                        ; offset konce bufferu index–
SrcIndx  dd        50 dup(0)                ; buffer index– pro hled n¡ soubor–

TextFre1 db        11,' B volne z '
TextFil1 db        16,' souboru zabira '
TextFil2 db        2,' B'
DskTxtC  db        10,'<POD--ADR>'
DskTxtD  db        10,'<NAD--ADR>'
TextSel1 db        9,'oznaceno '
TextSel2 db        9,' souboru/'
TextReq1 db        20,'pozadovana kapacita '
TextReq2 db        14,'delka zaznamu '
TextReq3 db        6,' hod, '
TextReq4 db        6,' min, '
TextReq5 db        4,' sek'
LicTxt1  db        ' Video Streamer V1.00 '
LicTxt2  db        ' (c) Miroslav Nemecek '

TextHlp0 db        0,0,0

TextHlp1 db        0
         db        47,'Nacitam strukturu disku ',0
TextHl11 db        ' ',0,': '
TextHl12 db        '°°°°°°°°°°°°°°°°°°°°'
         db        15,'<ESC>=preruseni'

; ------ barvy

Color1   db        1bh                      ; z kladn¡ barva okna
Color2   db        1eh                      ; vysv¡cen  barva okna
Color3   db        30h                      ; barva kurzoru
Color4   db        3eh                      ; kurzor na vysv¡cen‚ polo‘ce
ColorH1  db        70h                      ; bˆ‘n  barva okna n povˆdy
ColorH2  db        74h                      ; vysv¡cen  barva n povˆdy

; ------ tabulka DTA

         EVEN
DTA      label     byte
         db        15h dup(?)
DTAAtrib db        ?                        ; (1) atributy nalezen‚ho souboru
DTATime  dw        ?                        ; (2) ‡as nalezen‚ho souboru
DTADate  dw        ?                        ; (2) datum nalezen‚ho souboru
DTASize  dd        ?                        ; (4) velikost nalezen‚ho souboru
DTAName  db        13 dup(?)                ; (13) jm‚no nalez. souboru ASCIIZ
         db        ?                        ; zarovn n¡ na sudou adresu

; ------ definice seznam–

DatSegm  dw        0                        ; za‡ tek datov‚ho segmentu
DatFre   dw        0                        ; konec voln‚ho m¡sta v dat.segmentu
DatMax   dw        0                        ; maxim ln¡ konec datov‚ho segmentu

DatParm  db        0                        ; parametry seznam–
                                            ;   b0: 1=aktivn¡ prav‚ okno (str.)

DatParmL db        b0                       ; t©¡dˆn¡ seznamu L (disk)
                                            ;      b0: 1=t©¡dˆn¡ podle jm‚na
                                            ;      b1: 1=t©¡dˆn¡ podle p©¡pony
                                            ;      b2: 1=t©¡dˆn¡ podle velikosti
                                            ;      b3: 1=t©¡dˆn¡ podle data+‡asu
                                            ;      b4: 1=adres © se net©¡d¡
                                            ;      b5: 1=sestupn‚ t©¡dˆn¡ adres.

DatParmR db        b0                       ; t©¡dˆn¡ seznamu R (streamer)
                                            ;      b0: 1=t©¡dˆn¡ podle jm‚na
                                            ;      b1: 1=t©¡dˆn¡ podle p©¡pony
                                            ;      b2: 1=t©¡dˆn¡ podle velikosti
                                            ;      b3: 1=t©¡dˆn¡ podle data+‡asu
                                            ;      b4: 1=adres © se net©¡d¡
                                            ;      b5: 1=sestupn‚ t©¡dˆn¡ adres.

         EVEN

; ------ definice lev‚ho okna - disk

DatDefL  db        SeznSum dup(?)           ; definice seznamu disku

; ------ definice prav‚ho okna - streamer

DatDefR  db        SeznSum dup(?)           ; definice seznamu streameru

; ------ z sobn¡k

         dw        800h dup(?)
Zasob    label     word                     ; konec z sobn¡ku

Code     ENDS

         END       Start
