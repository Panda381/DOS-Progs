

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                             Hled n¡ soubor– na disku
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

; N vratov‚ k¢dy:
;      0 = nalezeny soubory
;      1 = ‘ dn‚ soubory nenalezeny
;      2 = program p©eru¨en bez zad n¡ hled n¡
;      3 = chyba programu (nedostatek pamˆti a jin‚)

; -----------------------------------------------------------------------------
;        konstanty
; -----------------------------------------------------------------------------

HI       EQU       256

bit0     EQU       1
bit1     EQU       2
bit2     EQU       4
bit3     EQU       8
bit4     EQU       10h
bit5     EQU       20h
bit6     EQU       40h
bit7     EQU       80h
bit8     EQU       100h
bit9     EQU       200h
bit10    EQU       400h
bit11    EQU       800h
bit12    EQU       1000h
bit13    EQU       2000h
bit14    EQU       4000h
bit15    EQU       8000h

INDXSIZE EQU       5000                     ; maxim ln¡ po‡et index–

HISTSIZE EQU       400h                     ; velikost historie
MAXLINE  EQU       HISTSIZE-2               ; maxim ln¡ d‚lka editovan‚ho © dku
SIRKA    EQU       56                       ; ¨¡©ka okna voleb soubor–

VIEWBMAX EQU       1C00h                    ; velikost bufferu prohl¡‘e‡e
SRCBMAX  EQU       3000h                    ; velikost bufferu hled n¡ textu

InfoKPoz EQU       62                       ; po‡ te‡n¡ pozice inform. kurzoru
InfoKSir EQU       10                       ; ¨¡©ka informa‡n¡ho kurzoru

; -----------------------------------------------------------------------------
;        atributy soubor–
; -----------------------------------------------------------------------------

RO       EQU       bit0                     ; R/O
HID      EQU       bit1                     ; HID
SYS      EQU       bit2                     ; SYS
VOL      EQU       bit3                     ; VOL
DIR      EQU       bit4                     ; DIR
ARC      EQU       bit5                     ; ARC

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                          Start programu
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

Code     SEGMENT
         ASSUME    cs:Code,ds:Code

; ------ inicializace segmentov˜ch registr–

Start:   sti
         push      cs
         pop       ds                       ; DS <- CS
         mov       ds:[SegPSP],es           ; £schova segmentu PSP
         push      cs
         pop       es                       ; ES <- CS

; ------ inicializace p©¡kazov‚ho © dku

         call      InitCom                  ; inicializace p©¡kazov‚ho © dku

; ------ inicializace pamˆti

         call      InitMem                  ; inicializace pamˆti
         jnc       Start3                   ; pamˆŸ OK

; ------ chyba - nedostatek pamˆti

Start1:  mov       si,offset UvTxt          ; £vodn¡ text
         call      StdErr                   ; zobrazen¡ textu
         mov       si,offset MemTxt         ; chybov˜ text
         call      StdErr                   ; hl ¨en¡ - chyba pamˆti
         mov       ax,4c02h                 ; chyba pamˆti
         int       21h                      ; konec programu s chybou

; ------ skok na obsluhu © dkov‚ho re‘imu

Start3:  test      byte ptr ds:[Param],bit7 ; je p©¡kazov˜ © dek ?
         jz        Start4                   ; nen¡ p©¡kazov˜ © dek
         jmp       HledC                    ; hled n¡ v © dkov‚m re‘imu

; ------ inicializace videom¢du

Start4:  call      DetCard                  ; detekce videokarty
         call      InitVMod                 ; inicializace videom¢du
         call      PushScr                  ; £schova obsahu obrazovky

; ------ vypnut¡ p©¡znaku BREAK

         mov       ax,3300h
         int       21h                      ; poskytnut¡ p©ep¡na‡e BREAK
         mov       ds:[OldBreak],dl         ; £schova p©ep¡na‡e BREAK
         mov       dl,0                     ; BREAK bude vypnut
         mov       ax,3301h
         int       21h                      ; vypnut¡ p©¡znaku BREAK

; ------ obsluha p©eru¨en¡

         mov       ax,2523h
         mov       dx,offset INT23
         int       21h                      ; INT 23h se bude ignorovat
         mov       ax,2524h
         mov       dx,offset INT24
         int       21h                      ; INT 24h se bude tak‚ ignorovat

; ------ oprava barev pro monochromatick˜ displej

         cmp       byte ptr ds:[AdrVRAM+3],0b8h
         je        Start5

         mov       si,offset ColMono
         mov       di,offset ColCol
         mov       cx,offset(ColCol0-ColCol)
         cld
         rep       movsb

; ------ inicializace historie

Start5:  call      InitHist                 ; sestaven¡ jm‚na souboru historie
         call      OpenHist                 ; otev©en¡ souboru historie
         call      ReadHist                 ; na‡ten¡ souboru historie
         mov       word ptr ds:[DispNum],0  ; nen¡ nic zobrazeno

Start60: call      CRHist                   ; spo‡ten¡ © dk– historie
         call      InitPred                 ; inicializace p©edvolby
         call      InitEdit                 ; p©¡prava ukazatel– editace

; ------ volba soubor– k vyhled n¡

Start6:  or        byte ptr ds:[Param],bit4 ; zad v  se soubor
         call      Volba                    ; volba soubor–
         jnc       Start555

         and       byte ptr ds:[Param],not bit4 ; zru¨en¡ p©¡znaku zad v n¡
         call      DispSezn                 ; zobrazen¡ seznamu
         test      byte ptr ds:[Param],bit0 ; bylo prvn¡ zad n¡ ?
         jnz       VolEnt49                 ; n vrat
         mov       al,1                     ; p©¡znak p©eru¨en¡ programu
         jmp       Main0                    ; p©eru¨en¡ voleb

Start555:and       byte ptr ds:[Param],not bit4 ; zru¨en¡ p©¡znaku zad v n¡

; ------ test, zda bylo nˆco zad no

         push      ds
         pop       es
         mov       di,offset EditBuff
         mov       cx,MAXLINE
         mov       al," "
         cld
         repe      scasb                    ; jsou sam‚ mezery ?
         je        Start6                   ; sam‚ mezery neplat¡

; ------ rozbor zad n¡ parametr–

         call      Rozbor                   ; rozbor zad n¡ parametr–
         jnc       VolEnt4                  ; zad n¡ OK

; ------ chyba zad n¡ parametr–
                                            ; zde je SI = adresa textu chyby
         sub       si,offset EditBuff       ; offset v edita‡n¡m bufferu
         mov       ds:[EditKurz],si         ; offset kurzoru na © dku
         mov       ax,0e07h                 ; znak p¡pnut¡
         mov       bx,7
         call      Int10P                   ; p¡pnut¡
         jmp       short Start6             ; nov‚ zobrazen¡ voleb

; ------ zru¨en¡ stejn‚ho © dku z bufferu historie

VolEnt4: call      LineLen                  ; stanoven¡ d‚lky © dku
         inc       cx
         inc       cx                       ; p©id n¡ koncov‚ho CR/LF
         call      LineSrc                  ; nalezen¡ stejn‚ho © dku v bufferu
         jc        VolEnt2                  ; stejn˜ © dek nenalezen
         call      LineDel                  ; zru¨en¡ stejn‚ho © dku z bufferu

; ------ zaji¨tˆn¡ voln‚ho m¡sta v bufferu

VolEnt2: mov       ax,ds:[HistNum]          ; po‡et bajt– v bufferu historie
         add       ax,cx                    ; nov  d‚lka bufferu historie
         cmp       ax,HISTSIZE              ; je ji‘ dost voln‚ho m¡sta ?
         jbe       VolEnt3                  ; je ji‘ dost voln‚ho m¡sta
         mov       si,offset HistBuff       ; prvn¡ © dek v bufferu
         call      LineDel                  ; zru¨en¡ prvn¡ho © dku v bufferu
         jmp       short VolEnt2            ; dal¨¡ © dek

; ------ vlo‘en¡ nov‚ho © dku do bufferu historie

VolEnt3: mov       di,offset HistBuff       ; buffer historie
         add       di,ds:[HistNum]          ; konec bufferu historie
         mov       si,offset EditBuff       ; edita‡n¡ buffer © dku
         add       ds:[HistNum],cx          ; zv˜¨en¡ velikosti dat v bufferu
         cld
         rep       movsb                    ; p©enos © dku do bufferu

; ------ vyhled n¡ zadan‚ho souboru

         or        byte ptr ds:[Param],bit0+bit2 ; prvn¡ zad n¡ + hled n¡
         call      KurzOff
         call      HledFile
         and       byte ptr ds:[Param],not bit2 ; zru¨en¡ p©¡znaku hled n¡
VolEnt49:call      DispHlp                  ; zobrazen¡ n povˆdy

; ------ obsluha seznamu

VolEnt4b:call      KurzOff
         call      InpChr
         test      byte ptr ds:[Param],bit5 ; je zobrazen¡ F3 ?
         jnz       VolEnt4d                 ; je zobrazen¡ F3
         cmp       bx,4100h                 ; F7
         jne       VolEnt4c
         jmp       Start60                  ; nov‚ hled n¡ souboru
VolEnt4c:cmp       bl,13
         je        VolEnt5                  ; konec s ENTER
VolEnt4d:call      Sezn                     ; obsluha seznamu
         cmp       bl,27
         jne       VolEnt4b                 ; nen¡ konec ESC
         mov       al,1                     ; p©¡znak p©eru¨en¡ programu
Main000: jmp       Main0                    ; konec ESC

; ------ konec programu ENTER

VolEnt5: cmp       word ptr ds:[InsNum],0   ; je ozna‡en nˆjak˜ soubor ?
         jne       VolEnt6                  ; je ozna‡en nˆjak˜ soubor

; ------ zmˆna adres ©e

         mov       bx,ds:[SeznAkt]          ; aktivn¡ polo‘ka
         call      GetKPth                  ; poskytnut¡ cesty pod kurzorem
         mov       al,0
         jc        Main000                  ; nen¡ ‘ dn  polo‘ka
         cmp       byte ptr ds:[di-2],":"   ; je to ROOT ?
         je        VolEnt51                 ; je to ROOT
         mov       byte ptr ds:[di-1],0     ; jinak zkr cen¡ o "\"
VolEnt51:mov       dl,ds:[ViewName]         ; ozna‡en¡ disku
         sub       dl,"A"                   ; ‡¡slo po‘adovan‚ho disku
         mov       ah,0eh
         int       21h                      ; nastaven¡ nov‚ho aktivn¡ho disku
         mov       dx,offset ViewName+2
         mov       ah,3bh
         int       21h                      ; nastaven¡ po‘adovan‚ho adres ©e
         jmp       short Main00

; ------ otev©en¡ souboru v˜pisu seznamu

VolEnt6: mov       dx,offset SoubList       ; soubor v˜pisu
         mov       ah,3ch
         xor       cx,cx
         int       21h                      ; vytvo©en¡ souboru v˜pisu
         mov       bx,ax                    ; identifik tor souboru
         mov       al,2                     ; p©¡znak chyby
         jc        Main0                    ; chyba vytvo©en¡ souboru

; ------ zobrazen¡ textu n povˆdy

         mov       ah,ds:[HelpCol]          ; barva textu n povˆdy
         mov       dl,0                     ; po‡ te‡n¡ pozice textu
         mov       dh,ds:[MaxRow]           ; posledn¡ © dek displeje
         mov       cl,80                    ; d‚lka textu n povˆdy
         mov       si,offset HelpTxt6       ; text n povˆdy
         push      ds
         pop       es
         call      DispTxt                  ; zobrazen¡ textu n povˆdy

; ------ adresa polo‘ky k v˜pisu

         xor       bp,bp                    ; ukazatel v seznamu
         mov       es,ds:[SeznSegm]         ; segment seznamu
VolEnt61:mov       si,bp
         shl       si,1
         mov       si,ds:[si+DispIndx]      ; adresa polo‘ky
         test      byte ptr es:[si],bit7    ; je to ozna‡en  polo‘ka ?
         jz        VolEnt63                 ; nen¡ to ozna‡en  polo‘ka

; ------ sestaven¡ jm‚na polo‘ky

         push      bx
         mov       bx,bp                    ; ‡¡slo polo‘ky
         call      GetKPth                  ; poskytnut¡ cesty souboru
         pop       bx
         jc        VolEnt62                 ; neplatn  polo‘ka
         add       si,9                     ; jm‚no souboru
VolEnt62:mov       al,es:[si]
         mov       ds:[di],al
         inc       si
         inc       di
         cmp       al,0
         jne       VolEnt62
         mov       word ptr ds:[di-1],0a0dh

; ------ z pis © dku se jm‚nem souboru

         mov       cx,di
         sub       cx,offset ViewName-1     ; d‚lka jm‚na souboru
         mov       dx,offset ViewName       ; jm‚no souboru
         mov       ah,40h
         int       21h                      ; z pis jm‚na souboru

; ------ dal¨¡ soubor

VolEnt63:inc       bp                       ; zv˜¨en¡ ‡¡sla polo‘ky
         cmp       bp,ds:[DispNum]          ; jsou ji‘ v¨echny polo‘ky ?
         jb        VolEnt61                 ; dal¨¡ polo‘ka

; ------ uzav©en¡ souboru v˜pisu

         mov       ah,3eh
         int       21h

Main00:  mov       al,0

; ------ n vrat z programu

Main0:   push      ax
         push      cs
         pop       ds

         mov       bx,ds:[SouborI]
         or        bx,bx
         jz        Main02

         test      byte ptr ds:[Param],bit0 ; byl zad n text ?
         jz        Main01                   ; text nebyl zad n

         mov       ax,4200h
         xor       cx,cx
         xor       dx,dx
         int       21h                      ; resetov n¡ souboru na za‡ tek

         mov       dx,offset HistBuff       ; buffer historie
         mov       cx,ds:[HistNum]          ; po‡et bajt– v bufferu
         mov       ah,40h
         int       21h                      ; ulo‘en¡ bufferu do souboru

         xor       cx,cx
         mov       ah,40h
         int       21h                      ; useknut¡ konce souboru

Main01:
         mov       ah,3eh
         int       21h                      ; uzav©en¡ souboru

Main02:
         push      cs
         pop       ds
         call      PopScr                   ; n vrat obsahu obrazovky

         mov       dl,ds:[OldBreak]         ; p–vodn¡ p©¡znak BREAK
         mov       ax,3301h
         int       21h                      ; n vrat p©¡znaku BREAK

         pop       ax

         mov       ah,4ch
         int       21h

; -----------------------------------------------------------------------------
;                              LineLen
;                  stanoven¡ d‚lky editovan‚ho © dku
; -----------------------------------------------------------------------------
; VSTUP: CX=d‚lka © dku (bez koncov‚ho CR/LF)
; -----------------------------------------------------------------------------

LineLen  PROC      NEAR

         push      si
         mov       cx,MAXLINE               ; max. d‚lka textu
         mov       si,offset EditBuff+MAXLINE-1 ; konec bufferu
LineLen1:cmp       byte ptr ds:[si]," "     ; je platn˜ znak ?
         jne       LineLen2                 ; je platn˜ znak
         dec       si
         loop      LineLen1
LineLen2:mov       word ptr ds:[si+1],0a0dh ; ozna‡en¡ konce © dku
         pop       si
         ret

LineLen  ENDP

; -----------------------------------------------------------------------------
;                                 LineSrc
;                  nalezen¡ stejn‚ho © dku v bufferu
; -----------------------------------------------------------------------------
; VSTUP: CX=d‚lka © dku (s koncov˜m CR/LF)
; VSTUP:SI=adresa nalezen‚ho © dku
;        CY=stejn˜ © dek nenalezen
; -----------------------------------------------------------------------------

LineSrc  PROC      NEAR

         push      ax
         push      bx
         push      di
         push      es

; ------ p©¡prava registr–

         mov       si,offset HistBuff
         mov       bx,ds:[HistLine]         ; po‡et © dk– v bufferu historie
         or        bx,bx                    ; je nˆjak˜ © dek ?
         jz        LineSrc4                 ; nen¡ ‘ dn˜ © dek
         push      ds
         pop       es
         cld

; ------ porovn n¡ jednoho © dku

LineSrc1:push      si
         push      cx
         mov       di,offset EditBuff       ; edita‡n¡ buffer
         repe      cmpsb                    ; porovn n¡ © dk–
         pop       cx
         pop       si
         je        LineSrc6                 ; © dek nalezen OK

; ------ p©¡prava pro dal¨¡ © dek

         call      LineNxt                  ; nalezen¡ dal¨¡ho © dku
         dec       bx                       ; ‡¡ta‡ © dk–
         jnz       LineSrc1                 ; porovn n¡ dal¨¡ho © dku

; ------ n vrat registr–

LineSrc4:stc
LineSrc6:pop       es
         pop       di
         pop       bx
         pop       ax
         ret

LineSrc  ENDP

; -----------------------------------------------------------------------------
;                             LineNxt
;               nalezen¡ dal¨¡ho © dku v bufferu
; -----------------------------------------------------------------------------
; VSTUP: SI=sou‡asn˜ ukazatel (nesm¡ b˜t konec bufferu histori¡ !)
; VSTUP:SI=adresa dal¨¡ho © dku
; -----------------------------------------------------------------------------

LineNxt  PROC      NEAR

         inc       si
LineNxt1:inc       si
         cmp       word ptr ds:[si-2],0a0dh
         jne       LineNxt1
         ret

LineNxt  ENDP

; -----------------------------------------------------------------------------
;                             LineDel
;                      zru¨en¡ © dku z bufferu
; -----------------------------------------------------------------------------
; VSTUP: SI=adresa © dku v bufferu (nesm¡ b˜t konec bufferu histori¡ !)
; -----------------------------------------------------------------------------

LineDel  PROC      NEAR

; ------ £schova registr–

         push      cx
         push      si
         push      di
         push      es

; ------ zru¨en¡ © dku z bufferu

         mov       di,si                    ; za‡ tek © dku
         call      LineNxt                  ; adresa dal¨¡ho © dku
         push      ds
         pop       es
         mov       cx,ds:[HistNum]          ; po‡et bajt– v bufferu historie
         add       cx,offset HistBuff       ; adresa konce historie
         sub       cx,si                    ; po‡et zbyl˜ch znak– k p©¡sunu
         cld
         rep       movsb                    ; p©¡sun zbytku dat

; ------ oprava £daje d‚lky dat

         sub       si,di                    ; d‚lka ru¨en‚ho © dku
         sub       ds:[HistNum],si          ; oprava £daje d‚lky dat

; ------ n vrat registr–

         pop       es
         pop       di
         pop       si
         pop       cx
         ret

LineDel  ENDP

; -----------------------------------------------------------------------------
;        obsluha INT 24h
; -----------------------------------------------------------------------------

INT24    PROC      FAR

         mov       al,0
INT23:   iret

INT24    ENDP


; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                       Inicializace programu
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

; -----------------------------------------------------------------------------
;        p©¡prava p©¡kazov‚ho © dku (CY=nen¡ nic zad no)
; -----------------------------------------------------------------------------

InitCom  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      si
         push      di
         push      ds
         push      es

; ------ p©¡prava registr–

         push      ds
         pop       es
         mov       si,81h                   ; za‡ tek p©¡kazov‚ho © dku
         mov       ds,ds:[SegPSP]           ; segment PSP
         mov       cl,ds:[si-1]             ; d‚lka p©¡kazov‚ho © dku
         mov       ch,0
         cld

; ------ nalezen¡ za‡ tku textu p©¡kaz. © dku

InitCom1:stc                                ; p©¡znak konce
         jcxz      InitCom9                 ; nen¡ nic zad no
         lodsb                              ; na‡ten¡ dal¨¡ho znaku
         dec       cx                       ; sn¡‘en¡ ‡¡ta‡e znak–
         cmp       al," "                   ; mezera ?
         je        InitCom1                 ; ignorov n¡ mezery
         cmp       al,9                     ; tabel tor ?
         je        InitCom1                 ; ignorov n¡ tabel toru

; ------ p©enesen¡ p©¡kazov‚ho © dku do bufferu

         or        byte ptr es:[Param],bit7 ; p©¡znak zad n¡ p©¡kaz. © dku
         inc       cx
         dec       si                       ; n vrat ukazatele
         mov       di,offset EditBuff       ; edita‡n¡ buffer
         mov       bx,MAXLINE               ; d‚lka edit. bufferu
         sub       bx,cx                    ; d‚lka zbytku
         rep       movsb                    ; £schova textu do bufferu
         mov       cx,bx                    ; d‚lka zbytku bufferu
         mov       al," "                   ; znak mezery
         rep       stosb                    ; doplnˆn¡ mezerami
         clc                                ; p©¡znak, ‘e je nˆco zad no

; ------ n vrat registr–

InitCom9:pop       es
         pop       ds
         pop       di
         pop       si
         pop       cx
         pop       bx
         pop       ax
         ret

InitCom  ENDP

; -----------------------------------------------------------------------------
;        inicializace pamˆti (CY=chyba pamˆti)
; -----------------------------------------------------------------------------

InitMem  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      es

; ------ zmen¨en¡ bloku programu

         mov       bx,SEG KonSegm           ; segment konce programu
         sub       bx,ds:[SegPSP]           ; velikost programu
         mov       es,ds:[SegPSP]           ; segment PSP
         mov       ah,4ah
         int       21h                      ; zmen¨en¡ bloku programu
         jc        InitMem9                 ; chyba - nedostatek pamˆti

; ------ vytvo©en¡ bufferu pro seznam soubor–

         mov       ah,48h
         mov       bx,0fffh                 ; velikost bloku 64 KB
         int       21h                      ; p©idˆlen¡ bloku 64 KB
         jnc       InitMem4                 ; blok p©idˆlen OK
         mov       ah,48h
         int       21h                      ; p©idˆlen¡ men¨¡ho bloku
         jc        InitMem9                 ; chyba pamˆti
InitMem4:shl       bx,1
         shl       bx,1
         shl       bx,1
         shl       bx,1                     ; velikost bufferu v bajtech
         mov       ds:[SeznSegm],ax         ; segment adresy bufferu seznamu
         mov       ds:[SeznSize],bx         ; velikost bufferu seznamu
         cmp       bx,200h                  ; minim ln¡ velikost bufferu (bajt–)

; ------ n vrat registr–

InitMem9:pop       es
         pop       bx
         pop       ax
         ret

InitMem  ENDP

; -----------------------------------------------------------------------------
;        £schova obsahu obrazovky
; -----------------------------------------------------------------------------

PushScr  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      es

; ------ vytvo©en¡ bufferu pro £schovu obrazovky

         mov       al,160/16                ; po‡et bajt– na © dek/16
         mul       byte ptr ds:[Radku]      ; v˜po‡et velikosti bufferu
         mov       bx,ax                    ; po‘adovan  velikost bufferu
         mov       ah,48h
         int       21h                      ; vytvo©en¡ bufferu
         jnc       PushScr1                 ; buffer vytvo©en OK
         mov       ah,48h
         int       21h                      ; vytvo©en¡ men¨¡ho bufferu
         jnc       PushScr1                 ; buffer vytvo©en OK
         xor       bx,bx                    ; jinak velikost = 0
PushScr1:mov       ds:[ObrSegm],ax          ; adresa bufferu
         shl       bx,1
         shl       bx,1
         shl       bx,1                     ; p©epo‡et na slova
         mov       ds:[ObrSize],bx          ; velikost bufferu (slov)

; ------ £schova obsahu obrazovky

         mov       es,ax                    ; adresa bufferu
         xor       di,di                    ; ukl dac¡ adresa
         mov       cx,bx                    ; velikost bufferu (slov)
         push      ds
         lds       si,ds:[AdrVRAM]          ; adresa videopamˆti
         cld
         rep       movsw                    ; £schova obsahu obrazovky
         pop       ds

; ------ £schova kurzoru

         call      GetKurz                  ; poskytnut¡ pozice kurzoru
         mov       ds:[OldKurz],dx          ; £schova pozice kurzoru

; ------ n vrat registr–

         pop       es
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

PushScr  ENDP

; -----------------------------------------------------------------------------
;        n vrat obsahu obrazovky
; -----------------------------------------------------------------------------

PopScr   PROC      NEAR

; ------ £schova registr–

         push      cx
         push      dx
         push      si
         push      di
         push      es

; ------ n vrat obsahu obrazovky

         push      ds
         les       di,ds:[AdrVRAM]          ; adresa videopamˆti
         mov       cx,ds:[ObrSize]          ; velikost videopamˆti
         xor       si,si
         mov       ds,ds:[ObrSegm]          ; adresa bufferu
         cld
         rep       movsw                    ; n vrat obsahu obrazovky
         pop       ds

; ------ n vrat kurzoru

         mov       dx,ds:[OldKurz]          ; p–vodn¡ pozice kurzoru
         call      SetKurz                  ; n vrat pozice kurzoru

; ------ n vrat registr–

         pop       es
         pop       di
         pop       si
         pop       dx
         pop       cx
         ret

PopScr   ENDP

; -----------------------------------------------------------------------------
;        sestaven¡ jm‚na souboru historie
; -----------------------------------------------------------------------------

InitHist PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      es

; ------ kontrola verze syst‚mu

         mov       ah,30h
         int       21h                      ; poskytnut¡ verze syst‚mu
         cmp       al,3
         jb        InitHst5                 ; n¡zk  verze syst‚mu

; ------ kontrola, zda je platn‚ prost©ed¡

         mov       es,ds:[SegPSP]           ; segment PSP
         mov       cx,es:[2ch]              ; segment prost©ed¡
         jcxz      InitHst5                 ; nen¡ platn‚ prost©ed¡

; ------ p©¡prava k prohled v n¡ prost©ed¡

         push      ds
         mov       ds,cx                    ; adresa prost©ed¡
         xor       si,si                    ; po‡ te‡n¡ ukazatel
         mov       cx,7fffh                 ; max. velikost prost©ed¡

; ------ nalezen¡ jm‚na souboru v prost©ed¡

         xor       ax,ax                    ; hledan‚ slovo
InitHst2:inc       si                       ; dal¨¡ bajt
         cmp       word ptr ds:[si-1],ax    ; je konec prost©ed¡ ?
         loopne    InitHst2                 ; nalezen¡ konce prost©ed¡
         pop       es                       ; ES <- datov˜ segment
         add       si,3                     ; za‡ tek jm‚na souboru

; ------ p©enesen¡ jm‚na do bufferu

         mov       di,offset SoubHist       ; buffer jm‚na souboru
         mov       cx,128                   ; max. d‚lka jm‚na
         cld
InitHst3:lodsb                              ; na‡ten¡ znaku
         cmp       al,"a"
         jb        InitHst4
         cmp       al,"z"
         ja        InitHst4
         sub       al,32                    ; korekce na velk‚ p¡smeno
InitHst4:stosb                              ; ulo‘en¡ znaku do bufferu
         cmp       al,0                     ; je konec jm‚na ?
         loopne    InitHst3                 ; p©enos dal¨¡ho znaku
         push      es
         pop       ds                       ; DS <- datov˜ segment

; ------ nalezen¡ konce cesty

InitHst5:mov       si,offset SoubHist       ; buffer se souborem
         cld
InitHst6:mov       di,si                    ; konec cesty
InitHst7:lodsb                              ; na‡ten¡ znaku
         cmp       al,"\"                   ; oddˆlova‡ cesty ?
         je        InitHst6                 ; oddˆlova‡ cesty
         cmp       al,":"                   ; oddˆlova‡ disku ?
         je        InitHst6                 ; oddˆlova‡ disku
         cmp       al,0                     ; konec textu ?
         jne       InitHst7                 ; nen¡ konec textu - dal¨¡ znak

; ------ p©id n¡ jm‚na souboru histori¡

         mov       si,offset SoubHst
         mov       cx,11                    ; d‚lka jm‚na
         rep       movsb                    ; p©enesen¡ jm‚na souboru

; ------ kopie do bufferu souboru v˜pisu

         mov       cx,di
         mov       si,offset SoubHist
         mov       di,offset SoubList
         sub       cx,si                    ; d‚lka textu
         rep       movsb                    ; kopie do souboru v˜pisu
         mov       byte ptr ds:[di-4],"L"   ; zmˆna na p©¡ponu LST

; ------ n vrat registr–

         pop       es
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

InitHist ENDP

; -----------------------------------------------------------------------------
;        otev©en¡ souboru historie
; -----------------------------------------------------------------------------

OpenHist PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx

; ------ otev©en¡ souboru pro z pis i ‡ten¡

         mov       dx,offset SoubHist       ; soubor historie
         mov       ax,3d02h
         int       21h                      ; otev©en¡ pro z pis
         jnc       OpenHst7                 ; soubor otev©en OK

; ------ vytvo©en¡ nov‚ho souboru

         mov       ah,3ch
         xor       cx,cx                    ; atributy
         int       21h                      ; vytvo©en¡ souboru
         jnc       OpenHst7                 ; soubor vytvo©en OK

; ------ otev©en¡ souboru alespo¤ pro ‡ten¡

         mov       ax,3d00h
         int       21h                      ; otev©en¡ alespo¤ pro ‡ten¡
         jnc       OpenHst7                 ; soubor otev©en OK

; ------ £schova identifik toru souboru

         xor       ax,ax                    ; soubor nen¡ otev©en
OpenHst7:mov       ds:[SouborI],ax          ; identifik tor souboru

; ------ n vrat registr–

         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

OpenHist ENDP

; -----------------------------------------------------------------------------
;        na‡ten¡ souboru historie
; -----------------------------------------------------------------------------

ReadHist PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx

; ------ na‡ten¡ souboru historie

         mov       bx,ds:[SouborI]          ; identifik tor souboru historie
         or        bx,bx
         jz        ReadHst1                 ; soubor nen¡ otev©en
         mov       dx,offset HistBuff       ; buffer historie
         mov       cx,HISTSIZE              ; velikost bufferu
         mov       ah,3fh
         int       21h                      ; na‡ten¡ historie
         jnc       ReadHst2                 ; ‡ten¡ OK
ReadHst1:xor       ax,ax                    ; nen¡ nic na‡teno
ReadHst2:mov       ds:[HistNum],ax          ; po‡et bajt– v bufferu

; ------ n vrat registr–

         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

ReadHist ENDP

; -----------------------------------------------------------------------------
;        spo‡ten¡ po‡tu © dk– historie
; -----------------------------------------------------------------------------

CRHist   PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si

; ------ p©¡prava ke spo‡ten¡ © dk–

         mov       si,offset HistBuff       ; buffer historie
         mov       cx,ds:[HistNum]          ; po‡et bajt– v bufferu historie
         mov       dx,si                    ; £schova konce dat v historii
         xor       bx,bx                    ; ‡¡ta‡ © dk– historie
         mov       ax,0a0dh                 ; hledan˜ konec © dku
         sub       cx,1                     ; 1 bajt rezerva
         jbe       CRHist4                  ; je 0 nebo 1 bajt - nen¡ © dek

; ------ spo‡ten¡ po‡tu © dk–

CRHist1: cmp       ds:[si],ax               ; je konec © dku ?
         jne       CRHist2                  ; nen¡ konec © dku
         inc       bx                       ; zv˜¨en¡ ‡¡ta‡e © dk–
         mov       dx,si                    ; £schova konec dat
         inc       dx
         inc       dx                       ; oprava konce o znaky CR/LF
CRHist2: inc       si                       ; posun adresy v bufferu
         loop      CRHist1                  ; test dal¨¡ho znaku

; ------ ulo‘en¡ po‡tu © dk– a oprava velikosti dat v bufferu

CRHist4: sub       dx,offset HistBuff       ; offset v bufferu historie
         mov       ds:[HistNum],dx          ; po‡et bajt– historie
         mov       ds:[HistLine],bx         ; po‡et © dk– historie

; ------ n vrat registr–

         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

CRHist   ENDP

; -----------------------------------------------------------------------------
;        inicializace p©edvolby
; -----------------------------------------------------------------------------

InitPred PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      es

; ------ ulo‘en¡ ozna‡en¡ disku

         mov       di,offset SelBuff        ; buffer p©edvolby
         push      ds
         pop       es                       ; ES <- datov˜ segment
         mov       ah,19h
         int       21h                      ; poskytnut¡ aktivn¡ho disku
         mov       dl,al                    ; aktivn¡ disk
         inc       dx                       ; aktivn¡ disk + 1
         add       al,"A"                   ; korekce na znak ASCII
         mov       ah,":"                   ; oddˆlova‡ jm‚na disku
         cld
         stosw                              ; ulo‘en¡ jm‚na disku a znaku ":"
         mov       ax,"\"                   ; ozna‡en¡ ROOT "\" a koncov  0
         stosw                              ; ulo‘en¡ "\" a koncov‚ 0
         dec       di                       ; n vrat ukazatele na koncovou 0

; ------ na‡ten¡ aktivn¡ho adres ©e disku

         mov       si,di                    ; buffer k na‡ten¡ adres ©e
         mov       ah,47h
         int       21h                      ; poskytnut¡ aktivn¡ho adres ©e

; ------ nalezen¡ konce cesty

         cld
         mov       cx,80                    ; max. d‚lka textu cesty
         mov       al,0                     ; hledan˜ bajt 0
         repne     scasb                    ; nalezen¡ koncov‚ 0
         dec       di                       ; n vrat na koncovou 0

; ------ zaji¨tˆn¡ koncov‚ho znaku "\"

         mov       al,"\"                   ; koncov‚ lom¡tko
         cmp       ds:[di-1],al             ; je text zakon‡en znakem "\" ?
         je        InitPrd3                 ; je ji‘ zakon‡en znakem "\"
         stosb                              ; ulo‘en¡ koncov‚ho znaku "\"

; ------ ulo‘en¡ specifikace "*.*"

InitPrd3:mov       ax,".*"                  ; za‡ tek specifikace
         stosw                              ; ulo‘en¡ textu "*."
         stosb                              ; ulo‘en¡ znaku "*"
         mov       ax,0a0dh                 ; koncov‚ znaky CR/LF
         stosw                              ; ulo‘en¡ znak– CR/LF

; ------ n vrat registr–

         pop       es
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

InitPred ENDP

; -----------------------------------------------------------------------------
;        p©¡prava ukazatel– editace
; -----------------------------------------------------------------------------

InitEdit PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx

; ------ aktivn¡ © dek s kurzorem

         mov       ax,ds:[HistLine]         ; po‡et © dk– v historii
         mov       ds:[EditKurL],ax         ; © dek s kurzorem

; ------ prvn¡ zobrazen˜ © dek

         mov       ch,0
         mov       cl,ds:[Radku]            ; po‡et © dk– displeje
         sub       cl,6+1                   ; ode‡ten¡ okraj– + 1
         sub       ax,cx                    ; po‡ te‡n¡ zobrazen˜ © dek
         jnc       InitEdi2                 ; je v¡ce © dk– ne‘ je displej
         xor       ax,ax                    ; omezen¡ na 0
InitEdi2:mov       ds:[EditTopL],ax         ; po‡ te‡n¡ zobrazen˜ © dek

; ------ p©¡prava ostatn¡ch ukazatel–

         mov       word ptr ds:[EditKurz],0 ; offset kurzoru v © dku
         mov       word ptr ds:[EditTop],0  ; offset po‡ tku © dku
         and       byte ptr ds:[Param],not bit1 ; p©¡znak, ‘e nebyla editace

; ------ n vrat registr–

         pop       cx
         pop       ax
         ret

InitEdit ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                      hled n¡ soubor– v © dkov‚m re‘imu
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

;þ

; ------ test, zda je po‘adov no zobrazen¡ n povˆdy

HledC:   mov       si,offset EditBuff       ; buffer s textem
         mov       cx,MAXLINE               ; d‚lka bufferu
         cld
HledC11: lodsb
         cmp       al," "
         loope     HledC11
         je        HledC3
         cmp       al,"/"
         jne       HledC14
         lodsb
         cmp       al,"?"
         je        HledC1

; ------ rozbor zad n¡ parametr–

HledC14: call      Rozbor                   ; rozbor zad n¡ parametr–
         jnc       HledC3                   ; zad n¡ soubor– OK

; ------ chyba zad n¡ parametr–

HledC1:  mov       si,offset UvTxt          ; £vodn¡ text - nadpis
         call      StdOut                   ; zobrazen¡ £vodn¡ho textu

; ------ konverze n povˆdy do © dkov‚ho tvaru

         mov       si,offset HelpTx11+2     ; za‡ tek textu
         mov       cx,(offset(HelpTx12-HelpTx11)/80) ; po‡et © dk–
         push      si
HledC2:  add       si,80                    ; adresa dal¨¡ho © dku
         mov       byte ptr ds:[si-3]," "
         mov       word ptr ds:[si-2],0a0dh ; od© dkov n¡
         loop      HledC2                   ; dal¨¡ © dek
         mov       byte ptr ds:[si],0       ; konec textu
         pop       si
         call      Stdout                   ; zobrazen¡ textu n povˆdy
         mov       ax,4c02h
         int       21h                      ; konec s chybou

; ------ vyhled n¡ soubor– podle zad n¡

HledC3:  call      DispCek                  ; zobrazen¡ hl ¨en¡ "€ekejte"
         or        byte ptr ds:[Param],bit2 ; p©¡znak hled n¡ souboru
         call      HledFile                 ; vyhled n¡ v¨ech soubor–
         pushf
         call      ClrCek                   ; vymaz n¡ hl ¨en¡
         popf
         mov       al,1                     ; p©¡znak p©eru¨en¡
         jc        HledCX                   ; p©eru¨en¡ programu

; ------ zobrazen¡ hl ¨en¡ o nalezen˜ch souborech

         push      ds
         pop       es
         mov       di,offset BuffNum0       ; buffer k dek¢dov n¡ ‡¡sla
         mov       ax,ds:[CitSoub]          ; po‡et soubor–
         mov       si,offset FndTxt0        ; text - nic nenalezeno
         or        ax,ax                    ; bylo nˆco nalezeno ?
         jz        HledCB4                  ; nebylo nic nalezeno

         mov       si,offset FndTxt1        ; text "Nalezeno"
         call      StdErr                   ; zobrazen¡ "Nalezeno"

         xor       dx,dx
         call      DekNum                   ; dek¢dov n¡ po‡tu soubor–
         call      StdErr                   ; zobrazen¡ po‡tu soubor–

         mov       si,offset FndTxt2        ; text "souboru, celkem"
         call      StdErr                   ; zobrazen¡ textu

         mov       ax,word ptr ds:[CitCelk] ; sou‡et LOW
         mov       dx,word ptr ds:[CitCelk+2] ; sou‡et HIGH
         call      DekNum                   ; dek¢dov n¡ sou‡tu velikost¡
         call      StdErr                   ; zobrazen¡ ‡¡sla

         mov       si,offset FndTxt3        ; text "obsazena kapacita"
         call      StdErr                   ; zobrazen¡ textu

         mov       ax,word ptr ds:[CitObs]  ; kapacita LOW
         mov       dx,word ptr ds:[CitObs+2] ; kapacita HIGH
         call      DekNum                   ; dek¢dov n¡ obsazen‚ kapacity
         call      StdErr                   ; zobrazen¡ ‡¡sla

         mov       si,offset FndTxt4        ; text "B."
HledCB4: call      StdErr                   ; zobrazen¡ hl ¨en¡
         mov       al,0

HledCX:  mov       ah,4ch
         int       21h

; -----------------------------------------------------------------------------
;        v˜pis na‡ten‚ ‡ sti seznamu
; -----------------------------------------------------------------------------

ListSezn PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      es

; ------ po‡et soubor– k zobrazen¡

         mov       cx,ds:[DispNum]          ; po‡et soubor– k zobrazen¡
         mov       bx,ds:[DispNumO]         ; za‡ tek - prvn¡ polo‘ka
         inc       bx
         sub       cx,bx                    ; po‡et nov˜ch polo‘ek
         jbe       ListSzn9                 ; nen¡ nic

; ------ dek¢dov n¡ polo‘ek

         cmp       word ptr ds:[FilePthN],7
         jae       ListSzn0                 ; nemus¡ se mazat
         call      ClrCek                   ; maz n¡ hl ¨en¡ "€ekejte"
ListSzn0:mov       es,ds:[SeznSegm]         ; segment se seznamem
ListSzn1:mov       si,bx
         shl       si,1
         mov       si,ds:[si+DispIndx]      ; adresa polo‘ky
         add       si,9                     ; jm‚no souboru
         call      AddGFil                  ; p©id n¡ jm‚na k cestˆ
         jc        ListSzn2
         mov       si,offset FilePath       ; jm‚no souboru
         call      StdOut                   ; zobrazen¡ textu
         mov       si,offset CRLFTxt        ; text od© dkov n¡
         call      StdOut                   ; od© dkov n¡ textu
         call      SubGFil                  ; odstranˆn¡ polo‘ky
ListSzn2:inc       bx                       ; zv˜¨en¡ ‡¡sla polo‘ky
         loop      ListSzn1                 ; dal¨¡ polo‘ka
         call      DispCek                  ; zobrazen¡ hl ¨en¡ "‡ekejte"

; ------ n vrat registr–

ListSzn9:pop       es
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

ListSezn ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                   Obsluha volby soubor– k vyhled n¡
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

;þ

; -----------------------------------------------------------------------------
;     volba (pokra‡ov n¡ volby) soubor– k editaci (CY=p©eru¨en¡ volby ESC)
; -----------------------------------------------------------------------------

Volba    PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx

; ------ z kladn¡ zobrazen¡ obrazovky

Volba1:  call      DispAll                  ; zobrazen¡ obrazovky
         jmp       short Volba3

; ------ nov‚ zobrazen¡ voleb

Volba2:  call      KorVol                   ; korekce voleb
         call      DispVol                  ; nov‚ zobrazen¡ voleb

; ------ zobrazen¡ editovan‚ho © dku

Volba3:  call      KorEdit                  ; korekce editovan‚ho © dku
         call      DispEdit                 ; zobrazen¡ editovan‚ho © dku

; ------ zobrazen¡ kurzoru a ‡ek n¡ na znak z kl vesnice

Volba4:  call      KurzEdit                 ; zobrazen¡ kurzoru edit. © dku
         call      InpChr                   ; vstup znaku z kl vesnice
         call      JumpBX                   ; obsluha kl vesy

; ------ zvl ¨tn¡ ©¡dic¡ funkce

         dw        11bh,VolEsc              ; p©eru¨en¡ operace ESC
         dw        1c0dh,VolEnt             ; ENTER
         dw        3b00h,VolbaN             ; F1 n povˆda
         dw        180fh,VolbaO             ; Ctrl-O zobrazen¡ podkladu

; ------ posun kurzoru vertik lnˆ po © dc¡ch

         dw        4800h,VolUp              ; kurzor nahoru
         dw        5000h,VolDwn             ; kurzor dol–
         dw        4900h,VolPgUp            ; PageUp
         dw        5100h,VolPgDn            ; PageDown
         dw        8400h,VolCPgUp           ; ^PageUp
         dw        7600h,VolCPgDn           ; ^PageDown

; ------ posun kurzoru horizont lnˆ na © dku

         dw        4d00h,VolRght            ; kurzor vpravo
         dw        4b00h,VolLft             ; kurzor vlevo
         dw        7300h,VolCLft            ; ^vlevo
         dw        7400h,VolCRght           ; ^vpravo
         dw        4700h,VolHome            ; HOME
         dw        4f00h,VolEnd             ; END

; ------ editace © dku

         dw        5300h,VolDel             ; DELETE
         dw        0e08h,VolBS              ; BS
         dw        1414h,VolCDel            ; ^T
         dw        0e7fh,VolCBS             ; ^BS
         dw        0,VolbaCh                ; zad n¡ znaku

; ****** editace © dku

; ------ zad n¡ jednoho znaku

VolbaCh: cmp       bx,300h                  ; je Ctrl-@ ?
         je        VolbaCh1                 ; je Ctrl-@
         cmp       bl,0                     ; je platn˜ znak ?
         jne       VolbaCh1                 ; je platn˜ znak
         jmp       Volba4                   ; nen¡ platn  kl vesa - dal¨¡ znak
VolbaCh1:test      byte ptr ds:[Param],bit1 ; je editace zah jena ?
         jnz       VolbaCh2                 ; editace je ji‘ zah jena
         call      BegEdit                  ; zah jen¡ editace
         call      ClrEdit                  ; nulov n¡ edita‡n¡ho bufferu
         mov       word ptr ds:[EditKurz],0 ; pozice kurzoru na © dku
VolbaCh2:call      InsChr                   ; vlo‘en¡ znaku BL do bufferu
         jmp       short VolRght1           ; posun kurzoru vpravo

; ------ vymaz n¡ znaku BS

VolBS:   call      BegEdit                  ; zah jen¡ editace
         cmp       word ptr ds:[EditKurz],0 ; je ji‘ za‡ tek © dku ?
         je        VolDel4                  ; je ji‘ za‡ tek © dku
         dec       word ptr ds:[EditKurz]   ; posun kurzoru

; ------ vymaz n¡ znaku DELETE

VolDel:  call      BegEdit                  ; zah jen¡ editace
         mov       bx,1                     ; 1 znak ke zru¨en¡
VolDel3: call      DelCh                    ; zru¨en¡ 1 znaku na pozici kurzoru
VolDel4: jmp       Volba3

; ------ zru¨en¡ slova p©ed kurzorem

VolCBS:  mov       bx,ds:[EditKurz]         ; aktu ln¡ pozice kurzoru
         call      EdiCLeft                 ; posun o slovo vlevo
         sub       bx,ds:[EditKurz]         ; rozd¡l kurzoru
         jmp       short VolDel3            ; vymaz n¡ znak–

; ------ zru¨en¡ slova za kurzorem

VolCDel: mov       bx,ds:[EditKurz]         ; aktu ln¡ pozice kurzoru
         call      EdiCRght                 ; posun o slovo vpravo
         xchg      bx,ds:[EditKurz]         ; n vrat kurzoru
         sub       bx,ds:[EditKurz]         ; rozd¡l kurzoru
         jmp       short VolDel3            ; zru¨en¡ slova za kurzorem

; ****** posun kurzoru horizont lnˆ na © dku

; ------ posun kurzoru o pozici vpravo

VolRght: call      BegEdit                  ; zah jen¡ editace
VolRght1:cmp       word ptr ds:[EditKurz],MAXLINE-1 ; je ji‘ maxim ln¡ d‚lka ?
         jae       VolRght4                 ; je ji‘ maxim ln¡ d‚lka
         inc       word ptr ds:[EditKurz]   ; posun kurzoru
VolRght4:jmp       Volba3                   ; nov‚ zobrazen¡ editovan‚ho © dku

; ------ posun o slovo vpravo

VolCRght:call      BegEdit                  ; zah jen¡ editace
         call      EdiCRght                 ; posun o slovo vpravo
         jmp       Volba3

; ------ posun kurzoru o pozici vlevo

VolLft:  call      BegEdit                  ; zah jen¡ editace
         cmp       word ptr ds:[EditKurz],0
         je        VolLft4
         dec       word ptr ds:[EditKurz]
VolLft4: jmp       Volba3

; ------ posun o slovo vlevo

VolCLft: call      BegEdit                  ; zah jen¡ editace
         call      EdiCLeft                 ; posun o slovo vlevo
         jmp       Volba3

; ------ za‡ tek © dku HOME

VolHome: call      BegEdit                  ; zah jen¡ editace
         mov       word ptr ds:[EditKurz],0 ; kurzor na za‡ tek © dku
         jmp       Volba3

; ------ konec © dku END

VolEnd:  call      BegEdit                  ; zah jen¡ editace
         call      EditEnd                  ; kurzor na konec © dku
         jmp       Volba3

; ****** posun kurzoru vertik lnˆ po © dc¡ch

; ------ prvn¡ © dek ^PageUp

VolCPgUp:mov       bx,-1                    ; posun o maximum © dk–
         jmp       short VolUp0             ; posun na za‡ tek

; ------ str nka nahoru PageUp

VolPgUp: mov       bh,0
         mov       bl,ds:[Radku]            ; po‡et © dk– displeje
         sub       bl,8                     ; ode‡ten¡ okraj–
         jmp       short VolUp0             ; posun o str nku

; ------ kurzor nahoru

VolUp:   mov       bx,1                     ; posun o 1 © dek

VolUp0:  call      EndEdit                  ; ukon‡en¡ editace © dku
VolUp1:  cmp       word ptr ds:[EditKurL],0 ; je ji‘ prvn¡ © dek ?
         je        VolUp9                   ; je ji‘ prvn¡ © dek
         dec       word ptr ds:[EditKurL]   ; posun © dkov‚ho kurzoru
         dec       bx                       ; ‡¡ta‡ © dk– k posunu
         jnz       VolUp1                   ; posun o dal¨¡ © dek
VolUp9:  jmp       Volba2

; ------ posledn¡ © dek ^PageDown

VolCPgDn:mov       bx,-1                    ; maximum © dk–
         jmp       short VolDwn0            ; posun kurzoru

; ------ str nka dol– PageDown

VolPgDn: mov       bh,0
         mov       bl,ds:[Radku]            ; po‡et © dk– displeje
         sub       bl,8                     ; ode‡ten¡ okraj–
         jmp       short VolDwn0            ; posun kurzoru

; ------ kurzor dol–

VolDwn:  mov       bx,1                     ; posun o 1 © dek

VolDwn0: call      EndEdit                  ; ukon‡en¡ editace © dku
VolDwn1: mov       ax,ds:[EditKurL]         ; © dkov˜ kurzor
         cmp       ax,ds:[HistLine]         ; je ji‘ posledn¡ © dek ?
         je        VolDwn9                  ; je ji‘ posledn¡ © dek
         inc       word ptr ds:[EditKurL]   ; zv˜¨en¡ ‡¡sla © dku
         dec       bx                       ; ‡¡ta‡ © dk– k posunu
         jnz       VolDwn1                  ; posun o dal¨¡ © dek
VolDwn9: jmp       Volba2

; ****** zvl ¨tn¡ ©¡dic¡ funkce

; ------ zobrazen¡ podkladu

VolbaO:  call      PopScr                   ; n vrat podkladu
         call      InpChr                   ; ‡ek n¡ na stisk kl vesnice
         jmp       short VolbaN1

; ------ n povˆda

VolbaN:  call      Napoveda                 ; obsluha n povˆdy
VolbaN1: jmp       Volba1                   ; nov‚ zobrazen¡ obrazovky

; ------ ukon‡en¡ volby ENTER

VolEnt:  call      BegEdit                  ; p©¡padn‚ zah jen¡ editace
         clc                                ; p©¡znak zad n¡ OK
         jmp       short VolX

; ------ p©eru¨en¡ volby ESC, Ctrl-Break

VolEsc:  stc                                ; p©¡znak p©eru¨en¡ volby ESC
VolX:    pop       bx
         pop       ax
         ret

Volba    ENDP

; -----------------------------------------------------------------------------
;        kurzor na konec © dku
; -----------------------------------------------------------------------------

EditEnd  PROC      NEAR

; ------ £schova registr–

         push      cx
         push      si

; ------ p©¡prava registr–

         mov       cx,MAXLINE-1             ; ukazatel kurzoru
         mov       si,offset EditBuff+MAXLINE-1 ; adresa v bufferu

; ------ posledn¡ znak je platn˜ - kurzor bude na konci

         cmp       byte ptr ds:[si]," "     ; je posledn¡ znak platn˜ ?
         jne       EditEnd4                 ; posledn¡ znak je platn˜

; ------ test, zda je ji‘ dosa‘eno konce textu

EditEnd2:cmp       byte ptr ds:[si-1]," "   ; je p©edchoz¡ znak platn˜ ?
         jne       EditEnd4                 ; je konec textu OK
         dec       si                       ; sn¡‘en¡ ukazatele v textu
         loop      EditEnd2                 ; dal¨¡ znak

; ------ ulo‘en¡ nov‚ pozice kurzoru

EditEnd4:mov       ds:[EditKurz],cx         ; nov  pozice kurzoru

; ------ n vrat registr–

         pop       si
         pop       cx
         ret

EditEnd  ENDP

; -----------------------------------------------------------------------------
;        posun o slovo vpravo
; -----------------------------------------------------------------------------

EdiCRght PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      si

; ------ adresa kurzoru v bufferu

         mov       si,offset EditBuff       ; edita‡n¡ buffer
         mov       cx,ds:[EditKurz]         ; kurzor
         add       si,cx                    ; adresa znaku v bufferu

; ------ posun na dal¨¡ znak

EdiCRgh2:cmp       cx,MAXLINE-1             ; je ji‘ konec © dku ?
         jae       EdiCRgh4                 ; je ji‘ konec © dku
         inc       si                       ; zv˜¨en¡ adresu v bufferu
         inc       cx                       ; zv˜¨en¡ ukazatele kurzoru

; ------ zji¨tˆn¡ znaku nad kurzorem a p©ed kurzorem

         mov       al,ds:[si-1]             ; p©ede¨l˜ znak
         call      KonvOdd                  ; konverze na hodnotu oddˆlova‡e
         mov       ah,al                    ; £schova hodnoty
         mov       al,ds:[si]               ; aktu ln¡ znak
         call      KonvOdd                  ; konverze na hodnotu oddˆlova‡e

; ------ test dosa‘en¡ stavu, zda je ji‘ za‡ tek slova

         cmp       al,ah                    ; je za‡ tek slova ?
         jbe       EdiCRgh2                 ; nen¡ za‡ tek slova - dal¨¡ znak

; ------ nov  pozice kurzoru

EdiCRgh4:mov       ds:[EditKurz],cx         ; nov  pozice kurzoru

; ------ konec © dku - kurzor na konec © dku

         cmp       cx,MAXLINE-1             ; je konec © dku ?
         jb        EdiCRgh9                 ; nen¡ konec © dku
         call      EditEnd                  ; kurzor na konec © dku

; ------ n vrat registr–

EdiCRgh9:pop       si
         pop       cx
         pop       ax
         ret

EdiCRght ENDP

; -----------------------------------------------------------------------------
;        posun o slovo vlevo
; -----------------------------------------------------------------------------

EdiCLeft PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      si

; ------ adresa kurzoru v bufferu

         mov       cx,ds:[EditKurz]         ; kurzor
         jcxz      EdiCLft9                 ; je ji‘ za‡ tek © dku
         mov       si,offset EditBuff       ; edita‡n¡ buffer
         add       si,cx                    ; adresa znaku

; ------ nastaven¡ na p©edch zej¡c¡ znak

EdiCLft2:dec       si                       ; adresa p©edposledn¡ho znaku
         dec       cx                       ; offset p©edposledn¡ho znaku
         jz        EdiCLft8                 ; je ji‘ za‡ tek © dku

; ------ zji¨tˆn¡ znaku na kurzoru a p©ed kurzorem

         mov       al,ds:[si-1]             ; p©ede¨l˜ znak
         call      KonvOdd                  ; konverze na hodnotu oddˆlova‡e
         mov       ah,al                    ; £schova hodnoty
         mov       al,ds:[si]               ; aktu ln¡ znak
         call      KonvOdd                  ; konverze na hodnotu oddˆlova‡e

; ------ test dosa‘en¡ stavu, zda je za‡ tek slova

         cmp       al,ah                    ; je za‡ tek slova ?
         jbe       EdiCLft2                 ; nen¡ za‡ tek slova - dal¨¡ znak

; ------ nov  pozice kurzoru

EdiCLft8:mov       ds:[EditKurz],cx         ; nov  pozice kurzoru

; ------ n vrat registr–

EdiCLft9:pop       si
         pop       cx
         pop       ax
         ret

EdiCLeft ENDP

; -----------------------------------------------------------------------------
;        vlo‘en¡ znaku BL do bufferu (CY=buffer je ji‘ pln˜)
; -----------------------------------------------------------------------------

InsChr   PROC      NEAR

; ------ £schova registr–

         push      cx
         push      si
         push      di
         push      es

; ------ kontrola, zda je buffer ji‘ pln˜

         mov       di,offset EditBuff + MAXLINE - 1 ; posledn¡ znak v bufferu
         cmp       byte ptr ds:[di]," "     ; je buffer ji‘ pln˜ ?
         stc                                ; p©¡znak, ‘e buffer je ji‘ pln˜
         jne       InsChr9                  ; buffer je ji‘ pln˜

; ------ odsun zbytku textu v bufferu

         mov       si,di                    ; konec bufferu
         dec       si                       ; p©edposledn¡ znak v bufferu
         mov       cx,MAXLINE-1             ; velikost bufferu - 1
         sub       cx,ds:[EditKurz]         ; po‡et zbyl˜ch znak– - 1
         push      ds
         pop       es                       ; ES <- datov˜ segment
         std                                ; smˆr dol–
         rep       movsb                    ; odsun zbytku textu

; ------ vlo‘en¡ nov‚ho znaku do bufferu

         mov       ds:[di],bl               ; ulo‘en¡ nov‚ho znaku do bufferu
         clc                                ; p©¡znak operace OK

; ------ n vrat registr–

InsChr9: pop       es
         pop       di
         pop       si
         pop       cx
         ret

InsChr   ENDP

; -----------------------------------------------------------------------------
;      zru¨en¡ BX znak– na pozici kurzoru (neprov d¡ se kontrola p©ete‡en¡ !)
; -----------------------------------------------------------------------------

DelCh    PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      si
         push      di
         push      es

; ------ p©¡sun zbytku textu

         mov       si,offset EditBuff       ; edita‡n¡ buffer
         add       si,ds:[EditKurz]         ; adresa kurzoru
         mov       di,si                    ; adresa kurzoru
         add       si,bx                    ; po‡ tek textu k zkop¡rov n¡
         mov       cx,offset EditBuff+MAXLINE ; konec bufferu
         sub       cx,si                    ; d‚lka zbytku textu
         cld
         rep       movsb                    ; p©¡sun zbytku textu
         mov       cx,bx                    ; offset posunu
         mov       al," "
         rep       stosb                    ; vymaz n¡ zbytku tetxu

; ------ n vrat registr–

         pop       es
         pop       di
         pop       si
         pop       cx
         pop       ax
         ret

DelCh    ENDP

; -----------------------------------------------------------------------------
;                                  KonvOdd
;                          konverze znaku na oddˆlova‡
; -----------------------------------------------------------------------------
; VSTUP: AL=znak
; VSTUP:AL=hodnota znaku (0=mezera, 1=jin˜ znak, 2=p¡smeno)
; -----------------------------------------------------------------------------

KonvOdd  PROC      NEAR

; ------ £schova registr–

         push      bx

; ------ 0: mezera

         mov       bl,0                     ; 0=mezera
         cmp       al," "
         je        KonvOdd9                 ; je mezera

; ------ 2: mal‚ p¡smeno

         mov       bl,2                     ; p¡smeno
         cmp       al,"a"
         jb        KonvOdd3                 ; nen¡ mal‚ p¡smeno
         cmp       al,"z"
         jbe       KonvOdd9                 ; je mal‚ p¡smeno

; ------ 2: velk‚ p¡smeno

KonvOdd3:cmp       al,"A"
         jb        KonvOdd4                 ; nen¡ velk‚ p¡smeno
         cmp       al,"Z"
         jbe       KonvOdd9                 ; velk‚ p¡smeno

; ------ 2: ‡¡slice

KonvOdd4:cmp       al,"0"
         jb        KonvOdd5
         cmp       al,"9"
         jbe       KonvOdd9                 ; ‡¡slice

; ------ 2: jin‚ textov‚ znaky

KonvOdd5:cmp       al,"_"
         je        KonvOdd9                 ; podtr‘¡tko
         cmp       al,"*"
         je        KonvOdd9
         cmp       al,"?"
         je        KonvOdd9

; ------ 1: ostatn¡ znaky

         mov       bl,1                     ; jinak jin˜ znak

; ------ n vrat registr–

KonvOdd9:mov       al,bl                    ; hodnota znaku
         pop       bx
         ret

KonvOdd  ENDP

; -----------------------------------------------------------------------------
;        zah jen¡ editace © dku
; -----------------------------------------------------------------------------

BegEdit  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      si
         push      di
         push      es

; ------ test, zda je ji‘ editace zah jena

         test      byte ptr ds:[Param],bit1 ; je ji‘ editace © dku ?
         jnz       BegEdit9                 ; je ji‘ editace © dku

; ------ inicializa‡n¡ vymaz n¡ edita‡n¡ho bufferu

         call      ClrEdit                  ; vymaz n¡ edita‡n¡ho bufferu

; ------ nalezen¡ adresy aktivn¡ho © dku

         mov       ax,0a0dh                 ; konec © dku CR/LF
         mov       si,offset SelBuff        ; buffer p©edvolby
         mov       cx,ds:[EditKurL]         ; aktivn¡ © dek s kurzorem
         cmp       cx,ds:[HistLine]         ; je aktivn¡ p©edvolba ?
         je        BegEdit3                 ; je aktivn¡ © dek s p©edvolbou
         mov       si,offset HistBuff       ; buffer histori¡
         jcxz      BegEdit3                 ; je aktivn¡ prvn¡ © dek
BegEdit1:inc       si                       ; zv˜¨en¡ ukazatele v bufferu
         cmp       ds:[si-1],ax             ; je konec © dku ?
         jne       BegEdit1                 ; nalezen¡ konce © dku
         inc       si                       ; za‡ tek dal¨¡ho © dku
         loop      BegEdit1                 ; nalezen¡ po‘adovan‚ho © dku

; ------ p©enesen¡ obsahu © dku do edita‡n¡ho bufferu

BegEdit3:mov       di,offset EditBuff       ; edita‡n¡ buffer
         push      ds
         pop       es                       ; ES <- datov˜ segment
         mov       cx,MAXLINE               ; velikost bufferu
         cld
BegEdit4:cmp       ds:[si],ax               ; je konec © dku ?
         je        BegEdit5                 ; je konec © dku
         movsb                              ; p©enos jednoho znaku
         loop      BegEdit4                 ; dal¨¡ znak

; ------ p©¡znak zah jen¡ editace © dku

BegEdit5:or        byte ptr ds:[Param],bit1 ; p©¡znak zah jen¡ editace © dku

; ------ n vrat registr–

BegEdit9:pop       es
         pop       di
         pop       si
         pop       cx
         pop       ax
         ret

BegEdit  ENDP

; -----------------------------------------------------------------------------
;        ukon‡en¡ editace © dku
; -----------------------------------------------------------------------------

EndEdit  PROC      NEAR

         and       byte ptr ds:[Param],not bit1 ; zru¨en¡ p©¡znaku editace
         push      ax
         mov       ax,ds:[EditTop]          ; po‡ tek © dku
         sub       ds:[EditKurz],ax         ; offset kurzoru relativnˆ
         pop       ax
         call      ClrEdit                  ; vymaz n¡ edita‡n¡ho bufferu
         ret

EndEdit  ENDP

; -----------------------------------------------------------------------------
;        vymaz n¡ edita‡n¡ho bufferu
; -----------------------------------------------------------------------------

ClrEdit  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      di
         push      es

; ------ vymaz n¡ bufferu

         push      ds
         pop       es                       ; ES <- datov˜ segment
         mov       di,offset EditBuff       ; edita‡n¡ buffer
         mov       al," "                   ; znak mezery k vymaz n¡
         mov       cx,MAXLINE               ; d‚lka bufferu
         cld
         rep       stosb                    ; vymaz n¡ bufferu
         mov       ds:[EditTop],cx          ; po‡ te‡n¡ pozice © dku = 0

; ------ n vrat registstr–

         pop       es
         pop       di
         pop       cx
         pop       ax
         ret

ClrEdit  ENDP

; -----------------------------------------------------------------------------
;        kurzor editovan‚ho © dku
; -----------------------------------------------------------------------------

KurzEdit PROC      NEAR

         push      dx
         mov       dl,byte ptr ds:[EditKurz] ; pozice kurzoru v © dku
         sub       dl,byte ptr ds:[EditTop] ; offset od po‡ tku © dku
         add       dl,(80-SIRKA)/2+1        ; pozice kurzoru
         mov       dh,ds:[KurzRad]          ; © dek s kurzorem
         call      SetKurz                  ; nastaven¡ pozice kurzoru
         pop       dx
         ret

KurzEdit ENDP

; -----------------------------------------------------------------------------
;        korekce editovan‚ho © dku
; -----------------------------------------------------------------------------

KorEdit  PROC      NEAR

; ------ test, zda byla zah jena editace

         test      byte ptr ds:[Param],bit1 ; byla zah jena editace ?
         jz        KorEdit6                 ; nebyla zah jena editace

; ------ £schova registr–

         push      ax
         push      cx

; ------ podte‡en¡ kurzoru p©ed po‡ tek © dku

         mov       ax,ds:[EditKurz]         ; offset kurzoru na © dku
         mov       cx,ds:[EditTop]          ; po‡ tek © dku
         cmp       ax,cx                    ; podte‡en¡ kurzoru p©ed po‡ tek ?
         jae       KorEdit1                 ; nen¡ podte‡en¡ kurzoru
         mov       cx,ax                    ; omezen¡ po‡ tku © dku

; ------ stanoven¡ maxim ln¡ho po‡ tku

KorEdit1:sub       ax,SIRKA-3               ; maxim ln¡ po‡ tek
         jnc       KorEdit2                 ; nen¡ p©ete‡en¡
         xor       ax,ax                    ; omezen¡ po‡ tku na 0

; ------ p©ete‡en¡ kurzoru za konec © dku

KorEdit2:cmp       ax,cx                    ; je p©ete‡en¡ kurzoru za konec ?
         jbe       KorEdit3                 ; kurzor je OK
         mov       cx,ax                    ; omezen¡ po‡ tku © dku
KorEdit3:mov       ds:[EditTop],cx          ; nov˜ po‡ tek © dku

; ------ n vrat registr–

         pop       cx
         pop       ax
KorEdit6:ret

KorEdit  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ editovan‚ho © dku
; -----------------------------------------------------------------------------

DispEdit PROC      NEAR

; ------ test, zda byla zah jena editace

         test      byte ptr ds:[Param],bit1 ; byla zah jena editace ?
         jz        DispEdi4                 ; nebyla zah jena editace

; ------ £schova registr–

         push      ax
         push      cx
         push      dx
         push      si
         push      es

; ------ zobrazen¡ editovan‚ho © dku

         push      ds
         pop       es
         mov       si,offset EditBuff       ; edita‡n¡ buffer
         add       si,ds:[EditTop]          ; adresa za‡ tku textu
         mov       dh,ds:[KurzRad]          ; © dek s kurzorem na obrazovce
         mov       dl,(80-SIRKA)/2+1        ; po‡ te‡n¡ pozice
         mov       ah,ds:[BarvaR3]          ; barva editovan‚ho © dku
         mov       cx,SIRKA-2               ; d‚lka textu © dku
         call      DispTxt                  ; zobrazen¡ editovan‚ho © dku

; ------ n vrat registr–

         pop       es
         pop       si
         pop       dx
         pop       cx
         pop       ax
DispEdi4:ret

DispEdit ENDP


; -----------------------------------------------------------------------------
;        korekce okna voleb
; -----------------------------------------------------------------------------

KorVol   PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx

; ------ podte‡en¡ kurzoru pod za‡ tkem okna

         mov       ax,ds:[EditKurL]         ; ‡¡slo aktivn¡ho © dku
         mov       cx,ds:[EditTopL]         ; po‡ tek okna
         cmp       ax,cx                    ; je pod za‡ tkem okna ?
         jae       KorVol1                  ; nen¡ pod za‡ tkem okna
         mov       cx,ax                    ; omezen¡ po‡ tku okna

; ------ stanoven¡ minim ln¡ho po‡ te‡n¡ho © dku

KorVol1: mov       bh,0
         mov       bl,ds:[Radku]            ; po‡et © dk– displeje
         sub       bl,6+1                   ; ode‡ten¡ okraj– + 1 = po‡et © dk–
         sub       ax,bx                    ; maxim ln¡ po‡ tek
         jnc       KorVol2                  ; nen¡ podte‡en¡
         xor       ax,ax                    ; omezen¡ prvn¡ho © dku na 0

; ------ p©ete‡en¡ kurzoru za konec okna

KorVol2: cmp       ax,cx                    ; je kurzor za koncem okna ?
         jbe       KorVol3                  ; kurzor je OK
         mov       cx,ax                    ; omezen¡ po‡ tku okna
KorVol3: mov       ds:[EditTopL],cx         ; nov˜ po‡ te‡n¡ © dek okna

; ------ n vrat registr–

         pop       cx
         pop       bx
         pop       ax
         ret

KorVol   ENDP

; -----------------------------------------------------------------------------
;                                 DispVol
;                         zobrazen¡ voleb souboru
; -----------------------------------------------------------------------------
; VSTUP: DS=datov˜ segment
; lok ln¡ promˆnn‚: SS:[BP-1] (1) ukazatel © dku na obrazovce
;                   SS:[BP-2] (1) ‡¡ta‡ © dk– k zobrazen¡
;                   SS:[BP-4] (2) ‡¡ta‡ aktivn¡ho © dku
;                   SS:[BP-6] (2) ukazatel adresy © dku
;                   SS:[BP-8] (2) ‡¡ta‡ © dku p©edvolby
; -----------------------------------------------------------------------------

DispVol  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         push      es
         mov       bp,sp

; ------ vytvo©en¡ bufferu v z sobn¡ku

         sub       sp,8+SIRKA*2             ; vytvo©en¡ bufferu v z sobn¡ku
         push      ss
         pop       es                       ; ES <- z sobn¡k

; ------ ‡¡ta‡ aktivn¡ho © dku

         mov       cx,ds:[EditTopL]         ; ‡¡slo prvn¡ho © dku
         mov       ax,ds:[EditKurL]         ; ‡¡slo aktivn¡ho © dku
         sub       ax,cx                    ; offset aktivn¡ho © dku
         inc       ax                       ; p©ednastaven¡ ‡¡ta‡e © dku
         mov       ss:[bp-4],ax             ; ‡¡ta‡ aktivn¡ho © dku

; ------ ‡¡ta‡ © dku p©edvolby

         mov       ax,ds:[HistLine]         ; ‡¡slo © dku p©edvolby
         sub       ax,cx                    ; offset © dku p©edvolby
         inc       ax                       ; p©ednastaven¡ ‡¡ta‡e © dku
         mov       ss:[bp-8],ax             ; ‡¡ta‡ © dku p©edvolby

; ------ nalezen¡ adresy prvn¡ho © dku

         mov       si,offset HistBuff       ; buffer historie
         jcxz      DispVol2                 ; je zobrazen prvn¡ © dek
         mov       ax,0a0dh                 ; hledan˜ znak CR/LF
DispVol1:inc       si                       ; zv˜¨en¡ ukazatele v bufferu
         cmp       ds:[si-1],ax             ; je CR/LF ?
         jne       DispVol1                 ; nalezen¡ konce © dku
         inc       si                       ; za‡ tek dal¨¡ho © dku
         loop      DispVol1                 ; nalezen¡ po‘adovan‚ho © dku
DispVol2:mov       ss:[bp-6],si             ; ukazatel adresy © dku

; ------ stanoven¡ po‡tu © dk– k zobrazen¡

         mov       cl,ds:[Radku]            ; CX=po‡et © dk– displeje
         sub       cl,6                     ; ode‡ten¡ okraj–
         mov       ax,ds:[HistLine]         ; po‡et © dk– v historii
         inc       ax                       ; + © dek p©edvolby
         cmp       cx,ax                    ; je po‡et © dk– men¨¡ ?
         jbe       DispVol3                 ; po‡et © dk– v CX je OK
         mov       cx,ax                    ; omezen¡ po‡tu © dk–
DispVol3:mov       ss:[bp-2],cl             ; ‡¡ta‡ © dk– k zobrazen¡

; ------ stanoven¡ po‡ te‡n¡ho © dku na obrazovce

         mov       dh,ds:[Radku]            ; po‡et © dk– displeje celkem
         add       cl,2                     ; p©i‡ten¡ okraj– = v˜¨ka okna
         sub       dh,cl                    ; zbytek na okraje
         shr       dh,1                     ; vyst©edˆn¡ okna
         mov       ss:[bp-1],dh             ; ukazatel © dku na obrazovce

; ------ zobrazen¡ horn¡ho okraje (horn¡ linka)

         mov       dl,(80-SIRKA)/2          ; po‡ te‡n¡ pozice
         mov       ah,ds:[BarvaR1]          ; barva r mu
         mov       al,218                   ; lev˜ horn¡ roh
         call      Disp1Chr                 ; zobrazen¡ lev‚ho horn¡ho rohu
         mov       cl,SIRKA-2               ; vnit©n¡ ¨¡©ka © dku
         mov       al,196                   ; horn¡ linka
         call      DispChr                  ; zobrazen¡ horn¡ linky
         mov       al,191                   ; prav˜ horn¡ roh
         call      Disp1Chr                 ; zobrazen¡ prav‚ho horn¡ho rohu
         inc       byte ptr ss:[bp-1]       ; zv˜¨en¡ ukazatele © dku

; ------ p©¡prava okraj– © dku v bufferu

DispVol4:mov       di,sp                    ; buffer © dku v z sobn¡ku
         cld
         mov       ah,ds:[BarvaR1]          ; z kladn¡ barva r mu voleb
         mov       al,179                   ; lev  hrana
         stosw                              ; ulo‘en¡ lev‚ho okraje
         mov       es:[di+2*SIRKA-4],ax     ; ulo‘en¡ prav‚ho okraje

; ------ p©¡prava barvy © dku

         dec       word ptr ss:[bp-4]       ; ‡¡ta‡ aktivn¡ho © dku
         jnz       DispVol5                 ; nen¡ aktivn¡ © dek
         mov       ah,ds:[BarvaR2]          ; barva kurzoru voleb
         mov       al,ss:[bp-1]             ; © dek na obrazovce
         mov       ds:[KurzRad],al          ; © dek s kurzorem

; ------ adresa © dku k dek¢dov n¡

DispVol5:mov       si,ss:[bp-6]             ; ukazatel adresy © dku
         dec       word ptr ss:[bp-8]       ; ‡¡ta‡ © dku p©edvolby
         jnz       DispVol6                 ; nen¡ p©edvolba
         mov       si,offset SelBuff        ; buffer p©edvolby

; ------ dek¢dov n¡ textu © dku

DispVol6:mov       cx,SIRKA-2               ; maxim ln¡ d‚lka textu
         mov       dx,0a0dh                 ; hledan˜ konec © dku CR/LF
DispVol7:cmp       ds:[si],dx               ; je konec © dku ?
         je        DispVol8                 ; je konec © dku
         lodsb                              ; znak textu
         stosw                              ; ulo‘en¡ znaku k zobrazen¡
         loop      DispVol7                 ; dal¨¡ znak

; ------ vymaz n¡ zbytku © dku

DispVol8:mov       al," "                   ; znak mezery k vymaz n¡ © dku
         rep       stosw                    ; vymaz n¡ zbytku © dku

; ------ nalezen¡ za‡ tku dal¨¡ho © dku

DispVol9:inc       si                       ; zv˜¨en¡ ukazatele textu
         cmp       ds:[si-1],dx             ; je konec © dku ?
         jne       DispVol9                 ; nalezen¡ konce © dku
         inc       si                       ; nastaven¡ na za‡ tek dal¨¡ho © dku
         mov       ss:[bp-6],si             ; £schova adresy dal¨¡ho © dku

; ------ zobrazen¡ © dku z bufferu

         mov       si,sp                    ; buffer v z sobn¡ku s textem
         mov       cx,SIRKA                 ; d‚lka © dku
         mov       dl,(80-SIRKA)/2          ; po‡ te‡n¡ pozice
         mov       dh,ss:[bp-1]             ; © dek k zobrazen¡
         call      DispStr                  ; zobrazen¡ textu z bufferu
         mov       cl,2
         call      DispStin                 ; zobrazen¡ st¡nu za © dkem

; ------ p©¡prava pro dal¨¡ © dek

         inc       byte ptr ss:[bp-1]       ; zv˜¨en¡ ukazatele © dk–
         dec       byte ptr ss:[bp-2]       ; sn¡‘en¡ ‡¡ta‡e © dk– k zobrazen¡
         jnz       DispVol4                 ; zobrazen¡ dal¨¡ho © dku

; ------ zobrazen¡ doln¡ho okraje

         mov       dl,(80-SIRKA)/2          ; po‡ te‡n¡ pozice
         mov       dh,ss:[bp-1]             ; © dek k zobrazen¡
         mov       ah,ds:[BarvaR1]          ; barva r mu
         mov       al,192                   ; lev˜ doln¡ roh
         call      Disp1Chr                 ; zobrazen¡ lev‚ho doln¡ho rohu
         mov       cl,SIRKA-2               ; vnit©n¡ ¨¡©ka © dku
         mov       al,196                   ; doln¡ linka
         call      DispChr                  ; zobrazen¡ doln¡ linky
         mov       al,217                   ; prav˜ doln¡ roh
         call      Disp1Chr                 ; zobrazen¡ prav‚ho doln¡ho rohu
         mov       cl,2
         call      DispStin                 ; zobrazen¡ st¡nu
         inc       dh
         mov       dl,(80-SIRKA)/2+2
         mov       cl,SIRKA
         call      DispStin                 ; zobrazen¡ spodn¡ho st¡nu

; ------ n vrat registr–

         mov       sp,bp
         pop       es
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

DispVol  ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                       Obsluha prohl¡‘e‡e souboru F3
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

;þ

; -----------------------------------------------------------------------------
;        obsluha prohl¡‘e‡e souboru (BX=kl vesa)
; -----------------------------------------------------------------------------

View     PROC      NEAR

; ------ otev©en¡ prohl¡‘e‡e souboru

         test      byte ptr ds:[Param],bit5 ; prob¡h  zobrazen¡ souboru ?
         jnz       View1                    ; prohl¡‘e‡ je ji‘ aktivn¡
         call      ViewOpen                 ; otev©en¡ prohl¡‘e‡e
         test      byte ptr ds:[Param],bit5 ; byl £spˆ¨nˆ otev©en ?
         jz        View0                    ; nebyl otev©en
         call      ViewDisp                 ; zobrazen¡ prohl¡‘e‡e
View0:   xor       bx,bx
         ret

; ------ opu¨tˆn¡ prohl¡‘e‡e souboru

View1:   cmp       bx,3d00h                 ; F3 - opu¨tˆn¡ prohl¡‘e‡e
         je        View11
         cmp       bl,27                    ; ESC
         jne       View2
View11:  and       byte ptr ds:[Param],not bit5 ; zru¨en¡ p©¡znaku prohl¡‘e‡e
         call      DispSezn                 ; obnoven¡ zobrazen¡ seznamu
         mov       bx,ds:[ViewIdnt]         ; identifik tor souboru
         mov       ah,3eh
         int       21h                      ; uzav©en¡ souboru
         xor       bx,bx                    ; zru¨en¡ p©¡znaku ESC
         ret

; ------ posun o © dek dol–

View2:   call      ViewAkt                  ; aktualizace bufferu prohl¡‘e‡e
         cmp       bx,5000h                 ; dol–
         jne       View3
         call      ViewDwn                  ; posun o © dek dol–
View22:  call      ViewDisp                 ; nov‚ zobrazen¡ str nky
View23:  xor       bx,bx                    ; zru¨en¡ kl vesy
         ret

; ------ posun o © dek nahoru

View3:   cmp       bx,4800h                 ; nahoru
         jne       View4
         call      ViewUp
         jmp       short View22

; ------ posun na za‡ tek souboru

View4:   cmp       bx,4700h                 ; HOME
         jne       View5
         mov       word ptr ds:[ViewTop],0  ; nov˜ po‡ tek obrazovky
         mov       word ptr ds:[ViewTop+2],0
         jmp       short View22

; ------ posun na konec souboru

View5:   cmp       bx,4f00h                 ; END
         jne       View6
         mov       ax,word ptr ds:[ViewSize] ; velikost LOW
         mov       word ptr ds:[ViewTop],ax
         mov       ax,word ptr ds:[ViewSize+2] ; velikost HIGH
         mov       word ptr ds:[ViewTop+2],ax
         call      ViewAkt                  ; aktualizace bufferu
         jmp       short View62

; ------ posun o str nku nahoru

View6:   cmp       bx,4900h                 ; Page Up
         jne       View7
View62:  mov       cx,ds:[SeznRad]          ; d‚lka str nky (© dk–)
         dec       cx
View63:  call      ViewUp                   ; nahoru
         jc        View22
         loop      View63
         jmp       short View22

; ------ posun o str nku dol–

View7:   cmp       bx,5100h                 ; Page Down
         jne       View8
         mov       cx,ds:[SeznRad]          ; d‚lka str nky (© dk–)
         dec       cx
View73:  call      ViewDwn                  ; dol–
         jc        View22
         loop      View73
         jmp       short View22

; ------ zobrazen¡ podkladu ^O

View8:   cmp       bx,180fh                 ; ^O
         jne       View22
         call      PopScr
         call      InpChr
         jmp       short View22

View     ENDP

; -----------------------------------------------------------------------------
;        otev©en¡ prohl¡‘e‡e souboru
; -----------------------------------------------------------------------------

ViewOpen PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      es
         test      byte ptr ds:[Param],bit5 ; je aktivn¡ prohl¡‘e‡ ?
         jz        ViewOpn0                 ; nen¡ aktivn¡
ViewOp99:jmp       ViewOpn9                 ; prohl¡‘e‡ je ji‘ aktivn¡

; ------ p©¡prava registr–

ViewOpn0:mov       bx,ds:[SeznAkt]          ; aktivn¡ © dek
         call      GetKPTh                  ; poskytnut¡ cesty kurzoru
         jc        ViewOpn9                 ; nen¡ platn˜ soubor

; ------ adresa aktivn¡ho souboru

         mov       es,ds:[SeznSegm]         ; segment seznamu
         mov       bx,ds:[SeznAkt]          ; aktivn¡ © dek
         shl       bx,1
         mov       si,ds:[bx+DispIndx]      ; adresa souboru

; ------ kontrola platnosti souboru

         test      byte ptr es:[si],bit6+DIR ; je to platn  polo‘ka ?
         jnz       ViewOpn9                 ; je to cesta nebo adres © - neplat¡

; ------ £schova velikosti souboru

         mov       ax,es:[si+5]             ; velikost LOW
         mov       word ptr ds:[ViewSize],ax ; velikost LOW
         mov       ax,es:[si+7]             ; velikost HIGH
         mov       word ptr ds:[ViewSize+2],ax ; velikost HIGH

; ------ p©id n¡ jm‚na souboru

         add       si,9                     ; jm‚no souboru
ViewOpn4:mov       al,es:[si]
         mov       ds:[di],al
         inc       si
         inc       di
         cmp       al,0
         jne       ViewOpn4

; ------ otev©en¡ souboru

         mov       dx,offset ViewName
         mov       ax,3d00h
         int       21h                      ; otev©en¡ souboru
         jc        ViewOpn9                 ; chyba - nenalezen

; ------ inicializace ukazatel–

         mov       ds:[ViewIdnt],ax         ; identifik tor souboru
         xor       ax,ax
         mov       word ptr ds:[ViewTop],ax ; adresa za‡ tku str nky
         mov       word ptr ds:[ViewTop+2],ax
         mov       word ptr ds:[ViewBeg],ax
         mov       word ptr ds:[ViewBeg+2],ax
         mov       ds:[ViewNum],ax          ; d‚lka dat v bufferu
         or        byte ptr ds:[Param],bit5 ; p©¡znak aktivn¡ho prohl¡‘e‡e

; ------ inicializa‡n¡ na‡ten¡ souboru

         call      ViewRBuf                 ; na‡ten¡ souboru do bufferu

; ------ n vrat registr–

ViewOpn9:pop       es
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

ViewOpen ENDP

; -----------------------------------------------------------------------------
;        poskytnut¡ cesty souboru BX (CY=neplat¡, DI=konec cesty)
; -----------------------------------------------------------------------------

GetKPth  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      si
         push      es

; ------ p©¡prava registr–

         mov       es,ds:[SeznSegm]         ; segment seznamu
         cmp       bx,ds:[DispNum]          ; je platn˜ © dek ?
         cmc
         jc        GetKPth9                 ; nen¡ platn˜ © dek
         shl       bx,1                     ; index * 2

         mov       si,ds:[bx+DispIndx]      ; adresa polo‘ky
         test      byte ptr es:[si],bit6    ; je to cesta ?
         jnz       GetKPt13                 ; je to cesta

; ------ nalezen¡ domovsk‚ho adres ©e souboru

GetKPth1:dec       bx
         dec       bx
         mov       si,ds:[bx+DispIndx]      ; adresa polo‘ky
         test      byte ptr es:[si],bit6    ; je to cesta ?
         jz        GetKPth1                 ; nalezen¡ cesty

; ------ p©enesen¡ domovsk‚ho adres ©e do bufferu

GetKPt13:mov       di,offset ViewName-1     ; buffer jm‚na souboru
GetKPth2:inc       di
         inc       si
         mov       al,es:[si]
         mov       ds:[di],al
         cmp       al,0
         jne       GetKPth2

; ------ zaji¨tˆn¡ koncov‚ho znaku "\"

         cmp       byte ptr ds:[di-1],"\"
         je        GetKPth3
         mov       word ptr ds:[di],"\"
         inc       di
GetKPth3:clc                                ; p©¡znak operace OK

; ------ n vrat registr–

GetKPth9:pop       es
         pop       si
         pop       bx
         pop       ax
         ret

GetKPth  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ prohl¡‘e‡e
; -----------------------------------------------------------------------------
; lok ln¡ promˆnn‚: SS:[BP-1] (1) ukazatel © dku na displeji
;                   SS:[BP-2] (1) ‡¡ta‡ © dk– k zobrazen¡
;                   SS:[BP-4] (2) offset za‡ tku © dku v bufferu
;                   SS:[BP-6] (2) offset za‡ tku str nky
; -----------------------------------------------------------------------------

ViewDisp PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         push      es
         mov       bp,sp

; ------ p©¡prava registr–

         sub       sp,160+6                 ; lok ln¡ promˆnn‚ a buffer
         push      ss
         pop       es                       ; ES <- segment bufferu
         mov       byte ptr ss:[bp-1],1     ; ukazatel © dk– na displeji
         mov       al,byte ptr ds:[SeznRad] ; po‡et © dk– k zobraze¡
         mov       ss:[bp-2],al             ; ‡¡ta‡ © dk– k zobrazen¡
         call      ViewAkt                  ; aktualizace bufferu
         mov       ax,word ptr ds:[ViewTop] ; za‡ tek str nky
         sub       ax,word ptr ds:[ViewBeg] ; offset v bufferu
         mov       ss:[bp-4],ax             ; offset za‡ tku © dku v bufferu
         mov       ss:[bp-6],ax             ; offset za‡ tku str nky
         mov       bx,ds:[ViewNum]          ; po‡et bajt– v bufferu

; ------ dek¢dov n¡ jednoho © dku

ViewDsp1:cld
         mov       di,sp                    ; buffer v z sobn¡ku
         mov       si,ss:[bp-4]             ; offset dat v bufferu
         mov       cx,80                    ; max. po‡et znak–
         mov       dh,ds:[Barva1]           ; z kladn¡ barva textu
         mov       dl,ds:[Barva6]           ; barva ©¡dic¡ch znak– textu
ViewDsp2:cmp       si,bx                    ; jsou dal¨¡ data ?
         jae       ViewDsp5                 ; posun dat v bufferu
ViewDsp3:mov       al,ds:[si+ViewBuf]       ; znak z bufferu
         inc       si                       ; zv˜¨en¡ ukazatele
         mov       ah,dh                    ; bˆ‘n  barva textu
         cmp       al,10                    ; je nov˜ © dek LF ?
         je        ViewDsp6                 ; je LF - konec © dku
         cmp       al,13                    ; je CR ?
         je        ViewDsp2                 ; CR se ignoruje
         cmp       al,9                     ; je tabel tor ?
         je        ViewDsp4                 ; je tabel tor
         cmp       al," "
         jae       ViewDs30
         mov       ah,dl                    ; barva ©¡dic¡ch znak–
         cmp       al,0
         jne       ViewDs30
         mov       al,"."                   ; n hrada znaku 0 znakem "."
ViewDs30:stosw                              ; ulo‘en¡ znaku
         loop      ViewDsp2                 ; dal¨¡ znak

; ------ vypu¨tˆn¡ LF z konce © dku

ViewDs31:cmp       si,bx                    ; jsou dal¨¡ data ?
         jb        ViewDs32                 ; jsou dal¨¡ data
         call      ViewNxt                  ; dal¨¡ blok dat
         cld
         jc        ViewDsp7                 ; nen¡ dal¨¡ blok dat
         mov       bx,ds:[ViewNum]          ; nov˜ po‡et bajt– v bufferu
         sub       si,VIEWBMAX/2            ; posun ukazatele v bufferu
         sub       word ptr ss:[bp-6],VIEWBMAX/2 ; posun uschovan‚ho za‡ tku
ViewDs32:mov       al,ds:[si+ViewBuf]       ; znak z bufferu
         inc       si                       ; zv˜¨en¡ ukazatele
         cmp       al,13
         je        ViewDs31                 ; znak CR se ignoruje
         cmp       al,10                    ; je LF ?
         je        ViewDsp7                 ; je LF - vypust¡ se
         dec       si                       ; jinak n vrat znaku
         jmp       short ViewDsp7

; ------ dek¢dov n¡ tabel toru

ViewDsp4:mov       al," "
         stosw
         dec       cx
         test      cl,7
         jnz       ViewDsp4
         jcxz      ViewDs31                 ; konec © dku
         jmp       short ViewDsp2

; ------ dal¨¡ blok dat

ViewDsp5:call      ViewNxt                  ; dal¨¡ blok dat
         cld
         jc        ViewDsp6                 ; nen¡ dal¨¡ blok dat
         mov       bx,ds:[ViewNum]          ; nov˜ po‡et bajt– v bufferu
         sub       si,VIEWBMAX/2            ; posun ukazatele v bufferu
         sub       word ptr ss:[bp-6],VIEWBMAX/2 ; posun uschovan‚ho za‡ tku
         cmp       si,bx                    ; jsou dal¨¡ data ?
         jb        ViewDsp3                 ; je dal¨¡ znak

; ------ vymaz n¡ zbytku © dku

ViewDsp6:mov       al," "
         mov       ah,dh
         rep       stosw                    ; vymaz n¡ zbytku © dku

; ------ zobrazen¡ © dku

ViewDsp7:mov       ss:[bp-4],si             ; ukazatel dal¨¡ho © dku
         mov       si,sp
         mov       dl,0
         mov       dh,ss:[bp-1]             ; © dek na displeji
         mov       cl,80
         call      DispStr                  ; zobrazen¡ © dku

; ------ p©¡prava pro dal¨¡ © dek

         inc       byte ptr ss:[bp-1]       ; zv˜¨en¡ ukazatele © dk–
         dec       byte ptr ss:[bp-2]
         jz        ViewDsp9
         jmp       ViewDsp1                 ; dek¢dov n¡ dal¨¡ho © dku

; ------ d‚lka zobrazen‚ ‡ sti

ViewDsp9:mov       ax,ss:[bp-4]             ; adresa © dku
         sub       ax,ss:[bp-6]             ; po‡et zobrazen˜ch bajt–
         mov       ds:[ViewDis],ax          ; d‚lka zobrazen‚ ‡ sti

; ------ n vrat registr–

         mov       sp,bp
         pop       es
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         call      DispNadp                 ; zobrazen¡ informa‡n¡ho © dku
         call      DispHlp                  ; zobrazen¡ n povˆdy
         ret

ViewDisp ENDP

; -----------------------------------------------------------------------------
;        posun kurzoru o jeden © dek dol– (CY=nen¡ dal¨¡ © dek)
; -----------------------------------------------------------------------------

ViewDwn  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si

; ------ ukazatel dat v bufferu

         mov       si,word ptr ds:[ViewTop] ; za‡ tek obrazovky
         sub       si,word ptr ds:[ViewBeg] ; offset v bufferu
         mov       bx,ds:[ViewNum]          ; po‡et bajt– v bufferu
         mov       dx,si                    ; £schova za‡ tku dat
         mov       cx,81                    ; max. po‡et znak–

; ------ na‡ten¡ znaku z bufferu

ViewDwn1:cmp       si,bx                    ; je konec dat v bufferu ?
         jae       ViewDwn6                 ; dal¨¡ blok dat
ViewDwn2:mov       al,ds:[si+ViewBuf]       ; znak z bufferu
         inc       si                       ; zv˜¨en¡ ukazatele dat
         cmp       al,13
         je        ViewDwn1                 ; znak CR se ignoruje

; ------ obsluha tabel toru

         cmp       al,9                     ; je tabel tor ?
         jne       ViewDwn3                 ; nen¡ tabel tor
         dec       cx                       ; sn¡‘en¡ ‡¡ta‡e znak–
         jz        ViewDwn4
         dec       cx
         and       cl,not 7                 ; zarovn n¡ na tabela‡n¡ pozici
         jz        ViewDwn4                 ; konec
         inc       cx
         inc       cx

; ------ test, zda je konec © dku

ViewDwn3:cmp       al,10                    ; je konec © dku ?
         loopne    ViewDwn1                 ; dal¨¡ znak
         je        ViewDwn4                 ; byl LF
         dec       si                       ; n vrat posledn¡ho znaku
ViewDwn4:sub       si,dx                    ; rozd¡l dat
         add       word ptr ds:[ViewTop],si ; posun za‡ tku str nky
         adc       word ptr ds:[ViewTop+2],0
         clc

; ------ n vrat registr–

ViewDwn9:pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

ViewDwn  ENDP

; ------ na‡ten¡ dal¨¡ho bloku dat

ViewDwn6:call      ViewNxt                  ; dal¨¡ blok dat
         jc        ViewDwn9                 ; nen¡ dal¨¡ blok dat
         mov       bx,ds:[ViewNum]          ; nov˜ po‡et bajt– v bufferu
         sub       si,VIEWBMAX/2            ; posun ukazatele v bufferu
         sub       dx,VIEWBMAX/2            ; posun p–vodn¡ho ukazatele dat
         cmp       si,bx                    ; jsou dal¨¡ data ?
         cmc
         jc        ViewDwn9                 ; konec dat
         jmp       short ViewDwn2

; -----------------------------------------------------------------------------
;        posun kurzoru o jeden © dek nahoru (CY=nen¡ dal¨¡ © dek)
; -----------------------------------------------------------------------------

ViewUp   PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx
         push      si

; ------ ukazatel dat v bufferu

         mov       si,word ptr ds:[ViewTop] ; za‡ tek obrazovky
         sub       si,word ptr ds:[ViewBeg] ; offset v bufferu
         mov       dx,si                    ; £schova za‡ tku © dku
         mov       cx,80                    ; max. po‡et znak–

; ------ p©esko‡en¡ p©edchoz¡ho znaku LF

         or        si,si                    ; je za‡ tek v bufferu ?
         jnz       ViewUp11                 ; nen¡ za‡ tek bufferu - je znak
         call      ViewPred                 ; dal¨¡ blok dat
         jc        ViewUp9                  ; nen¡ dal¨¡ blok dat
         mov       si,VIEWBMAX/2            ; nov˜ offset v bufferu
ViewUp11:dec       si                       ; sn¡‘en¡ ukazatele dat
         cmp       byte ptr ds:[si+ViewBuf],10 ; je konec © dku LF ?
         je        ViewUp1                  ; je konec © dku LF
         inc       si                       ; n vrat platn‚ho znaku

; ------ na‡ten¡ znaku z bufferu

ViewUp1: or        si,si                    ; je konec dat v bufferu ?
         jz        ViewUp6                  ; dal¨¡ blok dat
ViewUp2: dec       si                       ; sn¡‘en¡ ukazatele dat
         mov       al,ds:[si+ViewBuf]       ; znak z bufferu
         cmp       al,13
         je        ViewUp1                  ; znak CR se ignoruje

; ------ test, zda je konec © dku

ViewUp3: cmp       al,10                    ; je konec © dku ?
         loopne    ViewUp1                  ; dal¨¡ znak
         jne       ViewUp5                  ; nebyl to znak LF (konec © dku)
         inc       si                       ; n vrat znaku LF
ViewUp5: sub       dx,si                    ; rozd¡l dat
         sub       word ptr ds:[ViewTop],dx ; posun za‡ tku str nky
         sbb       word ptr ds:[ViewTop+2],0 ; p©enos HIGH
         clc

; ------ n vrat registr–

ViewUp9: pop       si
         pop       dx
         pop       cx
         pop       ax
         ret

ViewUp   ENDP

; ------ na‡ten¡ dal¨¡ho bloku dat

ViewUp6: call      ViewPred                 ; dal¨¡ blok dat
         jc        ViewUp5                  ; nen¡ dal¨¡ blok dat
         mov       si,VIEWBMAX/2            ; nov˜ ukazatel dat v bufferu
         add       dx,VIEWBMAX/2            ; posun p–vodn¡ho offsetu dat
         jmp       short ViewUp2

; -----------------------------------------------------------------------------
;        aktualizace bufferu prohl¡‘e‡e (aby za‡ tek obrazovky byl v bufferu)
; -----------------------------------------------------------------------------

ViewAkt  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx

; ------ v˜po‡et offsetu od za‡ tku bufferu

ViewAkt1:mov       cx,VIEWBMAX/2            ; polovina bufferu
         mov       ax,word ptr ds:[ViewTop] ; offset po‡ tku obrazovky
         mov       dx,word ptr ds:[ViewTop+2]
         sub       ax,word ptr ds:[ViewBeg] ; offset od za‡ tku bufferu
         sbb       dx,word ptr ds:[ViewBeg+2] ; p©enos HIGH
         jc        ViewAkt5                 ; obrazovka je pod za‡ tkem bufferu

; ------ obrazovka je nad za‡ tkem bufferu

         ja        ViewAkt4                 ; je daleko za koncem
         cmp       ax,VIEWBMAX              ; je za koncem bufferu ?
         jb        ViewAkt9                 ; je uvnit© bufferu OK
         cmp       ax,VIEWBMAX+VIEWBMAX/2   ; je daleko za bufferem ?
         jae       ViewAkt4                 ; je daleko za bufferem
         call      ViewNxt                  ; posun o blok dat vp©ed
         jmp       short ViewAkt9

; ------ obrazovka je daleko za koncem bufferu

ViewAkt4:add       word ptr ds:[ViewBeg],cx ; posun po‡ tku bufferu
         adc       word ptr ds:[ViewBeg+2],0 ; p©enos HIGH
         sub       ax,cx                    ; posun ukazatele obrazovky
         sbb       dx,0                     ; p©enos HIGH
         jnz       ViewAkt4
         cmp       ax,VIEWBMAX
         jae       ViewAkt4                 ; nalezen¡ za‡ tku bufferu
         jmp       short ViewAkt8

; ------ obrazovka je pod za‡ tkem bufferu

ViewAkt5:cmp       dx,-1
         jne       ViewAkt7                 ; je p©¡li¨ daleko
         cmp       ax,-VIEWBMAX/2
         jb        ViewAkt7                 ; je p©¡li¨ daleko
         call      ViewPred                 ; posun bufferu k za‡ tku
         jmp       short ViewAkt9

; ------ obrazovka je daleko pod za‡ tkem bufferu

ViewAkt7:sub       word ptr ds:[ViewBeg],cx  ; posun po‡ tku bufferu
         sbb       word ptr ds:[ViewBeg+2],0 ; p©enos HIGH
         add       ax,cx                    ; posun ukazatele obrazovky
         adc       dx,0                     ; p©enos HIGH
         jnc       ViewAkt7                 ; nalezen¡ za‡ tku bufferu

; ------ nov‚ na‡ten¡ cel‚ho bufferu na aktu ln¡ pozici

ViewAkt8:call      ViewRBuf                 ; nov‚ na‡ten¡ obsahu bufferu

; ------ n vrat registr–

ViewAkt9:pop       dx
         pop       cx
         pop       ax
         ret

ViewAkt  ENDP

; -----------------------------------------------------------------------------
;        posun na dal¨¡ blok dat (CY=nen¡ dal¨¡ blok)
; -----------------------------------------------------------------------------

ViewNxt  PROC      NEAR

; ------ test, zda je ji‘ konec souboru

         cmp       word ptr ds:[ViewNum],VIEWBMAX ; je buffer pln˜ ?
         jb        ViewNxt9                 ; je ji‘ konec souboru

; ------ £schova registr–

         push      ax
         push      cx
         push      si
         push      di
         push      es

; ------ posun ukazatel–

         mov       cx,VIEWBMAX/2            ; polovina bufferu
         sub       ds:[ViewNum],cx          ; sn¡‘en¡ dat v bufferu
         add       word ptr ds:[ViewBeg],cx ; posun ukazatele za‡ tku bufferu
         adc       word ptr ds:[ViewBeg+2],0 ; p©enos HIGH

; ------ p©esun bloku dat o polovinu bufferu dol–

         push      ds
         pop       es
         mov       di,offset ViewBuf        ; za‡ tek bufferu
         mov       si,offset ViewBuf+VIEWBMAX/2 ; za‡ tek druh‚ poloviny
         shr       cx,1                     ; d‚lka dat ve slovech
         cld
         rep       movsw                    ; p©enos bufferu

; ------ na‡ten¡ druh‚ho bloku dat do bufferu

         mov       si,di                    ; adresa druh‚ poloviny bufferu
         call      ViewRead                 ; na‡ten¡ bloku dat
         add       word ptr ds:[ViewNum],ax ; zv˜¨en¡ ‡¡ta‡e dat v bufferu

; ------ n vrat registr–

         pop       es
         pop       di
         pop       si
         pop       cx
         pop       ax
         clc                                ; p©¡znak operace OK
ViewNxt9:ret

ViewNxt  ENDP

; -----------------------------------------------------------------------------
;        posun na p©edchoz¡ blok dat (CY=je ji‘ za‡ tek souboru)
; -----------------------------------------------------------------------------

ViewPred PROC      NEAR

; ------ test, zda je ji‘ za‡ tek souboru

         cmp       word ptr ds:[ViewBeg],0  ; je za‡ tek bufferu ?
         jne       ViewPrd1                 ; nen¡ za‡ tek bufferu
         cmp       word ptr ds:[ViewBeg+2],0
         stc
         je        ViewPrd9                 ; je ji‘ za‡ tek bufferu

; ------ £schova registr–

ViewPrd1:push      ax
         push      cx
         push      si
         push      di
         push      es

; ------ posun ukazatel–

         mov       word ptr ds:[ViewNum],VIEWBMAX ; nov˜ po‡et bajt– v bufferu
         mov       cx,VIEWBMAX/2            ; polovina bufferu
         sub       word ptr ds:[ViewBeg],cx ; posun ukazatele za‡ tku bufferu
         sbb       word ptr ds:[ViewBeg+2],0 ; p©enos HIGH

; ------ p©esun bloku dat o polovinu bufferu v˜¨e

         push      ds
         pop       es
         mov       si,offset ViewBuf        ; za‡ tek bufferu
         mov       di,offset ViewBuf+VIEWBMAX/2 ; za‡ tek druh‚ poloviny
         shr       cx,1                     ; d‚lka dat ve slovech
         cld
         rep       movsw                    ; p©enos bufferu

; ------ na‡ten¡ prvn¡ho bloku dat do bufferu

         mov       si,offset ViewBuf        ; adresa prvn¡ poloviny bufferu
         call      ViewRead                 ; na‡ten¡ bloku dat

; ------ n vrat registr–

         pop       es
         pop       di
         pop       si
         pop       cx
         pop       ax
         clc                                ; p©¡znak operace OK
ViewPrd9:ret

ViewPred ENDP

; -----------------------------------------------------------------------------
;        na‡ten¡ obsahu cel‚ho bufferu
; -----------------------------------------------------------------------------

ViewRBuf PROC      NEAR

         push      ax
         push      si
         mov       si,offset ViewBuf        ; datov˜ buffer
         call      ViewRead                 ; na‡ten¡ prvn¡ho bloku dat
         mov       ds:[ViewNum],ax          ; po‡et bajt– v bufferu
         call      ViewRead                 ; na‡ten¡ druh‚ho bloku dat
         add       ds:[ViewNum],ax          ; po‡et bajt– v bufferu
         pop       si
         pop       ax
         ret

ViewRBuf ENDP

; -----------------------------------------------------------------------------
;        ‡ten¡ bloku dat ze souboru (polovina bufferu)
; -----------------------------------------------------------------------------
; VSTUP: SI=adresa v bufferu
; VSTUP:AX=po‡et na‡ten˜ch bajt–
;        SI=n sleduj¡c¡ adresa
; -----------------------------------------------------------------------------

ViewRead PROC      NEAR

; ------ £schova registr–

         push      bx
         push      cx
         push      dx

; ------ nastaven¡ ukazatele v souboru

         xor       cx,cx                    ; CX <- 0 offset HIGH
         mov       dx,si                    ; adresa v bufferu
         sub       dx,offset ViewBuf        ; offset v bufferu
         add       dx,word ptr ds:[ViewBeg] ; offset v souboru LOW
         adc       cx,word ptr ds:[ViewBeg+2] ; offset v souboboru HIGH
         mov       bx,ds:[ViewIdnt]         ; identifik tor souboru
         mov       ax,4200h
         int       21h                      ; nastaven¡ ukazatele v souboru

; ------ ‡ten¡ bloku dat ze souboru

         mov       dx,si                    ; adresa ke ‡ten¡ dat
         mov       cx,VIEWBMAX/2            ; velikost dat ke ‡ten¡
         mov       ah,3fh
         int       21h                      ; na‡ten¡ bloku dat
         jnc       ViewRd2                  ; operace OK
         xor       ax,ax                    ; jinak nic na‡teno
ViewRd2: add       si,ax                    ; zv˜¨en¡ adresy v bufferu

; ------ n vrat registr–

         pop       dx
         pop       cx
         pop       bx
         ret

ViewRead ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                          Obsluha seznamu
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;þ

; *****************************************************************************
;                              Sezn
;                 ovl d n¡ seznamu (t‚‘ bˆhem na‡¡t n¡)
; -----------------------------------------------------------------------------
; VSTUP: BX=kl vesa
;        DS=datov˜ segment
; VSTUP:BX=0 - kl vesa obslou‘ena
; *****************************************************************************

Sezn     PROC      NEAR

; ------ rozli¨en¡, zda je prohl¡‘en¡ souboru

         test      byte ptr ds:[Param],bit7 ; je © dkov˜ re‘im ?
         jz        Sezn00                   ; nen¡ © dkov˜ re‘im
         ret

Sezn00:  cmp       bx,3d00h                 ; F3 zobrazen¡ souboru ?
         je        Sezn0                    ; zobrazen¡ souboru
         test      byte ptr ds:[Param],bit5 ; je prohl¡‘en¡ souboru ?
         jz        Sezn1
Sezn0:   jmp       View                     ; obsluha prohl¡‘en¡ souboru

; ------ £schova registr–

Sezn1:   push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      es

; ------ obsluha kl vesy

         mov       ax,ds:[SeznTop]          ; po‡ tek seznamu
         mov       cx,ds:[DispNum]          ; po‡et © dk– seznamu
         mov       dx,ds:[SeznAkt]          ; aktivn¡ polo‘ka

         call      JumpBX                   ; obsluha kl vesy

         dw        4800h,SeznUp             ; kurzor nahoru
         dw        5000h,SeznDwn            ; kurzor dol–
         dw        4700h,SeznHome           ; HOME za‡ tek seznamu
         dw        4f00h,SeznEnd            ; END konec seznamu
         dw        8400h,SeznHome           ; ^PgUp za‡ tek seznamu
         dw        7600h,SeznEnd            ; ^PgDn konec seznamu
         dw        4900h,SeznPgUp           ; PgUp str nka nahoru
         dw        5100h,SeznPgDn           ; PgDn str nka dol–
         dw        5200h,SeznIns            ; INSERT ozna‡en¡ polo‘ky
         dw        5300h,SeznDel            ; DELETE ozna‡en¡ v¨ech polo‘ek
         dw        180fh,SeznCO             ; ^O zobrazen¡ podkladu

         dw        0,Sezn9                  ; neobsluhovan  kl vesa

; ------ nastaven¡ kurzoru

Sezn8:   mov       ds:[SeznAkt],dx          ; nov  aktivn¡ polo‘ka
         mov       ds:[SeznTop],ax          ; po‡ tek seznamu
         call      NormSezn                 ; normalizace seznamu
         call      DispSezn                 ; zobrazen¡ seznamu

; ------ n vrat registr–

Sezn9:   pop       es
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

Sezn     ENDP

; -----------------------------------------------------------------------------
;        ozna‡en¡ v¨ech polo‘ek
; -----------------------------------------------------------------------------

SeznDel  PROC      NEAR

         jcxz      SeznDel2                 ; nen¡ ‘ dn  polo‘ka
         test      byte ptr ds:[Param],bit2 ; je hled n¡ souboru ?
         jnz       SeznDel2                 ; je hled n¡ souboru
         xor       bx,bx
SeznDel1:call      Invert                   ; zmˆna ozna‡en¡ polo‘ky
         inc       bx                       ; zv˜¨en¡ ukazatele polo‘ek
         loop      SeznDel1                 ; ozna‡en¡ dal¨¡ polo‘ky
SeznDel2:jmp       short Sezn8

SeznDel  ENDP

; -----------------------------------------------------------------------------
;        kurzor nahoru
; -----------------------------------------------------------------------------

SeznUp   PROC      NEAR

         or        dx,dx                    ; je ji‘ za‡ tek seznamu ?
         jz        SeznUp1                  ; je ji‘ za‡ tek seznamu
         dec       dx                       ; posun kurzoru
SeznUp1: jmp       short Sezn8

SeznUp   ENDP

; -----------------------------------------------------------------------------
;        ozna‡en¡ polo‘ky (mus¡ n sledovat posun kurzoru dol–)
; -----------------------------------------------------------------------------

SeznIns  PROC      NEAR

         mov       bx,dx                    ; ‡¡slo polo‘ky
         call      Invert                   ; zmˆna ozna‡en¡ polo‘ky
                                          ;* n sleduje kurzor dol–

SeznIns  ENDP

; -----------------------------------------------------------------------------
;        kurzor dol–
; -----------------------------------------------------------------------------

SeznDwn  PROC      NEAR

         inc       dx                       ; posun kurzoru
         cmp       dx,cx
         jb        SeznDwn1
         dec       dx
SeznDwn1:jmp       short Sezn8

SeznDwn  ENDP

; -----------------------------------------------------------------------------
;        za‡ tek senzamu
; -----------------------------------------------------------------------------

SeznHome PROC      NEAR

         xor       dx,dx
         jmp       short Sezn8

SeznHome ENDP

; -----------------------------------------------------------------------------
;        konec seznamu
; -----------------------------------------------------------------------------

SeznEnd  PROC      NEAR

         mov       dx,cx
         jcxz      Sezn8
         dec       dx
Sezn88:  jmp       short Sezn8

SeznEnd  ENDP

; -----------------------------------------------------------------------------
;        str nka nahoru
; -----------------------------------------------------------------------------

SeznPgUp PROC      NEAR

         or        ax,ax                    ; zobrazen po‡ tek seznamu ?
         jz        SeznHome                 ; je po‡ tek seznamu
         mov       bx,ds:[SeznRad]          ; po‡et © dk– na str nku
         dec       bx                       ; po‡et © dk– - 1
         cmp       bx,ax                    ; lze posunout o celou str nku ?
         jbe       SeznPgU1                 ; posun je OK
         mov       bx,ax                    ; omezen¡ d‚lky posunu
SeznPgU1:sub       ax,bx                    ; posun po‡ tku str nky
         sub       dx,bx                    ; posun kurzoru
         jmp       short Sezn88

SeznPgUp ENDP

; -----------------------------------------------------------------------------
;        str nka dol–
; -----------------------------------------------------------------------------

SeznPgDn PROC      NEAR

         mov       bx,cx                    ; konec seznamu
         sub       bx,ds:[SeznRad]          ; maxim ln¡ po‡ tek seznamu
         jnc       SeznPgD1                 ; nen¡ p©ete‡en¡
         xor       bx,bx                    ; omezen¡ po‡ tku
SeznPgD1:cmp       ax,bx                    ; je ji‘ konec seznamu ?
         jae       SeznEnd                  ; je konec seznamu
         sub       bx,ax                    ; po‡et zbyl˜ch © dk– k posunu
         mov       cx,ds:[SeznRad]          ; po‡et © dk– na str nku
         dec       cx                       ; po‡et © dk– - 1
         cmp       cx,bx                    ; je po‡et © dk– OK ?
         jbe       SeznPgD2                 ; po‡et © dk– je OK
         mov       cx,bx                    ; omezen¡ po‡tu © dk–
SeznPgD2:add       ax,cx                    ; posun po‡ tku str nky
         add       dx,cx                    ; posun kurzoru
         jmp       short Sezn88

SeznPgDn ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ podkladu ^O
; -----------------------------------------------------------------------------

SeznCO   PROC      NEAR

         call      PopScr                   ; n vrat obsahu obrazovky
         call      InpChr
         jmp       short Sezn88

SeznCO   ENDP

; -----------------------------------------------------------------------------
;        inverze ozna‡en¡ polo‘ky BX
; -----------------------------------------------------------------------------

Invert   PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      es

; ------ kontrola platnosti polo‘ky

         cmp       bx,ds:[DispNum]
         jae       Invert4                  ; nen¡ platn  polo‘ka

; ------ adresa polo‘ky

         shl       bx,1                     ; ‡¡slo polo‘ky * 2
         mov       bx,ds:[bx+DispIndx]      ; adresa polo‘ky
         mov       es,ds:[SeznSegm]         ; segment seznamu soubor–

; ------ test, zda je platn  polo‘ka

         test      byte ptr es:[bx],bit6    ; je to platn  polo‘ka ?
         jnz       Invert4                  ; nen¡ to platn  polo‘ka

; ------ inverze ozna‡en¡ polo‘ky

         xor       byte ptr es:[bx],bit7    ; zmˆna p©¡znaku ozna‡en¡ polo‘ky

; ------ v˜po‡et obsazen‚ kapacity souborem

         mov       ax,es:[bx+5]             ; velikost polo‘ky LOW
         mov       dx,es:[bx+7]             ; velikost polo‘ky HIGH
         mov       si,ax
         mov       di,dx
         mov       cx,ds:[DiskBlok]         ; po‡et bajt– na alok. blok
         jcxz      Invert1                  ; nˆjak  chyba
         cmp       dx,cx                    ; je velikost OK ?
         jae       Invert1                  ; chybn  velikost - nezaokrouhl¡ se
         dec       cx                       ; velikost bloku - 1
         add       ax,cx                    ; zaokrouhlen¡ na alok. blok
         adc       dx,0                     ; p©enos
         inc       cx                       ; velikost alok. bloku
         cmp       dx,cx                    ; je chybn  velikost ?
         jae       Invert1                  ; chybn  velikost - nezaokrouhl¡ se
         div       cx                       ; v˜po‡et aloka‡n¡ch blok–
         mul       cx                       ; velikost se zaokrouhlen¡m

; ------ rozli¨en¡, zda je ozna‡en¡ nebo odzna‡en¡ polo‘ky

Invert1: test      byte ptr es:[bx],bit7    ; je polo‘ka nyn¡ ozna‡en  ?
         jz        Invert2                  ; ozna‡en¡ se zru¨ilo

; ------ p©i‡ten¡ velikosti polo‘ky

         add       word ptr ds:[InsSumm],si ; p©i‡ten¡ velikosti LOW
         adc       word ptr ds:[InsSumm+2],di ; p©i‡ten¡ velikosti HIGH
         add       word ptr ds:[InsObs],ax  ; p©i‡ten¡ obsaz. kapacity LOW
         adc       word ptr ds:[InsObs+2],dx ; p©i‡ten¡ obsaz. kapacity HIGH
         inc       word ptr ds:[InsNum]     ; zv˜¨en¡ ‡¡ta‡e polo‘ek
         jmp       short Invert4

; ------ ode‡ten¡ velikosti polo‘ky

Invert2: sub       word ptr ds:[InsSumm],si ; ode‡ten¡ velikosti LOW
         sbb       word ptr ds:[InsSumm+2],di ; ode‡ten¡ velikosti LOW
         sub       word ptr ds:[InsObs],ax  ; ode‡ten¡ obsaz. kapacity LOW
         sbb       word ptr ds:[InsObs+2],dx ; ode‡ten¡ obsaz. kapacity HIGH
         dec       word ptr ds:[InsNum]     ; sn¡‘en¡ ‡¡ta‡e polo‘ek

; ------ n vrat registr–

Invert4: pop       es
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

Invert   ENDP

; *****************************************************************************
;                                DispSezn
;                        zobrazen¡ seznamu soubor–
; -----------------------------------------------------------------------------
; VSTUP: DS=datov˜ segment
; lok ln¡ promˆnn‚: SS:[BP-2] (2) ‡¡ta‡ aktivn¡ho © dku
;                   SS:[BP-3] (1) barva bˆ‘n‚ho © dku s kurzorem
;                   SS:[BP-4] (1) barva bˆ‘n‚ho © dku
;                   SS:[BP-5] (1) barva vysv¡cen‚ho © dku s kurzorem
;                   SS:[BP-6] (1) barva vysv¡cen‚ho © dku
;                   SS:[BP-8] (2) ukazatel v indexov‚ tabulce zobrazen¡
;                   SS:[BP-10] (2) po‡et index– v indexov‚ tabulce
;                   SS:[BP-11] (1) ‡¡ta‡ © dk– k zobrazen¡
;                   SS:[BP-12] (1) ukazatel © dku na obrazovce
;                   SS:[BP-14] (2) segment seznamu
;                   SS:[BP-16] (2) datov˜ segment
;                   SS:[BP-18] (1) barva cesty
; *****************************************************************************

DispSezn PROC      NEAR

         test      byte ptr ds:[Param],bit5 ; je prohl¡‘en¡ soubor– ?
         jz        DispSz01                 ; nen¡ prohl¡‘en¡
         call      DispNadp                 ; zobrazen¡ informa‡n¡ho © dku
         call      DispHlp                  ; zobrazen¡ n povˆdy
         ret

; ------ £schova registr–

DispSz01:push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         push      ds
         push      es
         mov       bp,sp

; ------ p©¡prava registr–

         sub       sp,18+2*80               ; m¡sto pro lok ln¡ promˆnn‚
         mov       bx,ds:[SeznTop]          ; prvn¡ © dek
         mov       ss:[bp-8],bx             ; ukazatel ‡¡sla © dku
         mov       ax,ds:[SeznAkt]          ; aktivn¡ © dek
         sub       ax,bx                    ; offset od prvn¡ho © dku
         inc       ax                       ; p©ednastaven¡
         mov       ss:[bp-2],ax             ; ‡¡ta‡ aktivn¡ho © dku
         mov       ax,word ptr ds:[Barva1]  ; bˆ‘n  barva a kurzor
         mov       ss:[bp-4],ax             ; barva bˆ‘n‚ho © dku a kurzor
         mov       ax,word ptr ds:[Barva3]  ; vysv¡cen  barva a kurzor
         mov       ss:[bp-6],ax             ; barva vysv¡cen‚ho © dku a kurzor
         mov       al,ds:[Barva5]           ; barva cesty
         mov       ss:[bp-18],al            ; barva cesty
         mov       ax,ds:[DispNum]          ; po‡et index– v tabulce
         mov       ss:[bp-10],ax            ; po‡et index– v tabulce
         or        ax,ax                    ; je nˆco zobrazeno ?
         jnz       DispSzn0                 ; je nˆco zobrazeno
         dec       word ptr ss:[bp-2]       ; kurzor nen¡ zobrazen
DispSzn0:mov       ah,ds:[Radku]            ; po‡et © dk– displeje
         sub       ah,2                     ; po‡et © dk– k zobrazen¡
         mov       al,1                     ; ukazatel © dku na obrazovce
         mov       ss:[bp-12],ax            ; ukazatel a ‡¡ta‡ © dku
         mov       ax,ds:[SeznSegm]         ; segment seznamu
         mov       ss:[bp-14],ax            ; segment seznamu
         mov       ss:[bp-16],ds            ; datov˜ segment
         push      ss
         pop       es                       ; ES <- SS

; ------ p©¡prava barvy polo‘ky

DispSzn1:mov       ah,ss:[bp-4]             ; bˆ‘n  barva
         mov       al,ss:[bp-6]             ; vysv¡cen  barva
         dec       word ptr ss:[bp-2]       ; ‡¡ta‡ aktivn¡ho © dku
         jnz       DispSzn2                 ; nen¡ aktivn¡ © dek
         mov       ah,ss:[bp-3]             ; bˆ‘n˜ kurzor
         mov       al,ss:[bp-5]             ; vysv¡cen˜ kurzor
DispSzn2:mov       si,ss:[bp-8]             ; ukazatel v indexov‚ tabulce
         cmp       si,ss:[bp-10]            ; je platn˜ index ?
         jae       DispSzn3                 ; nen¡ platn˜ index
         shl       si,1
         mov       ds,ss:[bp-16]            ; datov˜ segment
         mov       si,ds:[si+DispIndx]      ; © dek k zobrazen¡
         mov       ds,ss:[bp-14]            ; segment seznamu
         test      byte ptr ds:[si],bit7    ; je polo‘ka ozna‡en  ?
         jz        DispSzn3                 ; nen¡ ozna‡en 
         mov       ah,al                    ; barva ozna‡en‚ polo‘ky

; ------ inicializa‡n¡ vymaz n¡ buferu

DispSzn3:mov       al," "                   ; znak mezery k vymaz n¡ buferu
         mov       di,sp                    ; buffer © dku
         mov       cx,80                    ; po‡et znak– k vymaz n¡
         cld
         rep       stosw                    ; vymaz n¡ bufferu
         mov       di,sp                    ; adresa bufferu

; ------ adresa polo‘ky v bufferu

         mov       si,ss:[bp-8]             ; ukazatel v indexov‚ tabulce
         cmp       si,ss:[bp-10]            ; je platn˜ index ?
         jb        DispSzn4
DispSz88:jmp       DispSzn8                 ; nen¡ platn˜ index
DispSzn4:shl       si,1
         mov       ds,ss:[bp-16]            ; datov˜ segment
         mov       si,ds:[si+DispIndx]      ; © dek k zobrazen¡
         mov       ds,ss:[bp-14]            ; segment seznamu

; ------ dek¢dov n¡ textu adres ©e

         test      byte ptr ds:[si],bit6    ; je to text adres ©e ?
         jz        DispSzn6                 ; nen¡ to text adres ©e
         and       ah,0f0h
         or        ah,ss:[bp-18]            ; barva cesty
         inc       si
DispSzn5:lodsb
         cmp       al,0
         je        DispSz88                 ; konec textu
         stosw                              ; ulo‘en¡ znaku
         jmp       short DispSzn5           ; dal¨¡ znak

; ------ dek¢dov n¡ jm‚na souboru

DispSzn6:add       di,4*2                   ; za‡ tek k dek¢dov n¡ jm‚na souboru
         mov       al,"û"
         test      byte ptr ds:[si],bit7
         jnz       DispSz61
         mov       al," "
DispSz61:stosw
         mov       al," "
         stosw
         mov       bl,ds:[si]               ; atributy
         and       bl,DIR                   ; atribut adres ©e
         shl       bl,1                     ; na hodnotu 20h
         xor       bl,20h                   ; oprava hodnoty
         push      si
         add       si,9                     ; text jm‚na
         push      di
         cld
DispSz62:lodsb
         cmp       al,0
         je        DispSz64
         cmp       al,"."
         je        DispSz64
         cmp       al,"A"
         jb        DispSz63
         cmp       al,"Z"
         ja        DispSz63
         add       al,bl                    ; korekce na mal‚ p¡smeno
DispSz63:stosw
         jmp       short DispSz62
DispSz64:pop       di
         add       di,2*9

; ------ dek¢dov n¡ p©¡pony jm‚na souboru

         push      di
         cmp       al,0
         je        DispSz68
DispSz66:lodsb
         cmp       al,0
         je        DispSz68
         cmp       al,"A"
         jb        DispSz67
         cmp       al,"Z"
         ja        DispSz67
         add       al,bl                    ; korekce na mal‚ p¡smeno
DispSz67:stosw
         jmp       short DispSz66
DispSz68:pop       di
         pop       si
         add       di,3*2

; ------ dek¢dov n¡ velikosti souboru

         test      byte ptr ds:[si],DIR     ; je to adres © ?
         jz        DispSz69                 ; nen¡ to adres ©
         push      si
         push      ds
         add       di,2*4
         mov       ds,ss:[bp-16]            ; datov˜ segment
         mov       cx,9
         mov       si,offset AdrTxt
DspSz682:lodsb
         stosw
         loop      DspSz682
         pop       ds
         pop       si
         jmp       short DispSz6a

DispSz69:add       di,13*2
         push      ax
         mov       bh,ah                    ; barva
         mov       bl,"."                   ; oddˆlova‡ © d–
         mov       ax,ds:[si+5]             ; velikost LOW
         mov       dx,ds:[si+7]             ; velikost HIGH
         call      DekCNum                  ; dek¢dov n¡ ‡¡sla s barvou
         pop       ax
         cld
DispSz6a:add       di,2*2

; ------ dek¢dov n¡ data souboru

         mov       bx,ds:[si+3]             ; datum souboru
         mov       al,bl                    ; den
         and       al,31                    ; den
         call      DekNm                    ; dek¢dov n¡ ‡¡sla
         mov       al,"."
         stosw                              ; odddˆlovac¡ te‡ka
         mov       cl,5                     ; po‡et rotac¡
         shr       bx,cl                    ; rotace
         mov       al,bl
         and       al,15                    ; mˆs¡c
         call      DekNm0                   ; dek¢dov n¡ mˆs¡ce
         mov       al,"."
         stosw                              ; oddˆlovac¡ te‡ka
         mov       cl,4                     ; po‡et rotac¡
         shr       bx,cl                    ; rotace
         add       di,4*2
         push      ax
         mov       bh,ah                    ; barva
         mov       al,bl
         mov       bl,0                     ; nen¡ oddˆlova‡ © d–
         mov       ah,0
         add       ax,1980                  ; korekce roku
         xor       dx,dx
         call      DekCNum                  ; dek¢dov n¡ roku
         pop       ax
         add       di,2*2

; ------ dek¢dov n¡ ‡asu souboru

         mov       bx,ds:[si+1]             ; ‡as souboru
         mov       ch,bl                    ; £schova sekundy
         mov       al,bh                    ; hodina
         mov       cl,3                     ; po‡et rotac¡
         shr       al,cl                    ; rotace na pozici
         call      DekNm                    ; dek¢dov n¡ hodiny
         mov       al,":"
         stosw                              ; oddˆlova‡ ‡asu
         shl       bx,cl                    ; rotace na pozici
         mov       al,bh                    ; minuta
         and       al,63
         call      DekNm0                   ; dek¢dov n¡ minuty
         mov       al,":"
         stosw                              ; oddˆlova‡
         mov       al,ch                    ; sekunda/2
         and       al,31                    ; sekunda/2
         shl       al,1                     ; sekunda
         call      DekNm0                   ; dek¢dov n¡ sekundy
         add       di,2*2

; ------ dek¢dov n¡ atribut– souboru

         mov       bl,ds:[si]               ; atributy
         mov       ds,ss:[bp-16]            ; datov˜ segment
         mov       bh,5                     ; po‡et atribut–
         mov       si,offset AtrTab
DispSz71:lodsb
         mov       cl,al                    ; po‡et rotac¡
         shl       bl,cl                    ; rotace atributu
         jnc       DispSz72                 ; nen¡ nastaven
         lodsb
         stosw
         lodsb
         stosw
         lodsb
         stosw
         jmp       short DispSz73

DispSz72:mov       al," "
         stosw
         mov       al,"-"
         stosw
         mov       al," "
         stosw
         add       si,3

DispSz73:mov       al," "
         stosw
         dec       bh
         jnz       DispSz71                 ; dal¨¡ atribut

; ------ zobrazen¡ © dku seznamu

DispSzn8:mov       ds,ss:[bp-16]            ; datov˜ segment
         mov       si,sp                    ; adresa bufferu s © dkem
         mov       cl,80                    ; d‚lka textu
         mov       dl,0
         mov       dh,ss:[bp-12]            ; © dek k zobrazen¡
         call      DispStr                  ; zobrazen¡ © dku seznamu

; ------ p©¡prava pro dal¨¡ © dek

         inc       word ptr ss:[bp-8]       ; zv˜¨en¡ ‡¡sla © dku
         inc       byte ptr ss:[bp-12]      ; zv˜¨en¡ ukazatele na obrazovce
         dec       byte ptr ss:[bp-11]      ; ‡¡ta‡ © dk– k zobrazen¡
         jz        DispSzn9                 ; konec
         jmp       DispSzn1                 ; zobrazen¡ dal¨¡ho © dku

; ------ n vrat registr–

DispSzn9:mov       sp,bp
         pop       es
         pop       ds
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         call      DispNadp                 ; zobrazen¡ informa‡n¡ho © dku
         call      DispHlp                  ; zobrazen¡ n povˆdy
         ret

DispSezn ENDP

; -----------------------------------------------------------------------------
;        normalizace seznamu
; -----------------------------------------------------------------------------

NormSezn PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx

; ------ p©¡prava registr–

         mov       ax,ds:[SeznAkt]          ; aktivn¡ polo‘ka
         mov       bx,ds:[SeznTop]          ; prvn¡ polo‘ka

; ------ kontrola podte‡en¡ pod po‡ tek

         cmp       ax,bx                    ; je podte‡en¡ pod po‡ tek ?
         ja        NormSzn2                 ; nen¡ podte‡en¡ pod po‡ tek
         mov       bx,ax                    ; omezen¡ po‡ tku zdola
         or        bx,bx
         jz        NormSzn2                 ; je prvn¡ © dek
         dec       bx

; ------ v˜po‡et maxim ln¡ho prvn¡ho © dku

NormSzn2:mov       cx,ds:[DispNum]
         jcxz      NormSz23                 ; nen¡ nic
         dec       cx
         cmp       ax,cx                    ; je posledn¡ © dek ?
         mov       cx,ds:[SeznRad]          ; po‡et © dk– na str nku
         jae       NormSz22                 ; je posledn¡ © dek
         dec       cx                       ; je¨tˆ jeden © dek rezerva
NormSz22:dec       cx                       ; po‡et © dk– - 1
         sub       ax,cx                    ; maxim ln¡ prvn¡ © dek
         jae       NormSzn3
NormSz23:xor       ax,ax                    ; omezen¡ max. prvn¡ho © dku

; ------ kontrola p©ete‡en¡ kurzoru za konec

NormSzn3:cmp       ax,bx                    ; je p©ete‡en¡ za konec ?
         jbe       NormSzn4                 ; kurzor je OK
         mov       bx,ax                    ; omezen¡ prvn¡ho © dku

; ------ ulo‘en¡ registr–

NormSzn4:mov       ds:[SeznTop],bx          ; nov  prvn¡ polo‘ka

; ------ n vrat registr–

         pop       cx
         pop       bx
         pop       ax
         ret

NormSezn ENDP

; -----------------------------------------------------------------------------
;        dek¢dov n¡ ‡¡sla 2 ‡¡slice (AL=‡¡slo, AH=barva)
; -----------------------------------------------------------------------------

DekNm0:  mov       byte ptr es:[di],"0"     ; prvn¡ znak bude nula

DekNm    PROC      NEAR

         push      ax
         push      bx
         push      dx

         add       di,2*2
         mov       bh,ah                    ; barva
         mov       bl,0                     ; nen¡ oddˆlova‡ © d–
         xor       dx,dx
         mov       ah,0
         call      DekCNum                  ; dek¢dov n¡ ‡¡sla

         pop       dx
         pop       bx
         pop       ax
         ret

DekNm    ENDP

; -----------------------------------------------------------------------------
;        dek¢dov n¡ textu ASCIIZ
; -----------------------------------------------------------------------------

DekTxt   PROC      NEAR

         push      ax
         push      si
         cld
DekTxt1: lodsb
         cmp       al,0
         je        DekTxt2
         stosw
         jmp       short DekTxt1
DekTxt2: pop       si
         pop       ax
         ret

DekTxt   ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                   Rozbor zad n¡ parametr– operace
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

; *****************************************************************************
;                               Rozbor
;                  Rozbor zad n¡ parametr– operace
;        Buffer editovan‚ho © dku je doplnˆn mezerami na plnou velikost
; -----------------------------------------------------------------------------
; VSTUP: DS=datov˜ segment
; VSTUP:CY=chyba zad n¡ parametr–
;        SI=adresa chyby zad n¡ v edita‡n¡m bufferu
; *****************************************************************************
;þ
Rozbor   PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      di
         push      es

         push      ds
         pop       es                       ; ES <- datov˜ segment

         and       byte ptr ds:[Param],not bit3 ; zru¨en¡ p©¡znaku hled n¡ textu
         or        byte ptr ds:[Param2],bit0 ; p©¡znak podadres ©–

; ------ p©ednastaven¡ z kladn¡ho adres ©e aktivn¡ho disku ("C:\")

         mov       ah,19h
         int       21h                      ; poskytnut¡ aktivn¡ho disku
         add       al,"A"                   ; korekce na znak ASCII
         mov       di,offset FilePath       ; buffer adres ©e
         cld
         mov       ah,":"                   ; oddˆlova‡ disk– ":"
         stosw                              ; ozna‡en¡ disku a oddˆlova‡
         mov       ax,"\"                   ; adres © ROOT a koncov  0
         stosw                              ; lom¡tko ROOT a koncov  0
         mov       word ptr ds:[FilePthN],3 ; d‚lka textu

; ------ nalezen¡ za‡ tku parametr– ( -> BX)

         mov       di,offset EditBuff       ; edita‡n¡ buffer
         mov       cx,MAXLINE               ; d‚lka © dku edit. bufferu
         mov       al,"/"                   ; znak parametr–
         cld
         repne     scasb                    ; nalezen¡ oddˆlova‡e parametr– "/"
         dec       di                       ; n vrat za‡ tku parametr– "/"
         mov       bx,di                    ; £schova za‡ tku parametr–

; ------ nalezen¡ za‡ tku text– masek ( -> DI)

Rozbor1: cmp       di,offset EditBuff       ; je ji‘ za‡ tek bufferu ?
         jbe       Rozbor2                  ; je ji‘ za‡ tek bufferu
         cmp       byte ptr ds:[di-1],"\"   ; je oddˆlova‡ adres ©e ?
         je        Rozbor2                  ; je posledn¡ znak adres ©e
         cmp       byte ptr ds:[di-1],":"   ; je oddˆlova‡ disku ?
         je        Rozbor2                  ; je ozna‡en¡ disku
         dec       di                       ; posun ukazatele
         jmp       short Rozbor1            ; jinak hled n¡ dal¨¡ho znaku

; ------ nalezen¡ za‡ tku zadan‚ cesty ( -> SI)

Rozbor2: mov       si,offset EditBuff       ; za‡ tek edit. bufferu
Rozbor21:cmp       si,di                    ; je ji‘ konec cesty ?
         jae       Rozbor4                  ; adres © nen¡ zad n - bude ROOT
         cmp       byte ptr ds:[si]," "     ; je platn˜ text ?
         jne       Rozbor3                  ; je zad n adres ©
         inc       si                       ; p©esko‡en¡ znaku mezery
         jmp       short Rozbor21           ; dal¨¡ znak

; ------ rozbor zad n¡ cesty (je nˆco zad no)

Rozbor3: mov       cx,di                    ; konec textu cesty
         sub       cx,si                    ; d‚lka textu cesty (nen¡ = 0)
         call      RozbPth                  ; rozbor zad n¡ textu adres ©e

; ------ rozbor zad n¡ masek

Rozbor4: mov       si,di                    ; za‡ tek textu masek
         mov       cx,bx                    ; za‡ tek parametr–
         sub       cx,si                    ; d‚lka textu masek
         call      RozbMsk                  ; rozbor zad n¡ masek

; ------ rozbor zad n¡ parametr–

         mov       si,bx                    ; za‡ tek parametr– (znak "/")
         mov       cx,offset EditBuff+MAXLINE ; konec bufferu + 1
         sub       cx,si                    ; d‚lka textu parametr–
         call      RozbParm                 ; rozbor zadan˜ch parametr–

; ------ n vrat registr–

Rozbor9: pop       es
         pop       di
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

Rozbor   ENDP

; *****************************************************************************
;                               RozbPth
;                         rozbor zad n¡ cesty
; -----------------------------------------------------------------------------
; VSTUP: SI=zadan˜ text (odstranˆny £vodn¡ a z vˆre‡n‚ mezery)
;        CX=d‚lka textu (nen¡=0)
;        DS,ES=datov˜ segment
; *****************************************************************************

RozbPth  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di

; ------ implicitn¡ disk (aktivn¡)

         mov       ah,19h
         int       21h                      ; poskytnut¡ aktivn¡ho disku
         add       al,"A"                   ; korekce na p¡smeno

; ------ na‡ten¡ ozna‡en¡ disku ze zad n¡

         cld
         mov       ah,":"                   ; oddˆlova‡ disk– ":"
         cmp       cx,2                     ; zb˜v  dost znak– ?
         jb        RozbPth1                 ; zb˜v  m lo znak–
         cmp       ds:[si+1],ah             ; je zad n disk (ozna‡en¡ ":") ?
         jne       RozbPth1                 ; nen¡ zad n disk
         lodsb                              ; zadan˜ disk
         inc       si                       ; p©esko‡en¡ oddˆlova‡e disku ":"
         dec       cx                       ; sn¡‘en¡ ‡¡ta‡e zbyl˜ch znak–
         dec       cx                       ; sn¡‘en¡ ‡¡ta‡e zbyl˜ch znak–
         call      UpCase                   ; korekce na velk‚ p¡smeno

; ------ ulo‘en¡ ozna‡en¡ disku a za‡ tku cesty (ROOT)

RozbPth1:mov       dl,al                    ; £schova ozna‡en¡ disku ASCII
         mov       di,offset FilePath       ; buffer k ulo‘en¡ cesty
         cld
         stosw                              ; ulo‘en¡ ozna‡en¡ disku
         mov       ax,"\"                   ; ozna‡en¡ ROOT adres ©e a koncov  0
         stosw                              ; ulo‘en¡ ozna‡en¡ ROOT a koncov‚ 0
         dec       di                       ; n vrat na koncovou 0

; ------ na‡ten¡ aktivn¡ho adres ©e disku

         sub       dl,"A"-1                 ; ‡¡slo disku 1,...
         push      si                       ; ukazatel textu
         mov       si,di                    ; adresa k na‡ten¡ adres ©e
         mov       ah,47h
         int       21h                      ; na‡ten¡ aktivn¡ho adres ©e
         pop       si

; ------ nalezen¡ konce na‡ten‚ cesty

         mov       al,0                     ; hledan˜ bajt 0
         cld
         push      cx                       ; po‡et zbyl˜ch znak–
         mov       cx,MAXLINE-3             ; d‚lka zbytku textu
         repne     scasb                    ; nalezen¡ koncov‚ 0
         pop       cx
         dec       di                       ; n vrat na koncovou 0

; ------ zaji¨tˆn¡ koncov‚ho znaku "\"

         mov       al,"\"                   ; znak "\"
         cmp       ds:[di-1],al             ; je posledn¡ znak "\" ?
         je        RozbPth2                 ; je posledn¡ znak "\"
         stosb                              ; nastaven¡ posledn¡ho znaku "\"

; ------ je-li zad n ROOT, nastaven¡ na ROOT

RozbPth2:jcxz      RozbPth3                 ; nezbyl dal¨¡ znak
         cmp       byte ptr ds:[si],"\"     ; je zad n ROOT ?
         jne       RozbPth3                 ; nen¡ zad n ROOT
         mov       di,offset FilePath+3     ; jinak nastaven¡ za ROOT
         inc       si                       ; p©esko‡en¡ znaku "\"
         dec       cx                       ; ‡¡ta‡ zbyl˜ch znak–

; ------ test po‡tu zbyl˜ch znak–, konec zad n¡

RozbPth3:cmp       cx,1                     ; po‡et zbyl˜ch znak–
         jb        RozbPth8                 ; konec zad n¡
         je        RozbPth5                 ; mal˜ po‡et znak– pro ".."

; ------ sn¡‘en¡ £rovnˆ o adres © ".."

         cmp       word ptr ds:[si],".."    ; je sn¡‘en¡ £rovnˆ ?
         jne       RozbPth5                 ; nen¡ sn¡‘en¡ £rovnˆ
RozbPth4:cmp       di,offset FilePath + 3   ; je ji‘ ROOT ?
         jbe       RozbPth7                 ; je ji‘ ROOT
         dec       di                       ; sn¡‘en¡ ukazatele
         cmp       byte ptr ds:[di-1],"\"   ; je ji‘ za‡ tek podadres ©e ?
         jne       RozbPth4                 ; nen¡ za‡ tek - dal¨¡ znak
         jmp       short RozbPth7           ; dal¨¡ podadres ©

; ------ stejn  £rove¤ adres ©e "."

RozbPth5:cmp       byte ptr ds:[si],"."     ; je stejn˜ adres © ?
         je        RozbPth7                 ; stejn  £rove¤ - ignoruje se

; ------ p©enesen¡ textu podadres ©e

RozbPth6:cmp       byte ptr ds:[si],"\"     ; bude ji‘ konec adres ©e ?
         je        RozbPth7                 ; konec adres ©e
         cld
         lodsb                              ; na‡ten¡ znaku
         call      UpCase                   ; korekce na velk‚ p¡smeno
         cld
         stosb                              ; ulo‘en¡ znaku do cesty
         loop      RozbPth6                 ; dal¨¡ znak

; ------ nalezen¡ konce jednoho adres ©e

RozbPth7:jcxz      RozbPth8                 ; nen¡ dal¨¡ znak zad n¡
         cld
         lodsb                              ; na‡ten¡ dal¨¡ho znaku
         dec       cx                       ; sn¡‘en¡ ‡¡ta‡e zbyl˜ch znak–
         cmp       al,"\"                   ; je konec adres ©e ?
         jne       RozbPth7                 ; nen¡ konec - dal¨¡ znak

; ------ ozna‡en¡ konce podadres ©e

         cmp       es:[di-1],al             ; byl ozna‡en konec podadres ©e ?
         je        RozbPth3                 ; je ji‘ znak "\" - dal¨¡ adres ©
         stosb                              ; ulo‘en¡ znaku "\"
         jmp       short RozbPth3           ; dal¨¡ adres ©

; ------ zaji¨tˆn¡ koncov‚ho znaku "\"

RozbPth8:mov       al,"\"
         cmp       ds:[di-1],al             ; byla cesta ukon‡ena "\" ?
         je        RozbPth9                 ; je ji‘ koncov˜ znak "\"
         stosb                              ; ozna‡en¡ konce cesty

; ------ nastaven¡ d‚lky textu

RozbPth9:mov       byte ptr ds:[di],0       ; ozna‡en¡ konce cesty
         sub       di,offset FilePath       ; d‚lka textu v bufferu
         mov       ds:[FilePthN],di         ; d‚lka textu v bufferu

; ------ n vrat registr–

         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

RozbPth  ENDP

; *****************************************************************************
;                                RozbMsk
;                         rozbor zad n¡ masek
; -----------------------------------------------------------------------------
; VSTUP: SI=zadan˜ text
;        CX=d‚lka textu (m–‘e b˜t i CX=0)
;        DS,ES=datov˜ segment
; *****************************************************************************

RozbMsk  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di

         mov       di,offset FileMask       ; buffer k dek¢dov n¡ zadan˜ch masek
         cld

; ------ vypu¨tˆn¡ po‡ te‡n¡ch mezer jedn‚ masky

         jcxz      RozbMsk8                 ; nejsou ‘ dn‚ znaky
RozbMsk2:mov       bl,0                     ; p©¡znak, ‘e nen¡ te‡ka
         cmp       byte ptr ds:[si]," "     ; je ji‘ platn˜ znak ?
         jne       RozbMsk3                 ; je platn˜ znak
         inc       si                       ; zv˜¨en¡ ukazatele v textu
         loop      RozbMsk2                 ; test dal¨¡ho znaku

; ------ p©enos textu jedn‚ masky

RozbMsk3:jcxz      RozbMsk8                 ; konec textu
         lodsb                              ; na‡ten¡ znaku masky
         dec       cx                       ; sn¡‘en¡ ‡¡ta‡e zbyl˜ch znak–
         cmp       al," "                   ; oddˆlovac¡ mezera ?
         je        RozbMsk5                 ; konec textu jedn‚ masky
         cmp       al,"."                   ; je te‡ka ?
         jne       RozbMsk4                 ; nen¡ te‡ka
         mov       bl,al                    ; p©¡znak te‡ky
RozbMsk4:call      UpCase                   ; konverze na velk‚ p¡smeno
         cld
         stosb                              ; ulo‘en¡ do bufferu
         jmp       short RozbMsk3           ; dal¨¡ znak masky

; ------ nalezen¡ za‡ tku dal¨¡ masky

RozbMsk5:cmp       bl,0                     ; byla te‡ka ?
         jne       RozbMs55                 ; byla te‡ka
         mov       al,"*"
         cmp       byte ptr ds:[di-1],al    ; byl na konci znak "*" ?
         je        RozbMs55                 ; byl ji‘ znak "*"
         stosb                              ; ulo‘en¡ znaku "*"
RozbMs55:jcxz      RozbMsk8                 ; nejsou dal¨¡ znaky
         mov       al," "                   ; hledan˜ znak mezery
RozbMsk6:cmp       ds:[si],al               ; je ji‘ platn˜ znak ?
         jne       RozbMsk7                 ; je platn˜ znak
         inc       si                       ; zv˜¨en¡ ukazatele v textu
         loop      RozbMsk6                 ; test dal¨¡ho znaku
         jmp       short RozbMsk8           ; nen¡ dal¨¡ znak
RozbMsk7:stosb                              ; mezera mezi maskami
         jmp       short RozbMsk3           ; dek¢dov n¡ dal¨¡ masky

; ------ konec dek¢dov n¡

RozbMsk8:mov       byte ptr ds:[di],0       ; ozna‡en¡ konce textu
         sub       di,offset FileMask       ; po‡et znak– v bufferu
         mov       ds:[FileMskN],di         ; po‡et znak– v bufferu

; ------ n vrat registr–

         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

RozbMsk  ENDP

; *****************************************************************************
;                                RozbParm
;                      rozbor zad n¡ parametr– operace
; -----------------------------------------------------------------------------
; VSTUP: SI=ukazatel za‡ tku textu
;        CX=po‡et zbyl˜ch znak– textu
;        DS,ES=datov˜ segment
; VSTUP:CY=chyba zad n¡
;        SI=adresa textu s chybou
; *****************************************************************************

RozbParm PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      di

; ------ inicializace parametr– na standardn¡ hodnoty

         push      cx
         mov       di,offset SrcInit1       ; za‡ tek tabulky parametr– hled n¡
         mov       cx,offset(SrcInit2-SrcInit1) ; d‚lka tabulky
         xor       ax,ax                    ; nulovac¡ bajt
         cld
         rep       stosb                    ; vynulov n¡ tabulky
         dec       ax                       ; AX <- 0ffffh
         mov       word ptr ds:[SrcSiz2],ax ; koncov  velikost LOW
         mov       word ptr ds:[SrcSiz2+2],ax ; koncov  velikost HIGH
         pop       cx

; ------ na‡ten¡ prvn¡ho znaku operace

RozbPar1:call      RozbPCh                  ; na‡ten¡ dal¨¡ho znaku
         cmc
         jnc       RozbPar9                 ; nen¡ dal¨¡ znak
         cmp       al,"/"
         je        RozbPar1                 ; znak "/" se ignoruje
         call      UpCase                   ; p©evod na velk‚ p¡smeno

; ------ zad n¡ data

RozbPar2:cmp       al,"D"                   ; je zad n¡ data ?
         jne       RozbPar3                 ; nen¡ zad n¡ data
         call      RozbPDat                 ; rozbor zad n¡ data
         jmp       short RozbPar1           ; dal¨¡ parametr

; ------ zad n¡ ‡asu

RozbPar3:cmp       al,"T"                   ; je zad n¡ ‡asu ?
         jne       RozbPar4                 ; nen¡ zad n¡ ‡asu
         call      RozbPTim                 ; rozbor zad n¡ ‡asu
         jmp       short RozbPar1           ; dal¨¡ parametr

; ------ zad n¡ velikosti

RozbPar4:cmp       al,"N"                   ; je zad n¡ velikosti ?
         jne       RozbPar5                 ; nen¡ zad n¡ velikosti
         call      RozbPSiz                 ; rozbor zad n¡ velikosti
         jmp       short RozbPar1           ; dal¨¡ parametr

; ------ zad n¡ textu

RozbPar5:cmp       al,'"'
         je        RozbPar6                 ; je zad n text
         cmp       al,"'"
         jne       RozbPar7                 ; nen¡ zad n text
RozbPar6:call      RozbPTxt                 ; rozbor zad n¡ textu k hled n¡
         jmp       short RozbPar1           ; dal¨¡ parametr

; ------ z kaz podadres ©–

RozbPar7:cmp       al,"Z"
         jne       RozbPar8
         and       byte ptr ds:[Param2],not bit0
         jmp       short RozbPar1

; ------ zad n¡ atribut–

RozbPar8:call      RozbPAtr                 ; rozbor zad n¡ atribut–
         jnc       RozbPar1                 ; byl to platn˜ znak atributu

                                            ; jinak n vrat p©i chybˆ zad n¡

; ------ n vrat registr–

RozbPar9:pop       di
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

RozbParm ENDP

; -----------------------------------------------------------------------------
;        rozbor zad n¡ textu k hled n¡ (VSTUP: AL=£vodn¡ znak uvozovek)
;                          (ni‡¡ registry AX a DI)
; -----------------------------------------------------------------------------

RozbPTxt PROC      NEAR

         mov       di,offset SrcText        ; buffer textu k hled n¡
         mov       ah,al                    ; £schova £vodn¡ho znaku parametru
         cld

; ------ na‡ten¡ znaku

RozbPTx1:jcxz      RozbPTx5                 ; konec © dku - p©ed‡asn‚ ukon‡en¡
         lodsb                              ; na‡ten¡ znaku textu
         dec       cx                       ; sn¡‘en¡ ‡¡ta‡e znak–

; ------ test, zda je konec textu

         cmp       al,ah                    ; je znak konce textu ?
         jne       RozbPTx3                 ; nen¡ konec textu
         jcxz      RozbPTx7                 ; byl to posledn¡ znak
         cmp       ds:[si],al               ; je to zdvojen˜ znak ?
         jne       RozbPTx7                 ; konec parametru
         lodsb                              ; na‡ten¡ zdvojen‚ho znaku
         dec       cx                       ; sn¡‘en¡ ‡¡ta‡e znak–

; ------ ulo‘en¡ platn‚ho znaku

RozbPTx3:call      UpCase                   ; p©evod na velk‚ p¡smeno
         cld
         stosb                              ; ulo‘en¡ znaku
         jmp       short RozbPTx1           ; dek¢dov n¡ dal¨¡ho znaku

; ------ p©ed‡asn‚ ukon‡en¡ © dku - odstranˆn¡ koncov˜ch mezer

RozbPTx5:cmp       di,offset SrcText        ; je ji‘ za‡ tek © dku ?
         je        RozbPTx7                 ; je ji‘ za‡ tek © dku
         cmp       byte ptr ds:[di-1]," "   ; je mezera ?
         jne       RozbPTx7                 ; nen¡ mezera
         dec       di                       ; sn¡‘en¡ ukazatele textu
         jmp       short RozbPTx5           ; dal¨¡ znak

; ------ nastaven¡ parametr– textu

RozbPTx7:sub       di,offset SrcText        ; d‚lka zadan‚ho textu
         mov       ds:[SrcTextN],di         ; ulo‘en¡ d‚lky textu
         jz        RozbPTx8                 ; nebylo nic zad no
         or        byte ptr ds:[Param],bit3 ; p©¡znak zad n¡ textu k hled n¡

RozbPTx8:ret

RozbPTxt ENDP

; -----------------------------------------------------------------------------
;        rozbor zad n¡ atribut– (AL=znak) (v˜stup: CY=nezn m˜ znak)
; -----------------------------------------------------------------------------

RozbPAtr PROC      NEAR

         mov       di,offset SrcAtr         ; atributy hledan‚ho souboru

; ------ rozli¨en¡, zda bylo velk‚ nebo mal‚ p¡smeno

         mov       dl,1                     ; hodnota atributu + (velk‚ p¡smeno)
         cmp       al,ds:[si-1]             ; zmˆnil se znak z mal‚ho na velk‚ ?
         je        RozbPAt2                 ; bylo velk‚ p¡smeno
         mov       dl,0                     ; hodnota atributu - (mal‚ p¡smeno)

; ------ rozli¨en¡ atribut–

RozbPAt2:mov       dh,0                     ; po‡et rotac¡ pro bit R/O
         cmp       al,"R"                   ; je R/O ?
         je        RozbPAt3                 ; je R/O
         mov       dh,1                     ; po‡et rotac¡ pro bit HID
         cmp       al,"H"                   ; je HID ?
         je        RozbPAt3                 ; je HID
         mov       dh,2                     ; po‡et rotac¡ pro bit SYS
         cmp       al,"S"                   ; je SYS ?
         je        RozbPAt3                 ; je SYS
         mov       dh,4                     ; po‡et rotac¡ pro bit DIR
         cmp       al,"C"                   ; je DIR ?
         je        RozbPAt3                 ; je DIR
         mov       dh,5                     ; po‡et rotac¡ pro bit ARC
         cmp       al,"A"                   ; je ARC ?
         je        RozbPAt3                 ; je ARC
         dec       si                       ; n vrat znaku
         inc       cx
         stc                                ; p©¡znak chyby
         ret

; ------ na‡ten¡ znaku hodnoty bitu (+ nebo -)

RozbPAt3:call      RozbPCh                  ; na‡ten¡ dal¨¡ho znaku
         jc        RozbPAt6                 ; nen¡ dal¨¡ znak
         cmp       al,"+"                   ; je nastaven¡ atributu "+" ?
         jne       RozbPAt4                 ; nen¡ nastaven¡ atributu "+"
         mov       dl,1                     ; hodnota pro nastaven¡
         jmp       short RozbPAt6
RozbPAt4:cmp       al,"-"                   ; je nulov n¡ atributu "-" ?
         jne       RozbPAt5                 ; nen¡ nulov n¡ atributu "-"
         mov       dl,0                     ; hodnota pro nulov n¡
         jmp       short RozbPAt6
RozbPAt5:dec       si                       ; jinak n vrat na‡ten‚ho znaku
         inc       cx                       ; n vrat ‡¡ta‡e znak–

; ------ nastaven¡ hodnoty bitu

RozbPAt6:push      cx
         mov       cl,dh                    ; po‡et rotac¡
         mov       dh,1                     ; maska pro nulov n¡
         shl       dh,cl                    ; rotace masky
         shl       dl,cl                    ; rotace hodnoty bitu
         or        ds:[di+1],dh             ; povolen¡ testu masky
         not       dh                       ; negace - bude se nulovat
         and       ds:[di],dh               ; nulov n¡ star‚ hodnoty bitu
         or        ds:[di],dl               ; po‘adovan  hodnota bitu
         pop       cx                       ; zde je p©¡znak NC = operace OK
RozbPAt9:
         ret

RozbPAtr ENDP

; -----------------------------------------------------------------------------
;        rozbor zad n¡ data
; -----------------------------------------------------------------------------

RozbPDat PROC      NEAR

; ------ rozbor po‡ te‡n¡ho data

         mov       di,offset SrcDat1        ; po‡ te‡n¡ datum
         mov       dx,0*HI + 31             ; rotace + maxim ln¡ hodnota dne
         call      RozbDT                   ; rozbor dne
         mov       dx,5*HI + 15             ; rotace + maxim ln¡ hodnota mˆs¡ce
         call      RozbDT                   ; rozbor mˆs¡ce
         call      RozbRok                  ; rozbor roku

; ------ kopie do koncov‚ho data

         mov       ax,ds:[di]               ; zadan  hodnota
         mov       ds:[di+4],ax
         mov       ax,ds:[di+2]             ; maska
         mov       ds:[di+6],ax

; ------ oddˆlovac¡ znam‚nko "-"

         call      RozbInt                  ; test znaku intervalu "-"
         jc        RozbPDt6                 ; nen¡ znak intervalu

; ------ rozbor koncov‚ho data

RozbPDt4:mov       di,offset SrcDat2        ; koncov‚ datum
         mov       dx,0*HI + 31             ; rotace + maxim ln¡ hodnota dne
         call      RozbDT                   ; rozbor dne
         mov       dx,5*HI + 15             ; rotace + maxim ln¡ hodnota mˆs¡ce
         call      RozbDT                   ; rozbor mˆs¡ce
         call      RozbRok                  ; rozbor roku

; ------ test, zda bylo nˆco zad no

RozbPDt6:cmp       word ptr ds:[SrcDat1M],0 ; je po‡ te‡n¡ datum ?
         jne       RozbPDt9                 ; je zad no
         cmp       word ptr ds:[SrcDat2M],0 ; je koncov‚ datum ?
         jne       RozbPDt9                 ; je zad no

; ------ nastaven¡ aktu ln¡ho data

         push      cx
         mov       ah,2ah
         int       21h                      ; poskytnut¡ syst‚mov‚ho data
         xchg      ax,cx                    ; rok
         sub       ax,1980                  ; offset roku od 1980
         shl       al,1                     ; rotace na pozici
         xchg      dh,al                    ; st©ada‡ hodnoty
         mov       cl,5                     ; po‡et rotac¡
         shl       ax,cl                    ; rotace mˆs¡ce na pozici
         or        dx,ax                    ; aktu ln¡ datum
         mov       ds:[SrcDat1],dx          ; po‡ te‡n¡ datum
         mov       ds:[SrcDat2],dx          ; koncov‚ datum
         mov       word ptr ds:[SrcDat1M],-1 ; maska po‡ te‡n¡ho data
         mov       word ptr ds:[SrcDat2M],-1 ; maska koncov‚ho data
         pop       cx

RozbPDt9:
         ret

RozbPDat ENDP

; -----------------------------------------------------------------------------
;        rozbor zad n¡ ‡asu
; -----------------------------------------------------------------------------

RozbPTim PROC      NEAR

; ------ rozbor po‡ te‡n¡ho ‡asu

         mov       di,offset SrcTim1        ; po‡ te‡n¡ ‡as
         mov       dx,11*HI + 31            ; rotace + maxim ln¡ hodnota hodin
         call      RozbDT                   ; rozbor hodin
         mov       dx,5*HI + 63             ; rotace + maxim ln¡ hodnota minuty
         call      RozbDT                   ; rozbor minut
         call      RozbSek                  ; rozbor sekund

; ------ kopie do koncov‚ho ‡asu

         mov       ax,ds:[di]               ; £daj
         mov       ds:[di+4],ax
         mov       ax,ds:[di+2]             ; maska
         mov       ds:[di+6],ax

; ------ oddˆlovac¡ znam‚nko "-"

         call      RozbInt                  ; test zad n¡ znaku intervalu
         jc        RozbPTm6                 ; nen¡ znak intervalu

; ------ rozbor koncov‚ho ‡asu

RozbPTm4:mov       di,offset SrcTim2        ; koncov˜ ‡as
         mov       dx,11*HI + 31            ; rotace + maxim ln¡ hodnota hodin
         call      RozbDT                   ; rozbor hodin
         mov       dx,5*HI + 63             ; rotace + maxim ln¡ hodnota minuty
         call      RozbDT                   ; rozbor minut
         call      RozbSek                  ; rozbor sekund

; ------ test, zda bylo nˆco zad no

RozbPTm6:cmp       word ptr ds:[SrcTim1M],0 ; je po‡ te‡n¡ ‡as ?
         jne       RozbPTm9                 ; je zad n
         cmp       word ptr ds:[SrcTim2M],0 ; je koncov˜ ‡as ?
         jne       RozbPTm9                 ; je zad n

; ------ nastaven¡ aktu ln¡ho ‡asu (hodina)

         push      cx
         mov       ah,2ch
         int       21h                      ; poskytnut¡ syst‚mov‚ho ‡asu
         shl       ch,1
         shl       ch,1
         shl       ch,1                     ; rotace hodiny na pozici
         mov       cl,0
         mov       ds:[SrcTim1],cx          ; po‡ te‡n¡ ‡as - hodina
         mov       ds:[SrcTim2],cx          ; koncov˜ ‡as - hodina
         mov       word ptr ds:[SrcTim1M],bit11+bit12+bit13+bit14+bit15 ; maska
         mov       word ptr ds:[SrcTim2M],bit11+bit12+bit13+bit14+bit15 ; maska
         pop       cx

RozbPTm9:ret

RozbPTim ENDP

; -----------------------------------------------------------------------------
;        rozbor jednoho £daje data nebo ‡asu
; -----------------------------------------------------------------------------
; VSTUP: DS=datov˜ segment
;        DH=po‡et rotac¡ £daje
;        DL=maxim ln¡ hodnota £daje (maska)
;        DS:[DI+0] (2) parametr
;        DS:[DI+2] (2) maska parametru
; -----------------------------------------------------------------------------

RozbDT   PROC      NEAR

         push      ax
         push      bx

; ------ sestaven¡ masky BX

         push      cx
         mov       cl,dh                    ; po‡et rotac¡ £daje
         mov       bh,0
         mov       bl,dl                    ; maxim ln¡ hodnota (maska)
         shl       bx,cl                    ; rotace masky
         pop       cx

; ------ inicializace hodnoty

         not       bx                       ; £daj se bude nulovat
         and       ds:[di],bx               ; nulov n¡ hodnoty
         and       ds:[di+2],bx             ; nulov n¡ masky

; ------ dek¢dov n¡ ‡¡sla

         call      RozbPNum                 ; na‡ten¡ ‡¡sla AX
         jc        RozbDT5                  ; ‡¡slo nen¡ zad no

; ------ omezen¡ hodnoty ‡¡sla na maximum

         cmp       ah,0                     ; je velk‚ ‡¡slo ?
         jne       RozbDT3                  ; omezen¡ ‡¡sla
         cmp       al,dl                    ; p©ete‡en¡ ‡¡sla ?
         jbe       RozbDT4                  ; hodnota ‡¡sla je OK
RozbDT3: mov       al,dl                    ; omezen¡ ‡¡sla
         mov       ah,0

; ------ nastaven¡ zadan‚ho ‡¡sla

RozbDT4: push      cx
         mov       cl,dh                    ; po‡et rotac¡
         shl       ax,cl                    ; rotace ‡¡sla na pozici
         or        ds:[di],ax               ; ulo‘en¡ ‡¡sla
         not       bx                       ; maska pro nastaven¡
         or        ds:[di+2],bx             ; ‡¡slo zad no - povolen¡ masky
         pop       cx

; ------ vypu¨tˆn¡ oddˆlovac¡ho znaku za ‡¡slem

RozbDT5: call      RozbOdd                  ; vypu¨tˆn¡ oddˆlova‡e mezi ‡¡sly

         pop       bx
         pop       ax
         ret

RozbDT   ENDP

; -----------------------------------------------------------------------------
;        rozbor £daje sekund
; -----------------------------------------------------------------------------
; VSTUP: DS=datov˜ segment
;        DS:[DI+0] (2) parametr
;        DS:[DI+2] (2) maska parametru
; -----------------------------------------------------------------------------

RozbSek  PROC      NEAR

         push      ax
         push      bx

; ------ inicializace hodnoty

         mov       bx,not 31                ; maska
         and       ds:[di],bx               ; nulov n¡ hodnoty
         and       ds:[di+2],bx             ; nulov n¡ masky

; ------ dek¢dov n¡ ‡¡sla

         call      RozbPNum                 ; na‡ten¡ ‡¡sla
         jc        RozbSk5                  ; ‡¡slo nen¡ zad no

; ------ ‡¡slo zad no - jeho nastaven¡

         shr       ax,1                     ; korekce ‡¡sla / 2
         cmp       ax,31                    ; p©ete‡en¡ ‡¡sla ?
         jbe       RozbSk4                  ; hodnota ‡¡sla je OK
         mov       ax,31                    ; omezen¡ ‡¡sla
RozbSk4: or        ds:[di],ax               ; ulo‘en¡ ‡¡sla
         not       bx                       ; maska pro nastaven¡
         or        ds:[di+2],bx             ; povolen¡ masky

; ------ vypu¨tˆn¡ oddˆlovac¡ho znaku za ‡¡slem

RozbSk5: call      RozbOdd                  ; vypu¨tˆn¡ oddˆlova‡– mezi ‡¡sly

         pop       bx
         pop       ax
         ret

RozbSek  ENDP

; -----------------------------------------------------------------------------
;        rozbor zad n¡ £daje roku
; -----------------------------------------------------------------------------
; VSTUP: DS=datov˜ segment
;        DS:[DI+0] (2) parametr
;        DS:[DI+2] (2) maska parametru
; -----------------------------------------------------------------------------

RozbRok  PROC      NEAR

         push      ax
         push      bx

; ------ inicializace hodnoty

         mov       bx,not bit9+bit10+bit11+bit12+bit13+bit14+bit15 ; maska
         and       ds:[di],bx               ; nulov n¡ hodnoty
         and       ds:[di+2],bx             ; nulov n¡ masky

; ------ dek¢dov n¡ ‡¡sla

         call      RozbPNum                 ; na‡ten¡ ‡¡sla
         jc        RozbRok5                 ; ‡¡slo nen¡ zad no

; ------ oprava hodnoty ‡¡sla

         cmp       ax,80                    ; £daj < 80 bude asi 2000
         jae       RozbRok0
         add       ax,2000                  ; oprava na rok 20xx
RozbRok0:cmp       ax,100                   ; £daj < 100 bude asi offset od 1900
         jae       RozbRok1
         add       ax,1900                  ; asi offset od roku 1900
RozbRok1:cmp       ax,200                   ; £daj < 200 bude asi 2000
         jae       RozbRok2
         add       ax,2000
RozbRok2:cmp       ax,1000                  ; £daj < 1000 bude asi 1xxx
         jae       RozbRk22
         add       ax,1000
RozbRk22:cmp       ax,1980                  ; minim ln¡ rok mus¡ b˜t 1980
         jae       RozbRok3
         mov       ax,1980                  ; minim ln¡ rok
RozbRok3:sub       ax,1980
         cmp       ax,127
         jbe       RozbRok4
         mov       ax,127                   ; omezen¡ na maximum

; ------ ‡¡slo zad no - jeho nastaven¡

RozbRok4:push      cx
         mov       cl,9
         shl       ax,cl                    ; rotace ‡¡sla na pozici
         pop       cx
         or        ds:[di],ax               ; ulo‘en¡ ‡¡sla
         not       bx                       ; maska pro nastaven¡
         or        ds:[di+2],bx             ; povolen¡ masky

; ------ vypu¨tˆn¡ oddˆlovac¡ho znaku za ‡¡slem

RozbRok5:call      RozbOdd                  ; vypu¨tˆn¡ oddˆlova‡e za ‡¡slem

         pop       bx
         pop       ax
         ret

RozbRok  ENDP

; -----------------------------------------------------------------------------
;        rozbor zad n¡ velikosti
; -----------------------------------------------------------------------------

RozbPSiz PROC      NEAR

; ------ inicializace hodnot

         mov       di,offset SrcSiz1
         xor       ax,ax
         mov       ds:[di],ax
         mov       ds:[di+2],ax             ; po‡ te‡n¡ velikost
         dec       ax                       ; AX <- 0ffffh
         mov       ds:[di+4],ax
         mov       ds:[di+6],ax             ; koncov  velikost

; ------ rozbor po‡ te‡n¡ velikosti

         call      RozbDNum                 ; rozbor po‡ te‡n¡ velikosti
         jc        RozbPSz3                 ; po‡ te‡n¡ velikost nen¡ zad na
         mov       ds:[di],ax
         mov       ds:[di+2],dx             ; po‡ te‡n¡ velikost
         mov       ds:[di+4],ax
         mov       ds:[di+6],dx             ; bude i koncov  velikost

; ------ oddˆlovac¡ znam‚nko intervalu "-"

RozbPSz3:call      RozbInt                  ; test znaku intervalu
         jc        RozbPSz9                 ; nen¡ interval

; ------ rozbor koncov‚ velikosti

RozbPSz4:call      RozbDNum                 ; rozbor koncov‚ velikosti
         jnc       RozbPSz5                 ; koncov  velikost zad na
         mov       ax,0ffffh                ; jinak maximum
         mov       dx,ax
RozbPSz5:mov       ds:[di+4],ax
         mov       ds:[di+6],dx             ; koncov  velikost

RozbPSz9:ret

RozbPSiz ENDP

; -----------------------------------------------------------------------------
;        vypu¨tˆn¡ oddˆlova‡e mezi ‡¡sly (CY=nen¡ oddˆlova‡)
; -----------------------------------------------------------------------------

RozbOdd  PROC      NEAR

         call      RozbPCh                  ; na‡ten¡ dal¨¡ho znaku
         jc        RozbOdd9                 ; nen¡ dal¨¡ znak
         cmp       al,"."
         je        RozbOdd9                 ; te‡ka - plat¡
         cmp       al,","
         je        RozbOdd9                 ; ‡ rka - plat¡
         cmp       al,"/"
         je        RozbOdd9                 ; lom¡tko - plat¡
         cmp       al,":"
         je        RozbOdd9                 ; dvojte‡ka - plat¡
         dec       si                       ; jinak n vrat znaku
         inc       cx                       ; n vrat ‡¡ta‡e znak–
         stc                                ; p©¡znak chyby - nen¡ oddˆlova‡
RozbOdd9:ret

RozbOdd  ENDP

; -----------------------------------------------------------------------------
;        test zad n¡ znaku intervalu "-" (CY=nen¡)
; -----------------------------------------------------------------------------

RozbInt  PROC      NEAR

         call      RozbPCh                  ; na‡ten¡ dal¨¡ho znaku
         jc        RozbInt9                 ; nen¡ dal¨¡ znak
         cmp       al,"-"                   ; je oddˆlovac¡ znam‚nko "-" ?
         je        RozbInt9                 ; je oddˆlova‡ intervalu OK
         dec       si                       ; n vrat znaku
         inc       cx
         stc                                ; p©¡znak, ‘e nen¡ znaku intervalu
RozbInt9:ret

RozbInt  ENDP

; -----------------------------------------------------------------------------
;        na‡ten¡ dal¨¡ho znaku z bufferu p©i rozboru (CY=nen¡ dal¨¡ znak)
; -----------------------------------------------------------------------------

RozbPCh  PROC      NEAR

RozbPCh1:stc                                ; p©¡znak konce textu
         jcxz      RozbPCh6                 ; nen¡ dal¨¡ znak

         cld
         lodsb                              ; na‡ten¡ znaku
         dec       cx                       ; sn¡‘en¡ ‡¡ta‡e znak–
         cmp       al," "                   ; mezera ?
         je        RozbPCh1                 ; ignorov n¡ mezery
         cmp       al,"?"                   ; n hradn¡ znaky se tak‚ ignoruj¡
         je        RozbPCh1
         clc                                ; znak je OK

RozbPCh6:ret

RozbPCh  ENDP

; -----------------------------------------------------------------------------
;        na‡ten¡ ‡¡sla p©i rozboru AX (CY=nen¡ platn‚ ‡¡slo)
; -----------------------------------------------------------------------------

RozbPNum PROC      NEAR

         push      dx
         call      RozbDNum                 ; na‡ten¡ ‡¡sla DX:AX
         jc        RozbPNm2                 ; nen¡ platn‚ ‡¡slo
         or        dx,dx                    ; je ‡¡slo > 64 K ?
         jz        RozbPNm2                 ; nen¡ vˆt¨¡
         mov       ax,0ffffh                ; omezen¡ AX
RozbPNm2:pop       dx
         ret

RozbPNum ENDP

; -----------------------------------------------------------------------------
;        na‡ten¡ velk‚ho ‡¡sla p©i rozboru DX:AX (CY=nen¡ platn‚ ‡¡slo)
; -----------------------------------------------------------------------------

RozbDNum PROC      NEAR

; ------ £schova registr–

         push      bx
         push      di

; ------ inicializace st©ada‡e na 0

         xor       di,di                    ; HIGH
         xor       bx,bx                    ; LOW

; ------ na‡ten¡ prvn¡ ‡¡slice

         call      RozbPNm                  ; na‡ten¡ jedn‚ ‡¡slice
         jc        RozbDNm6                 ; nen¡ platn‚ ‡¡slo

; ------ vyn soben¡ st©ada‡e 10x

RozbDNm1:push      ax                       ; zadan‚ ‡¡slo
         mov       ax,10
         mul       bx                       ; posun st© dan‚ho ‡¡sla LOW o © d
         mov       bx,ax                    ; BX<-nov‚ LOW
         xchg      dx,di                    ; DI<-nov‚ HIGH, DX<-star‚ HIGH
         mov       ax,10
         mul       dx                       ; posun st© dan‚ho ‡¡sla HIGH o © d
         add       di,ax                    ; v˜sledn‚ nov‚ HIGH
         jc        RozbDN12                 ; p©ete‡en¡ ‡¡sla
         or        dx,dx                    ; je p©ete‡en¡ ?
         jz        RozbDNm2                 ; nen¡ p©ete‡en¡
RozbDN12:mov       bx,0ffffh                ; n hrada maxim ln¡ hodnotou
         mov       di,0ffffh
RozbDNm2:pop       ax                       ; zadan‚ ‡¡slo

; ------ p©i‡ten¡ nov‚ ‡¡slice ke st©ada‡i

         mov       ah,0
         add       bx,ax                    ; p©i‡ten¡ nov‚ ‡¡slice
         adc       di,0                     ; p©enos
         jnc       RozbDNm3                 ; nen¡ p©ete‡en¡
         mov       bx,0ffffh                ; p©ete‡en¡ £daje - omezen¡ na max.
         mov       di,0ffffh

; ------ na‡ten¡ dal¨¡ ‡¡slice

RozbDNm3:call      RozbPNm                  ; na‡ten¡ dal¨¡ ‡¡slice
         jnc       RozbDNm1                 ; je platn  ‡¡slice - jej¡ p©i‡ten¡
         clc                                ; p©¡znak operace OK

; ------ v˜sledn‚ na‡ten‚ ‡¡slo

RozbDNm6:mov       ax,bx                    ; ‡¡slo LOW
         mov       dx,di                    ; ‡¡slo HIGH

; ------ n vrat registr–

         pop       di
         pop       bx
         ret

RozbDNum ENDP

; -----------------------------------------------------------------------------
;        na‡ten¡ ‡¡slice p©i rozboru (CY=nen¡ platn  ‡¡slice)
; -----------------------------------------------------------------------------

RozbPNm  PROC      NEAR

; ------ na‡ten¡ jedn‚ ‡¡slice

         call      RozbPCh                  ; na‡ten¡ znaku
         jc        RozbPNm9                 ; nen¡ dal¨¡ znak

; ------ kontrola, zda je platn  ‡¡slice

         cmp       al,"0"
         jb        RozbPNm8                 ; nen¡ ‡¡slice
         cmp       al,"9"
         ja        RozbPNm8                 ; nen¡ ‡¡slice
         sub       al,"0"
         clc                                ; p©¡znak platn‚ho znaku
         ret                                ; platn  ‡¡slice

; ------ chyba - neplatn  ‡¡slice

RozbPNm8:dec       si                       ; n vrat neplatn‚ho znaku
         inc       cx
         stc                                ; p©¡znak - nen¡ platn˜ znak
RozbPNm9:ret

RozbPNm  ENDP


; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                     Obsluha informa‡n¡ho © dku
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;þ

; *****************************************************************************
;                             InfoClr
;                 vymaz n¡ bufferu informa‡n¡ho © dku
; -----------------------------------------------------------------------------
; VSTUP: DS=datov˜ segment
; *****************************************************************************

InfoClr  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      di
         push      es

; ------ vymaz n¡ bufferu informa‡n¡ho © dku

         push      ds
         pop       es
         mov       cx,80                    ; d‚lka © dku
         mov       di,offset InfoBuf        ; buffer informa‡n¡ho © dku
         mov       al," "                   ; mazac¡ znak mezery
         mov       ah,ds:[InfoCol1]         ; bˆ‘n  barva inform. © dku
         cld
         rep       stosw                    ; vymaz n¡ bufferu

; ------ inicializace promˆnn˜ch

         mov       ds:[InfoXCol],cl         ; 0 = p©¡znak bˆ‘n‚ barvy textu
         mov       word ptr ds:[InfoStor],offset InfoBuf+2 ; ukl dac¡ adresa
         mov       word ptr ds:[InfoKMax],cx ; maxim ln¡ velikost kurzoru
         mov       word ptr ds:[InfoKMax+2],cx
         mov       word ptr ds:[InfoKCit],cx ; ‡¡ta‡ velikosti kurzoru
         mov       word ptr ds:[InfoKCit+2],cx

; ------ n vrat registr–

         pop       es
         pop       di
         pop       cx
         pop       ax
         ret

InfoClr  ENDP

; *****************************************************************************
;                             InfoTxt
;                dek¢dov n¡ textu do informa‡n¡ho bufferu
; -----------------------------------------------------------------------------
; VSTUP: DS:SI=adresa definice textu (1.slovo - d‚lka textu + 2, text n sleduje)
;        DS=datov˜ segment
; *****************************************************************************

InfoTxt  PROC      NEAR

; ------ £schova registr–

         push      cx
         push      si
         push      es

; ------ dek¢dov n¡ textu

         mov       cx,ds:[si]               ; d‚lka textu + 2
         dec       cx
         dec       cx                       ; d‚lka textu
         inc       si
         inc       si                       ; za‡ tek textu
         push      ds
         pop       es                       ; ES <- DS
         call      InfoDek                  ; dek¢dov n¡ textu do bufferu

; ------ n vrat registr–

         pop       es
         pop       si
         pop       cx
         ret

InfoTxt  ENDP

; *****************************************************************************
;                            InfoDek
;             dek¢dov n¡ textu do informa‡n¡ho © dku
; -----------------------------------------------------------------------------
; VSTUP: ES:SI=adresa textu
;        CX=po‡et znak–
;        DS=datov˜ segment
; *****************************************************************************

InfoDek  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx
         push      si
         push      di

; ------ p©¡prava registr–

         jcxz      InfoDek9                 ; nen¡ ‘ dn˜ znak k dek¢dov n¡
         mov       di,ds:[InfoStor]         ; ukl dac¡ adresa do bufferu
         cld
         mov       dx,word ptr ds:[InfoCol1] ; barva textu
         mov       ah,dl                    ; bˆ‘n  barva textu
         cmp       byte ptr ds:[InfoXCol],0 ; je bˆ‘n  barva ?
         je        InfoDek1                 ; je bˆ‘n  barva textu
         xchg      ah,dh                    ; zmˆna barvy textu

; ------ dek¢dov n¡ textu

InfoDek1:mov       al,es:[si]               ; znak k dek¢dov n¡
         inc       si                       ; zv˜¨en¡ ‡tec¡ adresy
         cmp       al,0                     ; je zmˆna barvy ?
         jne       InfoDek2                 ; nen¡ zmˆna barvy - je platn˜ znak
         xchg      ah,dh                    ; zmˆna barvy
         not       byte ptr ds:[InfoXCol]   ; zmˆna p©¡znaku barvy
         jmp       short InfoDek3
InfoDek2:cmp       di,offset InfoBuf + 2*80 ; je buffer ji‘ pln˜ ?
         jae       InfoDek3                 ; buffer je ji‘ pln˜
         mov       ds:[di],ax               ; ulo‘en¡ znaku
         inc       di
         inc       di                       ; zv˜¨en¡ ukl dac¡ adresy
InfoDek3:loop      InfoDek1                 ; dek¢dov n¡ dal¨¡ho znaku
         mov       ds:[InfoStor],di         ; nov  ukl dac¡ adresa do bufferu

; ------ n vrat registr–

InfoDek9:pop       di
         pop       si
         pop       dx
         pop       cx
         pop       ax
         ret

InfoDek  ENDP

; *****************************************************************************
;                             InfoKurz
;                 zobrazen¡ informa‡n¡ho © dku s kurzorem
; -----------------------------------------------------------------------------
; VSTUP: CX=p©¡rustek informa‡n¡ho kurzoru
;        DS=datov˜ segment
; *****************************************************************************

InfoKurz PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      di
         push      es
         cld

; ------ zv˜¨en¡ ‡¡ta‡e

         mov       ax,word ptr ds:[InfoKCit] ; ‡¡ta‡ LOW
         mov       dx,word ptr ds:[InfoKCit+2] ; ‡¡ta‡ HIGH
         add       ax,cx                    ; zv˜¨en¡ ‡¡ta‡e LOW
         adc       dx,0                     ; p©enos do HIGH

; ------ kontrola p©ete‡en¡ ‡¡ta‡e

         mov       cx,word ptr ds:[InfoKMax] ; maximum LOW
         mov       bx,word ptr ds:[InfoKMax+2] ; maximum HIGH
         cmp       dx,bx                    ; p©ete‡en¡ ?
         jne       InfoKur1
         cmp       ax,cx
InfoKur1:jbe       InfoKur2                 ; nen¡ p©ete‡en¡
         mov       ax,cx                    ; omezen¡ na maximum
         mov       dx,bx
InfoKur2:mov       word ptr ds:[InfoKCit],ax
         mov       word ptr ds:[InfoKCit+2],dx ; ulo‘en¡ nov‚ho kurzoru

; ------ normalizace ukazatel– (aby byl z klad < 64 KB)

InfoKur3:or        bx,bx                    ; je z klad > 64 KB ?
         jz        InfoKur4                 ; nen¡ vˆt¨¡ ne‘ 64 KB
         shr       dx,1
         rcr       ax,1
         shr       bx,1
         rcr       cx,1
         jmp       short InfoKur3
InfoKur4:jcxz      InfoKur9                 ; dˆlitel = 0 (kurzor vypnut˜)

; ------ v˜po‡et d‚lky nov‚ho kurzoru

         mov       bx,InfoKSir              ; ¨¡©ka informa‡n¡ho kurzoru
         mul       bx                       ; ‡ st * ¨¡©ka kurzoru
         div       cx                       ; vydˆlen¡ celkovou ‡ st¡
         mov       cx,ax                    ; relativn¡ ¨¡©ka kurzoru
         mov       bx,InfoKSir              ; ¨¡©ka informa‡n¡ho kurzoru
         sub       bx,cx                    ; zbytek bez kurzoru

; ------ dek¢dov n¡ kurzoru

         push      ds
         pop       es                       ; ES <- DS
         mov       di,offset InfoBuf + 2*InfoKPoz - 2; adresa k dek¢dov n¡ kurzoru
         mov       ah,ds:[InfoCol1]         ; bˆ‘n  barva inform. © dku
         mov       al,16                    ; lev  ¨ipka
         stosw
         mov       al,178                   ; znak kurzoru
         rep       stosw                    ; ulo‘en¡ ‡ sti s kurzorem
         mov       cx,bx                    ; d‚lka zbytku
         mov       al,176                   ; znak bez kurzoru
         rep       stosw                    ; ulo‘en¡ ‡ sti bez kurzoru
         mov       al,17                    ; prav  ¨ipka
         stosw

; ------ n vrat registr–

InfoKur9:pop       es
         pop       di
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         call      InfoDisp                 ; zobrazen¡ informa‡n¡ho © dku
         ret

InfoKurz ENDP

; *****************************************************************************
;                            InfoDisp
;                   zobrazen¡ informa‡n¡ho © dku
; -----------------------------------------------------------------------------
; VSTUP: DS=datov˜ segment
; *****************************************************************************

InfoDisp PROC      NEAR

; ------ £schova registr–

         push      cx
         push      dx
         push      si
         push      es

; ------ zobrazen¡ informa‡n¡ho © dku

         test      byte ptr ds:[Param],bit7 ; je © dkov˜ re‘im ?
         jnz       InfoDis6                 ; je © dkov˜ re‘im
         push      ds
         pop       es
         mov       cx,80                    ; d‚lka informa‡n¡ho © dku
         mov       dl,0                     ; po‡ te‡n¡ pozice
         mov       dh,ds:[MaxRow]           ; posledn¡ © dek displeje
         mov       si,offset InfoBuf        ; buffer informa‡n¡ho © dku
         call      DispStr                  ; zobrazen¡ informa‡n¡ho © dku
         call      KurzOff                  ; vypnut¡ kurzoru

; ------ n vrat registr–

InfoDis6:pop       es
         pop       si
         pop       dx
         pop       cx
         ret

InfoDisp ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ hl ¨en¡ "€ekejte"
; -----------------------------------------------------------------------------

DispCek  PROC      NEAR

; ------ test, zda se hl ¨en¡ zobraz¡

         test      byte ptr ds:[Param],bit7 ; je © dkov˜ re‘im ?
         jz        DispCek9                 ; nen¡ © dkov˜ re‘im

; ------ zobrazen¡ hl ¨en¡

         push      si
         mov       si,offset CekTxt         ; text hl ¨en¡ "€ekejte"
         call      StdErr                   ; zobrazen¡ hl ¨en¡
         pop       si

DispCek9:ret

DispCek  ENDP

; -----------------------------------------------------------------------------
;        maz n¡ hl ¨en¡ "€ekejte"
; -----------------------------------------------------------------------------

ClrCek   PROC      NEAR

; ------ test, zda se hl ¨en¡ vyma‘e

         test      byte ptr ds:[Param],bit7 ; je © dkov˜ re‘im ?
         jz        ClrCek9                  ; nen¡ © dkov˜ re‘im

; ------ vymaz n¡ hl ¨en¡

         push      si
         mov       si,offset CekTxt0        ; text k vymaz n¡ hl ¨en¡ "€ekejte"
         call      StdErr                   ; vymaz n¡ hl ¨en¡
         pop       si

ClrCek9: ret

ClrCek   ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                       Obsluha vyhled v n¡ soubor–
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

;þ

; -----------------------------------------------------------------------------
;        p©i‡ten¡ souboru k ‡¡ta‡i soubor–
; -----------------------------------------------------------------------------

PridejF  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx

; ------ zv˜¨en¡ ‡¡ta‡e soubor–

         inc       word ptr ds:[CitSoub]    ; zv˜¨en¡ ‡¡ta‡e soubor–

; ------ p©i‡ten¡ velikosti k celkov‚ kapacitˆ

         mov       ax,word ptr ds:[DTASize] ; velikost LOW
         mov       dx,word ptr ds:[DTASize+2] ; velikost LOW
         add       word ptr ds:[CitCelk],ax ; ‡¡ta‡ celkov‚ kapacity LOW
         adc       word ptr ds:[CitCelk+2],dx ; ‡¡ta‡ celkov‚ kapacity HIGH

; ------ v˜po‡et obsazen‚ kapacity na disku

         mov       cx,ds:[DiskBlok]         ; po‡et bajt– na alok. blok
         jcxz      PridejF5                 ; nˆjak  chyba
         cmp       dx,cx                    ; je velikost OK ?
         jae       PridejF3                 ; chybn  velikost - nezaokrouhl¡ se
         dec       cx                       ; velikost bloku - 1
         add       ax,cx                    ; zaokrouhlen¡ na alok. blok
         adc       dx,0                     ; p©enos
         inc       cx                       ; velikost alok. bloku
         cmp       dx,cx                    ; je chybn  velikost ?
         jae       PridejF3                 ; chybn  velikost - nezaokrouhl¡ se
         div       cx                       ; v˜po‡et aloka‡n¡ch blok–
         mul       cx                       ; velikost se zaokrouhlen¡m
PridejF3:add       word ptr ds:[CitObs],ax  ; ‡¡ta‡ obsazen‚ kapacity LOW
         adc       word ptr ds:[CitObs+2],dx ; ‡¡ta‡ obsazen‚ kapacity HIGH

; ------ n vrat registr–

PridejF5:pop       dx
         pop       cx
         pop       ax
         ret

PridejF  ENDP

; *****************************************************************************
;                               HledFile
;                           nalezen¡ soubor–
; -----------------------------------------------------------------------------
; VSTUP: DS=datov˜ segment
; VSTUP:CY=p©eru¨en¡ hled n¡
; *****************************************************************************

HledFile PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      es

; ------ inicializace ukazatel–

         xor       ax,ax
         mov       word ptr ds:[CitSoub],ax ; ‡¡ta‡ nalezen˜ch soubor–
         mov       word ptr ds:[CitCelk],ax ; ‡¡ta‡ celkov‚ kapacity soubor– LOW
         mov       word ptr ds:[CitCelk+2],ax ; ‡¡ta‡ celkov‚ kapacity soub.HIGH
         mov       word ptr ds:[CitObs],ax ; ‡¡ta‡ obsazen‚ kapacity soubor– LOW
         mov       word ptr ds:[CitObs+2],ax ; ‡¡ta‡ obsaz. kapacity soub. HIGH
         mov       word ptr ds:[InsSumm],ax
         mov       word ptr ds:[InsSumm+2],ax ; sou‡et velikost¡ soubor–
         mov       ds:[InsNum],ax           ; po‡et ozna‡en˜ch soubor–
         mov       ds:[DispNum],ax          ; po‡et index– zobrazen˜ch soubor–
         mov       ds:[HledNum],ax          ; po‡et index– pro hled n¡
         mov       ds:[SeznNum],ax          ; velikost seznamu soubor–
         mov       ds:[SeznTop],ax          ; po‡ te‡n¡ © dek
         mov       ds:[SeznAkt],ax          ; aktivn¡ © dek
         test      byte ptr ds:[Param],bit7 ; je © dkov˜ re‘im ?
         jnz       HledFl02                 ; je © dkov˜ re‘im
         call      DispSezn

; ------ stanoven¡ velikosti aloka‡n¡ho bloku disku

HledFl02:mov       ah,36h
         mov       dl,ds:[FilePath]         ; ozna‡en¡ disku
         sub       dl,"A"-1                 ; ‡¡slo disku
         int       21h                      ; poskytnut¡ parametr– disku
         cmp       ax,0ffffh
         je        HledFil1                 ; neplatn˜ disk
         mul       cx                       ; velikost aloka‡n¡ho bloku
         or        dx,dx
         jnz       HledFil1
         or        ax,ax
         jnz       HledFl11
HledFil1:mov       ax,200h                  ; implicitn¡ velikost 1 sektor
HledFl11:mov       ds:[DiskBlok],ax         ; velikost aloka‡n¡ho bloku

; ------ na‡ten¡ obsahu v˜choz¡ho adres ©e

         call      ReadDir                  ; na‡ten¡ v˜choz¡ho adres ©e
         call      SortDis                  ; t©¡dˆn¡ pro zobrazen¡
         call      SortHled                 ; t©¡dˆn¡ k vyhled v n¡
         test      byte ptr ds:[Param],bit7 ; je © dkov˜ re‘im ?
         jz        HledFl14                 ; nen¡ © dkov˜ re‘im
         call      ListSezn                 ; v˜pis v © dkov‚m re‘imu
         jmp       short HledFil2
HledFl14:call      DispSezn                 ; zobrazen¡ seznamu

; ------ test p©eru¨en¡ programu

HledFil2:call      TestKey                  ; test kl vesnice
         jz        HledFl22
HledFl21:call      InpChr                   ; vstup znaku z kl vesnice
         call      Sezn                     ; obsluha seznamu
         cmp       bl,27
         stc                                ; p©¡znak p©eru¨en¡ hled n¡
         je        HledFil9                 ; je p©eru¨en¡ hled n¡
         jmp       short HledFil2

; ------ test, zda jsou nˆjak‚ dal¨¡ adres ©e

HledFl22:mov       si,ds:[HledNum]          ; po‡et index– pro hled n¡
         or        si,si                    ; je dal¨¡ adres © k hled n¡ ?
         jz        HledFil9                 ; nen¡ dal¨¡ adres © - konec
         test      byte ptr ds:[Param2],bit0 ; adres ©e povoleny ?
         jz        HledFil9                 ; nejsou podadres ©e
         dec       si                       ; sn¡‘en¡ ukazatele
         mov       ds:[HledNum],si          ; nov˜ po‡et index– pro hled n¡

; ------ adresa ©etˆzce v bufferu

         shl       si,1                     ; offset v bufferu index–
         mov       si,ds:[si+HledIndx]      ; adresa ©etˆzce v bufferu
         mov       es,ds:[SeznSegm]         ; adresa bufferu

; ------ inicializace, je-li specifikace cel‚ cesty

         test      byte ptr es:[si],bit6    ; je to cesta ?
         jz        HledFil4                 ; nen¡ to cesta
         mov       di,offset FilePath       ; buffer k na‡ten¡ cesty
HledFil3:inc       si                       ; ‡tec¡ adresa
         mov       al,es:[si]               ; znak cesty k p©enesen¡
         mov       ds:[di],al               ; ulo‘en¡ znaku
         inc       di                       ; ukl dac¡ adresa
         cmp       al,0                     ; je konec textu ?
         jne       HledFil3                 ; p©enesen¡ textu ASCIIZ
         sub       di,offset FilePath+1     ; offset konce v bufferu
         mov       ds:[FilePthN],di         ; d‚lka textu cesty
         jmp       short HledFil2           ; dal¨¡ adres ©

; ------ p©id n¡ nov‚ho adres ©e k aktivn¡ cestˆ

HledFil4:add       si,9                     ; za‡ tek jm‚na adres ©e
         call      AddGFil                  ; p©id n¡ jm‚na adres ©e
         jc        HledFil2                 ; chyba p©ete‡en¡

; ------ na‡ten¡ obsahu nov‚ho adres ©e

         call      ReadDir                  ; na‡ten¡ v˜choz¡ho adres ©e
         call      SortDis                  ; t©¡dˆn¡ pro zobrazen¡
         call      SortHled                 ; t©¡dˆn¡ k vyhled v n¡
         test      byte ptr ds:[Param],bit7 ; je © dkov˜ re‘im ?
         jz        HledFil5                 ; nen¡ © dkov˜ re‘im
         call      ListSezn                 ; v˜pis v © dkov‚m re‘imu
         jmp       short HledFil6
HledFil5:call      DispSezn                 ; zobrazen¡ seznamu

; ------ zru¨en¡ na‡¡tan‚ho adres ©e

HledFil6:call      SubGFil                  ; zru¨en¡ na‡¡tan‚ho adres ©e
         jmp       short HledFil2           ; dal¨¡ podadres ©

; ------ n vrat registr–

HledFil9:pop       es
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

HledFile ENDP

; *****************************************************************************
;                                 SortDis
;                   set©¡dˆn¡ adres ©e - zobrazen‚ soubory
; -----------------------------------------------------------------------------
; VSTUP: DS=datov˜ segment
; *****************************************************************************

SortDis  PROC      NEAR

; ------ test, zda je v adres ©i nˆco k set©¡dˆn¡

         push      cx
         mov       cx,ds:[DispNum]          ; po‡et index– v adres ©i
         sub       cx,ds:[DispNumO]         ; po‡et nov˜ch index– v adres ©i
         sub       cx,2                     ; po‡et p r– k set©¡dˆn¡
         jbe       SortDis9                 ; nen¡ nic k set©¡dˆn¡

; ------ £schova registr–

         push      ax
         push      bx
         push      dx
         push      si
         push      di
         push      es

; ------ p©¡prava registr–

         mov       es,ds:[SeznSegm]         ; adresa seznamu
         mov       bx,ds:[DispNumO]         ; ‡¡slo prvn¡ho indexu
         shl       bx,1                     ; offset v tabulce index–
         add       bx,offset DispIndx+2     ; adresa prvn¡ho indexu
         mov       dx,bx                    ; £schova adresy prvn¡ho indexu

; ------ adresy soubor– k porovn n¡

SortDis3:mov       si,ds:[bx]               ; adresa prvn¡ho souboru
         mov       di,ds:[bx+2]             ; adresa druh‚ho souboru

; ------ porovn n¡ atribut– soubor–

         mov       al,es:[si]               ; atributy prvn¡ho souboru
         mov       ah,es:[di]               ; atributy druh‚ho souboru
         and       ax,DIR*HI + DIR          ; atributy adres ©–
         cmp       al,ah                    ; souhlas¡ ?
         ja        SortDis7                 ; adres © p©ed souborem = OK
         jb        SortDis5                 ; soubor p©ed adres ©em = z mˆna
         add       si,9                     ; jm‚no prvn¡ho souboru
         add       di,9                     ; jm‚no druh‚ho souboru

; ------ na‡ten¡ znak– jmen

SortDis4:mov       al,es:[si]               ; znak prvn¡ho jm‚na
         mov       ah,es:[di]               ; znak druh‚ho jm‚na

; ------ porovn n¡, zda je shoda znak–

         cmp       al,ah                    ; jsou znaky shodn‚ ?
         je        SortDis6                 ; znaky jsou shodn‚

; ------ test, zda je prvn¡ jm‚no krat¨¡ ne‘ druh‚

;         cmp       al,0                     ; prvn¡ jm‚no je krat¨¡ ?
;         je        SortDis7                 ; je krat¨¡ - to je OK
         cmp       al,"."                   ; je krat¨¡ ?
         je        SortDis7                 ; je krat¨¡ - to je OK

; ------ test, zda je prvn¡ jm‚no men¨¡ ne‘ druh‚

         cmp       al,ah                    ; je prvn¡ jm‚no men¨¡ ?
         jb        SortDis7                 ; je men¨¡ - to je OK

; ------ prvn¡ jm‚no je vˆt¨¡ ne‘ druh‚ - z mˆna index–

SortDis5:mov       ax,ds:[bx]
         xchg      ax,ds:[bx+2]             ; z mˆna index–
         mov       ds:[bx],ax

; ------ posun ukazatele smˆrem dol–

         cmp       bx,dx                    ; je ji‘ prvn¡ polo‘ka ?
         jbe       SortDis7                 ; je ji‘ prvn¡ polo‘ka - dal¨¡ soub.
         dec       bx
         dec       bx                       ; posun k p©ede¨l‚ dvojici
         inc       cx                       ; n vrat ‡¡ta‡e soubor–
         jmp       short SortDis3           ; porovn n¡ p©ede¨l‚ polo‘ky

; ------ znaky jsou shodn‚ - zv˜¨en¡ ukazatele a test konce jm‚na

SortDis6:inc       si
         inc       di
         cmp       al,0                     ; konec jm‚na ?
         jne       SortDis4                 ; nen¡ konec - dal¨¡ znak

; ------ soubory jsou set©¡dˆn‚ OK - p©¡prava k dal¨¡mu souboru

SortDis7:inc       bx
         inc       bx                       ; zv˜¨en¡ adresy v tabulce index–
         loop      SortDis3                 ; porovn n¡ dal¨¡ho souboru

; ------ n vrat registr–

SortDis8:pop       es
         pop       di
         pop       si
         pop       dx
         pop       bx
         pop       ax
SortDis9:pop       cx
         ret

SortDis  ENDP

; *****************************************************************************
;                                 SortHled
;                   set©¡dˆn¡ adres ©– k prohled v n¡
; -----------------------------------------------------------------------------
; VSTUP: DS=datov˜ segment
; *****************************************************************************

SortHled PROC      NEAR

; ------ test, zda je v adres ©i nˆco k set©¡dˆn¡

         push      cx
         mov       cx,ds:[HledNum]          ; po‡et index– v adres ©i
         sub       cx,ds:[HledNumO]         ; po‡et nov˜ch index– v adres ©i
         shr       cx,1                     ; po‡et dvojslov
         sub       cx,1                     ; po‡et p r– k set©¡dˆn¡
         jbe       SortHld9                 ; nen¡ nic k set©¡dˆn¡

; ------ £schova registr–

         push      ax
         push      bx
         push      dx
         push      si
         push      di
         push      es

; ------ p©¡prava registr–

         mov       es,ds:[SeznSegm]         ; adresa seznamu
         mov       bx,ds:[HledNumO]         ; ‡¡slo prvn¡ho indexu
         shl       bx,1                     ; offset v tabulce index–
         add       bx,offset HledIndx       ; adresa prvn¡ho indexu
         mov       dx,bx                    ; £schova adresy prvn¡ho indexu

; ------ adresy soubor– k porovn n¡

SortHld3:mov       si,ds:[bx]               ; adresa prvn¡ho souboru
         add       si,9                     ; jm‚no prvn¡ho souboru
         mov       di,ds:[bx+4]             ; adresa druh‚ho souboru
         add       di,9                     ; jm‚no druh‚ho souboru

; ------ na‡ten¡ znak– jmen

SortHld4:mov       al,es:[si]               ; znak prvn¡ho jm‚na
         mov       ah,es:[di]               ; znak druh‚ho jm‚na

; ------ porovn n¡, zda je shoda znak–

         cmp       al,ah                    ; jsou znaky shodn‚ ?
         je        SortHld6                 ; znaky jsou shodn‚

; ------ test, zda je druh‚ jm‚no krat¨¡ ne‘ druh‚

         cmp       ah,"."                   ; je krat¨¡ ?
         je        SortHld7                 ; je krat¨¡ - to je OK

; ------ test, zda je druh‚ jm‚no men¨¡ ne‘ druh‚

         cmp       ah,al                    ; je druh‚ jm‚no men¨¡ ?
         jb        SortHld7                 ; je men¨¡ - to je OK

; ------ druh‚ jm‚no je vˆt¨¡ ne‘ druh‚ - z mˆna index–

SortHld5:mov       ax,ds:[bx]
         xchg      ax,ds:[bx+4]             ; z mˆna index–
         mov       ds:[bx],ax

; ------ posun ukazatele smˆrem dol–

         cmp       bx,dx                    ; je ji‘ prvn¡ polo‘ka ?
         jbe       SortHld7                 ; je ji‘ prvn¡ polo‘ka - dal¨¡ soub.
         sub       bx,4                     ; posun k p©ede¨l‚ dvojici
         inc       cx                       ; n vrat ‡¡ta‡e soubor–
         jmp       short SortHld3           ; porovn n¡ p©ede¨l‚ polo‘ky

; ------ znaky jsou shodn‚ - zv˜¨en¡ ukazatele a test konce jm‚na

SortHld6:inc       si
         inc       di
         cmp       al,0                     ; konec jm‚na ?
         jne       SortHld4                 ; nen¡ konec - dal¨¡ znak

; ------ soubory jsou set©¡dˆn‚ OK - p©¡prava k dal¨¡mu souboru

SortHld7:add       bx,4                     ; zv˜¨en¡ adresy v tabulce index–
         loop      SortHld3                 ; porovn n¡ dal¨¡ho souboru

; ------ n vrat registr–

SortHld8:pop       es
         pop       di
         pop       si
         pop       dx
         pop       bx
         pop       ax
SortHld9:pop       cx
         ret

SortHled ENDP

; *****************************************************************************
;                                ReadDir
;                    na‡ten¡ obsahu adres ©e FILEPATH
; -----------------------------------------------------------------------------
; VSTUP: DS=datov˜ segment
; *****************************************************************************

ReadDir  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         push      es

; ------ zobrazen¡ informa‡n¡ho textu

         push      ds
         pop       es
         call      InfoClr                  ; vymaz n¡ informa‡n¡ho bufferu
         mov       si,offset HledTxt1       ; informa‡n¡ text
         call      InfoTxt                  ; dek¢dov n¡ textu
         mov       si,offset FilePath       ; aktivn¡ cesta
         mov       cx,ds:[FilePthN]         ; d‚lka textu cesty
         call      InfoDek                  ; dek¢dov n¡ jm‚na adres ©e
         call      InfoDisp                 ; zobrazen¡ informa‡n¡ho © dku

; ------ p©¡prava ukazatel–

         mov       ax,ds:[DispNum]
         mov       ds:[DispNumO],ax
         mov       ax,ds:[HledNum]
         mov       ds:[HledNumO],ax
         mov       ax,ds:[SeznNum]
         mov       ds:[SeznNumO],ax

; ------ nastaven¡ pracovn¡ adresy DTA

         mov       si,offset DTA            ; buffer DTA
         call      SetDTA                   ; nastaven¡ pracovn¡ adresy DTA

; ------ p©¡prava specifikace soubor–

         mov       si,offset All            ; specifikace "*.*"
         call      AddGFil                  ; p©id n¡ specifikace souboru
         jnc       ReadDir1                 ; OK
         jmp       ReadDir9                 ; nˆjak  chyba

; ------ nalezen¡ prvn¡ho souboru

ReadDir1:mov       si,offset FilePath       ; specifikace souboru
         mov       cx,RO + HID + SYS + ARC + DIR ; atributy pro hled n¡
         call      SrcFirst                 ; nalezen¡ prvn¡ polo‘ky adres ©e

; ------ odstranˆn¡ textu "*.*" z textu cesty

         pushf
         call      SubGFil                  ; odstranˆn¡ specifikace "*.*"
         popf
         jmp       short ReadDir3           ; zhodnocen¡ v˜sledku operace

; ------ ovl d n¡ seznamu

ReadDir2:call      TestKey
         jz        ReadDr23                 ; nen¡ kl vesa
         or        ax,ax
         jz        ReadDr21
         cmp       al,27
         jne       ReadDr22
ReadDr21:jmp       ReadDir9
ReadDr22:call      InpChr                   ; vstup znaku z kl vesnice
         call      Sezn                     ; obsluha seznamu
         jmp       short ReadDir2

; ------ nalezen¡ dal¨¡ho souboru

ReadDr23:call      SrcNext                  ; nalezen¡ dal¨¡ho souboru

; ------ zhodnocen¡ v˜sledku operace hled n¡

ReadDir3:jc        ReadD300                 ; byla nˆjak  chyba
         je        ReadDr30                 ; soubor nalezen OK
ReadD300:jmp       ReadDir7

; ------ adres ©e ".." a "." neplat¡ - p©esko‡¡ se

ReadDr30:cmp       word ptr ds:[DTAName],".." ; je to adres © ".." ?
         je        ReadDir2                 ; neplatn˜ adres © - dal¨¡
         cmp       word ptr ds:[DTAName],"." ; je to adres © "." ?
         je        ReadDir2                 ; neplatn˜ adres © - dal¨¡
         and       byte ptr ds:[DTAAtrib],not bit7+bit6 ; bit6 bude v‘dy = 0

; ------ p©evod jm‚na souboru na tvar FCB

         call      GetBFCB                  ; p©evod jm‚na souboru na FCB

; ------ kontrola jm‚na souboru, zda odpov¡d  po‘adavk–m

;         mov       si,offset FileFCB        ; jm‚no souboru ve tvaru FCB
;         mov       di,offset FileMask       ; maska jm‚na souboru
;         mov       cx,ds:[FileMskN]         ; d‚lka masky jm‚na souboru
         call      TestFile                 ; test jm‚na souboru podle masky
         jc        ReadDr31                 ; jm‚no souboru neodpov¡d 

; ------ kontrola ostatn¡ch parametr– souboru

         call      GetBTest                 ; kontrola ostatn¡ch parametr–
         jc        ReadDr31                 ; parametry neodpov¡daj¡
         call      TestText                 ; test, zda obsahuje hledan˜ text
         jnc       ReadDr32                 ; polo‘ka plnˆ vyhovuje OK

; ------ polo‘ka neodpov¡d  - test, zda je alespo¤ adres ©

ReadDr31:test      byte ptr ds:[DTAAtrib],DIR ; je to adres © ?
         jz        ReadDir2                 ; nen¡ adres © - neulo‘¡ se
         stc                                ; p©¡znak, ‘e polo‘ka nevyhovuje

; ------ zaji¨tˆn¡ ulo‘en¡ aktivn¡ cesty

ReadDr32:lahf                               ; £schova p©¡znaku CF
         mov       si,ds:[SeznNum]
         cmp       si,ds:[SeznNumO]         ; je cesta ji‘ ulo‘ena ?
         jne       ReadDr33                 ; cesta je ji‘ ulo‘ena
         call      UlozPath                 ; ulo‘en¡ cesty do seznamu
         jc        ReadDir2                 ; chyba p©ete‡en¡

; ------ ulo‘en¡ specifikace souboru do seznamu

ReadDr33:mov       bx,ds:[SeznNum]          ; £schova adresy souboru
         call      UlozFile                 ; ulo‘en¡ souboru do seznamu
         jc        ReadDir2                 ; chyba p©ete‡en¡ bufferu seznamu

; ------ test, zda je polo‘ka platn 

         test      ah,bit0                  ; byl p©¡znak CY ?
         jnz       ReadDr45                 ; byl p©¡znak CY - polo‘ka neplatn 

; ------ test, zda byl ji‘ ulo‘en index na cestu

         mov       si,ds:[DispNum]          ; po‡et index–
         cmp       si,ds:[DispNumO]         ; byl ji‘ index na cestu ?
         jne       ReadDr42                 ; byl ji‘ index na cestu

; ------ ulo‘en¡ indexu na cestu

         cmp       si,INDXSIZE              ; je buffer ji‘ pln˜ ?
         jae       ReadDr45                 ; buffer je ji‘ pln˜
         mov       ax,ds:[SeznNumO]         ; adresa cesty
         shl       si,1                     ; offset v bufferu
         mov       ds:[DispIndx+si],ax      ; ulo‘en¡ indexu na cestu
         inc       word ptr ds:[DispNum]    ; zv˜¨en¡ ‡¡ta‡e index–

; ------ ulo‘en¡ indexu na soubor

ReadDr42:mov       si,ds:[DispNum]          ; po‡et index–
         cmp       si,INDXSIZE              ; je buffer ji‘ pln˜ ?
         jae       ReadDr45                 ; buffer je ji‘ pln˜
         shl       si,1                     ; offset v bufferu
         mov       ds:[DispIndx+si],bx      ; ulo‘en¡ indexu na soubor
         inc       word ptr ds:[DispNum]    ; zv˜¨en¡ ‡¡ta‡e index–
         call      PridejF                  ; p©id n¡ souboru ke st©ada‡i

; ------ test, zda je polo‘ka adres ©

ReadDr45:test      byte ptr ds:[DTAAtrib],DIR ; je to adres © ?
         jz        ReadDr57                 ; nen¡ to adres ©

; ------ ulo‘en¡ indexu na adres ©

ReadDir5:mov       si,ds:[HledNum]          ; po‡et index– pro hled n¡
         cmp       si,INDXSIZE-1            ; je buffer ji‘ pln˜ ?
         jae       ReadDr57                 ; buffer je ji‘ pln˜
         test      byte ptr ds:[Param2],bit0 ; adres ©e povoleny ?
         jz        ReadDr57                 ; nejsou podadres ©e
         shl       si,1                     ; offset v bufferu
         mov       ds:[HledIndx+si],bx      ; ulo‘en¡ indexu na adres ©
         mov       ax,ds:[SeznNumO]         ; adresa cesty
         mov       ds:[HledIndx+si+2],ax    ; ulo‘en¡ indexu na cestu
         add       word ptr ds:[HledNum],2  ; zv˜¨en¡ ‡¡ta‡e index–

ReadDr57:jmp       ReadDir2                 ; hled n¡ dal¨¡ polo‘ky

; ------ oprava kurzoru, pokud sleduje hled n¡

ReadDir7:cmp       word ptr ds:[DispNum],0  ; je nˆco ?
         je        ReadDir9                 ; nen¡ nic
         mov       ax,ds:[SeznAkt]          ; aktivn¡ polo‘ka
         mov       bx,ds:[DispNumO]         ; star˜ po‡et polo‘ek
         or        bx,bx                    ; bylo nˆco ?
         jz        ReadDr72                 ; nebylo nic
         dec       bx                       ; ‡¡slo posledn¡ polo‘ky
         cmp       ax,bx                    ; byl na posledn¡ polo‘ce ?
         jne       ReadDir9                 ; nebyl na posledn¡ polo‘ce
ReadDr72:mov       ax,ds:[DispNum]          ; nov˜ po‡et polo‘ek
         dec       ax                       ; ‡¡slo posledn¡ polo‘ky
         mov       ds:[SeznAkt],ax          ; nov  aktivn¡ polo‘ka
         call      NormSezn                 ; normalizace seznamu

; ------ n vrat registr–

ReadDir9:pop       es
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

ReadDir  ENDP

; -----------------------------------------------------------------------------
;        ulo‘en¡ cesty do seznamu
; -----------------------------------------------------------------------------

UlozPath PROC      NEAR

; ------ £schova registr–

         push      cx
         push      si
         push      di
         push      es

; ------ p©¡prava registr–

         mov       di,ds:[SeznNum]          ; adresa konce seznamu
         mov       cx,ds:[FilePthN]         ; d‚lka textu adres ©e
         inc       cx                       ; + 1 bajt na identifik tor 0FFh
         inc       cx                       ; + 1 bajt na koncovou 0
         mov       es,ds:[SeznSegm]         ; adresa seznamu

; ------ kontrola p©ete‡en¡ bufferu

         mov       si,di                    ; za‡ tek k ulo‘en¡ cesty
         add       si,cx                    ; kontrola p©ete‡en¡ konce
         jc        UlozPth9                 ; p©ete‡en¡ bufferu
         cmp       ds:[SeznSize],si
         jb        UlozPth9

; ------ p©enos textu cesty do bufferu seznamu

         mov       byte ptr es:[di],bit6   ; identifik tor cesty
         inc       di                       ; zv˜¨en¡ ukazatele v bufferu
         mov       si,offset FilePath       ; text cesty
         dec       cx                       ; n vrat d‚lky cesty
         shr       cx,1                     ; d‚lka ve slovech
         rep       movsw                    ; p©enos textu po slovech
         adc       cx,cx                    ; lich˜ bajt
         rep       movsb                    ; p©enos lich‚ho bajtu
         mov       ds:[SeznNum],di          ; nov˜ konec dat v bufferu
                                            ; zde je p©¡znak NC

; ------ n vrat registr–

UlozPth9:pop       es
         pop       di
         pop       si
         pop       cx
         ret

UlozPath ENDP


; -----------------------------------------------------------------------------
;        ulo‘en¡ nalezen‚ho souboru do seznamu
; -----------------------------------------------------------------------------

UlozFile PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      si
         push      di
         push      es

; ------ stanoven¡ d‚lky jm‚na souboru

         push      ds
         pop       es                       ; ES <- datov˜ segment
         mov       di,offset DTAName        ; jm‚no souboru
         mov       cx,0ffffh-10+2           ; max. d‚lka - korekce
         mov       al,0                     ; hledan˜ bajt
         cld
         repne     scasb                    ; nalezen¡ konce textu
         neg       cx                       ; d‚lka jm‚na + 10

; ------ p©¡prava registr–

         mov       di,ds:[SeznNum]          ; adresa konce seznamu
         mov       es,ds:[SeznSegm]         ; adresa seznamu

; ------ kontrola p©ete‡en¡ bufferu

         mov       si,di                    ; za‡ tek k ulo‘en¡ cesty
         add       si,cx                    ; kontrola p©ete‡en¡ konce
         jc        UlozFil9                 ; p©ete‡en¡ bufferu
         cmp       ds:[SeznSize],si
         jb        UlozFil9

; ------ p©enos souboru do bufferu seznamu

         mov       si,offset DTAAtrib       ; za‡ tek parametr–
         shr       cx,1                     ; d‚lka ve slovech
         rep       movsw                    ; p©enos textu po slovech
         adc       cx,cx                    ; lich˜ bajt
         rep       movsb                    ; p©enos lich‚ho bajtu
         mov       ds:[SeznNum],di          ; nov˜ konec dat v bufferu
                                            ; zde je p©¡znak NC

; ------ n vrat registr–

UlozFil9:pop       es
         pop       di
         pop       si
         pop       cx
         pop       ax
         ret

UlozFile ENDP

; -----------------------------------------------------------------------------
;        p©evod jm‚na souboru na tvar FCB
; -----------------------------------------------------------------------------
; VSTUP: DS,ES=datov˜ segment
; -----------------------------------------------------------------------------

GetBFCB  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      si
         push      di
         push      cx

; ------ inicializa‡n¡ vymaz n¡ polo‘ky

         mov       di,offset FileFCB
         push      di
         cld
         mov       cx,11
         mov       al," "
         rep       stosb
         pop       di

; ------ dek¢dov n¡ jm‚na polo‘ky

         mov       si,offset DTAName        ; jm‚no souboru
         mov       cl,8
GetBFCB2:lodsb
         cmp       al,0
         je        GetBFCB4                 ; konec jm‚na
         cmp       al,"."
         jne       GetBFCB3
         mov       di,offset FileFCB+8
         mov       cl,3
         jmp       short GetBFCB2
GetBFCB3:jcxz      GetBFCB2
         stosb
         dec       cx
         jmp       short GetBFCB2

; ------ n vrat registr–

GetBFCB4:pop       cx
         pop       di
         pop       si
         pop       ax
         ret

GetBFCB  ENDP

; -----------------------------------------------------------------------------
;        kontrola ostatn¡ch parametr– souboru (CY=neodpov¡d )
; -----------------------------------------------------------------------------
; VSTUP: DS=datov˜ segment
; -----------------------------------------------------------------------------

GetBTest PROC      NEAR

         push      ax

; ------ kontrola atribut– souboru

         mov       al,ds:[DTAAtrib]         ; atributy souboru
         and       al,ds:[SrcAtrM]          ; maskov n¡ atribut–
         cmp       al,ds:[SrcAtr]           ; odpov¡daj¡ atributy ?
         jne       GetBTst8                 ; atributy neodpov¡daj¡

; ------ kontrola minim ln¡ho ‡asu souboru

         mov       ax,ds:[DTATime]          ; ‡as souboru
         and       ax,ds:[SrcTim1M]         ; maskov n¡ ‡asu
         cmp       ax,ds:[SrcTim1]          ; kontrola ‡asu
         jb        GetBTst8                 ; ‡as neodpov¡d 

; ------ kontrola maxim ln¡ho ‡asu souboru

         mov       ax,ds:[DTATime]          ; ‡as souboru
         and       ax,ds:[SrcTim2M]         ; maskov n¡ ‡asu
         cmp       ax,ds:[SrcTim2]          ; kontrola ‡asu
         ja        GetBTst8                 ; ‡as neodpov¡d 

; ------ kontrola minim ln¡ho data souboru

         mov       ax,ds:[DTADate]          ; datum souboru
         and       ax,ds:[SrcDat1M]         ; maskov n¡ data
         cmp       ax,ds:[SrcDat1]          ; kontrola data
         jb        GetBTst8                 ; datum neodpov¡d 

; ------ kontrola maxim ln¡ho data souboru

         mov       ax,ds:[DTADate]          ; datum souboru
         and       ax,ds:[SrcDat2M]         ; maskov n¡ data
         cmp       ax,ds:[SrcDat2]          ; kontrola data
         ja        GetBTst8                 ; datum neodpov¡d 

; ------ kontrola maxim ln¡ velikosti

         mov       ax,word ptr ds:[DTASize+2] ; velikost souboru HIGH
         cmp       ax,word ptr ds:[SrcSiz2+2] ; kontrola maxim ln¡ velikosti HIGH
         jne       GetBTst2                 ; li¨¡ se ve vy¨¨¡m slovu
         mov       ax,word ptr ds:[DTASize] ; velikost souboru LOW
         cmp       ax,word ptr ds:[SrcSiz2] ; kontrola maxim ln¡ velikosti LOW
GetBTst2:ja        GetBTst8                 ; velikost neodpov¡d 

; ------ kontrola minim ln¡ velikosti

         mov       ax,word ptr ds:[DTASize+2] ; velikost souboru HIGH
         cmp       ax,word ptr ds:[SrcSiz1+2] ; kontrola minim. velikosti HIGH
         jne       GetBTst3                 ; je rozd¡l ve vy¨¨¡m slovu
         mov       ax,word ptr ds:[DTASize] ; velikost souboru LOW
         cmp       ax,word ptr ds:[SrcSiz1] ; kontrola minim ln¡ velikosti LOW
GetBTst3:jae       GetBTst9                 ; velikost je OK

GetBTst8:stc
GetBTst9:pop       ax
         ret

GetBTest ENDP

; *****************************************************************************
;                            TestText
;               test, zda soubor obsahuje hledan˜ text
; -----------------------------------------------------------------------------
; VSTUP: DS=datov˜ segment
; VSUTP:CY=neobsahuje hledan˜ text
; *****************************************************************************

TestText PROC      NEAR

; ------ test, zda se text kontroluje

         test      byte ptr ds:[Param],bit2 ; hled  se text ?
         jz        TestTxt1                 ; text se nehled 
         cmp       word ptr ds:[SrcTextN],0 ; je nˆjak˜ text ?
         jz        TestTxt1                 ; nen¡ text
         test      byte ptr ds:[DTAAtrib],DIR ; je to adres © ?
         jz        TestTxt2                 ; nen¡ to adres © - OK
         stc                                ; adres © nevyhovuje
TestTxt1:ret

; ------ £schova registr–

TestTxt2:push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         push      es

; ------ p©id n¡ jm‚na souboru k cestˆ

         push      ds
         pop       es                       ; ES <- DS
         mov       si,offset DTAName        ; jm‚no nalezen‚ho souboru
         call      AddGFil                  ; p©id n¡ jm‚na souboru
         jc        TestTx99                 ; nˆjak  chyba

; ------ zobrazen¡ informa‡n¡ho textu

         call      InfoClr                  ; vymaz n¡ informa‡n¡ho bufferu
         mov       si,offset HledTxt1       ; informa‡n¡ text
         call      InfoTxt                  ; dek¢dov n¡ textu
         mov       si,offset FilePath       ; aktivn¡ cesta
         mov       cx,ds:[FilePthN]         ; d‚lka textu cesty
         call      InfoDek                  ; dek¢dov n¡ jm‚na adres ©e
         call      InfoDisp                 ; zobrazen¡ informa‡n¡ho © dku

; ------ otev©en¡ souboru pro ‡ten¡

         mov       dx,offset FilePath       ; jm‚no souboru
         mov       ax,3d00h
         int       21h                      ; otev©en¡ souboru
         mov       bx,ax                    ; identifik tor souboru

; ------ odstranˆn¡ jm‚na souboru z cesty

         pushf
         call      SubGFil                  ; odstranˆn¡ jm‚na souboru
         popf
TestTx99:jc        TestTxt9                 ; chyba otev©en¡ souboru

; ------ na‡ten¡ prvn¡ho bloku dat do bufferu

         mov       bp,ds:[SrcTextN]         ; velikost dat k hled n¡
         mov       cx,SRCBMAX               ; velikost bufferu hled n¡
         mov       dx,offset SrcBuff        ; buffer k na‡ten¡ souboru
TestTxt3:mov       ah,3fh
         int       21h                      ; na‡ten¡ bloku ze souboru
         jc        TestTxt8                 ; chyba ‡ten¡ - konec souboru
         mov       cx,ax                    ; velikost na‡ten˜ch dat
         stc                                ; p©¡padn˜ p©¡znak nenalezen¡ textu
         jcxz      TestTxt8                 ; je konec souboru - nic nenalezeno

; ------ konverze dat na velk  p¡smena

         mov       si,dx                    ; za‡ tek na‡ten˜ch dat
         mov       di,dx
         cld
TestTxt4:lodsb
         cmp       al,"a"
         jb        TestTxt5
         cmp       al,"z"
         ja        TestTxt5
         sub       al,32                    ; konverze na velk‚ p¡smeno
TestTxt5:stosb
         loop      TestTxt4

; ------ p©¡prava pro prohled n¡ jednoho bloku dat

         mov       cx,di                    ; konec dat v bufferu
         mov       di,offset SrcBuff        ; za‡ tek bufferu
         sub       cx,di                    ; po‡et bajt– v bufferu
         sub       cx,bp                    ; d‚lka zbytku dat
         jc        TestTxt8                 ; konec souboru - text nenalezen
         inc       cx                       ; po‡et ©etˆzc– k porovn n¡

; ------ nalezen¡ prvn¡ho znaku

         mov       al,ds:[SrcText]          ; prvn¡ znak textu
TestTxt6:repne     scasb                    ; nalezen¡ prvn¡ho znaku
         jne       TestTxt7                 ; nenalezen - dal¨¡ blok dat

; ------ porovn n¡ ©etˆzce

         mov       si,offset SrcText+1      ; hled n˜ text
         push      di
         push      cx
         mov       cx,bp                    ; d‚lka ©etˆzce
         dec       cx                       ; d‚lka - 1 (p©¡padn˜ p©¡znak ZY)
         repe      cmpsb                    ; porovn n¡ ©etˆzc–
         pop       cx
         pop       di
         jne       TestTxt6                 ; nenalezen - dal¨¡ blok dat
         jmp       short TestTxt8           ; ©etˆzec nalezen OK

; ------ p©esun dat z konce bufferu na za‡ tek

TestTxt7:mov       si,di                    ; n sleduj¡c¡ data
         dec       si
         mov       cx,bp                    ; d‚lka ©etˆzce
         mov       di,offset SrcBuff        ; za‡ tek bufferu
         rep       movsb                    ; p©esun dat

; ------ na‡ten¡ dal¨¡ho bloku dat

         mov       dx,di                    ; adresa k na‡ten¡ dat
         mov       cx,SRCBMAX               ; velikost bufferu
         sub       cx,bp                    ; velikost bez d‚lky ©etˆzce
         jmp       short TestTxt3

; ------ uzav©en¡ souboru

TestTxt8:pushf
         mov       ah,3eh
         int       21h
         popf

; ------ n vrat registr–

TestTxt9:pop       es
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

TestText ENDP

; *****************************************************************************
;                              TestFile
;              test nalezen‚ho souboru, zda odpov¡d  masce
; -----------------------------------------------------------------------------
; VSTUP:NC=polo‘ka odpov¡d , CY=neodpov¡d 
; *****************************************************************************

TestFile PROC      NEAR

         push      ax
         push      bx
         push      cx
         push      di
         push      si

; ------ p©¡prava registr–

         mov       di,offset FileMask       ; maska jm‚na souboru
         mov       cx,ds:[FileMskN]         ; d‚lka masky jm‚na souboru

; ------ test, zda maska obsahuje te‡ku

TestFil1:jcxz      TestFil9
         mov       si,di
         mov       bx,cx
         cld
TestFil2:lodsb
         cmp       al,"."
         je        TestFil4                 ; obsahuje te‡ku
         cmp       al," "
         je        TestFil3                 ; neobsahuje te‡ku
         dec       bx
         jnz       TestFil2                 ; dal¨¡ znak

; ------ kontrola jm‚na bez te‡ky

TestFil3:mov       bx,11                    ; d‚lka jm‚na i s p©¡ponou
         mov       si,offset FileFCB
         call      TestMs                   ; kopntrola jm‚na
         jmp       short TestFl77

; ------ kontrola jm‚na polo‘ky

TestFil4:mov       bx,8                     ; d‚lka jm‚na
         mov       si,offset FileFCB        ; jm‚no souboru ve tvaru FCB
         call      TestMs                   ; kontrola jm‚na
         jc        TestFil8                 ; jm‚no se neshoduje

; ------ nastaven¡ na p©¡ponu polo‘ky

TestFil5:jcxz      TestFil7                 ; konec jm‚na
         mov       al,ds:[di]               ; p©ipraven˜ znak
         cmp       al," "
         je        TestFil6                 ; konec jm‚na polo‘ky
         cmp       al,"."                   ; oddˆlovac¡ te‡ka ?
         je        TestFil6                 ; te‡ka - je p©¡pona
         inc       di                       ; dal¨¡ znak
         dec       cx                       ; sn¡‘en¡ ‡¡ta‡e znak–
         jmp       short TestFil5           ; dal¨¡ znak

; ------ kontrola p©¡pony polo‘ky

TestFil6:inc       di                       ; p©esko‡en¡ znaku "."
         dec       cx                       ; sn¡‘en¡ ‡¡ta‡e znak–
TestFil7:mov       bl,3                     ; d‚lka p©¡pony
         mov       si,offset FileFCB+8      ; jm‚no souboru ve tvaru FCB
         call      TestMs                   ; test p©¡pony jm‚na
TestFl77:jnc       TestFilA                 ; polo‘ka vyhovuje OK

; ------ polo‘ka nevyhovuje - dal¨¡ maska

TestFil8:jcxz      TestFil9                 ; nen¡ dal¨¡ maska - nenalezena
         mov       al,ds:[di]               ; p©ipraven˜ znak
         inc       di
         dec       cx
         cmp       al," "
         jne       TestFil8                 ; nalezen¡ znaku mezery
         jmp       short TestFil1           ; kontrola dal¨¡ masky

TestFil9:stc                                ; p©¡znak CY=nevyhovuje

TestFilA:pop       si
         pop       di
         pop       cx
         pop       bx
         pop       ax
         ret

TestFile ENDP

; *****************************************************************************
;        test jm‚na nebo p©¡pony souboru na jednu masku
; -----------------------------------------------------------------------------
; VSTUP: DS:SI=jm‚no nebo p©¡pona (ve tvaru FCB)
;        BX=po‡et zbyl˜ch znak– FCB ke kontrole
;        DS:DI=ukazatel masky
;        CX=po‡et zbyl˜ch znak– masky
; VSTUP:NC=polo‘ka vyhovuje, CY=nevyhovuje
;        pokud polo‘ka vyhovuje (je NC), zmˆn¡ se SI, BX, DI, CX podle operace
;        jinak z–stanou (p©i chybˆ CY) registry SI, BX, DI, CX zachov ny
; -----------------------------------------------------------------------------
; neuchov v  registry: AX
; *****************************************************************************

TestMs   PROC      NEAR

; ------ £schova registr–

         push      bx
         push      cx
         push      si
         push      di

; ------ konec zadan‚ ‡ sti jm‚na - zbytek mus¡ b˜t mezery

TestMs1: mov       al," "                   ; porovn van˜ znak
         jcxz      TestMs4                  ; konec - zbytek mus¡ b˜t mezery
         mov       ah,ds:[di]               ; p©ipraven˜ znak masky
         cmp       ah," "                   ; mezera - konec polo‘ky ?
         je        TestMs4                  ; konec - zbytek mus¡ b˜t mezery
         cmp       ah,"."                   ; te‡ka - konec jm‚na nebo p©¡pony ?
         je        TestMs4                  ; konec - zbytek mus¡ b˜t mezery

; ------ n hradn¡ znak "*" - voln˜ po‡et znak–

         inc       di                       ; zv˜¨en¡ ukazatele textu
         dec       cx                       ; sn¡‘en¡ ‡¡ta‡e zbyl˜ch znak–

         cmp       ah,"*"                   ; n hradn¡ znaky - v¨e vyhovuje
         jne       TestMs3

TestMs2: call      TestMs                   ; porovn n¡ zbytku masky
         jnc       TestMs6                  ; zbytek masky vyhovuje
         inc       si                       ; ukazatel v FCB
         dec       bx                       ; ‡¡ta‡ zbyl˜ch pozic FCB
         jnz       TestMs2                  ; dal¨¡ test
         jmp       short TestMs6            ; vyhovuje, zbytek mus¡ b˜t mezery

; ------ porovn n¡ znaku

TestMs3: mov       al,ah                    ; znak k porovn n¡
         cmp       al,"?"                   ; n hradn¡ znak
         je        TestMs5                  ; n hradn¡ znak - vyhovuje
TestMs4: cmp       al,ds:[si]               ; souhlas¡ jm‚no ?
         jne       TestMs9                  ; polo‘ka nesouhlas¡

; ------ znak vyhovuje - dal¨¡ znak

TestMs5: inc       si                       ; zv˜¨en¡ ukazatele v polo‘ce
         dec       bx                       ; ‡¡ta‡ znak– k testov n¡
         jnz       TestMs1                  ; dal¨¡ znak

; ------ test, zda je konec textu

TestMs6: jcxz      TestMs7                  ; konec textu OK
         cmp       byte ptr ds:[di]," "
         je        TestMs7
         cmp       byte ptr ds:[di],"."
         je        TestMs7
         cmp       byte ptr ds:[di],"*"
         jne       TestMs9
         inc       di
         loop      TestMs6

; ------ polo‘ka vyhovuje - registry se nevracej¡

TestMs7: add       sp,4*2                   ; registry se nevracej¡
         clc                                ; p©¡znak, ‘e vyhovuje
         ret

; ------ polo‘ka nevyhovuje

TestMs9: stc                                ; p©¡znak, ‘e nevyhovuje
         pop       di
         pop       si
         pop       cx
         pop       bx
         ret

TestMs   ENDP

; *****************************************************************************
;                             AddGFil
;               p©id n¡ nalezen‚ho souboru k cestˆ
; -----------------------------------------------------------------------------
; VSTUP: ES:SI=text souboru k p©id n¡
; VSTUP:CY=p©ete‡en¡ d‚lky bufferu
; *****************************************************************************

AddGFil  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      si
         push      di

; ------ stanoven¡ d‚lky jm‚na souboru

         mov       cx,0ffffh
         mov       di,si                    ; adresa textu
         mov       al,0                     ; hledan˜ bajt
         cld
         repne     scasb                    ; nalezen¡ konce textu
         neg       cx
         dec       cx
         dec       cx                       ; CX = d‚lka textu

; ------ kontrola, zda bude p©ete‡en¡ bufferu

         mov       ax,cx                    ; d‚lka textu
         add       ax,ds:[FilePthN]         ; nov  d‚lka textu cesty
         cmp       ax,MAXLINE-1             ; maxim ln¡ d‚lka bufferu
         cmc
         jc        AddGFil9                 ; p©ete‡en¡ bufferu

; ------ zaji¨tˆn¡ koncov‚ho znaku "\"

         mov       di,offset FilePath       ; buffer cesty
         add       di,ds:[FilePthN]         ; konec cesty
         mov       al,"\"
         cmp       ds:[di-1],al             ; kon‡¡ znakem "\" ?
         je        AddGFil3                 ; kon‡¡ znakem "\" - OK
         mov       ds:[di],al               ; oddˆlovac¡ znak
         inc       di                       ; zv˜¨en¡ ukazatele na dal¨¡ znak

; ------ p©enesen¡ textu

AddGFil3:push      ds
         push      es

         push      ds
         push      es
         pop       ds
         pop       es
         cld
         rep       movsb                    ; p©enesen¡ textu

         pop       es
         pop       ds

; ------ koncov  0, d‚lka textu

         mov       byte ptr ds:[di],0       ; koncov  0
         sub       di,offset FilePath       ; nov  d‚lka textu
         mov       ds:[FilePthN],di         ; nov  d‚lka textu
         clc                                ; p©¡znak operace OK

; ------ n vrat registr–

AddGFil9:pop       di
         pop       si
         pop       cx
         pop       ax
         ret

AddGFil  ENDP

; *****************************************************************************
;                            SubGFil
;             odstranˆn¡ posledn¡ho souboru z cesty
; -----------------------------------------------------------------------------
; VSTUP: DS=datov˜ segment
; *****************************************************************************

SubGFil  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      si

; ------ konec textu v bufferu

         mov       si,offset FilePath       ; cesta
         add       si,ds:[FilePthN]         ; konec textu cesty

; ------ nalezen¡ posledn¡ho znaku "\"

         mov       al,"\"                   ; hledan˜ znak "\"
SubGFil1:dec       si                       ; sn¡‘en¡ ukazatele v bufferu
         cmp       ds:[si],al               ; je konec cesty ?
         jne       SubGFil1                 ; nen¡ - dal¨¡ znak

; ------ ozna‡en¡ konce cesty

         cmp       byte ptr ds:[si-1],":"   ; je to ROOT ?
         jne       SubGFil2                 ; nen¡ to ROOT
         inc       si                       ; zv˜¨en¡ ukazatele - ponech  se "\"
SubGFil2:mov       byte ptr ds:[si],0       ; koncov  0

; ------ nov  d‚lka cesty

         sub       si,offset FilePath       ; d‚lka textu cesty
         mov       ds:[FilePthN],si         ; nov  d‚lka cesty

; ------ n vrat registr–

         pop       si
         pop       cx
         pop       ax
         ret

SubGFil  ENDP

; *****************************************************************************
;                             SetDTA
;                      Nastaven¡ adresy DTA
; -----------------------------------------------------------------------------
; VSTUP: ES:SI=adresa DTA
; *****************************************************************************

SetDTA   PROC      NEAR

; ------ £schova registr–

         push      ax
         push      dx
         push      ds

; ------ nastaven¡ adresy DTA

         push      es
         pop       ds                       ; segment adresy DTA
         mov       dx,si                    ; offset adresy DTA
         mov       ah,1ah                   ; funkce nastaven¡ adresy DTA
         int       21h                      ; nastaven¡ adresy DTA

; ------ n vrat registr–

         pop       ds
         pop       dx
         pop       ax
         ret

SetDTA   ENDP

; *****************************************************************************
;                               SrcFirst
;                   Nalezen¡ prvn¡ho vyhovuj¡c¡ho souboru
; -----------------------------------------------------------------------------
; VSTUP: CX=atributy souboru
;        ES:SI=specifikace souboru
;        DS=datov˜ segment
; VSTUP:CY=chyba
;        ZY=soubor nalezen
;        NZ=soubor nenalezen
; *****************************************************************************

SrcFirst PROC      NEAR

; ------ £schova registr–

         push      ax
         push      dx

; ------ nalezen¡ souboru nebo adres ©e

         push      ds
         push      es
         pop       ds                       ; segment adresy se jm‚nem souboru
         mov       dx,si                    ; offset adresy se jm‚nem souboru
         mov       ah,4eh                   ; funkce nalezen¡ souboru
SrcFrst1:int       21h                      ; nalezen¡ prvn¡ho souboru
         pop       ds
         jnc       SrcFrst3                 ; operace OK - soubor nalezen

; ------ rozli¨en¡ chyby - zda soubor nebyl nalezen nebo jin  chyba

         cmp       ax,2                     ; soubor nenalezen ?
         je        SrcFrst2                 ; soubor nenalezen
         cmp       ax,3                     ; cesta nenalezena ?
         je        SrcFrst2                 ; cesta nenalezena
         cmp       ax,12h                   ; nejsou dal¨¡ soubory ?
         stc                                ; p©¡znak chyby operace
         jne       SrcFrst4                 ; byla jin  chyba

; ------ soubor nenalezen (k¢dy chyb 2, 3 nebo 12h)

SrcFrst2:or        ax,ax                    ; nastaven¡ p©¡znak– NC, NZ
         jmp       short SrcFrst4

; ------ soubor nalezen OK

SrcFrst3:cmp       ax,ax                    ; nastaven¡ p©¡znak– NC, ZY
SrcFrst4:pop       dx
         pop       ax
         ret

SrcFirst ENDP


; *****************************************************************************
;                               SrcNext
;                   Nalezen¡ dal¨¡ho vyhovuj¡c¡ho souboru
; -----------------------------------------------------------------------------
; VSTUP: DS=datov˜ segment
; VSTUP:CY=chyba
;        ZY=soubor nalezen
;        NZ=soubor nenalezen
; *****************************************************************************

SrcNext  PROC      NEAR

         push      ax
         push      dx

         push      ds
         mov       ah,4fh                   ; funkce nalezen¡ dal¨¡ho souboru
         jmp       short SrcFrst1           ; nalezen¡ dal¨¡ho souboru

SrcNext  ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                         Obsluha velk‚ n povˆdy
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

;þ
; *****************************************************************************
;                                 Napoveda
;                             hlavn¡ n povˆda
; -----------------------------------------------------------------------------
; VSTUP: DS=datov˜ segment
; *****************************************************************************

Napoveda PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         push      es

; ------ p©¡prava registr–

         or        byte ptr ds:[Param],bit6 ; p©¡znak zobrazen¡ n povˆdy
         push      ds
         pop       es                       ; ES <- datov˜ segment
         call      DispHlp                  ; zobrazen¡ n povˆdy k n povˆdˆ
         call      DispNadp                 ; zobrazen¡ licen‡n¡ho textu
         call      KurzOff                  ; vypnut¡ kurzoru

         mov       bp,offset(HelpTxt0-HelpTxt)/80 ; po‡et © dk– n povˆdy celkem

; ------ zobrazen¡ n povˆdy

Napoved1:mov       si,ds:[HelpTop]          ; adresa textu n povˆdy
         mov       bh,0
         mov       bl,ds:[MaxRow]           ; po‡et © dk– displeje - 1
         dec       bx
         cmp       bx,bp
         jbe       Napoved2
         mov       bx,bp                    ; omezen¡ po‡tu © dk–
Napoved2:mov       dh,1
         mov       cl,80
         mov       ah,ds:[HelpColM]         ; barva textu
Napoved3:mov       dl,0
         call      DispTxt                  ; zobrazen¡ jednoho © dku n povˆdy
         inc       dh
         add       si,80
         dec       bx
         jnz       Napoved3

Napoved4:
         call      InpChr

         mov       ch,0
         mov       cl,ds:[MaxRow]           ; po‡et © dk– displeje - 1
         dec       cx                       ; po‡et © dk– na n povˆdu
         sub       cx,bp                    ; po‡et p©esahuj¡c¡ch © dk–
         jc        Napov42                  ; nezobraz¡ se cel  n povˆda
         xor       cx,cx                    ; omezen¡ - bude cel  n povˆda
Napov42: neg       cx                       ; maxim ln¡ po‡ te‡n¡ © dek

         call      JumpBX

         dw        5000h,Napov51            ; kurzor dol–
         dw        4f00h,Napov52            ; END
         dw        5100h,Napov53            ; Page Down

         dw        4800h,Napov61            ; kurzor nahoru
         dw        4700h,Napov62            ; HOME
         dw        4900h,Napov63            ; Page Up

         dw        3b00h,Napoved9           ; F1 konec n povˆdy
         dw        11bh,Napoved9            ; ESC
         dw        1c0dh,Napoved9           ; ENTER
         dw        0,Napoved4

; ------ kurzor dol–

Napov51: mov       bx,1                     ; po‡et © dk– k posunu

Napov50: cmp       cx,ds:[HelpTopN]         ; je ji‘ maxim ln¡ © dek ?
         jbe       Napoved1                 ; je ji‘ maxim ln¡ © dek
         inc       word ptr ds:[HelpTopN]   ; zv˜¨en¡ ‡¡sla © dku
         add       word ptr ds:[HelpTop],80 ; adresa dal¨¡ho © dku
         dec       bx
         jnz       Napov50
Napov11: jmp       short Napoved1

; ------ END

Napov52: mov       bx,-1
         jmp       short Napov50

; ------ PAGE DOWN

Napov53: mov       bh,0
         mov       bl,ds:[MaxRow]
         dec       bx
         dec       bx
         jmp       short Napov50

; ------ kurzor nahoru

Napov61: mov       bx,1

Napov60: cmp       word ptr ds:[HelpTopN],0
         je        Napov11
         dec       word ptr ds:[HelpTopN]
         sub       word ptr ds:[HelpTop],80
         dec       bx
         jnz       Napov60
         jmp       short Napov11

; ------ HOME

Napov62: mov       bx,-1
         jmp       short Napov60

; ------ Page Up

Napov63: mov       bh,0
         mov       bl,ds:[MaxRow]
         dec       bx
         dec       bx
         jmp       short Napov60

; ------ n vrat registr–

Napoved9:and       byte ptr ds:[Param],not bit6
         pop       es
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

Napoveda ENDP


; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                             Univerz ln¡ procedury
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

;þ

; *****************************************************************************
;        dek¢dov n¡ ‡¡sla
; -----------------------------------------------------------------------------
; VSTUP: DX:AX=‡¡slo
;        ES:DI=adresa konce bufferu (n sleduj¡c¡ adresa za koncem bufferu)
; VSTUP:SI=adresa za‡ tku textu ‡¡sla
; *****************************************************************************

DekNum   PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      di

; ------ p©¡prava registr–

         xor       cx,cx                    ; ‡¡ta‡ © du
         mov       si,10                    ; dˆlitel

; ------ dek¢dov n¡ jedn‚ ‡¡slice

DekNum1: xchg      ax,bx                    ; BX <- £schova ‡¡sla LOW
         xchg      ax,dx                    ; AX <- ‡¡slo HIGH
         xor       dx,dx                    ; DX <- 0
         div       si                       ; vydˆlen¡ ‡¡sla HIGH
         xchg      ax,bx                    ; AX <- n vrat LOW, BX<-£schova HIGH
         div       si                       ; vydˆlen¡ ‡¡sla LOW

; ------ oddˆlova‡ © d–

         cmp       cl,3
         jne       DekNum2
         dec       di                       ; sn¡‘en¡ ukazatele textu
         mov       byte ptr es:[di],"."     ; oddˆlova‡ © d–
         xor       cx,cx                    ; ‡¡ta‡ © d–
DekNum2: inc       cx                       ; zv˜¨en¡ ‡¡ta‡e © d–

; ------ ulo‘en¡ ‡¡sla

         add       dl,"0"                   ; korekce na znak ASCII
         dec       di                       ; sn¡‘en¡ ukazatele textu
         mov       es:[di],dl               ; ulo‘en¡ znaku
         mov       dx,bx                    ; n vrat ‡¡sla HIGH

; ------ test, zda bude dal¨¡ ‡¡slice

         or        bx,ax                    ; je ‡¡slo ji‘ = 0 ?
         jnz       DekNum1                  ; dek¢dov n¡ dal¨¡ ‡¡slice
         mov       si,di                    ; adresa za‡ tku textu

; ------ n vrat registr–

         pop       di
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

DekNum   ENDP

; *****************************************************************************
;        dek¢dov n¡ ‡¡sla s barvou
; -----------------------------------------------------------------------------
; VSTUP: DX:AX=‡¡slo
;        ES:DI=adresa konce bufferu (n sleduj¡c¡ adresa za koncem bufferu)
;        BH=atribut barvy
;        BL=oddˆlova‡ © d– (0=nen¡ nic)
; *****************************************************************************

DekCNum  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx
         push      si
         push      di
         push      bp

; ------ p©¡prava registr–

         mov       cl,0                     ; ‡¡ta‡ © du
         mov       si,10                    ; dˆlitel

; ------ dek¢dov n¡ jedn‚ ‡¡slice

DekCNum1:xchg      ax,bp                    ; BP <- £schova ‡¡sla LOW
         xchg      ax,dx                    ; AX <- ‡¡slo HIGH
         xor       dx,dx                    ; DX <- 0
         div       si                       ; vydˆlen¡ ‡¡sla HIGH
         xchg      ax,bp                    ; AX <- n vrat LOW, BX<-£schova HIGH
         div       si                       ; vydˆlen¡ ‡¡sla LOW

; ------ oddˆlova‡ © d–

         cmp       cl,6
         je        DekCNm21
         cmp       cl,9
         je        DekCNm21
         cmp       cl,3
         jne       DekCNum2
DekCNm21:cmp       bl,0
         je        DekCNum2
         dec       di                       ; sn¡‘en¡ ukazatele textu
         dec       di
         mov       es:[di],bx               ; oddˆlova‡ © d–
DekCNum2:inc       cx                       ; zv˜¨en¡ ‡¡ta‡e © d–

; ------ ulo‘en¡ ‡¡sla

         add       dl,"0"                   ; korekce na znak ASCII
         dec       di                       ; sn¡‘en¡ ukazatele textu
         mov       es:[di],bh               ; ulo‘en¡ barvy
         dec       di
         mov       es:[di],dl               ; ulo‘en¡ znaku
         mov       dx,bp                    ; n vrat ‡¡sla HIGH

; ------ test, zda bude dal¨¡ ‡¡slice

         or        bp,ax                    ; je ‡¡slo ji‘ = 0 ?
         jnz       DekCNum1                 ; dek¢dov n¡ dal¨¡ ‡¡slice

; ------ n vrat registr–

         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       ax
         ret

DekCNum  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ cel‚ obrazovky
; -----------------------------------------------------------------------------

DispAll  PROC      NEAR

         push      si
         call      DispSezn
         call      DispHlp                  ; zobrazen¡ spodn¡ho © dku
         call      DispNadp                 ; zobrazen¡ nadpisu
         call      KorVol                   ; korekce voleb
         call      DispVol                  ; zobrazen¡ voleb
         pop       si
         ret

DispAll  ENDP

; -----------------------------------------------------------------------------
;        p©evod znaku AL na velk‚ p¡smeno
; -----------------------------------------------------------------------------

UpCase   PROC      NEAR

         cmp       al,"a"
         jb        UpCase2
         cmp       al,"z"
         ja        UpCase2
         sub       al,32
UpCase2: ret

UpCase   ENDP

; -----------------------------------------------------------------------------
;                                 InpChr
;                        vstup znaku z kl vesnice
; -----------------------------------------------------------------------------
; VSTUP: BX=k¢d kl vesy
;         CY=p©eru¨en¡ ESC, BREAK
; -----------------------------------------------------------------------------

InpChr   PROC      NEAR

         push      ax

InpChr0: call      TestKey
         jz        InpChr0

         mov       ah,0
         int       16h

         or        ax,ax
         jnz       InpChr1
         mov       ax,11bh                  ; n hrada Ctrl-Break znakem ESC

InpChr1: cmp       al,1bh
         clc
         jne       InpChr2
         stc                                ; p©¡znak p©eru¨en¡ Esc, Break

InpChr2: mov       bx,ax
         pop       ax
         ret

InpChr   ENDP

; -----------------------------------------------------------------------------
;
; -----------------------------------------------------------------------------

TestKey  PROC      NEAR

         push      ds
         xor       ax,ax
         mov       ds,ax
         and       byte ptr ds:[418h],not 80h
         pop       ds

         mov       ah,1
         int       16h
         ret

TestKey  ENDP

; -----------------------------------------------------------------------------
;                    skok na obsluhu podle BX
; -----------------------------------------------------------------------------
; Procedura se vol  instrukc¡ CALL JumpBX, za kterou n sleduje tabulka
; skok–. Procedura zmˆn¡ svou n vratovou adresu na adresu podle nalezen‚
; polo‘ky v tabulce (nebo podle polo‘ky pro nenalezenou hodnotu).
; -----------------------------------------------------------------------------
; VSTUP: v z sobn¡ku slovo (n vratov  adresa NEAR) = za‡ tek tabulky skok–
;             stuktura tabulky: 1 slovo testovan  hodnota BX
;                               1 slovo adresa NEAR obsluhy
;             konec tabulky: 1 slovo = 0 (p©¡znak konce tabulky)
;                            1 slovo adresa NEAR obsluhy p©i nenalezen¡ k¢du
; -----------------------------------------------------------------------------

JumpBX   PROC      NEAR

; ------ £schova registr–

                                            ; SS:[BP+6] = IP
         push      si                       ; SS:[BP+4] = SI
         push      bp                       ; SS:[BP+2] = BP
         push      ds                       ; SS:[BP+0] = DS
         mov       bp,sp

; ------ nalezen¡ hodnoty v tabulce

         push      cs
         pop       ds
         mov       si,ss:[bp+6]             ; offset adresy tabulky
JumpBX1: cmp       word ptr ds:[si],0       ; je konec tabulky ?
         je        JumpBX2                  ; konec tabulky - nenalezeno
         cmp       word ptr ds:[si],bx      ; je to hledan  hodnota ?
         je        JumpBX2                  ; hodnota nalezena
         add       si,4                     ; adresa dal¨¡ polo‘ky
         jmp       short JumpBX1            ; test dal¨¡ polo‘ky

; ------ nastaven¡ n vratov‚ adresy podle tabulky

JumpBX2: mov       si,ds:[si+2]             ; adresa skoku
         mov       ss:[bp+6],si             ; nov  n vratov  adresa

; ------ n vrat registr–

         pop       ds
         pop       bp
         pop       si
         ret

JumpBX   ENDP

; -----------------------------------------------------------------------------
;                                 Cekej
;                        ‡ek n¡ po zadanou dobu
; -----------------------------------------------------------------------------
; VSTUP: CX=po‡et impuls– 1/18 s
; -----------------------------------------------------------------------------

Cekej    PROC      NEAR

         push      ax
         push      cx
         push      ds

         xor       ax,ax
         mov       ds,ax
         jcxz      Cekej3
         sti

Cekej1:  mov       ax,ds:[46ch]
Cekej2:  cmp       ax,ds:[46ch]
         je        Cekej2
         loop      Cekej1

Cekej3:  pop       ds
         pop       cx
         pop       ax
         ret

Cekej    ENDP

; -----------------------------------------------------------------------------
;                    zobrazen¡ textu n povˆdy DS:SI
; -----------------------------------------------------------------------------

DispHlp  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      dx
         push      si
         push      es

; ------ stanoven¡ textu n povˆdy

         mov       dl,ds:[Param]
         test      dl,bit2                  ; prob¡h  hled n¡ souboru ?
         jnz       DispHlp9                 ; prob¡h  hled n¡ souboru

         mov       si,offset HelpTxt2       ; n povˆda k n povˆdˆ
         test      dl,bit6                  ; je velk  n povˆda ?
         jnz       DispHlp1                 ; je velk  n povˆda

         mov       si,offset HelpTxt1       ; n povˆda k zad n¡ soubor–
         test      dl,bit4                  ; je zad n¡ soubor– ?
         jnz       DispHlp1                 ; je zad n¡ soubor–

         mov       si,offset HelpTxt5       ; zobrazen¡ souboru F3
         test      dl,bit5                  ; je zobrazen¡ souboru F3 ?
         jnz       DispHlp1                 ; je zobrazen¡ souboru F3

         mov       si,offset HelpTxt3       ; seznam bez ozna‡en¡
         cmp       word ptr ds:[InsNum],0   ; je nˆco ozna‡eno ?
         je        DispHlp1                 ; nen¡ nic ozna‡eno
         mov       si,offset HelpTxt4       ; jinak n povˆda p©i ozna‡en¡


DispHlp1:

; ------ zobrazen¡ textu n povˆdy

         mov       ah,ds:[HelpCol]          ; barva textu n povˆdy
         mov       dl,0                     ; po‡ te‡n¡ pozice textu
         mov       dh,ds:[MaxRow]           ; posledn¡ © dek displeje
         push      ds
         pop       es
         mov       cl,80                    ; d‚lka textu n povˆdy
         call      DispTxt                  ; zobrazen¡ textu n povˆdy

; ------ n vrat registr–

DispHlp9:pop       es
         pop       si
         pop       dx
         pop       cx
         pop       ax
         ret

DispHlp  ENDP

; -----------------------------------------------------------------------------
;                      zobrazen¡ horn¡ho © dku
; -----------------------------------------------------------------------------

DispNadp PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         push      es
         mov       bp,sp

; ------ zobrazen¡ licen‡n¡ho textu

         mov       ah,ds:[InfoCol1]         ; barva textu n povˆdy
         test      byte ptr ds:[Param],bit0 ; bylo prvn¡ zad n¡ ?
         jnz       DispNad2                 ; nylo prvn¡ zad n¡
         xor       dx,dx                    ; po‡ te‡n¡ pozice textu
         push      ds
         pop       es
         mov       si,offset LicTxt
         mov       cl,80                    ; d‚lka textu n povˆdy
         call      DispTxt                  ; zobrazen¡ textu n povˆdy
         jmp       DispNad9

; ------ vymaz n¡ bufferu © dku

DispNad2:sub       sp,2*80                  ; rezerva pro © dek
         push      ss
         pop       es
         mov       di,sp
         cld
         mov       al," "
         mov       cx,80
         rep       stosw                    ; vymaz n¡ © dku

; ------ dek¢dov n¡ po‡tu soubor–

         mov       di,sp
         push      di
         mov       si,offset TxtSou1
         call      DekTxt
         mov       cx,ds:[InsNum]           ; po‡et ozna‡en˜ch soubor–
         mov       bx,ds:[CitSoub]          ; po‡et nalezen˜ch soubor–
         jcxz      DispNd22                 ; neozna‡en ‘ dn˜ soubor
         mov       bx,cx                    ; po‡et ozna‡en˜ch soubor–
DispNd22:mov       ah,ds:[InfoCol2]         ; zv˜raznˆn  barva
         xor       dx,dx
         call      DekXNum                  ; ‡¡slo zarovnan‚ vpravo
         pop       di
         add       di,2*16

; ------ dek¢dov n¡ sou‡tu velikost¡ soubor–

         push      di
         mov       si,offset TxtSou2
         mov       ah,ds:[InfoCol1]
         call      DekTxt
         mov       dx,word ptr ds:[CitCelk+2]
         mov       bx,word ptr ds:[CitCelk]
         jcxz      DispNd23                 ; nen¡ nic ozna‡eno
         mov       dx,word ptr ds:[InsSumm+2]
         mov       bx,word ptr ds:[InsSumm]
DispNd23:mov       ah,ds:[InfoCol2]
         call      DekXNum
         pop       di
         add       di,2*21

; ------ dek¢dov n¡ obsazen‚ kapacity soubory

         push      di
         mov       si,offset TxtSou3
         mov       ah,ds:[InfoCol1]
         call      DekTxt
         mov       dx,word ptr ds:[CitObs+2]
         mov       bx,word ptr ds:[CitObs]
         jcxz      DispNd24                 ; nen¡ nic ozna‡eno
         mov       dx,word ptr ds:[InsObs+2]
         mov       bx,word ptr ds:[InsObs]
DispNd24:mov       ah,ds:[InfoCol2]
         call      DekXNum
         pop       di
         add       di,2*21

; ------ dek¢dov n¡ kurzoru

         mov       bx,word ptr ds:[ViewTop]
         mov       cx,word ptr ds:[ViewTop+2]
         mov       dx,word ptr ds:[ViewSize]
         mov       ax,word ptr ds:[ViewSize+2]

         or        ax,ax
         jnz       DispNd25
         jcxz      DispNd26

DispNd25:shr       ax,1
         rcr       dx,1
         shr       cx,1
         rcr       bx,1

         or        ax,ax
         jnz       DispNd25
         or        cx,cx
         jnz       DispNd25

DispNd26:mov       cx,ds:[ViewDis]          ; zobrazen  ‡ st souboru

         mov       ah,ds:[InfoCol1]         ; barva textu
         mov       si,sp
         add       si,2*80-4
         sub       si,di
         shr       si,1                     ; celkov  d‚lka ukazatele

         test      byte ptr ds:[Param],bit5 ; je prohl¡‘en¡ souboru ?
         jnz       DispNd27                 ; je prohl¡‘en¡ souboru

         mov       bx,ds:[SeznTop]          ; za‡ tek str nky
         mov       cx,ds:[SeznRad]          ; d‚lka str nky
         mov       dx,ds:[DispNum]          ; celkov˜ po‡et © dk–

DispNd27:call      DekKurz                  ; dek¢dov n¡ kurzoru

; ------ zobrazen¡ © dku

DispNd4: mov       si,sp
         mov       cl,80
         xor       dx,dx
         call      DispStr

; ------ n vrat registr–

DispNad9:mov       sp,bp
         pop       es
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

DispNadp ENDP

; -----------------------------------------------------------------------------
;        dek¢dov n¡ kurzoru
; -----------------------------------------------------------------------------
; VSTUP: ES:DI=ukl dac¡ adresa
;        AH=barva textu
;        BX=po‡ tek (jednotek)
;        CX=d‚lka (jednotek)
;        DX=celkov  d‚lka (jednotek)
;        SI=celkov  d‚lka kurzoru (pozic)
; -----------------------------------------------------------------------------
; lok ln¡ promˆnn‚: SS:[BP-2] (2) zobrazen˜ po‡ tek (jednotek)
;                   SS:[BP-4] (2) zobrazen  d‚lka (jednotek)
;                   SS:[BP-5] (1) barva textu
;                   SS:[BP-6] (1)
; -----------------------------------------------------------------------------

DekKurz  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         mov       bp,sp

; ------ lok ln¡ promˆnn‚

         sub       sp,6                     ; lok ln¡ promˆnn‚
         mov       ss:[bp-2],bx             ; zobrazen˜ po‡ tek (jednotek)
         mov       ss:[bp-4],cx             ; zobrazen  d‚lka (jednotek)
         mov       ss:[bp-6],ax             ; AH=barva textu

; ------ lev  oddˆlovac¡ ‡ ra kurzoru

         cld
         mov       al,179                   ; oddˆlovac¡ ‡ ra
         stosw                              ; ulo‘en¡ oddˆlovac¡ ‡ ry

; ------ v˜po‡et d‚lky kurzoru

         mov       ax,cx                    ; zobrazen  d‚lka (jednotek)
         mov       cx,dx                    ; celkov  d‚lka (jednotek)
         cmp       ax,cx                    ; je d‚lka OK ?
         jbe       DekKurz1                 ; d‚lka je OK
         mov       ax,cx                    ; omezen¡ d‚lky kurzoru
DekKurz1:jcxz      DekKurz8                 ; nen¡ ‘ dn˜ soubor
         mul       si                       ; * celkov  d‚lka kurzoru (pozic)
         div       cx                       ; v˜po‡et d‚lky kurzoru
         mov       bx,ax                    ; d‚lka kurzoru
         cmp       bl,0                     ; vy¨la d‚lka kurzoru = 0 ?
         jne       DekKurz2                 ; d‚lka kurzoru je OK
         inc       bx                       ; minim ln¡ d‚lka kurzoru = 1
DekKurz2:cmp       bx,si                    ; m  kurzor d‚lku OK ?
         jbe       DekKurz3                 ; d‚lka kurzoru je OK
         mov       bx,si                    ; omezen¡ d‚lky kurzoru

; ------ v˜po‡et po‡ tku kurzoru

DekKurz3:mov       ax,ss:[bp-2]             ; zobrazen˜ po‡ tek (jednotek)
         or        ax,ax                    ; je po‡ tek = 0 ?
         jz        DekKurz4                 ; po‡ tek je = 0
         mov       ax,ss:[bp-4]             ; zobrazen  d‚lka (jednotek)
;         sub       ax,3                     ;
         shr       ax,1                     ; polovina d‚lky
;         add       ax,ss:[bp-4]             ; to je 1.5 d‚lky
;         shr       ax,1                     ; to je 3/4 d‚lky
         add       ax,ss:[bp-2]             ; + po‡ tek = pozice st©edu
         mul       si                       ; * celkov  d‚lka kurzoru
         div       cx                       ; v˜po‡et konce kurzoru
DekKurz4:mov       bh,al                    ; po‡ tek kurzoru
         add       al,bl                    ; konec kurzoru
         cmp       ax,si                    ; p©ekro‡en konec ?
         jbe       DekKurz5                 ; konec OK
         mov       ax,si                    ; celkov  d‚lka ukazatele
         sub       al,bl                    ; ode‡ten¡ d‚lky kurzoru
         mov       bh,al                    ; za‡ tek kurzoru

; ------ dek¢dov n¡ kurzoru

DekKurz5:mov       ah,ss:[bp-5]             ; barva textu
         mov       al,176                   ; znak p©ed kurzorem
         mov       ch,0
         mov       cl,bh                    ; po‡ tek kurzoru
         rep       stosw                    ; za‡ tek kurzoru
         mov       al,178                   ; znak kurzoru
         mov       cl,bl                    ; d‚lka kurzoru
         rep       stosw                    ; kurzor
         mov       al,176                   ; znak za kurzorem
         mov       cx,si                    ; celkov  d‚lka ukazatele
         sub       cl,bl                    ; ode‡ten¡ d‚lky kurzoru
         sub       cl,bh                    ; ode‡ten¡ po‡ tku kurzoru
         rep       stosw                    ; konec kurzoru

; ------ n vrat registr–

DekKurz8:mov       sp,bp
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

DekKurz  ENDP

;; -----------------------------------------------------------------------------
;;        zobrazen¡ n povˆdy DS:SI
;; -----------------------------------------------------------------------------
;
;DispHlp  PROC      NEAR
;
;; ------ £schova registr–
;
;         push      ax
;         push      cx
;         push      dx
;         push      si
;         push      es
;
;; ------ zobrazen¡ n povˆdy
;
;         push      ds
;         pop       es
;         mov       cl,80
;         mov       dh,ds:[MaxRow]
;         mov       dl,0
;         mov       ah,ds:[InfoCol1]
;         call      DispTxt                  ; zobrazen¡ textu n povˆdy
;
;; ------ n vrat registr–
;
;         pop       es
;         pop       si
;         pop       dx
;         pop       cx
;         pop       ax
;         ret
;
;DispHlp  ENDP

; -----------------------------------------------------------------------------
;        dek¢dov n¡ ‡¡sla DX:BX zarovnan‚ho vlevo
; -----------------------------------------------------------------------------

DekXNum  PROC      NEAR

         push      si

         push      di
         push      es
         xchg      ax,bx                    ; ‡¡slo LOW
         push      ds
         pop       es
         mov       di,offset BuffNum0       ; buffer k dek¢dov n¡ ‡¡sla
         call      DekNum                   ; dek¢dov n¡ ‡¡sla
         pop       es
         pop       di

         xchg      ax,bx                    ; n vrat ‡¡sla a barvy
         call      DekTxt                   ; dek¢dov n¡ textu
         pop       si
         ret

DekXNum  ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                     Univerz ln¡ obsluha displeje
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

;þ

; *****************************************************************************
;                               StdErr
;              v˜stup textu ASCIIZ na standardn¡ chybov‚ za©¡zen¡
; -----------------------------------------------------------------------------
; VSTUP: DS:SI=text ASCIIZ k v˜stupu
; *****************************************************************************

StdErr   PROC      NEAR

; ------ identifik tor standardn¡ho chybov‚ho za©¡zen¡

         push      bx
         mov       bx,2                     ; identifik tor chybov‚ho za©¡zen¡
         jmp       short StdOut1            ; v˜stup textu

StdErr   ENDP

; *****************************************************************************
;                              StdOut
;                v˜stup textu ASCIIZ na standardn¡ v˜stupn¡ za©¡zen¡
; -----------------------------------------------------------------------------
; VSTUP: DS:SI=text ASCIIZ k v˜stupu
; *****************************************************************************

StdOut   PROC      NEAR

; ------ identifik tor standardn¡ho v˜stupn¡ho za©¡zen¡

         push      bx
         mov       bx,1                     ; identifik tor v˜stupn¡ho za©¡zen¡

; ------ £schova registr–

StdOut1: push      ax
         push      cx
         push      dx
         push      di
         push      es

; ------ p©¡prava registr–

         push      ds
         pop       es
         mov       di,si                    ; za‡ tek textu
         mov       dx,si                    ; za‡ tek textu

; ------ stanoven¡ d‚lky textu

         mov       cx,0ffffh
         cld
         mov       al,0                     ; hledan˜ znak konce
         repne     scasb                    ; nalezen¡ konce textu
         neg       cx                       ; d‚lka textu + 2
         dec       cx
         dec       cx                       ; d‚lka textu

; ------ zobrazen¡ textu

         mov       ah,40h
         int       21h                      ; zobrazen¡ textu

; ------ n vrat registr–

         pop       es
         pop       di
         pop       dx
         pop       cx
         pop       ax

         pop       bx
         ret

StdOut   ENDP

;; *****************************************************************************
;;                             Clear
;;                        vymaz n¡ displeje
;; -----------------------------------------------------------------------------
;; VSTUP: DS=datov˜ segment
;; *****************************************************************************
;
;Clear    PROC      NEAR
;
;         push      ax
;         push      cx
;         push      dx
;
;         mov       ch,ds:[Radku]            ; po‡et © dk–
;         sub       ch,2
;         mov       cl,80                    ; po‡et pozic na © dek
;         mov       al," "                   ; znak pro vymaz n¡
;         mov       ah,ds:[Barva1]           ; z kladn¡ barva
;         mov       dh,1                     ; po‡ te‡n¡ © dek
;
;Clear1:  mov       dl,0                     ; po‡ te‡n¡ pozice
;         call      DispChr                  ; vymaz n¡ © dku
;         inc       dh
;         dec       ch
;         jnz       Clear1                   ; dal¨¡ © dek
;
;         pop       dx
;         pop       cx
;         pop       ax
;         ret
;
;Clear    ENDP

; *****************************************************************************
;                            KurzOff
;                        vypnut¡ kurzoru
; -----------------------------------------------------------------------------
; VSTUP: DS=datov˜ segment
; *****************************************************************************

KurzOff  PROC      NEAR

         push      dx
         mov       dl,0
         mov       dh,ds:[Radku]            ; prvn¡ © dek "za rohem"
         call      SetKurz                  ; nastaven¡ pozice kurzoru
         pop       dx
         ret

KurzOff  ENDP

; *****************************************************************************
;                            SetKurz
;                     nastaven¡ pozice kurzoru
; -----------------------------------------------------------------------------
; VSTUP: DL=pozice kurzoru
;        DH=© dek kurzoru
;        DS=datov˜ segment
; *****************************************************************************

SetKurz  PROC      NEAR

         push      ax
         push      bx
         mov       bh,ds:[AktPage]          ; aktivn¡ videostr nka
         mov       ah,2                     ; funkce - nastaven¡ pozice kurzoru
         call      Int10P                   ; nastaven¡ pozice kurzoru
         pop       bx
         pop       ax
         ret

SetKurz  ENDP

; *****************************************************************************
;                            GetKurz
;                     poskytnut¡ pozice kurzoru
; -----------------------------------------------------------------------------
; VSTUP:DL=pozice kurzoru
;        DH=© dek kurzoru
;        DS=datov˜ segment
; *****************************************************************************

GetKurz  PROC      NEAR

         push      ax
         push      bx
         push      cx
         mov       bh,ds:[AktPage]          ; aktivn¡ videostr nka
         mov       ah,3                     ; funkce - poskytnut¡ pozice kurzoru
         call      Int10P                   ; poskytnut¡ pozice kurzoru
         pop       cx
         pop       bx
         pop       ax
         ret

GetKurz  ENDP

; *****************************************************************************
;                              Disp1Chr
;                    Z pis 1 znaku na obrazovku
; -----------------------------------------------------------------------------
; VSTUP:  AX=znak k z pisu (AL=znak, AH=atribut)
;         DL=pozice
;         DH=© dek
;         DS=datov˜ segment
; VSTUP: DL=nov  pozice
; *****************************************************************************

Disp1Chr PROC      NEAR

         push      cx
         mov       cl,1                     ; 1 znak k z pisu
         call      DispChr                  ; z pis 1 znaku na displej
         pop       cx
         ret

Disp1Chr ENDP

; *****************************************************************************
;                              DispChr
;                 Opakovan˜ z pis znaku na obrazovku
; -----------------------------------------------------------------------------
; VSTUP:  AX=znak k z pisu (AL=znak, AH=atribut)
;         CL=po‡et znak– k z pisu
;         DL=po‡ te‡n¡ pozice
;         DH=po‡ te‡n¡ © dek
;         DS=datov˜ segment
; VSTUP: DL=nov  pozice
; *****************************************************************************

DispChr  PROC      NEAR

; ------ kontrola maxim ln¡ pozice

         cmp       dl,79                    ; kontrola maxim ln¡ pozice
         ja        DispChr8                 ; pozice p©ekro‡ena
         cmp       dh,ds:[MaxRow]           ; kontrola maxim ln¡ho © dku
         ja        DispChr8                 ; © dek p©ekro‡en

; ------ £schova registr–

         push      ax                       ; £schova AX
         push      bx                       ; £schova BX
         push      cx                       ; £schova CX
         push      dx                       ; £schova DX
         push      di                       ; £schova DI
         push      es                       ; £schova ES
         mov       bx,ax                    ; £schova znaku k z pisu
         xor       ch,ch                    ; CX = po‡et znak–

; ------ omezen¡ po‡tu znak– (do konce © dku)

         mov       al,80                    ; po‡et pozic na © dku
         sub       al,dl                    ; zbyl˜ po‡et mo‘n˜ch pozic
         cmp       al,cl                    ; p©ekro‡en po‡et znak– ?
         jae       DispChr1                 ; nen¡ p©ekro‡en po‡et znak–
         mov       cl,al                    ; omezen¡ po‡tu znak–

; ------ v˜po‡et adresy ve videopamˆti

DispChr1:les       di,ds:[AdrVRAM]          ; adresa za‡ tku VRAM
         mov       al,80                    ; po‡et znak– na © dek
         mul       dh                       ; p©epo‡et © dk– na pozice
         xor       dh,dh                    ; DX=pozice
         add       ax,dx                    ; p©i‡ten¡ pozice
         shl       ax,1                     ; * 2 = offset ve videostr nce
         add       di,ax                    ; adresa ve videopamˆti

; ------ p©¡prava dat; zji¨tˆn¡, zda se kontroluje snˆ‘en¡

         sti                                ; p©eru¨en¡ povoleno
         cld                                ; smˆr posunu ukazatele nahoru
         cmp       byte ptr ds:[ChckSnow],0 ; prov d¡ se kontrola snˆ‘en¡ ?
         je        DispChr6                 ; neprov d¡ se kontrola snˆ‘en¡
         jcxz      DispChr7                 ; nen¡ ‘ dn˜ znak k p©enosu

; ------ p©enos dat s obsluhou snˆ‘en¡

         mov       dx,ds:[PortCRT]          ; stavov˜ registr ©adi‡e displeje
DispChr2:in        al,dx                    ; ‡ten¡ stavu video©adi‡e 6845
         test      al,8                     ; prob¡h  vertik ln¡ impuls ?
         jnz       DispChr5                 ; prob¡h  vertik ln¡ impuls
DispChr3:in        al,dx                    ; ‡ten¡ stavu video©adi‡e 6845
         rcr       al,1                     ; test horizont ln¡ho zpˆtn‚ho bˆhu
         jc        DispChr3                 ; je horiz. zpˆtn˜ bˆh - ‡ek n¡
DispChr4:in        al,dx                    ; ‡ten¡ stavu video©adi‡e 6845
         rcr       al,1                     ; test horiz. zpˆtn‚ho bˆhu
         jnc       DispChr4                 ; ‡ek n¡ na horiz. zpˆtn˜ bˆh
DispChr5:mov       ax,bx                    ; znak k zobrazen¡
         stosw                              ; z pis jednoho znaku
         loop      DispChr2                 ; z pis dal¨¡ho znaku

; ------ z pis znaku bez kontroly snˆ‘en¡

DispChr6:mov       ax,bx                    ; znak k zobrazen¡
         rep       stosw                    ; z pis znaku bez kontroly snˆ‘en¡

; ------ n vrat registr–, konec

DispChr7:pop       es                       ; n vrat ES
         pop       di                       ; n vrat DI
         pop       dx                       ; n vrat DX
         pop       cx                       ; n vrat CX
         pop       bx                       ; n vrat BX
         pop       ax                       ; n vrat AX

DispChr8:add       dl,cl                    ; zv˜¨en¡ pozice
         ret

DispChr  ENDP

; *****************************************************************************
;                              DispStin
;                           Zobrazen¡ st¡nu
; -----------------------------------------------------------------------------
; VSTUP:  CL=po‡et pozic k zobrazen¡ st¡nu
;         DL=po‡ te‡n¡ pozice
;         DH=po‡ te‡n¡ © dek
;         DS=datov˜ segment
; VSTUP: DL=nov  pozice
; *****************************************************************************

DispStin PROC      NEAR

; ------ kontrola maxim ln¡ pozice

         cmp       dl,79                    ; kontrola maxim ln¡ pozice
         ja        DispStn8                 ; pozice p©ekro‡ena
         cmp       dh,ds:[MaxRow]           ; kontrola maxim ln¡ho © dku
         ja        DispStn8                 ; © dek p©ekro‡en

; ------ £schova registr–

         push      ax                       ; £schova AX
         push      bx                       ; £schova BX
         push      cx                       ; £schova CX
         push      dx                       ; £schova DX
         push      di                       ; £schova DI
         push      es                       ; £schova ES
         xor       ch,ch                    ; CX = po‡et znak–

; ------ omezen¡ po‡tu znak– (do konce © dku)

         mov       al,80                    ; po‡et pozic na © dku
         sub       al,dl                    ; zbyl˜ po‡et mo‘n˜ch pozic
         cmp       al,cl                    ; p©ekro‡en po‡et znak– ?
         jae       DispStn1                 ; nen¡ p©ekro‡en po‡et znak–
         mov       cl,al                    ; omezen¡ po‡tu znak–

; ------ v˜po‡et adresy ve videopamˆti

DispStn1:les       di,ds:[AdrVRAM]          ; adresa za‡ tku VRAM
         mov       al,80                    ; po‡et znak– na © dek
         mul       dh                       ; p©epo‡et © dk– na pozice
         xor       dh,dh                    ; DX=pozice
         add       ax,dx                    ; p©i‡ten¡ pozice
         shl       ax,1                     ; * 2 = offset ve videostr nce
         add       di,ax                    ; adresa ve videopamˆti

; ------ zobrazen¡ st¡nu

         jcxz      DispStn7
         mov       al,7
         inc       di
DispStn6:mov       byte ptr es:[di],al
         inc       di
         inc       di
         loop      DispStn6

; ------ n vrat registr–, konec

DispStn7:pop       es                       ; n vrat ES
         pop       di                       ; n vrat DI
         pop       dx                       ; n vrat DX
         pop       cx                       ; n vrat CX
         pop       bx                       ; n vrat BX
         pop       ax                       ; n vrat AX

DispStn8:add       dl,cl                    ; zv˜¨en¡ pozice
         ret

DispStin ENDP

; *****************************************************************************
;                              DispTxt
;                  Zobrazen¡ textov‚ho ©etˆzce na obrazovce
; -----------------------------------------------------------------------------
; VSTUP:  AH=atribut barvy
;         CL=po‡et znak– textu
;         DL=po‡ te‡n¡ pozice
;         DH=po‡ te‡n¡ © dek
;         DS=datov˜ segment
;         ES:SI=adresa textov‚ho ©etˆzce (bez atribut–)
; VSTUP: DL=nov  pozice
; *****************************************************************************

DispTxt  PROC      NEAR

; ------ kontrola maxim ln¡ pozice

         cmp       dl,79                    ; kontrola maxim ln¡ pozice
         ja        DispTxt8                 ; pozice p©ekro‡ena
         cmp       dh,ds:[MaxRow]           ; kontrola maxim ln¡ho © dku
         ja        DispTxt8                 ; © dek p©ekro‡en

; ------ £schova registr–

         push      ax                       ; £schova AX
         push      cx                       ; £schova CX
         push      dx                       ; £schova DX
         push      si                       ; £schova SI
         push      di                       ; £schova DI
         push      ds                       ; £schova DS
         push      es                       ; £schova ES
         xor       ch,ch                    ; CX = po‡et znak–

; ------ omezen¡ po‡tu znak– (do konce © dku)

         mov       al,80                    ; po‡et pozic na © dku
         sub       al,dl                    ; zbyl˜ po‡et mo‘n˜ch pozic
         cmp       al,cl                    ; p©ekro‡en po‡et znak– ?
         jae       DispTxt1                 ; nen¡ p©ekro‡en po‡et znak–
         mov       cl,al                    ; omezen¡ po‡tu znak–

; ------ v˜po‡et adresy ve videopamˆti

DispTxt1:push      es                       ; £schova segmentu zdrojov‚ho textu
         les       di,ds:[AdrVRAM]          ; adresa za‡ tku VRAM
         push      ax
         mov       al,80                    ; po‡et znak– na © dek
         mul       dh                       ; p©epo‡et © dk– na pozice
         xor       dh,dh                    ; DX=pozice
         add       ax,dx                    ; p©i‡ten¡ pozice
         shl       ax,1                     ; * 2 = offset ve videostr nce
         add       di,ax                    ; adresa ve videopamˆti
         pop       ax

; ------ p©¡prava dat; zji¨tˆn¡, zda se kontroluje snˆ‘en¡

         sti                                ; p©eru¨en¡ povoleno
         cld                                ; smˆr posunu ukazatele nahoru
         mov       dx,ds:[PortCRT]          ; stavov˜ registr ©adi‡e displeje
         cmp       byte ptr ds:[ChckSnow],0 ; prov d¡ se kontrola snˆ‘en¡ ?
         pop       ds                       ; segment zdrojov‚ adresy
         jcxz      DispTxt7                 ; nen¡ ‘ dn˜ znak k p©enosu
         je        DispTxt6                 ; neprov d¡ se kontrola snˆ‘en¡

; ------ p©enos dat s obsluhou snˆ‘en¡

DispTxt2:in        al,dx                    ; ‡ten¡ stavu video©adi‡e 6845
         test      al,8                     ; prob¡h  vertik ln¡ impuls ?
         jnz       DispTxt5                 ; prob¡h  vertik ln¡ impuls
DispTxt3:in        al,dx                    ; ‡ten¡ stavu video©adi‡e 6845
         rcr       al,1                     ; test horizont ln¡ho zpˆtn‚ho bˆhu
         jc        DispTxt3                 ; je horiz. zpˆtn˜ bˆh - ‡ek n¡
DispTxt4:in        al,dx                    ; ‡ten¡ stavu video©adi‡e 6845
         rcr       al,1                     ; test horiz. zpˆtn‚ho bˆhu
         jnc       DispTxt4                 ; ‡ek n¡ na horiz. zpˆtn˜ bˆh
DispTxt5:lodsb                              ; na‡ten¡ znaku k zobrazen¡
         stosw                              ; p©enos jednoho znaku
         loop      DispTxt2                 ; p©enos dal¨¡ho znaku
         jmp       short DispTxt7

; ------ p©enos dat bez kontroly snˆ‘en¡

DispTxt6:lodsb                              ; znak k zobrazen¡
         stosw                              ; ulo‘en¡ znaku
         loop      DispTxt6                 ; p©enos dat bez kontroly snˆ‘en¡

; ------ n vrat registr–, konec

DispTxt7:pop       es                       ; n vrat ES
         pop       ds                       ; n vrat DS
         pop       di                       ; n vrat DI
         pop       si                       ; n vrat SI
         pop       dx                       ; n vrat DX
         pop       cx                       ; n vrat CX
         pop       ax                       ; n vrat AX

DispTxt8:add       dl,cl                    ; zv˜¨en¡ pozice
         ret

DispTxt  ENDP

; *****************************************************************************
;                              DispStr
;                 Zobrazen¡ textov‚ho ©etˆzce na obrazovku
; -----------------------------------------------------------------------------
; VSTUP:  CL=po‡et znak– textu
;         DL=po‡ te‡n¡ pozice
;         DH=po‡ te‡n¡ © dek
;         DS=datov˜ segment
;         ES:SI=adresa textov‚ho ©etˆzce (1 znak = 2 bajty = znak a atribut)
; VSTUP: DL=nov  pozice
; *****************************************************************************

DispStr  PROC      NEAR

; ------ kontrola maxim ln¡ pozice

         cmp       dl,79                    ; kontrola maxim ln¡ pozice
         ja        DispStr8                 ; pozice p©ekro‡ena
         cmp       dh,ds:[MaxRow]           ; kontrola maxim ln¡ho © dku
         ja        DispStr8                 ; © dek p©ekro‡en

; ------ £schova registr–

         push      ax                       ; £schova AX
         push      cx                       ; £schova CX
         push      dx                       ; £schova DX
         push      si                       ; £schova SI
         push      di                       ; £schova DI
         push      ds                       ; £schova DS
         push      es                       ; £schova ES
         xor       ch,ch                    ; CX = po‡et znak–

; ------ omezen¡ po‡tu znak– do konce © dku

         mov       al,80                    ; po‡et pozic na © dku
         sub       al,dl                    ; zbyl˜ po‡et mo‘n˜ch pozic
         cmp       al,cl                    ; p©ekro‡en po‡et znak– ?
         jae       DispStr1                 ; nen¡ p©ekro‡en po‡et znak–
         mov       cl,al                    ; omezen¡ po‡tu znak–

; ------ v˜po‡et adresy ve videopamˆti

DispStr1:push      es                       ; £schova segmentu zdrojov‚ho textu
         les       di,ds:[AdrVRAM]          ; adresa za‡ tku VRAM
         mov       al,80                    ; po‡et znak– na © dek
         mul       dh                       ; p©epo‡et © dk– na pozice
         xor       dh,dh                    ; DX=pozice
         add       ax,dx                    ; p©i‡ten¡ pozice
         shl       ax,1                     ; * 2 = offset ve videostr nce
         add       di,ax                    ; adresa ve videopamˆti

; ------ p©¡prava dat; zji¨tˆn¡, zda se kontroluje snˆ‘en¡

         cld                                ; smˆr posunu ukazatele nahoru
         sti                                ; povolen¡ p©eru¨en¡
         mov       dx,ds:[PortCRT]          ; stavov˜ registr ©adi‡e displeje
         cmp       byte ptr ds:[ChckSnow],0 ; prov d¡ se kontrola snˆ‘en¡ ?
         pop       ds                       ; segment zdrojov‚ adresy
         je        DispStr6                 ; neprov d¡ se kontrola snˆ‘en¡
         jcxz      DispStr7                 ; nen¡ ‘ dn˜ znak k p©enosu

; ------ p©enos dat s obsluhou snˆ‘en¡

DispStr2:in        al,dx                    ; ‡ten¡ stavu video©adi‡e 6845
         test      al,8                     ; prob¡h  vertik ln¡ impuls ?
         jnz       DispStr5                 ; prob¡h  vertik ln¡ impuls
DispStr3:in        al,dx                    ; ‡ten¡ stavu video©adi‡e 6845
         rcr       al,1                     ; test horizont ln¡ho zpˆtn‚ho bˆhu
         jc        DispStr3                 ; je horiz. zpˆtn˜ bˆh - ‡ek n¡
DispStr4:in        al,dx                    ; ‡ten¡ stavu video©adi‡e 6845
         rcr       al,1                     ; test horiz. zpˆtn‚ho bˆhu
         jnc       DispStr4                 ; ‡ek n¡ na horiz. zpˆtn˜ bˆh
DispStr5:movsw                              ; p©enos jednoho znaku
         loop      DispStr2                 ; p©enos dal¨¡ho znaku

; ------ p©enos dat bez kontroly snˆ‘en¡

DispStr6:rep       movsw                    ; p©enos dat bez kontroly snˆ‘en¡

; ------ n vrat registr–, konec

DispStr7:pop       es                       ; n vrat ES
         pop       ds                       ; n vrat DS
         pop       di                       ; n vrat DI
         pop       si                       ; n vrat SI
         pop       dx                       ; n vrat DX
         pop       cx                       ; n vrat CX
         pop       ax                       ; n vrat AX

DispStr8:add       dl,cl                    ; zv˜¨en¡ pozice
         ret

DispStr  ENDP

; *****************************************************************************
;                             InitVmod
;                      Inicializace videom¢du
;             (po‘aduje, aby byla rozpozn na karta EGA/VGA)
; -----------------------------------------------------------------------------
; VSTUP:  DS=datov˜ segment
; VSTUP: CY=videom¢d byl zmˆnˆn
;         neni‡¡ ‘ dn˜ registr
; *****************************************************************************

InitVMod PROC      NEAR

; ------ £schova registr–

         push      ax

; ------ kontrola, zda je povolen˜ videom¢d

         call      AktPDisp                 ; aktualizace parametr– displeje
         cmp       al,7                     ; MDA ?
         je        InitVM5                  ; povolen˜ videom¢d
         cmp       al,2
         je        InitVM5                  ; CGA
         cmp       al,3
         je        InitVM5                  ; CGA

; ------ stanoven¡ implicitn¡ho videom¢du

         int       11h                      ; poskytnut¡ tabulky vybaven¡
         and       al,30h                   ; inicializa‡n¡ videom¢d
         cmp       al,30h                   ; videom¢d 80x25 monochrom. ?
         mov       al,7                     ; videom¢d MDA
         je        InitVM1                  ; je videom¢d MDA
         mov       al,3                     ; jinak videom¢d CGA
InitVM1: call      SetVMode                 ; nastaven¡ po‘adovan‚ho videom¢du
         stc                                ; p©¡znak zmˆny videom¢du

; ------ n vrat registr–

InitVM5: pop       ax
         ret

InitVMod ENDP

; *****************************************************************************
;                               SetVMode
;                          Nastaven¡ videom¢du
;                (doporu‡uje se rozpozn n¡ videokarty EGA/VGA)
; -----------------------------------------------------------------------------
; VSTUP:  AL=po‘adovan˜ videom¢d
;         DS=datov˜ segment
; VSTUP: AL=nov˜ aktu ln¡ videom¢d
;         CY=videom¢d nenastaven
;         neni‡¡ ‘ dn˜ registr
; *****************************************************************************

SetVMode PROC      NEAR

; ------ £schova registr–

         push      bx
         push      ax                       ; £schova po‘adovan‚ho videom¢du

; ------ nastaven¡ videom¢du

         xor       ah,ah                    ; funkce nastaven¡ videom¢du
         call      Int10P                   ; nastaven¡ videom¢du
         call      AktPDisp                 ; poskytnut¡ aktivn¡ho videom¢du
         mov       bl,al                    ; £schova aktu ln¡ho videom¢du

; ------ n vrat registr–, zhodnocen¡ v˜sledku

         pop       ax                       ; n vrat po‘adovan‚ho videom¢du
         xchg      al,bl                    ; AL <- nov˜ videom¢d
         cmp       al,bl                    ; souhlas¡ videom¢d s po‘adovan˜m ?
         je        SetVMod1                 ; videom¢d nastaven OK
         stc                                ; p©¡znak chyby nastaven¡ videom¢du
SetVMod1:pop       bx
         ret

SetVMode ENDP

; *****************************************************************************
;                               AktPDisp
;            Atualizace parametr– displeje podle videom¢du
;
; Tato procedura pou‘¡v  k aktualizaci parametr– pouze operace v pamˆti,
; nepou‘¡v  ‘ dn‚ p©eru¨en¡. Vy‘aduje rozpozn n¡ karty EGA/VGA.
; -----------------------------------------------------------------------------
; VSTUP:  DS=datov˜ segment
; VSTUP: AL=aktu ln¡ videom¢d
;         neni‡¡ ‘ dn˜ registr
; *****************************************************************************

AktPDisp PROC    NEAR

; ------ £schova registr–

         push      es
         push      bx

; ------ zji¨tˆn¡ videom¢du

         mov       bx,40h
         mov       es,bx                    ; ES <- 40h segment dat BIOS
         mov       al,es:[49h]              ; aktu ln¡ videom¢d
         and       al,7fh                   ; zru¨en¡ p©¡znaku nemaz n¡ displeje

; ------ zji¨tˆn¡ videostr nky

         mov       bl,es:[62h]              ; aktivn¡ videostr nka
         mov       ds:[AktPage],bl          ; £schova aktivn¡ videostr nky

; ------ zji¨tˆn¡ adresy videopamˆti a p©ednastaven¡ p©¡znaku obsluhy snˆ‘en¡

         mov       byte ptr ds:[ChckSnow],0 ; zru¨en¡ p©¡znaku obsluhy snˆ‘en¡
         mov       word ptr ds:[AdrVRAM+2],0B000h ; segment videopamˆti pro MDA
         cmp       al,7                     ; je videom¢d MDA ?
         je        AktParD1                 ; je videom¢d MDA
         mov       word ptr ds:[AdrVRAM+2],0B800h ; segment videopamˆti pro CGA
         inc       byte ptr ds:[ChckSnow]   ; nastav. p©¡znaku obsluhy snˆ‘en¡
AktParD1:mov       bx,es:[4eh]              ; po‡ te‡n¡ adresa videopamˆti
         mov       word ptr ds:[AdrVRAM],bx ; po‡ te‡n¡ adresa videopamˆti

; ------ zji¨tˆn¡ po‡tu © dk– a up©esnˆn¡ p©¡znaku snˆ‘en¡

         mov       bl,24                    ; p©ednastaven¡ na 25 © dk–
         cmp       byte ptr ds:[EgaVga],0   ; je karta EGA, VGA ?
         je        AktParD6                 ; nen¡ karta EGA/VGA
         cmp       byte ptr es:[84h],15     ; je £daj po‡tu © dk– platn˜ ?
         jb        AktParD5                 ; £daj po‡tu © dk– nen¡ platn˜
         cmp       byte ptr es:[84h],59     ; maxim ln¡ © dek displeje
         ja        AktParD5
         mov       bl,es:[84h]              ; po‡et © dk– displeje - 1
AktParD5:mov       bh,es:[87h]              ; p©¡znak kontroly snˆ‘en¡
         shr       bh,1
         shr       bh,1
         and       bh,1                     ; p©¡znak obsluhy snˆ‘en¡
         mov       ds:[ChckSnow],bh         ; nastaven¡ p©¡znaku kontroly snˆ‘.
AktParD6:mov       ds:[MaxRow],bl           ; maxim ln¡ © dek na displeji
         inc       bx                       ; po‡et © dk– celkem
         mov       ds:[Radku],bl            ; po‡et © dk– celkem
         sub       bl,2
         mov       byte ptr ds:[SeznRad],bl ; po‡et © dk– str nky seznamu

; ------ zji¨tˆn¡ b zov‚ ©¡dic¡ adresy displeje CRT

         mov       bx,es:[63h]              ; b zov  adresa portu ©adi‡e CRT
         add       bx,6
         mov       ds:[PortCRT],bx          ; stavov˜ registr ©adi‡e displeje

; ------ n vrat registr–

         pop       bx
         pop       es
         ret

AktPDisp ENDP

; *****************************************************************************
;                              DetCard
;                      detekce videokarty EGA/VGA
; -----------------------------------------------------------------------------
; VSTUP:  DS=datov˜ segment
;         neni‡¡ ‘ dn˜ registr
; *****************************************************************************

DetCard  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx

; ------ test videokarty EGA/VGA

         mov       ah,12h                   ; slu‘ba EGA/VGA - informace o EGA
         mov       bx,4510h                 ; poskytnut¡ informac¡ o EGA
         call      Int10P                   ; poskytnut¡ informac¡ o EGA/VGA
         mov       byte ptr ds:[EgaVga],0   ; zru¨en¡ p©¡znaku karty EGA/VGA
         cmp       bh,1                     ; kontrola navr cen‚ho m¢du displeje
         ja        DetectC1                 ; nen¡ to karta EGA/VGA
         cmp       bl,5                     ; maxim lnˆ povolen 1 MB pamˆti
         ja        DetectC1                 ; nen¡ to karta EGA/VGA
         mov       byte ptr ds:[EgaVga],1   ; p©¡znak karty EGA/VGA

; ------ n vrat registr–

DetectC1:pop       cx
         pop       bx
         pop       ax
         ret

DetCard  ENDP

; *****************************************************************************
;                              Int10P
;                Vol n¡ INT 10h s £schovou registr–
; -----------------------------------------------------------------------------
;
; *****************************************************************************

Int10P   PROC      NEAR

         pushf
         push      si
         push      di
         push      bp
         push      ds
         push      es

         int       10h

         pop       es
         pop       ds
         pop       bp
         pop       di
         pop       si
         popf
         ret

Int10P   ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                                    Data
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

;þ

; ------ texty

HelpTxt  db        'ÉÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ HLEDEJ  verze 1.12 ÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ»'
HelpTx11 db        'º                                                                              º'
         db        'º SYNTAXE zadani souboru k hledani:   ADRESAR\maska1 maska2 ..... / PARAMETRY  º'
         db        'º ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ º'
         db        'º                                                                              º'
         db        'º ADRESAR   je vychozi adresar hledani. K sestaveni plne specifikace vychoziho º'
         db        'º           adresare  se  pouzije  aktivni  adresar.  Pouze v pripade, ze neni º'
         db        'º           adresar ani disk zadan vubec  (jsou  zadany  pouze  soubory), bude º'
         db        'º           prohledavan cely disk. Pri vyhledavani se  postupuje  z  vychoziho º'
         db        'º           adresare do vsech podadresaru.  Za  jmeno  vychoziho  adresare  se º'
         db        'º           povazuje text od zacatku radku po posledni znak "\"  pred zacatkem º'
         db        'º           parametru v radku (resp. ":", je-li zadan pouze disk).             º'
         db        'º                                                                              º'
         db        'º maska     maska specifikace souboru.  V zadani  masky pro hledani lze pouzit º'
         db        'º           nahradni znaky "?" (=na pozici muze byt libovolny zmak) a "*" (=na º'
         db        'º           pozici muze byt skupina libovolnych znaku).  Znak "*" muze byt (na º'
         db        'º           rozdil od bezne konvence v DOS) uveden i uprostred jmena vicekrat. º'
         db        'º           Neni-li  zadana  pripona jmena souboru (=neni zadana tecka), mohou º'
         db        'º           se  zadane  znaky  vyskytovat jak ve jmene, tak i v pripone jmena. º'
         db        'º           Masek specifikace souboru  lze  uvest  vice  najednou. Masky se od º'
         db        'º           sebe navzajem oddeluji znakem mezery. Prvni maska muze byt uvedena º'
         db        'º           bezprostredne za koncovym znakem cesty "\".                        º'
         db        'º                                                                              º'
         db        'º                                                                              º'
         db        'º                  PARAMETRY - nasleduji za oddelovacim znakem "/"             º'
         db        'º               ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ           º'
         db        'º D datum   podminkou hledani bude datum souboru (adresare).  Datum je  zadano º'
         db        'º           ve tvaru DEN.MESIC.ROK (jako oddelovaci znaky lze pouzit tez znaky º'
         db        'º           ":", "/" nebo ","). Rok lze zadat bud dvojciferne  (posledni dvoj- º'
         db        'º           cisli) nebo plnym zadanim  4 cislic.  Kterykoliv  z udaju data lze º'
         db        'º           vypustit (nebo nahradit znakem "?"), tento udaj se nebude porovna- º'
         db        'º           vat. Bude-li zadan pouze parametr  D  bez zadani data,  pouzije se º'
         db        'º           dnesni den. Datum lze zadat  tez  jako  interval  DATUM1 - DATUM2. º'
         db        'º           Kteroukoliv stranu intervalu lze vynechat.                         º'
         db        'º                                                                              º'
         db        'º T cas     podminkou hledani  bude  cas  souboru.  Cas  se  zadava  ve  tvaru º'
         db        'º           HODINA:MINUTA:SEKUNDA. Vse  ostatni  plati  stejne  jako  u  data. º'
         db        'º           Uvedenim parametru T bez zadani casu se pouzije aktualni hodina.   º'
         db        'º                                                                              º'
         db        'º N cislo   podminkou hledani bude velikost souboru  (v bajtech). Velikost lze º'
         db        'º           zadat bud jako urcitou hodnotu nebo jako interval OD - DO.         º'
         db        'º                                                                              º'
         db        'º R R+ r+   podminkou hledani je, ze soubor ma nastaven atribut R/O.           º'
         db        'º r R- r-   podminkou hledani je, ze soubor ma vynulovan atribut R/O.          º'
         db        'º H H+ h+   podminkou hledani je, ze soubor ma nastaven atribut HID.           º'
         db        'º h H- h-   podminkou hledani je, ze soubor ma vynulovan atribut HID           º'
         db        'º S S+ s+   podminkou hledani je, ze soubor ma nastaven atribut SYS.           º'
         db        'º s S- s-   podminkou hledani je, ze soubor ma vynulovan atribut SYS.          º'
         db        'º A A+ a+   podminkou hledani je, ze soubor ma nastaven atribut ARC.           º'
         db        'º a A- a-   podminkou hledani je, ze soubor ma vynulovan atribut ARC.          º'
         db        'º C C+ c+   budou vyhledavany jen adresare (nastaven atribut DIR).             º'
         db        'º c C- c-   budou vyhledavany jen soubory (vynulovan atribut DIR).             º'
         db        'º z Z       nebudou prohledavany podadresare.                                  º'
         db        'º                                                                              º'
         db        'º "text"    podminkou hledani bude text obsazeny  v souboru.  Pri  vyhledavani º'
         db        'º           textu se nerozlisuji velka a mala pismena  (to neplati o narodnich º'
         db        'º           znacich s diakritikou). V textu lze pouzit jakekoliv znaky s kodem º'
         db        'º           od 0 do 255 (lze zadavat dekadicky metodou Alt-cislo, znak s kodem º'
         db        'º           0 lze zadat jako Ctrl-@).  Obsahuje-li text znak uvozovek ",  musi º'
         db        'º           byt  tento  znak  uvnitr  textu  zadan  jako  dva  znaky  uvozovek º'
         db        'º           besprostredne za sebou. Nebude-li text  ukoncen  znakem  uvozovek, º'
         db        'º           budou koncove mezery ignorovany.                                   º'
         db        'º                                                                              º'
         db        'º Priklady:                                                                    º'
         db        'º ÄÄÄÄÄÄÄÄÄ                                                                    º'
         db        'º *.exe *.com /D ?.4.92     - vsechny programy EXE a COM z dubna 1992          º'
         db        'º juk /D - 12.5.            - soubory do 12.5. kazdeho roku, zacinajici "JUK"  º'
         db        'º *mac*                     - soubory obsahujici text "MAC" ve jmene           º'
         db        'º *.EXE *.COM /"Runtime error" - vsechny programy napsane v Pascalu            º'
         db        'º *.* /C d12.3.             - adresare vytvorene dne 12.3.                     º'
         db        'º *.BAK /d                  - zalozni soubory BAK dnes vytvorene               º'
         db        'º *.TXT /"DOS"              - textove soubory obsahujici text "DOS"            º'
         db        'º *.* /h+ c+                - vsechny skryte (uzamcene) adresare               º'
         db        'º *.zip *.arj /n362000-     - archivni soubor vetsi nez disketa 360K           º'
HelpTx12 db        'º                                                                              º'
         db        'ÈÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼'
HelpTxt0 label     byte

HelpTop  dw        offset HelpTxt           ; adresa za‡ tku zobrazen‚ n povˆdy
HelpTopN dw        0                        ; ‡¡slo po‡ te‡n¡ho © dku

LicTxt   db        '  HLEDEJ V 1.12 - vyhledavani souboru                     (c) Miroslav Nemecek  '

MemTxt   db        'Nedostatek pameti !',13,7,10,0

HelpTxt1 db        ' ESC=navrat ³ ENTER=zahajeni hledani ³ F1=napoveda k syntaxi zadani             '
HelpTxt2 db        ' ESC=navrat ³ listovani napovedou: sipky nahoru/dolu, Home/End, PageUp/PageDown '
HelpTxt3 db        ' ESC=konec ³ ENTER=zmena adresare ³ INSERT,DELETE=oznac ³ F3=zobraz ³ F7=hledej '
HelpTxt4 db        ' ESC=konec ³ ENTER=vypis seznamu  ³ INSERT,DELETE=oznac ³ F3=zobraz ³ F7=hledej '
HelpTxt5 db        ' ESC,F3=navrat z funkce zobrazeni souboru                                       '
HelpTxt6 db        ' Cekejte, ukladam vypis oznacenych souboru ...                                  '

SegPSP   dw        0                        ; segment PSP

UvTxt    db        'HLEDEJ V1.12 - vyhledavani souboru; (c) Miroslav Nemecek'
CRLFTxt  db        13,10,0

; -----------------------------------------------------------------------------
;        barvy
; -----------------------------------------------------------------------------

ColCol   label     byte

; ------ n povˆda

HelpColM db        1fh ; 0fh                ; barva hlavn¡ n povˆdy
HelpCol  db        30h ; 70h                ; barva mal‚ n povˆdy

; ------ polo‘ky seznamu (zachovat po©ad¡ !)

Barva1   db        1bh ; 07h                ; z kladn¡ barva
Barva2   db        30h ; 70h                ; barva kurzoru
Barva3   db        1eh ; 0fh                ; barva vysv¡cen‚ polo‘ky
Barva4   db        3eh ; 70h                ; kurzor na vysv¡cen‚ polo‘ce
Barva5   db        0fh ; 0fh                ; barva cesty (jenom text)
Barva6   db        1dh ; 07h                ; barva ©¡dic¡ch znak– textu

; ------ okno voleb

BarvaR1  db        70h ; 70h                ; z kladn¡ barva r mu voleb
BarvaR2  db        30h ; 07h                ; barva kurzoru voleb
BarvaR3  db        71h ; 70h                ; barva editovan‚ho © dku

; ------ informa‡n¡ © dek

InfoCol1 db        70h ; 70h                ; bˆ‘n  barva informa‡n¡ho © dku
InfoCol2 db        74h ; 70h                ; zv˜raznˆn  barva inform. © dku

ColCol0  label     byte

; -----------------------------------------------------------------------------
;        barvy pro monochrom. displej
; -----------------------------------------------------------------------------

ColMono  label     byte

; ------ n povˆda

         db        0fh                      ; barva hlavn¡ n povˆdy
         db        70h                      ; barva mal‚ n povˆdy

; ------ polo‘ky seznamu (zachovat po©ad¡ !)

         db        07h                      ; z kladn¡ barva
         db        70h                      ; barva kurzoru
         db        0fh                      ; barva vysv¡cen‚ polo‘ky
         db        70h                      ; kurzor na vysv¡cen‚ polo‘ce
         db        0fh                      ; barva cesty
         db        07h                      ; barva ©¡dic¡ch znak– textu

; ------ okno voleb

         db        70h                      ; z kladn¡ barva r mu voleb
         db        07h                      ; barva kurzoru voleb
         db        70h                      ; barva editovan‚ho © dku

; ------ informa‡n¡ © dek

         db        70h                      ; bˆ‘n  barva informa‡n¡ho © dku
         db        70h                      ; zv˜raznˆn  barva inform. © dku

; -----------------------------------------------------------------------------
;        Parametry displeje a videom¢du
; -----------------------------------------------------------------------------

         EVEN
AdrVRAM  dd        0b8000000h               ; adresa videostr nky displeje
PortCRT  dw        3dah                     ; b zov  adresa portu ©adi‡e CRT
Radku    db        25                       ; po‡et © dk– celkem
MaxRow   db        24                       ; maxim ln¡ © dek na displeji
EgaVga   db        0                        ; p©¡znak, ‘e je karta EGA/VGA
ChckSnow db        0                        ; p©¡znak, ‘e se kontroluje snˆ‘en¡
AktPage  db        0                        ; aktivn¡ videostr nka

; -----------------------------------------------------------------------------
;        obsluha hled n¡ soubor–
; -----------------------------------------------------------------------------

                                          ;* obsluhovan˜ soubor/adres ©
FilePthN dw        0                        ; d‚lka textu adres ©e + soubor
FilePath db        MAXLINE dup(0)           ; buffer adres ©e + soubor

FileFCB  db        11 dup(0)                ; jm‚no souboru ve tvaru FCB

; ------ podm¡nky pro vyhled n¡ souboru

                                          ;* buffer masek obsahuje texty masek
                                          ;* konvertovan‚ na velk  p¡smena a
                                          ;* navz jem oddˆlen‚ jednou mezerou
         EVEN
FileMskN dw        0                        ; d‚lka textu v bufferu masek
FileMask db        MAXLINE dup(0)           ; buffer masek

         EVEN                               ; pro zrychlen¡ sud  adresa
SrcInit1 label     byte                     ; za‡ tek tabulky parametr– hled n¡

SrcAtr   db        0                        ; atributy hledan‚ho souboru
SrcAtrM  db        0                        ; maska atribut– hledan‚ho souboru

SrcTim1  dw        0                        ; po‡ te‡n¡ ‡as souboru
SrcTim1M dw        0                        ; maska po‡ te‡n¡ho ‡asu souboru
SrcTim2  dw        0                        ; koncov˜ ‡as souboru
SrcTim2M dw        0                        ; maska koncov‚ho ‡asu souboru

SrcDat1  dw        0                        ; po‡ te‡n¡ datum souboru
SrcDat1M dw        0                        ; maska po‡ te‡n¡ho data souboru
SrcDat2  dw        0                        ; koncov‚ datum souboru
SrcDat2M dw        0                        ; maska koncov‚ho data souboru

SrcSiz1  dd        0                        ; po‡ te‡n¡ velikost souboru
SrcSiz2  dd        0                        ; koncov  velikost souboru

SrcTextN dw        0                        ; d‚lka hledan‚ho textu
SrcText  db        MAXLINE dup(0)           ; buffer hledan‚ho textu

SrcInit2 label     byte                     ; konec tabulky parametr– hled n¡

HledTxt1 dw        HledTxt2-$
         db        'Prohledavam ',0
HledTxt2 label     word

CekTxt   db        'Hledam...',13,0
CekTxt0  db        '         ',13,0

FndTxt0  db        'Nenalezen zadny zadany soubor !',13,10,0
FndTxt1  db        'nalezeno celkem  : ',0
FndTxt2  db        ' souboru/adresaru',13,10
         db        'soucet velikosti : ',0
FndTxt3  db        ' bajtu',13,10
         db        'obsazena kapacita: ',0
FndTxt4  db        ' bajtu',13,10,0


TxtSou1  db        ' Souboru: ',0
TxtSou2  db        179,'Soucet: ',0
TxtSou3  db        179,'Zabira: ',0

BuffNum  db        '              '         ; buffer k dek¢dov n¡ ‡¡sla
BuffNum0 db        0

All      db        '*.*',0

AdrTxt   db        '<ADRESAR>'

AtrTab   db        3,'Arc'
         db        1,'Dir'
         db        2,'Sys'
         db        1,'Hid'
         db        1,'R/O'

; -----------------------------------------------------------------------------
;        informa‡n¡ © dek
; -----------------------------------------------------------------------------

InfoXCol db        0                        ; p©¡znak zv˜raznˆn‚ barvy textu
InfoStor dw        0                        ; ukl dac¡ adresa do bufferu
InfoBuf  dw        80 dup(0)                ; buffer informa‡n¡ho © dku

InfoKMax dd        0                        ; celkov  velikost inform. kurzoru
InfoKCit dd        0                        ; ‡¡ta‡ velikosti inform. kurzoru

; -----------------------------------------------------------------------------
;        seznam nalezen˜ch soubor–
; -----------------------------------------------------------------------------

; Struktura:
;        cesta:   0: (1) (atribut) bit6: je nastaven na 1 (=p©¡znak)
;                 1: (N) pln˜ text cesty ASCIIZ
;       soubor:   0: (1) atributy
;                          bit 6: je v‘dy 0
;                          bit 7: 1=polo‘ka ozna‡ena
;                 1: (2) ‡as
;                 3: (2) datum
;                 5: (4) velikost
;                 9: (n) jm‚no ASCIIZ

SeznSegm dw        0                        ; segment seznamu soubor–
SeznSize dw        0                        ; velikost seznamu soubor–

SeznNumO dw        0                        ; p–vodn¡ velikost dat v seznamu
SeznNum  dw        0                        ; velikost dat v seznamu (bajt–)

SeznTop  dw        0                        ; ‡¡slo prvn¡ho © dku
SeznAkt  dw        0                        ; ‡¡slo aktivn¡ho © dku

SeznRad  dw        23                       ; d‚lka str nky seznamu (© dk–)

InsNum   dw        0                        ; po‡et ozna‡en˜ch soubor–
InsSumm  dd        0                        ; sou‡et ozna‡en˜ch soubor–
InsObs   dd        0                        ; obsazen  kapacita soubory

CitSoub  dw        0                        ; ‡¡ta‡ nalezen˜ch soubor–
CitCelk  dd        0                        ; ‡¡ta‡ kapacity celkem
CitObs   dd        0                        ; ‡¡ta‡ obsazen‚ kapacity
DiskBlok dw        0                        ; velikost aloka‡n¡ho bloku disku

; -----------------------------------------------------------------------------
;        historie hled n¡
; -----------------------------------------------------------------------------

Param    db        0                        ; parametry
                                            ;  bit 0: 1=bylo prvn¡ zad n¡ param.
                                            ;  bit 1: 1=zah jena editace © dku
                                            ;  bit 2: 1=prob¡h  hled n¡ souboru
                                            ;  bit 3: 1=hled  se text v souboru
                                            ;  bit 4: 1=zad v  se specifikace
                                            ;  bit 5: 1=je prohl¡‘en¡ souboru F3
                                            ;  bit 6: 1=je zobrazena n povˆda
                                            ;  bit 7: 1=zad n p©¡kazov˜ © dek

Param2   db        0                        ; parametry 2
                                            ;  bit 0: 1=prohled vaj¡ se podadr.

SouborI  dw        0                        ; identifik tor souboru (0=neplat¡)
SoubHst  db        'HLEDEJ.HST',0           ; jm‚no souboru histori¡

SoubHist db        128+11 dup(0)            ; buffer jm‚na souboru histori¡
SoubList db        128+11 dup(0)            ; buffer jm‚na souboru v˜pisu

HistNum  dw        0                        ; po‡et bajt– v bufferu historie
HistLine dw        0                        ; po‡et © dk– v bufferu historie

         EVEN                               ; zarovn n¡ na sudou adresu

HistBuff db        HISTSIZE dup(?)          ; buffer historie


SelBuff  db        90 dup(?)                ; buffer p©edvolby (ukon‡en˜ CR/LF)

EditTop  dw        ?                        ; offset po‡ tku © dku
EditKurz dw        ?                        ; offset kurzoru v © dku

KurzRad  db        ?                        ; © dek s kurzorem na obrazovce
         db        ?                        ; zarovn n¡ na sudou adresu

EditTopL dw        ?                        ; ‡¡slo po‡ te‡n¡ho © dku
EditKurL dw        ?                        ; ‡¡slo aktivn¡ho © dku

EditBuff db        MAXLINE dup(?)           ; edita‡n¡ buffer (doplnˆn˜ mezer.)
         dw        ?                        ; rezerva pro ulo‘en¡ znak– CR/LF

; -----------------------------------------------------------------------------
;        tabulka DTA - funkce nalezen¡ souboru
; -----------------------------------------------------------------------------

;         EVEN
DTA      label     byte
         db        15h dup(?)
DTAAtrib db        ?                        ; (1) atributy nalezen‚ho souboru
DTATime  dw        ?                        ; (2) ‡as nalezen‚ho souboru
DTADate  dw        ?                        ; (2) datum nalezen‚ho souboru
DTASize  dd        ?                        ; (4) velikost nalezen‚ho souboru
DTAName  db        13 dup(?)                ; (13) jm‚no nalez. souboru ASCIIZ
         db        ?                        ; zarovn n¡ na sudou adresu

; -----------------------------------------------------------------------------
;        obsluha prohl¡‘e‡e souboru
; -----------------------------------------------------------------------------

ViewIdnt dw        ?                        ; identifik tor souboru
ViewBeg  dd        ?                        ; adresa po‡ tku bufferu
ViewTop  dd        ?                        ; adresa po‡ tku obrazovky
ViewNum  dw        ?                        ; po‡et bajt– v bufferu
ViewName db        128 dup(?)               ; buffer jm‚na souboru
ViewSize dd        ?                        ; velikost souboru
ViewDis  dw        ?                        ; zobrazen  ‡ st souboru

;         EVEN
ViewBuf  db        VIEWBMAX dup(?)          ; buffer prohl¡‘e‡e

; -----------------------------------------------------------------------------
;        £schova obrazovky a stavu po‡¡ta‡e
; -----------------------------------------------------------------------------

OldBreak db        ?                        ; uschovan˜ p©¡znak BREAK
         db        ?                        ; zarovn n¡ na sudou adresu

OldKurz  dw        ?                        ; uschovan  pozice kurzoru
ObrSize  dw        ?                        ; velikost bufferu obrazovky (slov)
ObrSegm  dw        ?                        ; adresa bufferu displeje

; -----------------------------------------------------------------------------
;        indexov‚ tabulky seznamu
; -----------------------------------------------------------------------------

SrcIdnt  dw        ?                        ; identifik tor souboru
SrcBNum  dw        ?                        ; po‡et bajt– v bufferu hled n¡
SrcBuff  db        SRCBMAX dup(?)           ; buffer pro hled n¡ textu

DispNum  dw        ?                        ; po‡et index– zobrazen¡ (=po‡. ©.)
DispNumO dw        ?                        ; p–vodn¡ po‡et index– zobrazen¡
DispIndx dw        INDXSIZE dup(?)          ; indexy pro zobrazen¡

HledNum  dw        ?                        ; po‡et index– pro hled n¡
HledNumO dw        ?                        ; p–vodn¡ po‡et index–
HledIndx dw        INDXSIZE dup(?)          ; indexy pro hled n¡

CODE     ENDS

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                              Z sobn¡k
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

Zasob    SEGMENT   STACK

         dw        300h dup(?)

Zasob    ENDS

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                         Konec programu
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

KonSegm  SEGMENT
KonSegm  ENDS

         END       Start
