; *****************************************************************************
;                             Syst‚mov‚ informace
; *****************************************************************************
code     SEGMENT
         ASSUME    cs:code,ds:code
         org       100h

sysinfo:
                                          ;* test zad n¡ parametr–
         mov       di,offset prepinac
         mov       si,81h                   ; p©¡kazov˜ © dek
         mov       cl,ds:[si-1]             ; po‡et znak– p©¡kazov‚ho © dku
         and       cx,7fh                   ; omezen¡ na 127 znak–
         cmp       cx,1                     ; je 1 nebo ‘ dn˜ znak ?
         jbe       sysinfo9                 ; v¨eobecn‚ informace
                                          ;* dek¢dov n¡ parametr–
         mov       bx,cx                    ; po‡et znak– p©¡kaz. © dku
         mov       byte ptr ds:[si+bx],0    ; ozna‡en¡ konce p©¡kaz. © dku
         mov       word ptr ds:[di],0       ; zru¨en¡ p©ep¡na‡e
                                          ;* test p©ep¡na‡e "/S"
         mov       ah,"S"                   ; hledan˜ p©ep¡na‡ "/S"
         call      hledpar                  ; nalezen¡ parametru
         jc        sysinfo1                 ; parametr nenalezen
         or        word ptr ds:[di],1       ; nastaven¡ p©ep¡na‡e "/S"
sysinfo1:
                                          ;* test p©ep¡na‡e "/M"
         mov       ah,"M"                   ; hledan˜ p©ep¡na‡ "/M"
         call      hledpar                  ; nalezen¡ parametru
         jc        sysinfo2                 ; parametr nenalezen
         or        word ptr ds:[di],2       ; nastaven¡ p©ep¡na‡e "/M"
sysinfo2:
                                          ;* test p©ep¡na‡e "/I"
         mov       ah,"I"                   ; hledan˜ p©ep¡na‡ "/I"
         call      hledpar                  ; nalezen¡ parametru
         jc        sysinfo3                 ; parametr nenalezen
         or        word ptr ds:[di],4       ; nastaven¡ p©ep¡na‡e "/I"
sysinfo3:
                                          ;* test p©ep¡na‡e "/C"
         mov       ah,"C"                   ; hledan˜ p©ep¡na‡ "/C"
         call      hledpar                  ; nalezen¡ parametru
         jc        sysinfo4                 ; parametr nenalezen
         or        word ptr ds:[di],8       ; nastaven¡ p©ep¡na‡e "/C"
sysinfo4:                                 ;* test p©ep¡na‡e "/W"
         mov       ah,"W"                   ; hledan˜ p©ep¡na‡ "/W"
         call      hledpar                  ; nalezen¡ parametru
         jc        sysinfo5                 ; parametr nenalezen
         or        word ptr ds:[di],10h     ; nastaven¡ p©ep¡na‡e "/W"
sysinfo5:                                 ;* test p©ep¡na‡e "/B"
         mov       ah,"B"                   ; hledan˜ p©ep¡na‡ "/B"
         call      hledpar                  ; nalezen¡ parametru
         jc        sysinfo6                 ; parametr nenalezen
         lodsb                              ; ozna‡en¡ disku
         or        word ptr ds:[di],20h     ; nastaven¡ p©ep¡na‡e "/B"
         and       al,not 20h               ; p©evod na velk‚ p¡smeno
         sub       al,"A"                   ; korekce na bin rn¡ ‡¡slo
         mov       ds:[bootdisk],al         ; ulo‘en¡ ‡¡sla disku
sysinfo6:                                 ;* cejchov n¡ hodin "/$"
         mov       ah,"$"
         call      hledpar                  ; nalezen¡ parametru
         jc        sysinfo7                 ; parametr nenalezen
         or        word ptr ds:[di],8001h   ; p©¡znak cejchov n¡ hodin "/$"
sysinfo7:

sysinfo9:                                 ;* test spr vnosti zad n¡ parametr–
         cmp       word ptr ds:[di],0       ; je nˆjak˜ p©ep¡na‡ ?
         jne       sysinfoa                 ; je nˆjak˜ p©ep¡na‡ - OK
         lea       si,[nadpis]              ; text nadpisu
         call      outtxt                   ; zobrazen¡ nadpisu
         mov       si,offset texthlp        ; text n povˆdy
         call      outtxtx                  ; zobrazen¡ chybov‚ho textu
         int       20h                      ; konec programu

sysinfoa:                                 ;* zji¨tˆn¡ informac¡ o po‡¡ta‡i
         call      testtyp                  ; zji¨tˆn¡ typu po‡¡ta‡e
         call      testdos                  ; test verze opera‡n¡ho syst‚mu
         call      testbios                 ; test verze BIOSu
         call      testproc                 ; test hlavn¡ho procesoru
         call      testkopr                 ; test koprocesoru
         call      testcard                 ; test grafick‚ karty
         call      testdisk                 ; test disketov˜ch mechanik
         call      testwin                  ; test pevn˜ch disk–
         call      testmem                  ; test celkov‚ pamˆti
         call      testhod                  ; test hodin procesoru

         nop
                                          ;* zobrazen¡ v¨eobecn˜ch informac¡
         test      byte ptr cs:[prepinac],1 ; zobraz¡ se v¨eobecn‚ informace ?
         jz        sysinfob                 ; nezobraz¡ se v¨eobecn‚ informace

         lea       si,[nadpis]              ; text nadpisu
         call      outtxt                   ; zobrazen¡ nadpisu
         call      zobrtyp                  ; zobrazen¡ typu po‡¡ta‡e
         call      zobrdos                  ; zobrazen¡ verze oper. syst‚mu
         call      zobrbios                 ; zobrazen¡ verze BIOSu
         call      zobrproc                 ; zobrazen¡ hlavn¡ho procesoru
         call      zobrkopr                 ; zobrazen¡ koprocesoru
         call      zobrhod                  ; zobrazen¡ hodin procesoru
         call      zobrmem                  ; zobrazen¡ celkov‚ pamˆti
         call      zobrrom                  ; zobrazen¡ modul– ROM-BIOS
         call      zobrport                 ; zobrazen¡ port–
         call      zobrcard                 ; zobrazen¡ grafick‚ karty
         call      zobrdisk                 ; zobrazen¡ disketov˜ch mechanik
         call      zobrwin                  ; zobrazen¡ pevn˜ch disk–
         call      zobrlogd                 ; zobrazen¡ logick˜ch disk–
         lea       si,[zaver]               ; z vˆre‡n˜ text
         call      outtxtx                  ; z vˆre‡n‚ od© dkov n¡ textu

sysinfob:                                 ;* zobrazen¡ obsahu pamˆti DOS
         test      byte ptr cs:[prepinac],2 ; zobraz¡ se obsah pamˆti ?
         jz        sysinfoc                 ; nezobrazuje se obsah pamˆti
         call      memorydos                ; zobrazen¡ obsahu pamˆti
sysinfoc:                                 ;* zobrazen¡ vektor– p©eru¨en¡
         test      byte ptr cs:[prepinac],4 ; zobraz¡ se vektory p©eru¨en¡ ?
         jz        sysinfod                 ; nezobraz¡ se vektory p©eru¨en¡
         call      intlist                  ; zobrazen¡ vektor– p©eru¨en¡
sysinfod:                                 ;* zobrazen¡ obsahu pamˆti CMOS
         test      byte ptr cs:[prepinac],8 ; zobrazen¡ se obsah pamˆti CMOS ?
         jz        sysinfoe                 ; nezobraz¡ se CMOS
         call      cmoslist                 ; zobrazen¡ obsahu pamˆti CMOS
sysinfoe:                                 ;* zavadˆ‡ pevn˜ch disk–
         test      byte ptr cs:[prepinac],10h ; zobrazuj¡ se zavadˆ‡e ?
         jz        sysinfof                 ; nezobrazuj¡ se zavadˆ‡e
         mov       dl,80h                   ; ‡¡slo pevn‚ho disku 0
         call      winlist                  ; zobrazen¡ zavadˆ‡e pevn‚ho disku 0
         mov       dl,81h                   ; ‡¡slo pevn‚ho disku 1
         call      winlist                  ; zobrazen¡ zavadˆ‡e pevn‚ho disku 1
sysinfof:                                 ;* zavadˆ‡ disku
         test      byte ptr cs:[prepinac],20h ; zobraz¡ se zavadˆ‡ ?
         jz        sysinfog                 ; nezobrazuje se zavadˆ‡
         mov       dl,ds:[bootdisk]         ; ‡¡slo disku k zobrazen¡
         call      bootlist                 ; zobrazen¡ zavadˆ‡e disku
sysinfog:
         int       20h

; -----------------------------------------------------------------------------
;                         Zji¨tˆn¡ informac¡ o po‡¡ta‡i
; -----------------------------------------------------------------------------
public   testtyp
testtyp:                                  ;* zji¨tˆn¡ typu po‡¡ta‡e
         push      es
                                          ;* zji¨tˆn¡ modelu z BIOS
         mov       ax,0f000h                ; segment dat BIOS
         mov       es,ax                    ; segment dat BIOS
         mov       al,es:[0fffeh]           ; identifik tor syst‚mov‚ho modelu
         mov       ds:[smodel],al           ; £schova syst‚moveho modelu
         cmp       al,0feh
         jae       testtyp3                 ; je PC nebo PC-XT
         cmp       al,0fbh
         je        testtyp3                 ; je XT
                                          ;* pokus o zji¨tˆn¡ pomoc¡ INT 15h
         mov       ah,0c0h
         int       15h                      ; zji¨tˆn¡ dal¨¡ch informac¡
         jc        testtyp3                 ; chyba operace
         or        ah,ah                    ; je informace platn  ?
         jnz       testtyp3                 ; informace nen¡ platn 
         mov       ax,es:[bx+2]             ; ‡¡slo modelu a submodelu
         mov       word ptr ds:[smodel],ax  ; £schova modelu a submodelu
testtyp3:pop       es
         ret
; -----------------------------------------------------------------------------
public   testdos
testdos:                                  ;* zji¨tˆn¡ verze opera‡n¡ho syst‚mu
         mov       ah,30h
         int       21h                      ; dotaz na verzi opera‡n¡ho syst‚mu
         xchg      ah,al                    ; z mˆna verze a podverze
         or        ah,ah                    ; je verze syst‚mu 1.xx ?
         jnz       testdos2                 ; nen¡ verze 1.xx
         inc       ah                       ; verze syst‚mu 1.xx
testdos2:mov       ds:[verze],ax            ; £schova verze opera‡n¡ho syst‚mu
         ret
; -----------------------------------------------------------------------------
public   testbios
testbios:                                 ;* zji¨tˆn¡ data vzniku BIOS
         push      es
         mov       ax,0f000h                ; segment dat BIOS
         mov       es,ax                    ; segment dat BIOS
         mov       di,0fff5h                ; za‡ tek data v pamˆti
         mov       cx,3                     ; po‡et pokus–
testbi1: call      testbix                  ; test spr vnosti data
         jnc       testbi2                  ; nalezeno spr vn‚ datum
         inc       di                       ; adresa 0fff6h
         loop      testbi1                  ; dal¨¡ test
         jmp       short testbi4            ; nenalezeno platn‚ datum
testbi2:                                  ;* nalezeno platn‚ datum
         call      testbiy                  ; ‡ten¡ ‡¡sla mˆs¡ce
         mov       ds:[biosmes],al          ; ulo‘en¡ mˆs¡ce
         call      testbiy                  ; ‡ten¡ ‡¡sla dne
         mov       ds:[biosden],al          ; ulo‘en¡ dne
         call      testbiy                  ; ‡ten¡ ‡¡sla roku
         cbw                                ; AX <- AL
         cmp       al,80                    ; je rok p©ed 1980 ?
         jae       testbi3                  ; nen¡ rok p©ed 1980
         add       ax,100                   ; korekce na stolet¡ 2000
testbi3: add       ax,1900                  ; korekce na stolet¡
         mov       ds:[biosrok],ax          ; ulo‘en¡ ‡¡sla roku
testbi4: pop       es
         ret

testbix:                                  ;* test spr vnosti data ES:DI
         push      di
                                          ;* test mˆs¡ce
         call      testbinx                 ; test 2 ‡¡slic
         jc        testbix3                 ; neplatn˜ mˆs¡c
         call      testbio                  ; test oddˆlova‡e
         jne       testbix3                 ; neplatn˜ oddˆlova‡
                                          ;* test dne
         call      testbinx                 ; test 2 ‡¡slic
         jc        testbix3                 ; neplatn˜ den
         call      testbio                  ; test oddˆlova‡e
         jne       testbix3                 ; neplatn˜ oddˆlova‡
                                          ;* test roku
         call      testbinx                 ; test 2 ‡¡slic
testbix3:pop       di
         ret


testbio:                                  ;* test oddˆlova‡e data (-> ZF)
         mov       al,es:[di]               ; testovan  ‡¡slice
         inc       di                       ; zv˜¨en¡ ukazatele ‡¡slic
         cmp       al,"\"
         je        testbio2                 ; spr vn˜ oddˆlova‡
         cmp       al,"/"
         je        testbio2                 ; spr vn˜ oddˆlova‡
         cmp       al,"-"
         je        testbio2                 ; spr vn˜ oddˆlova‡
         cmp       al,"."
testbio2:ret


testbinx:                                 ;* test 2 ‡¡slic
         call      testbin                  ; test prvn¡ ‡¡slice
         jc        testbin2                 ; chyba - neplatn  ‡¡slice

testbin:                                  ;* test platnosti ‡¡slice AL (-> CF)
         mov       al,es:[di]               ; testovan  ‡¡slice
         inc       di                       ; zv˜¨en¡ ukazatele ‡¡slic
         sub       al,"0"
         jb        testbin2                 ; nen¡ platn  ‡¡slice
         cmp       al,10
         cmc                                ; CY - nen¡ platn  ‡¡slice
testbin2:ret

testbiy:                                  ;* ‡ten¡ ‡¡sla (2 ‡¡slice)
         call      testbin                  ; ‡ten¡ prvn¡ ‡¡slice
         mov       bl,10
         mul       bl                       ; vyn soben¡ 10x
         mov       bl,al
         call      testbin
         add       al,bl                    ; v˜sledn‚ ‡¡slo
         inc       di                       ; p©esko‡en¡ oddˆlova‡e
         ret
; -----------------------------------------------------------------------------
public   testproc
testproc:                                 ;* test hlavn¡ho procesoru

         call      getproc                  ; zji¨tˆn¡ hlavn¡ho procesoru
         mov       ds:[procesor],al         ; typ hlavn¡ho procesoru
         ret

;                                          ;* test ©ad 80286 / 8086
;         pushf                              ; £schova p©¡znak–
;         xor       ax,ax                    ; AX <- 0
;         push      ax
;         popf
;         pushf
;         pop       ax
;         popf                               ; n vrat p©¡znak–
;         and       ax,0f000h
;         cmp       ax,0f000h                ; je ni‘¨¡ procesor ?
;         je        testpro2                 ; je ni‘¨¡ procesor
;                                          ;* rozpozn n¡ 80286/80386
;         pushf                              ; £schova p©¡znak–
;         mov       ax,7000h
;         push      ax
;         popf
;         pushf
;         pop       ax
;         popf                               ; n vrat p©¡znak–
;         and       ax,7000h
;         mov       dl,5                     ; procesor 80286
;         jz        testpro3                 ; vynulovaly se - je 80286
;         inc       dl                       ; procesor 80386
;         jmp       short testpro3           ; je 80386
;testpro2:                                 ;* rozpozn n¡ 80188/8088
;         mov       ax,0ffffh
;         mov       cl,33
;         shl       ax,cl                    ; rotace AX
;         mov       dl,1                     ; procesor 8088
;         jz        testpro5                 ; je procesor 8088/8086
;         mov       dl,3                     ; je procesor 80188/80186
;testpro5:                                 ;* rozpozn n¡ sbˆrnice
;         std                                ; smˆr p©enosu dol–
;
;         EVEN                               ; zarovn n¡ na sudou adresu
;
;         mov       di,offset testpro6
;         mov       al,byte ptr ds:[testpro7]
;         mov       cx,3
;         cli
;         rep       stosb                    ; zru¨en¡ instrukce INC DL
;         cld
;         nop
;         nop
;         nop
;         inc       dx                       ; u 8086 tato instrukce z–stane
;testpro7:sti
;testpro6:sti
;testpro3:mov       ds:[procesor],dl         ; £schova ‡¡sla procesoru
;testpro4:sti
;         ret


getproc:                                  ;* zji¨tˆn¡ hlavn¡ho procesoru
                                            ; VSTUP: AX=typ procesoru
                                            ;   0=nezn m˜
                                            ;   1=Intel 8088
                                            ;   2=Intel 8086
                                            ;   3=NEC V20
                                            ;   4=NEC V30
                                            ;   5=Intel 80188
                                            ;   6=Intel 80186
                                            ;   7=Intel 80286
                                            ;   8=Intel 80386
                                            ;   9=Intel 80386 SX
                                            ;  10=Intel 80486
                                            ;  11=Intel 80486+
                                            ;  12=Pentium

                                          ;* rozli¨en¡ 808x,Vx0/vy¨¨¡
         pushf                              ; £schova registru p©¡znak–
         xor       ax,ax                    ; AX <- 0
         push      ax                       ; 0 -> stack
         popf                               ; F <- 0
         pushf                              ; F -> stack
         pop       ax                       ; AX <- F
         popf                               ; n vrat p©¡znak–
         and       ax,0f000h                ; test bit– 12 a‘ 15
         cmp       ax,0f000h                ; jsou napevno nastaveny na 1 ?
         jne       testpro2                 ; nejsou nast. - je 80186 nebo vyssi

                                          ;* rozli¨en¡ 8088,8086/V20,V30
                                            ; testuje, zda instrukce MUL AL
                                            ; ovliv¤uje p©¡znak ZF

         xor       al,al                    ; nastaven¡ p©¡znaku ZY
         mov       al,40h                   ; n soben‚ ‡¡slo
         mul       al                       ; vyn soben¡ 40h*40h
         mov       ax,1                     ; procesor 8088,8086
         jnz       testpro1                 ; je procesor 8088,8086
         mov       ax,3                     ; nemˆn¡ ZF - je procesor V20, V30

testpro1:                                 ;* test 8-bitov‚ sbˆrnice
         push      ax                       ; £schova k¢du procesoru
         EVEN                               ; nastaven¡ na sudou adresu
         mov       di,offset testproa       ; konec testovac¡ posloupnosti
         std                                ; smˆr dol–
         mov       al,byte ptr cs:[testpro9]; posledn¡ instrukce STI
         mov       cx,3                     ; po‡et bajt– k testu
         cli                                ; z kaz p©eru¨en¡
         rep       stosb                    ; p©epis isntrukc¡ STI
         cld                                ; n vrat smˆru dol–
         nop
         nop
testpro8:nop                                ; sem se zap¡¨¡ 3 instrukce STI
         inc       cx                       ; u 8086 tato instrukce z–stane
testpro9:sti
testproa:sti
         pop       ax
         add       ax,cx                    ; korekce procesoru podle sbˆrnice
         ret

testpro2:                                 ;* rozli¨en¡ 8018x/vy¨¨¡
                                            ; testuje, zda instrukce PUSH SP
                                            ; ulo‘¡ do z sobn¡ku stav SP p©ed
                                            ; operac¡ nebo po operaci (=8018x)

         push      sp                       ; ulo‘en¡ ukazatele z sobn¡ku
         pop       bx                       ; n vrat ‡¡sla, kter‚ se ulo‘ilo
         cmp       bx,sp                    ; ulo‘il se stav SP p©ed operac¡ ?
         mov       ax,5                     ; procesor 80188, 80186
         jne       testpro1                 ; je procesor 80188, 80186

                                          ;* test procesoru 80286
         pushf                              ; £schova registru p©¡znak–
         mov       ax,0f000h                ; AX <- F000h
         push      ax                       ; F000h -> stack
         popf                               ; F <- F000h
         pushf                              ; F -> stack
         pop       ax                       ; AX <- F
         popf                               ; n vrat registru p©¡znak–
         and       ax,0f000h                ; test bit– 12 a‘ 15
         jnz       testpro3                 ; nejsou v¨echny 0 - nen¡ 80286
         mov       ax,7                     ; jsou napevno 0 - je procesor 80286
         ret

testpro3:                                 ;* test procesoru 80486
                                            ; testuje se, zda lze mˆnit bit 18
                                            ; registru p©¡znak– FD
                                            ; VSTUP: AX=1 (lze mˆnit-je 80486)

db 0C8h,0,0,0       ;ENTER  0000,00         ; m¢d 32 bit–
db 66h,8Bh,0D4h     ;MOV    EDX,ESP         ; £schova ukazatele z sobn¡ku ESP
db 66h,83h,0E4h,0FCh;AND    ESP,FFFC        ; zaokrouhlen¡ ukazat. na dvojslovo
db 66h,9Ch          ;PUSHFD                 ; FD -> stack
db 66h,58h          ;POP    EAX             ; EAX <- FD
db 66h,8Bh,0C8h     ;MOV    ECX,EAX         ; ECX <- EAX (£schova FD)
db 66h,35h,0,0,4,0  ;XOR    EAX,00040000    ; zmˆna bitu 18 registru FD
db 66h,50h          ;PUSH   EAX             ; EAX -> stack
db 66h,9Dh          ;POPFD                  ; FD <- p–vodn¡ FD se zmˆnou bitu 18
db 66h,9Ch          ;PUSHFD                 ; FD -> stack (jak to FD ovlivnil ?)
db 66h,58h          ;POP    EAX             ; AX <- FD (nov  hodnota)
db 66h,33h,0C1h     ;XOR    EAX,ECX         ; XOR nov‚ho stavu FD se star˜m
db 66h,0C1h,0E8h,12h;SHR    EAX,12          ; rotace mˆnˆn‚ho p©¡znaku do bitu 0
db 66h,83h,0E0h,1   ;AND    EAX,+01         ; ponech  testovan˜ p©¡znak FD
db 66h,51h          ;PUSH   ECX             ; p–vodn¡ nastaven¡ FD
db 66h,9Dh          ;POPFD                  ; n vrat registru p©¡znak– FD
db 66h,8Bh,0E2h     ;MOV    ESP,EDX         ; n vrat ukazatele z sobn¡ku ESP
db 0C9h             ;LEAVE                  ; opustˆn¡ m¢du 32 bit–

         dec       ax                       ; je procesor 80486 ?
         jnz       testpro4                 ; nen¡ procesor 80486
;þ
.486
         cli
         pushfd                             ; £schova EFLAGS

         pushfd                             ; EFLAGS do z sobn¡ku
         pop       eax                      ; EAX <- EFLAGS
         mov       ebx,eax                  ; EBX <- £schova star‚ho stavu EFLAGS
         xor       eax,00200000h            ; zmˆna p©¡znaku CPUID
         push      eax                      ; nov˜ stav EFLAGS do z sobn¡ku
         popfd                              ; EFLAGS <- nov  hodnota
         pushfd                             ; EFLAGS do z sobn¡ku
         pop       eax                      ; EAX <- nov  hodnota EFLAGS

         popfd                              ; n vrat EFLAGS
         sti

         cmp       eax,ebx                  ; lze zmˆnit bit CPUID ?
         je        testpr34                 ; nelze zmˆnit - je 486

         xor       eax,eax                  ; EAX <- 0
db 0fh,0a2h                                 ; instrukce CPUID
         dec       eax                      ; instrukce podporov na ?
         jnz       testpr34                 ; nen¡ podporov na
         mov       dword ptr ds:[vendrcpu],ebx ; vendor ©etˆzec
         mov       dword ptr ds:[vendrcpu+4],edx ; vendor ©etˆzec
         mov       dword ptr ds:[vendrcpu+8],ecx ; vendor ©etˆzec

         inc       eax                      ; EAX <- 1
db 0fh,0a2h                                 ; instrukce CPUID
         mov       ds:[modelcpu],ax         ; model procesoru
.8086
         cmp       ax,500h                  ; je Pentium ?
         mov       ax,12                    ; procesor Pentium
         jae       testpr32                 ; je Pentium
testpr31:mov       ax,11                    ; procesor 80486+
testpr32:ret

testpr34:mov       ax,10                    ; je procesor 80486
         ret

testpro4:                                 ;* rozli¨en¡ 80386/80386 SX
                                            ; testuje bit 4 ©¡dic¡ho registru 0
                                            ; - pokud lze vynulovat, je 80386

db 0C8h,0,0,0       ;ENTER  0000,00         ; m¢d 32 bit–
db 0Fh,20h,0C3h     ;MOV    EBX,CR0         ; £schova ©¡dic¡ho registru CR0
db 0Fh,20h,0C0h     ;MOV    EAX,CR0         ; EAX <- CR0
db 66h,83h,0E0h,0EFh;AND    EAX,FFEF        ; vynulov n¡ bitu 4 reg. EAX
db 0Fh,22h,0C0h     ;MOV    CR0,EAX         ; CR0 <- nov  hodnota s nul. bit. 4
db 0Fh,20h,0C0h     ;MOV    EAX,CR0         ; EAX <- CR0 - vynuloval se bit 4 ?
db 66h,0C1h,0E8h,4  ;SHR    EAX,4           ; rotace bitu 4 do bitu 0
db 66h,83h,0E0h,1   ;AND    EAX,+01         ; bit 0 - p©¡znak nastav. bitu 4 CR0
db 0Fh,22h,0C3h     ;MOV    CR0,EBX         ; n vrat ©¡dic¡ho registru CR0
db 0C9h             ;LEAVE                  ; opustˆn¡ m¢du 32 bit–

         dec       ax                       ; je procesor 80386 SX ?
         mov       ax,8                     ; procesor 80386
         jnz       testpro5                 ; nen¡ procesor 80386 SX
         inc       ax                       ; je procesor 80386 SX (=9)
testpro5:ret
; -----------------------------------------------------------------------------
public   testkopr
testkopr:                                 ;* zji¨tˆn¡ koprocesoru

         int       11h                      ; poskytnut¡ tabulky vybaven¡
         mov       dl,1                     ; koprocesor nenainstalov n
         test      ax,2                     ; je koprocesor nainstalov n ?
         jz        testkop4                 ; koprocesor nen¡ nainstalov n
         mov       al,ds:[procesor]         ; hlavn¡ procesor
         xor       dx,dx                    ; p©ednastaven¡ - nezn m˜ koproc.
         or        al,al                    ; je procesor nezn m˜ ?
         jz        testkop4                 ; procesor je nezn m˜
         mov       dl,2                     ; koprocesor 8087
         cmp       al,7                     ; je procesor ni‘¨¡ ne‘ 80286 ?
         jb        testkop4                 ; je ni‘¨¡ - je koproc. 8087
         mov       dl,3                     ; koprocesor 80287
         je        testkop4                 ; je koprocesor 80287
         mov       dl,4                     ; koprocesor 80387
         cmp       al,10                    ; je koprocesor 80486 ?
         jb        testkop4                 ; je 80387
         inc       dx                       ; koprocesor 80486
         cmp       al,11                    ; je koprocesor Pentium ?
         jbe       testkop4                 ; je 80486
         inc       dx                       ; koprocesor Pentium
testkop4:mov       ds:[koproc],dl           ; ulo‘en¡ koprocesoru
         ret
; -----------------------------------------------------------------------------
public   testcard
testcard:                                 ;* test grafick‚ karty

         call      DetectCard               ; detekce karty
         ret
;         push      es
;
;         mov       ah,12h
;         mov       bl,10h
;         int       10h
;         mov       dl,3                     ; karta EGA
;         cmp       bl,10h
;         jne       testcard3                ; je karta EGA
;
;         xor       ax,ax
;         mov       es,ax
;         mov       ax,es:[463h]
;         mov       dl,1
;         cmp       ax,3b4h
;         je        testcard3                ; je MDA
;         mov       dl,2                     ; je CGA
;testcard3:mov      ds:[card],dl             ; grafick  karta
;         pop       es
;         ret
; -----------------------------------------------------------------------------
public   testdisk
testdisk:                                 ;* zji¨tˆn¡ disket. mechanik

         int       11h                      ; ‡ten¡ tabulky vybaven¡
         test      al,1                     ; jsou disket. mechaniky ?
         jz        testdsk6                 ; nen¡ ‘ dn  disketov  mechanika
         mov       cl,6
         shr       al,cl                    ; po‡et mechanik
         inc       al
         mov       ds:[disket],al           ; po‡et disketov˜ch mechanik

;         cmp       byte ptr ds:[smodel],0f9h ; je konvertabiln¡ PC ?
;         je        testdsk1                 ; je konvertabiln¡ PC
;         cmp       byte ptr ds:[smodel],0fch ; je AT ?
;         jne       testdsk6                 ; nen¡ AT
testdsk1:xor       ch,ch
         mov       cl,al                    ; po‡et disket. mechanik
         xor       dl,dl                    ; ukazatel disk–
         lea       di,[disket1]             ; tabulka typ– disket
testdsk2:push      cx
         push      dx
         push      di                       ; ukl dac¡ adresa do tabulky
         push      es
         xor       bl,bl                    ; p©ednastaven¡ typu diskety
         mov       ax,83eh
         int       13h                      ; poskytnut¡ informac¡ o disketˆ
         pop       es
         pop       di                       ; ukl dac¡ adresa do tabulky
         jc        testdsk4                 ; chyba operace
         or        ah,ah
         jnz       testdsk4                 ; funkce nen¡ podporovan 
         mov       al,bl
         cmp       al,5
         jbe       testdsk3
         mov       al,5
testdsk3:cld
         stosb                              ; typ disket. mechaniky
testdsk4:pop       dx                       ; ukazatel disket. mechanik
         pop       cx                       ; ‡¡ta‡ disket. mechanik
         inc       dl                       ; zv˜¨en¡ ‡¡sla diskety
         loop      testdsk2                 ; dal¨¡ disket. mechanika

testdsk6:ret
; -----------------------------------------------------------------------------
public   testwin
testwin:                                  ;* informace o pevn‚m disku
         mov       dl,80h                   ; pevn˜ disk 0
         mov       di,offset winchs0        ; tabulka pevn‚ho disku 0
         call      testwin2                 ; ‡ten¡ parametr– pevn‚ho disku 0
         mov       dl,81h                   ; pevn˜ disk 1
         mov       di,offset winchs1        ; tabulka pevn‚ho disku 1

testwin2:
         push      dx

         mov       ah,8
         push      es
         push      di
         int       13h                      ; poskytnut¡ parametr– disku
         pop       di
         pop       es

         jc        testwin3                 ; chyba operace
         pop       ax                       ; ‡¡slo disku
         push      ax                       ; ‡¡slo disku
         and       al,7fh                   ; zru¨en¡ bitu 7
         cmp       al,dl                    ; je platn‚ ‡¡slo disku ?
         jae       testwin3                 ; neplatn‚ ‡¡slo disku
                                          ;* v˜po‡et velikosti disku
         mov       ah,dh                    ; bity 10 a 11 ‡¡sla stopy
         and       ah,0c0h                  ; ponech  bity 10 a 11 ‡¡sla stopy
         shr       ah,1
         shr       ah,1
         shr       ah,1
         shr       ah,1                     ; p©esun na pozici bit– 10 a 11
         mov       al,cl                    ; bity 8 a 9 ‡¡sla stopy
         and       al,0c0h                  ; ponech  bity 8 a 9 ‡¡sla v lce
         rol       al,1
         rol       al,1                     ; p©esun na pozici bit– 8 a 9
         or        ah,al                    ; nejvy¨¨¡ 4 bity ‡¡sla v lce
         mov       al,ch                    ; ni‘¨¡ch 8 bit– ‡¡sla v lce
         inc       ax                       ; po‡et v lc– disku
         mov       bl,dh                    ; po‡et hlav disku - 1
         and       bx,3fh                   ; zru¨en¡ bit– ‡¡sla stopy
         inc       bx                       ; po‡et hlav disku
         mul       bx                       ; v˜po‡et celkov‚ho po‡tu stop
         and       cx,3fh                   ; po‡et sektor–
         mul       cx                       ; v˜po‡et po‡tu sektor– na disku
         mov       cx,1953                  ; dˆlitel pro p©epo‡et na MB
         div       cx                       ; p©epo‡et na MB
         mov       ds:[di],ax               ; ulo‘en¡ velikosti disku
testwin3:
         pop       dx

         call      testwinc                 ; ‡ten¡ typu pevn‚ho disku z CMOS
         mov       ds:[di+2],al             ; nastaven¡ typu pevn‚ho disku
testwin6:ret


testwinc:                                 ;* ‡ten¡ typu disku DL z CMOS
                                            ; VSTUP: DL=‡¡slo pevn‚ho disku
                                            ; VSTUP: AL=typ pevn‚ho disku
                                            ;         ZY=typ nezn m˜

         push      bx
         push      ax
         xor       bl,bl                    ; p©ednastaven¡ - neplatn˜ disk
         mov       ah,12h                   ; adresa typu pevn‚ho disku
         call      readcmos                 ; ‡ten¡ bajtu typ– pevn˜ch disk–
         jc        testwnc7                 ; data neplat¡
         test      dl,1                     ; je pevn˜ disk 1 ?
         jnz       testwnc4                 ; je pevn˜ disk 1
         shr       al,1
         shr       al,1
         shr       al,1
         shr       al,1                     ; rotace pro pevn˜ disk 1
testwnc4:and       al,0fh                   ; ponech  ‡¡slo disku
         cmp       al,0fh                   ; je roz¨¡©en‚ ‡¡slo typu ?
         jne       testwnc6                 ; nen¡ roz¨¡©en‚ ‡¡slo typu
         mov       ah,19h                   ; adresa pevn‚ho disku 0
         test      dl,1                     ; je pevn˜ disk 0 ?
         jz        testwnc5                 ; je pevn˜ disk 0
         inc       ah                       ; zv˜¨en¡ adresy na disk 1
testwnc5:call      readcmos                 ; ‡ten¡ typu disku z CMOS
         jc        testwnc7                 ; chyba ‡ten¡ dat z CMOS
testwnc6:mov       bl,al                    ; typ pevn‚ho disku
testwnc7:pop       ax
         mov       al,bl                    ; ‡¡slo disku
         or        al,al                    ; je platn˜ typ ?
         pop       bx
         ret
; -----------------------------------------------------------------------------
public   testmem
testmem:                                  ;* test celkov‚ pamˆti

         int       12h                      ; hl ¨en¡ o velikosti pamˆti
         mov       ds:[memory],ax

         cmp       byte ptr ds:[procesor],2 ; je procesor 80186 a vy¨¨¡ ?
         jbe       testmem4                 ; je n¡zk˜ procesor
                                          ;* zji¨tˆn¡ pamˆti z CMOS
         mov       ah,18h                   ; adresa vy¨¨¡ho bajtu
         call      readcmos                 ; p©e‡ten¡ vy¨¨¡ho bajtu pamˆti
         jc        testmem2                 ; chyba ‡ten¡ z CMOS
         mov       ch,al                    ; £schova vy¨¨¡ho bajtu pamˆti
         mov       ah,17h                   ; adresa ni‘¨¡ho bajtu
         call      readcmos                 ; p©e‡ten¡ ni‘¨¡ho bajtu pamˆti
         mov       ah,ch                    ; n vrat ni‘¨¡ho bajtu pamˆti
         jc        testmem2                 ; chyba ‡ten¡ z CMOS
         or        ax,ax
         jnz       testmem3
testmem2:
         mov       ax,8833h
         int       15h                      ; dotaz na roz¨i©uj¡c¡ pamˆŸ
         jc        testmem4                 ; chyba
         cmp       ax,8833h
         je        testmem4                 ; slu‘ba nen¡ obsluhovan 
testmem3:add       ds:[memory],ax           ; nov  velikost pamˆti

         cmp       word ptr ds:[memory],1100
         jb        testmem4
         add       word ptr ds:[memory],384 + 400 ; oprava pamˆti + zaokrouhlen¡
         and       word ptr ds:[memory],not 3ffh ; zaokrouhlen¡ na MB

testmem4:ret
; -----------------------------------------------------------------------------
public   testhod
testhod:                                  ;* test hodin procesoru


                                          ;* nastaven¡ vlastn¡ obsluhy INT 08h
;         cli                                ; z kaz p©eru¨en¡
;         xor       ax,ax                    ; AX <- 0
;         mov       ds,ax                    ; DS <- 0
;         push      word ptr ds:[8*4]        ; £schova offsetu adresy INT 08h
;         push      word ptr ds:[8*4+2]      ; £schova segmentu adresy INT 08h
;         mov       word ptr ds:[8*4],offset inthran ; nastaven¡ offsetu obsluhy
;         mov       word ptr ds:[8*4+2],cs   ; nastaven¡ segmentu obsluhy
;                                          ;* zamaskov n¡ ostatn¡ch p©eru¨en¡
         in        al,[21h]                 ; maska p©eru¨en¡
         push      ax                       ; £schova masky p©eru¨en¡
         mov       al,not 1                 ; z kaz v¨ech ostatn¡ch p©eru¨en¡
         out       [21h],al                 ; ponech  jen p©eru¨en¡ od hodin
;
;         push      cs
;         pop       ds

testhod1:
         xor       bx,bx                    ; vy¨¨¡ slovo
         xor       si,si                    ; ni‘¨¡ slovo
         mov       cx,6                     ; po‡et pokus–

testhod3:
         call      hrana                    ; mˆ©en¡ doby
         jc        testhod2                 ; p©ete‡en¡
         cmp       dx,bx                    ; je del¨¡ doba ?
         jne       testhod4
         cmp       ax,si                    ; je del¨¡ doba ?
testhod4:jbe       testhod5
         mov       si,ax                    ; nov‚ ni‘¨¡ slovo
         mov       bx,dx                    ; nov‚ vy¨¨¡ slovo
testhod5:loop      testhod3                 ; dal¨¡ pokus
         mov       ax,si                    ; ni‘¨¡ slovo
         mov       dx,bx                    ; vy¨¨¡ slovo

         xor       bx,bx
         mov       bl,ds:[procesor]         ; procesor
         add       bx,bx
         mov       cx,ds:[hodproc+bx]       ; dˆlic¡ konstanta pro hodiny
         mov       bx,cx
         shr       bx,1

         test      word ptr ds:[prepinac],8000h ; je cejchov n¡ hodin ?
         jnz       testhod9                 ; je cejchov n¡ hodin

         add       ax,bx                    ; zaokrouhlen¡
         adc       dx,0
         cmp       dx,cx
         jae       testhod9
         div       cx                       ; p©epo‡et na kmito‡et

testhod9:mov       ds:[hodiny],ax           ; hodiny procesoru

         mov       ds:[hodinyh],dx          ; vy¨¨¡ slovo hodin
testhod2:
                                          ;* navr cen¡ masky p©eru¨en¡
;         cli                                ; z kaz p©eru¨en¡
         pop       ax
         out       [21h],al                 ; n vrat masky p©eru¨en¡
;                                          ;* navr cen¡ obsluhy INT 08h
;         xor       ax,ax
;         mov       ds,ax                    ; DS <- 0
;         pop       word ptr ds:[8*4+2]      ; n vrat segmentu adresy INT 08h
;         pop       word ptr ds:[8*4]        ; n vrat offsetu adresy INT 08h
;         push      cs
;         pop       ds                       ; DS <- CS
;
;         sti                                ; povolen¡ p©eru¨en¡

         call      TstCpu                   ; test hodin procesoru

         ret

hrana:                                    ;* ‡ek n¡ na hranu hodin
                                            ; VSTUP: DX:AX=konstanta
                                            ;         CY=p©ete‡en¡

         sti                                ; povolen¡ p©eru¨en¡
         push      bx                       ; £schova BX
         push      ds
         xor       ax,ax                    ; AX <- 0 (inicializace)
         xor       dx,dx                    ; DX <- 0 (inicializace)
         mov       ds,ax
         mov       bx,ds:[46ch]             ; stav ‡¡ta‡e INT 08h
hrana1:  add       ax,1                     ; zv˜¨en¡ ‡¡ta‡e pr–chod–
         adc       dl,0
         jc        hrana2                   ; je p©ete‡en¡
         cmp       bx,ds:[46ch]             ; byla zmˆna ?
         je        hrana1                   ; nebyla je¨tˆ zmˆna
         clc                                ; p©¡znak operace OK
hrana2:  pop       ds
         pop       bx
         ret

;inthran: inc       word ptr cs:[cit08]      ; zv˜¨en¡ ukazatele INT 08h
;         push      ax
;         mov       al,20h
;         out       [20h],al
;         pop       ax
;         iret

; -----------------------------------------------------------------------------
;                         Zobrazen¡ informac¡ o po‡¡ta‡i
; -----------------------------------------------------------------------------
public   zobrtyp
zobrtyp:                                  ;* zobrazen¡ typu po‡¡ta‡e
         lea       si,[txttyp]              ; text "Typ po‡¡ta‡e:"
         call      outtxtx                  ; zobrazen¡ textu "Typ..."
         mov       ax,word ptr ds:[smodel]  ; syst‚mov˜ model a submodel
                                          ;* "PC" (ff00h)
         xor       cl,cl                    ; typ - origin ln¡ PC
         inc       al                       ; je model 0ffh ("PC")?
         jz        zobrtyp3                 ; je PC
                                          ;* "PC-XT" (fe00h)
         inc       cl                       ; typ - "PC-XT"
         inc       al                       ; je model 0feh ("PC XT") ?
         jz        zobrtyp3                 ; je "PC XT"
                                          ;* "PCjr" (fd00h)
         inc       cl                       ; typ - "PCjr"
         inc       al                       ; je model 0fdh ("PCjr") ?
         jz        zobrtyp3                 ; je "PCjr"
                                          ;* "AT/XT-286" (fch)
         inc       cl                       ; typ - "AT"
         inc       al                       ; je model 0fch ("AT/XT-286") ?
         jnz       zobrtyp2                 ; nen¡ model "AT/XT-286")
         cmp       ah,2                     ; je podmodel 2 ?
         jne       zobrtyp3                 ; nen¡ podmodel 2 - je "AT"
         inc       cl                       ; typ - "XT-286"
         jmp       short zobrtyp3           ; je "XT-286"
                                          ;* "XT"
zobrtyp2:inc       cl                       ; p©esko‡en¡ "XT-286"
         inc       cl                       ; typ - "XT"
         inc       al                       ; je model 0fbh ("XT") ?
         jz        zobrtyp3                 ; je "XT"
                                          ;* "Konvertabiln¡ PC"
         inc       al                       ; p©esko‡en¡ modelu 0fah
         inc       cl                       ; typ - "Konvertabiln¡ PC"
         inc       al                       ; je model 0f9h ("konvert. PC") ?
         jz        zobrtyp3                 ; je "Konvertabiln¡ PC"
                                          ;* nezn m˜ typ
         inc       cl                       ; nezn m˜ typ po‡¡ta‡e
zobrtyp3:lea       si,[tabtyp]              ; texty typ– po‡¡ta‡–
         call      outind                   ; zobrazen¡ textu
         ret
; -----------------------------------------------------------------------------
public   zobrdos
zobrdos:                                  ;* zobrazen¡ verze oper. syst‚mu
         lea       si,[txtdos]              ; text "Opera‡n¡ syst‚m: "
         call      outtxtx                  ; zobrazen¡ £vodn¡ho textu
                                          ;* n zev opera‡n¡ho syst‚mu
         lea       si,[tabdos]              ; tabulka n zv– DOS
         xor       cl,cl
         call      outind                   ; zobrazen¡ n zvu syst‚mu

         call      outspc                   ; zobrazen¡ oddˆlovac¡ mezery
                                          ;* zobrazen¡ hlavn¡ho ‡¡sla verze
         mov       ax,ds:[verze]
         push      ax
         xchg      al,ah
         call      outcis                   ; zobrazen¡ ‡¡slice
         mov       al,"."
         call      outch                    ; zobrazen¡ oddˆlovac¡ te‡ky
         pop       ax
         cbw                                ; AX <- AL
         mov       bl,10                    ; dˆlitel
         div       bl                       ; v˜po‡et des¡tek ‡¡sla
         call      outcis                   ; zobrazen¡ des¡tek podverze
         xchg      al,ah
         call      outcis                   ; zobrazen¡ jednotek podverze
         ret
; -----------------------------------------------------------------------------
public   zobrbios
zobrbios:                                 ;* zobrazen¡ verze BIOS
         lea       si,[txtbios]             ; text "Datum vzniku BIOS: "
         call      outtxtx                  ; zobrazen¡ textu "BIOS"
                                          ;* zobrazen¡ dne
         mov       al,ds:[biosden]          ; den vzniku BIOS
         call      outnum0                  ; zobrazen¡ dne
         mov       al,"."
         call      outch                    ; zobrazen¡ te‡ky
;         call      outspc                   ; oddˆlovac¡ mezera
         mov       cl,ds:[biosmes]          ; mˆs¡c BIOS
         cmp       cl,12
         jbe       zobrbio2
         xor       cl,cl
zobrbio2:lea       si,[tabmes]              ; tabulka mˆs¡c–
         call      outind                   ; zobrazen¡ mˆs¡ce
         call      outspc                   ; zobrazen¡ mezery
         mov       ax,ds:[biosrok]          ; rok BIOS
         call      outnum                   ; zobrazen¡ roku
         ret
; -----------------------------------------------------------------------------
public   zobrproc
zobrproc:                                 ;* zobrazen¡ hlavn¡ho procesoru
         lea       si,[txtproc]             ; text "Hlavn¡ procesor:"
         call      outtxtx                  ; zobrazen¡ textu "procesor"
         mov       cl,ds:[procesor]         ; hlavn¡ procesor
         lea       si,[tabproc]             ; tabulka procesor–
         call      outind                   ; zobrazen¡ procesoru

         mov       ax,ds:[modelcpu]
         or        ax,ax                    ; je zn m model procesoru ?
         jz        zobrprc2                 ; model nen¡ zn m
         mov       si,offset tabprocm
         call      outtxt                   ; zobrazen¡ textu "model"

         xchg      al,ah
         call      hexchr                   ; zobrazen¡ hlavn¡ho ‡¡sla CPU
         mov       al,"."
         call      outch
         mov       al,ah
         call      hexbyte                  ; zobrazen¡ modelu

zobrprc2:mov       si,offset vendrcpu
         cmp       byte ptr ds:[si],0       ; je ©etˆzec ?
         je        zobrprc4                 ; nen¡ ©etˆzec
         call      outspc
         call      outtxt

zobrprc4:ret
; -----------------------------------------------------------------------------
public   zobrkopr
zobrkopr:                                 ;* zobrazen¡ koprocesoru
         lea       si,[txtkopr]             ; text "Koprocesor:"
         call      outtxtx                  ; zobrazen¡ textu "koprocesor"
         mov       cl,ds:[koproc]           ; koprocesor
         lea       si,[tabkopr]             ; tabulka koprocesor–
         call      outind                   ; zobrazen¡ koprocesoru
         ret
; -----------------------------------------------------------------------------
public   zobrport
zobrport:                                 ;* zobrazen¡ port–
         lea       si,[txtserp]             ; test "Seriove porty"
         mov       di,400h
         call      zobradp0                 ; zobrazen¡ adres port–
zobradp0:                                 ;* zobrazen¡ adres port–
         push      es
         xor       ax,ax
         mov       es,ax                    ; ES <- 0
         call      outtxtx                  ; zobrazen¡ textu "seriove porty"
         mov       cx,4                     ; po‡et mo‘n˜ch adres
         xor       dx,dx                    ; ‡¡ta‡ platn˜ch port–
zobradp1:mov       ax,es:[di]               ; adresa portu
         add       di,2                     ; adresa dal¨¡ho portu
         or        ax,ax                    ; je port definov n ?
         jz        zobradp3                 ; port nen¡ definov n
         or        dx,dx                    ; je to prvn¡ port ?
         jz        zobradp2                 ; je to prvn¡ port
         call      outoddel                 ; zobrazen¡ oddˆlovac¡ ‡ rky
;         push      ax
 ;        mov       al,","                   ; oddˆlovac¡ ‡ rka
;         call      outch                    ; zobrazen¡ oddˆlovac¡ ‡ rky
;         pop       ax
zobradp2:call      outwordh                 ; zobrazen¡ adresy portu
         inc       dx                       ; zv˜¨en¡ ‡¡ta‡e port–
zobradp3:loop      zobradp1                 ; dal¨¡ port
         pop       es
         ret
; -----------------------------------------------------------------------------
public   zobrcard
zobrcard:                                 ;* zobrazen¡ grafick‚ karty
         lea       si,[txtcard]             ; text "Graf. karta:"
         call      outtxtx                  ; zobrazen¡ textu "Graf. karta"
         mov       cl,ds:[VideoCard]        ; graf. karta
         lea       si,[tabcard]             ; tabulka graf. karet
         call      outind                   ; zobrazen¡ graf. karty

         cmp       cl,11
         je        zobrcar4                 ; je VESA
         cmp       cl,9
         je        zobrcar1                 ; je VGA
         cmp       cl,3
         jb        zobrcar2
         cmp       cl,5
         ja        zobrcar2

zobrcar1:mov       ah,12h
         mov       bl,10h
         int       10h
         mov       cl,bl
         cmp       cl,3
         ja        zobrcar2
         call      outspc
         lea       si,[tabega]
         call      outind
zobrcar2:
         ret

zobrcar4:mov       ax,ds:[VesaMEM]          ; pamˆŸ VESA
         call      outnum                   ; zobrazen¡ velikosti pamˆti

         lea       si,[txtkb]
         call      outtxt                   ; zobrazen¡ textu "KB"
         ret

; -----------------------------------------------------------------------------
public   zobrdisk
zobrdisk:                                 ;* zobrazen¡ disketov˜ch mechanik
         lea       si,[txtdisk]
         call      outtxtx                  ; zobrazen¡ textu "Disket. mech.:"

         mov       al,ds:[disket]           ; po‡et disketov˜ch mechanik
         mov       cl,al                    ; po‡et disket. mechanik
                                          ;* je nˆjak˜ zn m˜ typ ?
         or        al,al
         jz        zobrdsk4                 ; nen¡ ‘ dn  mechanika
         mov       bx,word ptr ds:[disket1]
         or        bx,word ptr ds:[disket2]
         jz        zobrdsk4                 ; nen¡ zn m  disket. mechanika

         mov       si,offset disket1        ; tabulka disket. mechanik
         xor       ch,ch
zobrdsk2:lodsb                              ; typ disket. mechaniky
         and       al,0fh
         cmp       al,5                     ; max. typ mechaniky
         jbe       zobrdsk3                 ; povolen˜ typ mechaniky
         xor       al,al                    ; n hradn¡ ‡¡slo - nezn m 
zobrdsk3:push      cx
         push      si
         mov       cl,al                    ; ‡¡slo typu mechaniky
         lea       si,[tabdisk]             ; tabulka typ– disk–
         call      outind                   ; zobrazen¡ typu mechaniky
         pop       si
         pop       cx
         cmp       cl,1                     ; n sleduje je¨tˆ nˆjak  mechanika ?
         jbe       zobrdsk5                 ; nen¡ dal¨¡ mechanika
         call      outoddel                 ; zobrazen¡ oddˆlovac¡ ‡ rky
;         mov       al,","
;         call      outch
 ;        call      outspc
zobrdsk5:loop      zobrdsk2                 ; dal¨¡ mechanika
         ret

zobrdsk4:                                 ;* zobrazen¡ po‡tu mechanik
         call      outnum0                  ; zobrazen¡ po‡tu disket. mechanik
         ret
; -----------------------------------------------------------------------------
public   zobrwin
zobrwin:                                  ;* zobrazen¡ pevn˜ch disk–
         lea       si,[txtwin]
         call      outtxtx                  ; zobrazen¡ textu "Pevn‚ disky:"

         mov       di,offset winchs0        ; definice pevn‚ho disku 0
         cmp       word ptr ds:[di],0       ; je disk definovan˜ ?
         jz        zobrwin1                 ; disk nen¡ definovan˜
         call      zobrwin2                 ; zobrazen¡ pevn‚ho disku 0
         cmp       word ptr ds:[winchs1],0  ; je definov n pevn˜ disk 1 ?
         jz        zobrwin7                 ; pevn˜ disk 1 nen¡ definov n
         call      outoddel                 ; zobrazen¡ oddˆlovac¡ ‡ rky
zobrwin1:mov       di,offset winchs1        ; definice pevn‚ho disku 1
zobrwin2:                                 ;* zobrazen¡ pevn‚ho disku
         mov       ax,ds:[di]               ; velikost pevn‚ho disku
         or        ax,ax                    ; je disk definov n ?
         jz        zobrwin7                 ; disk nen¡ definov n
         call      outnum                   ; zobrazen¡ ‡¡sla v AX
         mov       si,offset txtwin1        ; text " MB"
         call      outtxt                   ; zobrazen¡ textu " MB"
         mov       al,ds:[di+2]             ; typ pevn‚ho disku
         or        al,al                    ; je typ definov n ?
         jz        zobrwin7                 ; typ nen¡ definov n
         mov       si,offset txtwin2        ; text "(typ"
         call      outtxt                   ; zobrazen¡ textu "(typ "
         call      outnum0                  ; zobrazen¡ ‡¡sla typu
         mov       al,")"
         call      outch                    ; zobrazen¡ z vˆre‡n‚ mezery
zobrwin7:ret
; -----------------------------------------------------------------------------
public   zobrmem
zobrmem:                                  ;* zobrazen¡ celkov‚ pamˆti
         lea       si,[txtmem]
         call      outtxtx                  ; zobrazen¡ textu "Celkov  pamˆŸ:"

         mov       ax,ds:[memory]           ; celkov  pamˆŸ
         lea       si,[txtkb]               ; text "KB"

         cmp       ax,1100
         jb        zobrmem1                 ; je mal  pamˆŸ

         mov       cl,10                    ; p©evod na MB
         shr       ax,cl                    ; p©evod na MB
         mov       si,offset txtmb          ; text "MB"

zobrmem1:call      outnum                   ; zobrazen¡ velikosti pamˆti
         call      outtxt                   ; zobrazen¡ textu "KB" nebo "MB"
         ret

; -----------------------------------------------------------------------------
public   zobrrom
zobrrom:                                  ;* zobrazen¡ modul– ROM-BIOS
         sti
         lea       si,[txtrom]
         call      outtxtx                  ; zobrazen¡ textu "Moduly ROM:"

         xor       dx,dx                    ; ‡¡ta‡ zobrazen˜ch adres
         mov       ax,0c000h                ; po‡ te‡n¡ segment pro testy
zobrrom1:call      findrom                  ; nalezen¡ ROM
         jc        zobrrom4                 ; nen¡ dal¨¡ modul ROM
         or        dx,dx                    ; je to prvn¡ adresa ?
         jz        zobrrom2                 ; je to prvn¡ adresa
         call      outoddel                 ; zobrazen¡ oddˆlova‡e
zobrrom2:call      outwordh                 ; zobrazen¡ segmentu modulu ROM
         inc       dx                       ; zv˜¨en¡ ‡¡ta‡e zobrazen˜ch adres
         mov       ax,bx                    ; dal¨¡ adresa pro hled n¡
         cmp       ax,0c000h                ; je spr vn  adresa ?
         ja        zobrrom1                 ; nalezen¡ dal¨¡ho modulu
zobrrom4:ret

public   findrom
findrom:                                  ;* nalezen¡ modulu ROM-BIOS
                                            ; VSTUP: AX=po‡ te‡n¡ segment
                                            ; VSTUP: AX=segment dal¨¡ho modulu
                                            ;         BX=segment za modulem
                                            ;         CY=nen¡ dal¨¡ modul ROM


         push      es
         push      dx
findrom2:mov       es,ax                    ; testovan˜ segment
         cmp       word ptr es:[0],55aah    ; je z kladn¡ modul ROM-BIOS ?
         je        findrom1                 ; je z kladn¡ modul ROM-BIOS
         cmp       word ptr es:[0],0aa55h   ; je identifikace modulu ROM ?
         je        findrom1                 ; nen¡ modul ROM
         add       ax,80h                   ; adresa dal¨¡ho modulu
         jnc       findrom2                 ; test dal¨¡ho modulu
         jmp       short findrom5           ; konec - modul nenalezen
findrom1:xor       bx,bx                    ; BX <- 0
         mov       bl,es:[2]                ; d‚lka modulu ROM
         mov       cl,5                     ; po‡et rotac¡ pro p©evod
         shl       bx,cl                    ; p©evod na odstavce
         add       bx,ax                    ; nov˜ konec modulu
         clc
findrom5:pop       dx
         pop       es
         ret
; -----------------------------------------------------------------------------
public   zobrhod
zobrhod:                                  ;* zobrazen¡ hodin procesoru
         lea       si,[txthod]
         call      outtxtx                  ; zobrazen¡ textu "Hodiny:"

         test      word ptr ds:[prepinac],8000h ; je cejchov n¡ hodin ?
         jz        zobrhod2                 ; nen¡ cejchov n¡ hodin
         mov       ax,ds:[hodinyh]          ; vy¨¨¡ slovo hodin
         call      outnum
         mov       al,"-"
         call      outch
zobrhod2:mov       ax,ds:[hodiny]
         call      outnum                   ; zobrazen¡ hodin procesoru
         cmp       byte ptr ds:[procesor],7
         jb        zobrhod4                 ; ni‘¨¡ procesor ne‘ 286
         mov       al,"."
         call      outch
         cmp       byte ptr ds:[hodinyd],10
         jae       zobrhod3
         mov       al,"0"
         call      outch
zobrhod3:xor       ah,ah
         mov       al,ds:[hodinyd]          ; desetiny udaje
         call      outnum
zobrhod4:lea       si,[txtmhz]
         call      outtxt                   ; zobrazen¡ textu "MHz"
         ret
; -----------------------------------------------------------------------------
public   zobrlogd
zobrlogd:                                 ;* zobrazen¡ logick˜ch disk–
         lea       si,[txtlogd]
         call      outtxtx                  ; zobrazen¡ textu "logick‚ disky:"
                                          ;* £schova aktivn¡ho disku
         mov       ah,19h
         int       21h                      ; poskytnut¡ aktivn¡ho disku
         push      ax                       ; £schova aktivn¡ho disku

         xor       bx,bx                    ; BH-p©¡znak zobrazen¡
         xor       dx,dx
         mov       cx,32                    ; po‡et disk– k provˆ©en¡
zobrlog1:                                 ;* test jednoho disku
         mov       ah,0eh
         int       21h                      ; v˜bˆr aktivn¡ho disku
         mov       ah,19h
         int       21h                      ; dotaz na nov˜ aktivn¡ disk
         cmp       al,dl                    ; byl disk zmˆnˆn ?
         jne       zobrlog4                 ; disk nebyl zmˆnˆn
         or        bx,bx                    ; byl ji‘ zobrazen nˆjak˜ disk ?
         jz        zobrlog2                 ; nebyl je¨tˆ ‘ dn˜ disk
         call      outoddel                 ; zobrazen¡ oddˆlova‡e parametr–
zobrlog2:inc       bx                       ; zv˜¨en¡ ‡¡ta‡e disk–
         add       al,"A"                   ; korekce na ozna‡en¡ ASCII
         call      outch                    ; zobrazen¡ ozna‡en¡ disku
zobrlog4:inc       dx                       ; zv˜¨en¡ ‡¡sla disku
         loop      zobrlog1                 ; test dal¨¡ho disku
                                          ;* n vrat aktivn¡ho disku
         pop       dx                       ; n vrat aktivn¡ho disku
         mov       ah,0eh
         int       21h                      ; n vrat p–vodn¡ho aktivn¡ho disku
         ret

; -----------------------------------------------------------------------------
;                          Univerz ln¡ podprogramy
; -----------------------------------------------------------------------------
public   readcmos
readcmos:                                 ;* ‡ten¡ bajtu z CMOS s kontrolou
                                            ; VSTUP: AH=adresa
                                            ; VSTUP: AL=bajt
                                            ;         CY=chyba (data neplat¡)

         push      dx
         xor       al,al                    ; p©ednastaven¡ dat
         call      chscmos                  ; kontroln¡ sou‡et CMOS
         jc        readcm4                  ; neplatn˜ typ po‡¡ta‡e
         stc
         jne       readcm4                  ; chybn˜ kontroln¡ sou‡et
         call      rcmos                    ; ‡ten¡ bajtu z CMOS
         clc                                ; p©¡znak - operace OK
readcm4: pop       dx
         ret

public   chscmos
chscmos:                                  ;* kontroln¡ sou‡et CMOS
                                            ; VSTUP: DX=kontroln¡ sou‡et
                                            ;         CY=chybn˜ typ po‡¡ta‡e
                                            ;         ZY=kontroln¡ sou‡et OK

         push      ax
         push      cx
         xor       dx,dx
         mov       cl,ds:[smodel]           ; syst‚mov˜ model
         cmp       cl,0feh                  ; je PC nebo PC-XT ?
         cmc
         jc        chscmos2                 ; je PC nebo PC-XT
         cmp       cl,0fbh                  ; je XT ?
         stc
         je        chscmos2                 ; je XT
         mov       cx,1eh                   ; po‡et bajt– ke kontrole
         mov       ah,10h                   ; po‡ te‡n¡ adresa
chscmos1:                                 ;* proveden¡ kontroln¡ho sou‡tu
         call      rcmos                    ; ‡ten¡ bajtu z CMOS
         add       dl,al                    ; p©i‡ten¡ ke kontroln¡mu sou‡tu
         adc       dh,0                     ; p©enos do vy¨¨¡ho bajtu
         inc       ah                       ; zv˜¨en¡ ukazatele pamˆti
         loop      chscmos1                 ; dal¨¡ bajt
                                          ;* kontrola kontroln¡ho sou‡tu
         mov       ah,2eh                   ; adresa vy¨¨¡ho bajtu
         call      rcmos                    ; ‡ten¡ vy¨¨¡ho bajtu z CMOS
         mov       ch,al                    ; £schova vy¨¨¡ho bajtu
         inc       ah                       ; adresa ni‘¨¡ho bajtu
         call      rcmos                    ; ‡ten¡ ni‘¨¡ho bajtu z CMOS
         mov       cl,al                    ; £schova ni‘¨¡ho bajtu
         cmp       dx,cx                    ; porovn n¡ kontroln¡ho sou‡tu
         clc                                ; p©¡znak - operace OK
chscmos2:pop       cx
         pop       ax
         ret

public   rcmos
rcmos:                                    ;* ‡ten¡ bajtu z CMOS
                                            ; VSTUP: AH=adresa
                                            ; VSTUP: AL=bajt

         push      ax
         mov       al,ah                    ; adresa bajtu z CMOS
         out       [70h],al                 ; nastaven¡ adresy pro ‡ten¡
         pop       ax
         in        al,[71h]                 ; data na adrese v CMOS
         ret
; -----------------------------------------------------------------------------
public   outtxt
outtxt:                                   ;* v˜stup textu DS:SI
         push      ax
outtxt1: lodsb
         or        al,al                    ; je konec textu ?
         jz        outtxt2
         call      outch                    ; v˜stup znaku
         jmp       short outtxt1
outtxt2: pop       ax
         ret
; -----------------------------------------------------------------------------
public   outtxtx
outtxtx:                                  ;* v˜stup textu s od© dkov n¡m
         push      ax
         mov       al,13
         call      outch
         mov       al,10
         call      outch
         pop       ax
         call      outtxt
         jmp       short outspc

public   outoddel
outoddel:                                 ;* zobrazen¡ oddˆlovac¡ ‡ rky
         push      ax
         mov       al,","
         call      outch
         pop       ax

public   outspc
outspc:                                   ;* zobrazen¡ mezery
         push      ax
         mov       al," "
         call      outch                    ; zobrazen¡ mezery
         pop       ax
         ret
; -----------------------------------------------------------------------------
public   outch
outch:                                    ;* v˜stup znaku z AL
         push      ax
         push      dx
         mov       dl,al
         mov       ah,2
         int       21h                      ; v˜stup znaku
         pop       dx
         pop       ax
         ret
; -----------------------------------------------------------------------------
public   outind
outind:                                   ;* zobrazen¡ indexovan‚ho textu
                                            ; VSTUP: DS:SI=tabulka text–
                                            ;        CL=‡¡slo polo‘ky

         push      ax
         push      cx
         push      si
         xor       ch,ch                    ; CH <- 0
         jcxz      outind2                  ; je polo‘ka 0
outind1: lodsb
         or        al,al                    ; je ji‘ konec polo‘ky ?
         jnz       outind1                  ; nalezen¡ konce polo‘ky
         loop      outind1                  ; dal¨¡ polo‘ka
outind2: call      outtxt                   ; zobrazen¡ textu
         pop       si
         pop       cx
         pop       ax
         ret
; -----------------------------------------------------------------------------
public   outnum0
outnum0:                                  ;* zobrazen¡ ‡¡sla AL
         push      ax
         cbw
         call      outnum
         pop       ax
         ret

public   outnum
outnum:                                   ;* zobrazen¡ ‡¡sla AX
         push      ax
         push      bx
         push      cx
         push      dx
         xor       cx,cx                    ; ‡¡ta‡ ‡¡slic
outnum1: xor       dx,dx                    ; DX <- 0
         mov       bx,10
         div       bx
         add       dl,"0"                   ; korekce na ‡¡slici ASCII
         push      dx                       ; ul¢zen¡ ‡¡slice do z sobn¡ku
         inc       cx                       ; zv˜¨en¡ ‡¡ta‡e ‡¡slic
         or        ax,ax                    ; je ji‘ 0 ?
         jnz       outnum1                  ; je ji‘ 0
                                          ;* zobrazen¡ ‡¡sla
outnum2: pop       ax
         call      outch                    ; zobrazen¡ ‡¡slice
         loop      outnum2                  ; dal¨¡ ‡¡slice
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret
; -----------------------------------------------------------------------------
public   outwordh
outwordh:                                 ;* zobrazen¡ slova HEX
         push      ax
         call      outword
         mov       al,"h"
         call      outch
         pop       ax
         ret

public   outword
outword:                                  ;* zobrazen¡ slova HEX
         xchg      ah,al
         call      outbyte
         xchg      ah,al

public   outbyte
outbyte:                                  ;* zobrazen¡ bajtu HEX
         push      ax
         shr       al,1
         shr       al,1
         shr       al,1
         shr       al,1
         call      outhex
         pop       ax
outhex:  push      ax
         and       al,0fh
         call      outcis
         pop       ax
         ret
; -----------------------------------------------------------------------------
public   outcis
outcis:                                   ;* zobrazen¡ ‡¡slice AL
         push      ax
         add       al,"0"
         cmp       al,"9"
         jbe       outcis2                  ; nen¡ ‡¡slice HEX
         add       al,7                     ; korekce na p¡smeno HEX
outcis2: call      outch                    ; zobrazen¡ ‡¡slice
         pop       ax
         ret
; -----------------------------------------------------------------------------
public   hledpar
hledpar:                                  ;* hled n¡ parametru
                                            ; VSTUP: AH=znak parametru
                                            ; VSTUP: CY=parametr nenalezen
                                            ;         DS:SI=text za parametrem

         push      cs
         pop       ds
         mov       si,81h                   ; za‡ tek p©¡kazov‚ho © dku
                                          ;* nalezen¡ p©ep¡na‡e "/"
hledpar1:lodsb                              ; na‡ten¡ znaku
hledpar2:or        al,al                    ; je konec ?
         stc                                ; p©¡znak - p©ep¡na‡ nenalezen
         jz        hledpar3                 ; je konec textu
         cmp       al,"/"                   ; je "/" ?
         jne       hledpar1                 ; nen¡ "/" - dal¨¡ znak
                                          ;* test hledan‚ho p©ep¡na‡e
         lodsb                              ; na‡ten¡ p©ep¡na‡e
         cmp       al,"a"
         jb        hledpar4
         cmp       al,"z"
         ja        hledpar4
         sub       al,32                    ; p©evod na velk‚ p¡smeno
hledpar4:
         cmp       al,ah                    ; je hledan˜ p©ep¡na‡ ?
         jne       hledpar2                 ; nen¡ hledan˜ p©ep¡na‡

hledpar3:ret
; -----------------------------------------------------------------------------
;                       Detekce videokarty
;
; VSTUP: AX=1: CGA
;            2: Mono CGA
;            3: EGA
;            4: EGA 64kB
;            5: EGA Mono
;           (6: IBM8514)
;            7: Hercules
;           (8: ATT400)
;            9: VGA
;           10: PC3270
; -----------------------------------------------------------------------------
PUBLIC   DetectCard
DetectCard:                               ;* detekce videokarty
                                            ; VSTUP: AX=detekovan  karta
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         push      ds
         push      es
         call      DetCard0                 ; detekce videokarty
         xor       ax,ax
         mov       al,ds:[VideoCard]        ; detekovan  videokarta
         pop       es
         pop       ds
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         ret

; ------ test VESA

DetCard0:call      GetVESA                  ; test VESA
         jc        DetCar02                 ; nen¡ VESA
         mov       byte ptr ds:[VideoCard],11 ; je karta VESA
         ret

DetCar02:call      GetVideoMod              ; dotaz na videom¢d
         cmp       al,7                     ; je monochromatick˜ videom¢d ?
         je        DetCard5                 ; je monochromatick˜ videom¢d
                                          ;* barevn˜ re‘im - rozpozn n¡ EGA/VGA
         call      DetCard8                 ; test karet EGA/VGA
         jc        DetCard2                 ; nen¡ karta EGA/VGA
DetCard1:call      DetCardA                 ; rozli¨en¡ karet EGA/VGA
         ret
DetCard2:                                 ;* rozpozn n¡ karty PC3270
         call      DetCardP                 ; detekce karty PC3270
         jc        DetCard3                 ; nen¡ karta PC3270
         mov       byte ptr ds:[VideoCard],10; je videokarta PC3270
         ret

DetCard3:                                 ;* rozpozn n¡ karet CGA/MCGA
         mov       byte ptr ds:[VideoCard],1; p©ednastaven¡ karty CGA
         call      DetCardG                 ; test karty MCGA
         jc        DetCard4                 ; nen¡ karta MCGA
         mov       byte ptr ds:[VideoCard],2; je karta MCGA
DetCard4:ret

DetCard5:                                 ;* monochrom. re‘im - test EGA/VGA
         call      DetCard8                 ; test karty EGA/VGA
         jnc       DetCard1                 ; je EGA/VGA - rozli¨en¡ EGA a VGA
                                          ;* test karty Hercules/MDA
         call      DetCardJ                 ; test karty Hercules
         jc        DetCard6                 ; nen¡ karta Hercules
         mov       byte ptr ds:[VideoCard],7; je karta Hercules
         ret
DetCard6:                                 ;* test karty CGA (test pamˆti)
         mov       si,0b800h                ; segment videopamˆti CGA
         mov       es,si                    ; segment videopamˆti CGA
         xor       si,si                    ; po‡ te‡n¡ adresa videopamˆti CGA
         mov       ax,es:[si]               ; p–vodn¡ slovo z videopamˆti
         not       ax                       ; inverze
         not       word ptr es:[si]         ; inverze i p–vodn¡ho slova
         nop
         nop
         cmp       ax,es:[si]               ; je tam pamˆŸ RAM ?
         jne       DetCard7                 ; nen¡ pamˆŸ VRAM
         mov       byte ptr ds:[VideoCard],1; je karta CGA
DetCard7:ret

DetCard8:                                 ;* test karty EGA/VGA
         mov       ax,1200h
         mov       bl,10h                   ; podslu‘ba poskytnut¡ parametr–
         mov       bh,0ffh                  ; p©ednastaven¡
         mov       cl,0fh                   ; p©ednastaven¡
         call      int10p                   ; dotaz na EGA kartu
         cmp       cl,0ch
         jge       DetCard9                 ; nen¡ karta EGA
         cmp       bh,1
         jg        DetCard9                 ; nen¡ karta EGA
         cmp       bl,3
         jg        DetCard9                 ; nen¡ karta EGA
         clc                                ; p©¡znak - je karta EGA
         ret
DetCard9:stc                                ; p©¡znak - nen¡ karta EGA
         ret

DetCardA:                                 ;* rozli¨en¡ karet EGA/VGA
         mov       byte ptr ds:[VideoCard],4; p©ednastaven¡ karty EGA64
;         cmp       bh,1                     ; je monochromatick˜ m¢d ?
;         je        DetCardD                 ; je monochromatick˜ m¢d
         call      DetCardE                 ; test karty EGA 64
         jnc       DetCardC                 ; je karta EGA 64
         or        bl,bl                    ; je EGA 64 ?
         jz        DetCardC                 ; je EGA 64
         mov       byte ptr ds:[VideoCard],3; p©ednastaven¡ karty EGA
         call      DetCardG                 ; test karty VGA
         jnc       DetCardB                 ; je karta VGA
         mov       bx,0c000h                ; segment s ROM VGA
         mov       es,bx                    ; segment s ROM VGA
         mov       bx,39h                   ; adresa textu "Z449"
         cmp       word ptr es:[bx],"Z4"    ; je text "Z4" ?
         jne       DetCardC                 ; nen¡ karta VGA
         cmp       word ptr es:[bx+2],"49"  ; je text "49" ?
         jne       DetCardC                 ; nen¡ karta VGA
DetCardB:mov       byte ptr ds:[VideoCard],9; je karta VGA
DetCardC:ret
;DetCardD:mov       byte ptr ds:[VideoCard],5; je karta EGA Mono
;         ret

DetCardE:                                 ;* test karty EGA 64
         cmp       cl,2
         jb        DetCardT                 ; je karta EGA 64
         cmp       cl,6
         jb        DetCardF                 ; nen¡ karta EGA 64
         cmp       cl,8
DetCardT:cmc
DetCardF:ret

DetCardG:                                 ;* test karty VGA
         mov       ax,1a00h
         call      int10p                   ; dotaz na po‡et linek na znak
         cmp       al,1ah                   ; je funkce obsluhovan  ?
         jne       DetCardI                 ; funkce nen¡ obsluhovan 
         cmp       bl,7                     ; je 7 linek na znak ?
         je        DetCardH                 ; je karta OK
         cmp       bl,8
         je        DetCardH                 ; je karta OK
         cmp       bl,11
         jb        DetCardI                 ; nen¡ karta
         cmp       bl,12
         ja        DetCardI                 ; nen¡ karta
DetCardH:clc                                ; p©¡znak - je karta
         ret
DetCardI:stc                                ; p©¡znak - nen¡ karta
         ret

DetCardJ:                                 ;* test karty Hercules
         mov       dx,03bah                 ; stavov˜ port karty
         xor       bl,bl                    ; ‡¡ta‡ doby impulsu
         in        al,dx                    ; po‡ te‡n¡ stav
         and       al,80h                   ; sign l zpˆtn‚ho bˆhu
         mov       ah,al                    ; £schova stavu registru
         mov       cx,8000h                 ; maxim ln¡ doba testu
DetCardK:                                 ;* ‡ek n¡ na zmˆnu stavu bitu 7
         in        al,dx                    ; ‡ten¡ stavov‚ho registru
         and       al,80h                   ; sign l zpˆtn‚ho bˆhu
         cmp       al,ah                    ; byla zmˆna stavu sign lu ?
         je        DetCardL                 ; nebyla zmˆna stavu sign lu
         inc       bl                       ; ‡¡ta‡ d‚lky impulsu
         cmp       bl,10                    ; je minim ln¡ doba impulsu ?
         jae       DetCardM                 ; impuls je dostate‡nˆ dlouh˜
DetCardL:loop      DetCardK                 ; dal¨¡ test impulsu
         stc                                ; p©¡znak - nen¡ karta Hercules
         ret

DetCardM:                                 ;* ‡ek n¡ na bity 5,4 -> 01
         mov       cx,8000h                 ; maxim ln¡ doba testu
DetCardN:in        al,dx                    ; ‡ten¡ stavov‚ho registru
         and       al,30h
         cmp       al,10h
         jne       DetCardO                 ; je pouze karta MDA
         loop      DetCardN                 ; dal¨¡ test
         mov       al,2                     ; p©¡znak - je karta Hercules
         clc
         ret
DetCardO:mov       al,1                     ; p©¡znak - je karta MDA
         clc
         ret

DetCardP:                                 ;* test videokarty PC3270
         mov       al,6
         xor       cx,cx                    ; CX <- 0
         xor       dx,dx
         mov       ah,30h
         call      int10p
         mov       ax,cx
         or        ax,dx
         jz        DetCardR                 ; nen¡ karta PC3270
         push      ds
         mov       ds,cx                    ; segment
         mov       bx,dx                    ; offset
         mov       al,ds:[bx+2]
         pop       ds
         or        al,al
         jz        DetCardQ                 ; je karta PC3270
         cmp       al,2
         jne       DetCardR                 ; nen¡ karta PC3270
DetCardQ:mov       dx,188h
         in        al,dx
         test      al,4
         jz        DetCardR                 ; nen¡ karta PC3270
         clc                                ; p©¡znak - je karta PC3270
         ret
DetCardR:stc                                ; p©¡znak - nen¡ karta PC3270
         ret

; ------ test karty VESA

GetVESA  PROC      NEAR

         mov       di,offset DiskBuf
         mov       word ptr ds:[di],0       ; p©ednastaven¡ - neplatn  data
         push      ds
         pop       es
         mov       ax,4f00h
         call      Int10P

         cmp       ax,4fh
         jne       GetVESA8                 ; chyba
         cmp       word ptr ds:[di],"EV"
         jne       GetVESA8
         cmp       word ptr ds:[di+2],"AS"
         jne       GetVESA8

         mov       ax,ds:[di+12h]           ; pamˆŸ celkem v 64K bloc¡ch
         mov       cl,6
         shl       ax,cl                    ; pamˆŸ v 1KB bloc¡ch
         mov       ds:[VesaMEM],ax          ; pamˆŸ VESA
         clc                                ; operace OK
         ret

GetVESA8:stc                                ; p©¡znak chyby
         ret

GetVESA  ENDP

; -----------------------------------------------------------------------------
;                       Poskytnut¡ videom¢du
;
; VSTUP: AL=aktu ln¡ videom¢d
;         AH=po‡et pozic na © dek
;         BH=‡¡slo aktivn¡ str nky
; -----------------------------------------------------------------------------
PUBLIC   GetVideoMod
GetVideoMod:                              ;* poskytnut¡ videom¢du
                                            ; VSTUP: AL=videm¢d
                                            ;         AH=po‡et pozic na © dek
                                            ;         BH=‡¡slo aktivn¡ str nky

         mov       ah,0fh                   ; funkce poskytnut¡ videom¢du
         call      int10p
         and       al,7fh                   ; zru¨en¡ bitu 7
         ret
; -----------------------------------------------------------------------------
int10p:                                   ;* vol n¡ INT 10h s £schovou registr–
         push      si
         push      di
         push      bp
         push      ds
         push      es
         int       10h
         pop       es
         pop       ds
         pop       bp
         pop       di
         pop       si
         ret

;------------------------------------------------------------------------------
;                         Zobrazen¡ obsahu pamˆti
;------------------------------------------------------------------------------
;þ
memorydos:                                ;* zobrazen¡ obsahu pamˆti
         push      cs
         pop       ds
;         mov       ax,ds:[2]                ; konec pamˆti
;         mov       cs:[memmax],ax           ; konec pamˆti
;
;                                          ;* zmen¨en¡ bloku pamˆti
;         mov       bx,offset konec + 1024   ; konec programu
;         add       bx,15                    ; zaokrouhlen¡ na odstavec
;         mov       cl,4
;         shr       bx,cl                    ; p©epo‡et na odstavce
;         mov       ah,4ah                   ; funkce modifikace bloku pamˆti
;         int       21h                      ; zmen¨en¡ bloku pamˆti
                                          ;* zobrazen¡ nadpisu
         lea       bx,[txtnadp]
         call      outtxtz                  ; zobrazen¡ nadpisu
         call      srcfrst                  ; nalezen¡ prvn¡ho aloka‡n¡ho bloku
         jnc       mem1
         jmp       mem9                     ; chyba pamˆti
                                          ;* zobrazen¡ obahu pamˆti
mem1:    mov       cs:[memmax],ax           ; po‡ tek pamˆti

mem2:    mov       ds,ax                    ; segment alok. bloku

         push      ax

         cmp       ax,0b800h                ; je pod hranic¡ UMB ?
         ja        mem21                    ; nen¡ pod hranic¡ UMB
         add       ax,ds:[3]                ; n sleduj¡c¡ blok
         cmp       ax,0b800h                ; p©eskakuje hranici UMB ?
         jae       mem22                    ; p©eskakuje hranici 1 MB

mem21:   mov       ax,ds:[3]                ; velikost bloku
         add       cs:[memmax],ax           ; zv˜¨en¡ ‡¡ta‡e pamˆti

mem22:   mov       ax,ds
         call      testfre                  ; test, zda je voln˜
         jc        mem4                     ; nen¡ to voln˜ blok
         mov       ax,ds:[3]                ; velikost bloku
         add       cs:[memfree],ax          ; ‡¡ta‡ voln‚ pamˆti

         cmp       ax,cs:[memspoj]          ; je vˆt¨¡ blok ?
         jbe       mem4
         mov       cs:[memspoj],ax          ; je to vˆt¨¡ voln˜ blok

mem4:    pop       ax

         mov       cl,2
         call      outtab                   ; nastaven¡ tabel toru
         push      ax
         inc       ax
         call      hexwrd                   ; zobrazen¡ slova HEX
         mov       cl,8
         call      outtab                   ; nastaven¡ na dal¨¡ zna‡ku tabel.
         call      vlastnik                 ; zobrazen¡ vlastn¡ka
         mov       cl,8
         call      outtab                   ; nastaven¡ na dal¨¡ zna‡ku tabel.
         mov       ax,ds:[3]                ; velikost bloku
         call      deknms                   ; zobrazen¡ velikosti segmentu
         mov       cl,8
         call      outtab                   ; nastaven¡ na dal¨¡ zna‡ku tabel.
         call      typ                      ; zobrazen¡ typu bloku
         mov       cl,11
         call      outtab                   ; nastaven¡ na dal¨¡ zna‡ku tabel.
         call      pathenv                  ; zobrazen¡ p©¡stupov‚ cesty
         call      outcr
         pop       ax
         mov       ds,ax

         cmp       byte ptr ds:[0],"Z"      ; je ji‘ posledn¡ blok ?
         jne       mem8

         cmp       ax,0b800h
         ja        mem9                     ; je to ji‘ posledn¡ blok UMB

         push      ax
         call      srcnxt                   ; dal¨¡ blok
         jc        mem6
         call      testblk
mem6:    pop       ax
         jc        mem9                     ; byl to ji‘ posledn¡ alok. blok

         lea       bx,[txtumb]              ; text "UMB"
         call      outtxtz                  ; zobrazen¡ nadpisu

mem8:    call      srcnxt                   ; nastaven¡ na dal¨¡ alok. blok
         jc        mem9
         jmp       mem2                     ; dal¨¡ alok. blok

mem9:                                     ;* konec zobrazen¡ pamˆti
         push      cs
         pop       ds
         call      outcr
                                          ;* celkov  pamˆŸ
         lea       bx,[txtcelk]             ; text "Celkov  pamˆŸ"
         call      outtxtz

         mov       ax,cs:[memmax]           ; konec pamˆti
         call      deknms                   ; zobrazen¡ informace o pamˆti
         call      outcr                    ; nov˜ © dek
                                          ;* voln  pamˆŸ
         lea       bx,[txtvoln]             ; text "Voln  pamˆŸ"
         call      outtxtz

         mov       ax,cs:[memfree]          ; voln  pamˆŸ
         call      deknms                   ; zobrazen¡ informace o voln‚ pamˆti
         call      outcr

         lea       bx,[txtspoj]             ; text "Voln  spojit "
         call      outtxtz

         mov       ax,cs:[memspoj]          ; spojit  pamˆŸ
         call      deknms
         call      outcr
         ret                                ; n vrat z programu

memmax   dw        0                        ; konec pamˆti
memfree  dw        0                        ; voln  pamˆŸ
memspoj  dw        0                        ; voln  pamˆŸ spojit 

; -----------------------------------------------------------------------------
public   pathenv
pathenv:                                  ;* zobrazen¡ p©¡stupov‚ cesty
                                            ; VSTUP: DS=segment alok. bloku
         push      ax
         push      bx
         push      si
         push      ds
         mov       ah,30h
         int       21h                      ; poskytnut¡ ‡¡sla verze syst‚mu
         cmp       al,3                     ; je verze alespo¤ 3.00 ?
         jb        pathenv3                 ; je n¡zk  verze syst‚mu

         mov       ax,ds
         call      testfre                  ; test, zda je voln˜
         jnc       pathenv3                 ; nem  vlastn¡ka

         call      testprog                 ; test, zda je program
         jc        pathenv3                 ; nen¡ to program
         mov       ds,bx                    ; popisova‡ prost©ed¡
         inc       ax
         cmp       ax,ds:[1]                ; pat©¡ to tomu programu ?
         jne       pathenv3                 ; nepat©¡
         inc       bx                       ; segment prost©ed¡
         jz        pathenv3                 ; neplat¡

         mov       ds,bx                    ; segment prost©ed¡
         mov       si,3                     ; po‡ te‡n¡ offset prost©ed¡ + 3
         xor       ax,ax                    ; AX <- 0 (hledan‚ slovo)
                                          ;* nalezen¡ za‡ tku cesty
pathenv1:inc       si                       ; zv˜¨en¡ adresy textu
         cmp       ds:[si-4],ax             ; je konec ©etˆzc– prost©ed¡ (0) ?
         jne       pathenv1                 ; nen¡ konec ©etˆzc– prost©ed¡
         inc       ax
         cmp       ds:[si-2],ax             ; je nˆjak˜ ©etˆzec ?
         jne       pathenv3                 ; chybn˜ ©etˆzec
pathenv2:                                 ;* zobrazen¡ ©etˆzce cesty
         lodsb
         or        al,al
         jz        pathenv3
         call      outchh
         jmp       short pathenv2
pathenv3:pop       ds
         pop       si
         pop       bx
         pop       ax
         ret
; -----------------------------------------------------------------------------
public   typ
typ:                                      ;* zobrazen¡ typu bloku
                                            ; VSTUP: DS=segment alok. bloku
         push      ax
         push      ds
         mov       ax,ds                    ; segment bloku
         call      testfre                  ; test, zda je voln˜
         lea       bx,[typvol]              ; text "voln "
         jnc       typ6                     ; je voln˜ blok

         call      testprog                 ; je to program ?
         lea       bx,[typprog]             ; text "PROGRAM"
         jnc       typ6                     ; je program
                                          ;* test, jestli je prost©ed¡
         mov       ax,dx                    ; nov˜ segment
         dec       ax
         call      testprog                 ; je to program ?
         jc        typ5                     ; pat©¡ syst‚mu
         mov       ax,ds
         cmp       bx,ax                    ; je to prost©ed¡ ?
         lea       bx,[typenv]              ; ozna‡en¡ - je to prost©ed¡
         je        typ6                     ; je to prost©ed¡
         lea       bx,[typdat]              ; jsou to data
         jmp       short typ6
typ5:    lea       bx,[typsys]              ; pat©¡ syst‚mu
typ6:    call      outtxtz                  ; zobrazen¡ textu typu
typ9:    pop       ds
         pop       ax
         ret
; -----------------------------------------------------------------------------
public   vlastnik
vlastnik:                                 ;* zobrazen¡ vlastn¡ka bloku
                                            ; VSTUP: DS=segment alok. bloku
         push      ax
         push      ds
                                          ;* voln˜ aloka‡n¡ blok
         mov       dx,ds:[1]                ; vlastn¡k bloku
         mov       ax,dx                    ; vlastn¡k bloku

         push      ax
         mov       ax,ds
         call      testfre                  ; test, zda je voln˜
         pop       ax
         jc        vlast0                   ; nen¡ voln˜
         xor       ax,ax                    ; je voln˜

vlast0:  call      hexwrd                   ; zobrazen¡ slova HEX
         mov       cl,7
         call      outtab                   ; nastaven¡ na dal¨¡ zna‡ku tabel.

         mov       ax,ds

         call      testfre                  ; test, zda je voln˜
         jc        vlast2                   ; nen¡ voln˜ blok

vlast1:  lea       bx,[vlsys]               ; vlastn¡k - system
         call      outtxtz                  ; zobrazen¡ textu
         jmp       short vlast9
                                          ;* je to program
vlast2:  call      namprog                  ; zobrazen¡ jm‚na programu
         jnc       vlast9                   ; byl to program

vlast4:                                   ;* vlastn¡k jej nˆkdo jin˜
         mov       ax,dx                    ; segment jin‚ho vlastn¡ka
         dec       ax
         call      namprog                  ; zobrazen¡ jm‚na jin‚ho vlastn¡ka
vlast9:  pop       ds
         pop       ax
         ret
; -----------------------------------------------------------------------------
namprog:                                  ;* zobrazen¡ jm‚na programu
                                            ; VSTUP: AX=adresa alok. bloku
                                            ; VSTUP: CY=nen¡ program
         push      ax
         push      bx
         push      cx
         push      si
         push      ds
         mov       ds,ax                    ; segment bloku

         call      testfre                  ; test, zda je voln˜
         jnc       namprg2                  ; je to voln˜ blok
         call      testprog                 ; je to program ?
         jc        namprg2                  ; nen¡ program
                                          ;* pro verzi 4.00 p©¡m‚ zobrazen¡
         push      ax
         mov       ah,30h
         int       21h                      ; poskytnut¡ ‡¡sla verze syst‚mu
         cmp       al,4                     ; je verze alespo¤ 4.00 ?
         pop       ax
         jae       namprg3                  ; je verze syst‚mu alespo¤ 4.00
                                          ;* nalezen¡ jm‚na programu pro DOS3.xx
         mov       ds,ds:[3ch]              ; segment prost©ed¡
         mov       si,3                     ; po‡ te‡n¡ offset prost©ed¡ + 3
         xor       ax,ax                    ; AX <- 0 (hledan‚ slovo)
                                          ;* nalezen¡ za‡ tku cesty
namprg4: inc       si                       ; zv˜¨en¡ adresy textu
         cmp       ds:[si-4],ax             ; je konec ©etˆzc– prost©ed¡ (0) ?
         jne       namprg4                  ; nen¡ konec ©etˆzc– prost©ed¡
         inc       ax
         cmp       ds:[si-2],ax             ; je nˆjak˜ ©etˆzec ?
         jne       namprg8                  ; chybn˜ ©etˆzec
                                          ;* nalezen¡ za‡ tku jm‚na
namprg5: mov       bx,si                    ; nov˜ za‡ tek jm‚na
namprg6: lodsb
         or        al,al
         jz        namprg7                  ; je konec jm‚na
         cmp       al,"\"                   ; je oddˆlova‡ cesty ?
         je        namprg5                  ; je nov˜ oddˆlova‡ cesty
         jmp       short namprg6            ; test dal¨¡ho znaku

namprg8:                                  ;* pokus o interpretaci jako COMMAND
         mov       bx,ds
         dec       bx
         mov       ds,bx                    ; popisova‡ bloku
         cmp       bx,ds:[1]                ; je blok nad vlastn¡kem ?
         jbe       namprg2                  ; blok je nad vlastn¡kem
         push      cs
         pop       ds                       ; DS <- CS
         lea       bx,[txtcomm]             ; text COMMAND

namprg7: mov       si,bx                    ; adresa jm‚na programu
         jmp       short namprg1            ; zobrazen¡ jm‚na
                                          ;* zobrazen¡ jm‚na pro DOS 4.xx
namprg3: mov       si,8                     ; adresa jm‚na programu
         mov       cx,8                     ; d‚lka jm‚na programu
namprg1: lodsb                              ; na‡ten¡ znaku jm‚na
         or        al,al                    ; konec jm‚na ?
         jz        namprg2                  ; konec jm‚na
         cmp       al,"."                   ; je konec jm‚na ?
         je        namprg2                  ; je konec jm‚na
         call      outchh                   ; zobrazen¡ znaku jm‚na
         loop      namprg1                  ; dal¨¡ znak jm‚na
         clc
namprg2: pop       ds
         pop       si
         pop       cx
         pop       bx
         pop       ax
         ret
; -----------------------------------------------------------------------------
;                      Operace s aloka‡n¡mi bloky pamˆti
; -----------------------------------------------------------------------------
public   testprog
testprog:                                 ;* test, zda je blok programem
                                            ; VSTUP: AX=adresa alok. bloku
                                            ; VSTUP: CY=nen¡ to program
                                            ;         BX=segment prost©ed¡-1
         push      ax
         push      ds
         mov       ds,ax
         cmp       byte ptr ds:[0],"M"      ; je vnit©n¡ blok ?
         je        testpr1                  ; je vnit©n¡ blok
         cmp       byte ptr ds:[0],"Z"      ; je posledn¡ blok ?
         stc
         jne       testpr2                  ; nen¡ to v–bec aloka‡n¡ blok
testpr1: inc       ax
         cmp       ax,ds:[1]                ; vlastn¡ s m sebe ?
         stc
         jne       testpr2                  ; nen¡ to program
         mov       bx,ds:[3ch]              ; adresa segmentu prost©ed¡
         dec       bx
         clc
testpr2: pop       ds
         pop       ax
         ret

; -----------------------------------------------------------------------------
; Test, zda je blok AX voln˜ (nebo je to tento program) -> CY=nen¡ voln˜

TestFre  PROC      NEAR

         push      ax
         push      bx
         push      ds

         mov       ds,ax
         mov       ax,ds:[1]                ; vlastn¡k
         mov       bx,cs
         cmp       ax,bx                    ; je to tento program ?
         je        TestFre2                 ; je to tento program

         or        ax,ax                    ; je to voln˜ blok
         jz        TestFre2                 ; je to voln˜ blok
         stc                                ; p©¡znak, ‘e nen¡ voln˜

TestFre2:pop       ds
         pop       bx
         pop       ax
         ret

TestFre  ENDP

; -----------------------------------------------------------------------------
public   srcfrst
srcfrst:                                  ;* nalezen¡ prvn¡ho aloka‡n¡ho bloku
                                            ; VSTUP: AX=adresa prvn¡ho bloku
         push      cx

         push      es
         push      bx
         mov       ah,52h
         int       21h
         mov       ax,es:[bx-2]             ; adresa prvn¡ho aloka‡n¡ho bloku
         pop       bx
         pop       es

         call      Testblk
         jnc       srcfrst2

         mov       cx,cs                    ; po‡et segment– pamˆti
         mov       ax,50h                   ; po‡ te‡n¡ segment k hled n¡
srcfrst1:inc       ax                       ; zv˜¨en¡ adresy segmentu
         call      testblk                  ; test, zda je aloka‡n¡ blok
         jnc       srcfrst2                 ; nalezen prvn¡ aloka‡n¡ blok
         loop      srcfrst1                 ; test dal¨¡ho bloku
srcfrst2:pop       cx
         ret
; -----------------------------------------------------------------------------
public   testblk
testblk:                                  ;* test, zda je aloka‡n¡ blok
                                            ; VSTUP: AX=adresa alok. bloku
                                            ; VSTUP: CY=nen¡ alok. blok
         push      bx
         push      ds
         push      ax
testblk1:mov       ds,ax                    ; aloka‡n¡ blok
         cmp       byte ptr ds:[0],"M"      ; je vnit©n¡ alok. blok ?
         je        testblk3                 ; je vnit©n¡ blok pamˆti - OK
         cmp       byte ptr ds:[0],"Z"      ; je posledn¡ blok pamˆti ?
         je        testblk2
         stc
         jmp       short testblk2           ; CY=nen¡ aloka‡n¡ blok

testblk3:call      srcnxt                   ; nastaven¡ na dal¨¡ alok. blok
         jnc       testblk1                 ; operace OK - dal¨¡ blok

testblk2:pop       ax
         pop       ds
         pop       bx
         ret
; -----------------------------------------------------------------------------
public   srcnxt
srcnxt:                                   ;* nastaven¡ na dal¨¡ aloka‡n¡ blok
                                            ; VSTUP: AX=segment bloku
                                            ; VSTUP: AX=segment dal¨¡ho bloku
                                            ;         CY=nen¡ dal¨¡ alok. blok
         push      bx
         push      ds
         mov       ds,ax                    ; segment aloka‡n¡ho bloku
         add       ax,ds:[3]                ; zv˜¨en¡ o velikost alok. bloku
         jc        srcnxt2                  ; p©ete‡en¡
         add       ax,1                     ; p©i‡ten¡ z hlav¡ bloku
srcnxt2: pop       ds
         pop       bx
         ret
; -----------------------------------------------------------------------------
;                               Znakov‚ v˜stupy
; -----------------------------------------------------------------------------
public   outcr
outcr:                                    ;* od© dkov n¡
         push      ax
         mov       al,13
         call      outchx
         mov       al,10
         call      outchx
         pop       ax
         ret
; -----------------------------------------------------------------------------
public   deknms
deknms:                                   ;* zobrazen¡ ‡¡sla segmentu
                                            ; VSTUP: AX=segment
         push      ax
         push      bx
         push      cx
         push      dx
         xor       dh,dh
         mov       dl,ah
         mov       cl,4
         shr       dx,cl
         shl       ax,cl
         call      deknum
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret
; -----------------------------------------------------------------------------
public   deknum,deknum1,deknum0
deknum:                                   ;* zobrazen¡ dekadick‚ho ‡¡sla
                                            ; VSTUP: DX:AX=‡¡slo k p©ek¢dov n¡

         mov       bl,0fh                   ; vy¨¨¡ slovo ‡¡sla 1000 000
         mov       cx,4240h                 ; ni‘¨¡ slovo ‡¡sla 1000 000
         call      deknum0                  ; dek¢dov n¡ 1 ‡¡slice
         mov       bl,1                     ; vy¨¨¡ slovo ‡¡sla 100 000
         mov       cx,86a0h                 ; ni‘¨¡ slovo ‡¡sla 100 000
         call      deknum0                  ; dek¢dov n¡ 1 ‡¡slice
         mov       cx,10000                 ; ni‘¨¡ slovo ‡¡sla 10 000
         call      deknum1                  ; dek¢dov n¡ 1 ‡¡slice
         mov       cx,1000                  ; ni‘¨¡ slovo ‡¡sla 1 000
         call      deknum1                  ; dek¢dov n¡ 1 ‡¡slice
         mov       cx,100                   ; ni‘¨¡ slovo ‡¡sla 100
         call      deknum1                  ; dek¢dov n¡ 1 ‡¡slice
         mov       cx,10                    ; ni‘¨¡ slovo ‡¡sla 10
         call      deknum1                  ; dek¢dov n¡ 1 ‡¡slice
         add       al,"0"                   ; korekce na ‡¡slici
         call      outchx                   ; zobrazen¡ ‡¡slice
         mov       byte ptr cs:[citdek],0   ; vynulov n¡ ‡¡ta‡e ‡¡slice
         ret

deknum1: xor       bl,bl                    ; BL <- 0
deknum0: xor       bh,bh                    ; BH <- 0
         mov       byte ptr cs:[citnum],2fh ; inicializace ‡¡ta‡e ‡¡slice
deknm1:  inc       byte ptr cs:[citnum]     ; zv˜¨en¡ ‡¡ta‡e ‡¡slice
         sub       ax,cx                    ; ode‡ten¡ ni‘¨¡ho slova ‡¡sla
         sbb       dx,bx                    ; ode‡ten¡ vy¨¨¡ho slova ‡¡sla
         jnc       deknm1                   ; nen¡ je¨tˆ p©ete‡en¡
         add       ax,cx                    ; n vrat ni‘¨¡ho slova ‡¡sla
         adc       dx,bx                    ; n vrat vy¨¨¡ho slova ‡¡sla
         cmp       byte ptr cs:[citnum],"0" ; je ‡¡slice 0 ?
         jne       deknm2                   ; nen¡ ‡¡slice 0 - dek¢dov n¡
         cmp       byte ptr cs:[citdek],0   ; byla ji‘ nˆjak  ‡¡slice ?
         jne       deknm2                   ; byla ji‘ nˆjak  ‡¡slice
         mov       byte ptr cs:[citnum]," " ; n hradn¡ znak mezery
         jmp       short deknm21
deknm2:  inc       byte ptr cs:[citdek]     ; zv˜¨en¡ ‡¡ta‡e ‡¡slic
deknm21: push      ax                       ; £schova AX
         mov       al,cs:[citnum]           ; dek¢dovan  ‡¡slice
         call      outchx                   ; zobrazen¡ ‡¡slice
         pop       ax                       ; n vrat AX
deknm3:  ret
; -----------------------------------------------------------------------------
public   outtab
outtab:                                   ;* nastaven¡ na zna‡ku tabel toru
                                            ; VSTUP: CL=po‡et pozic pro tabel tor
         push      ax
         push      cx
         mov       al,cs:[poztab]           ; star  pozice tabel toru
         add       al,cl                    ; nov  pozice tabel toru
         mov       cs:[poztab],al           ; nastaven¡ nov‚ pozice tabel toru
         sub       al,cs:[pozice]           ; po‡et pozic pro tabelaci
         jbe       outtab2                  ; p©ekro¨en¡ pozice tabel toru
         mov       cl,al                    ; po‡et pozic
         xor       ch,ch
outtab1: mov       al," "
         call      outchx                   ; zobrazen¡ mezery
         loop      outtab1                  ; dal¨¡ mezera
outtab2: pop       cx
         pop       ax
         ret
; -----------------------------------------------------------------------------
public   hexwrd,hexbyte,hexchr

hexdwrd:                                  ;* zobrazen¡ dvojslova DX:AX
         push      ax
         mov       ax,dx
         call      hexwrd                   ; zobrazen¡ vy¨¨¡ho slova
         mov       al,":"                   ; oddˆlova‡ slov
         call      outch                    ; zobrazen¡ oddˆlova‡e slov
         pop       ax

hexwrd:                                   ;* zobrazen¡ slova HEX
         push      ax
         mov       al,ah                    ; vy¨¨¡ bajt slova
         call      hexbyte                  ; zobrazen¡ vy¨¨¡ho bajtu slova
         pop       ax
;         call      hexbyte                  ; zobrazen¡ ni‘¨¡ho bajtu slova
;         mov       al,"h"
;         call      outchx
;         ret
;
hexbyte:                                  ;* zobrazen¡ bajtu HEX
         push      ax
         shr       al,1
         shr       al,1
         shr       al,1
         shr       al,1
         call      hexchr                   ; zobrazen¡ vy¨¨¡ tetr dy bajtu HEX
         pop       ax
hexchr:                                   ;* zobrazen¡ tetr dy bajtu
         push      ax
         and       al,0fh                   ; ni‘¨¡ tetr da bajtu
         cmp       al,10                    ; je znak HEX ?
         jb        hexchr2                  ; je ‡¡slice
         add       al,7                     ; korekce na znak HEX
hexchr2: add       al,"0"                   ; korekce na znak ASCII
         call      outchx                   ; zobrazen¡ znaku
         pop       ax
         ret
; -----------------------------------------------------------------------------
public   outtxtz
outtxtz:                                  ;* zobrazen¡ textu
                                            ; VSTUP: BX=adresa textu
         push      bx
         push      ax
outtx0:  mov       al,cs:[bx]
         inc       bx
         or        al,al
         jz        outtx1
         call      outchx                   ; zobrazen¡ znaku
         jmp       short outtx0
outtx1:  pop       ax
         pop       bx
         ret
; -----------------------------------------------------------------------------
public   outchh

outchh:                                   ;* zobrazen¡ velk‚ho znaku
                                            ; VSTUP: AL=znak
         push      ax
         cmp       al,"a"
         jb        outchh1
         cmp       al,"z"
         ja        outchh1
         sub       al,32
outchh1: cmp       al," "
         jae       outchh2
         mov       al," "
outchh2: call      outchx
         pop       ax
         ret

; -----------------------------------------------------------------------------
public   outchx
outchx:                                   ;* zobrazen¡ znaku
                                            ; VSTUP: AL=znak
         push      ax
         push      dx
         cmp       al,10
         je        outch3                   ; je znak <LF>
         cmp       al,13
         jne       outch1
         mov       byte ptr cs:[pozice],0   ; vynulov n¡ pozice znaku
         mov       byte ptr cs:[poztab],0   ; vynulov n¡ pozice tabel toru
         jmp       short outch3
outch1:  cmp       al,32
         jae       outch2
         mov       al," "
outch2:  inc       byte ptr cs:[pozice]     ; zv˜¨en¡ pozice na © dku
outch3:  mov       dl,al
         mov       ah,2
         int       21h                      ; zobrazen¡ znaku
         pop       dx
         pop       ax
         ret

;------------------------------------------------------------------------------
;                        Zobrazen¡ vektor– p©eru¨en¡
;------------------------------------------------------------------------------
intlist:
                                         ;* zobrazen¡ nadpisu
         mov       si,offset nadpint        ; nadpis v˜pisu vektor–
         call      outtxt                   ; zobrazen¡ nadpisu

         xor       ax,ax
         mov       es,ax                    ; str nka 0 pamˆti
         xor       di,di                    ; ukazatel ‡tec¡ adresy
         mov       cx,256                   ; po‡et p©eru¨en¡ k zobrazen¡
list1:   mov       al,0
         sub       al,cl                    ; ‡¡slo p©eru¨en¡
         call      hexbyte                  ; zobrazen¡ ‡¡sla p©eru¨en¡
         mov       al,"="
         call      outch                    ; oddˆlovac¡ znak
         mov       ax,es:[di]               ; ni‘¨¡ slovo adresy
         mov       dx,es:[di+2]             ; vy¨¨¡ slovo adresy
         call      hexdwrd                  ; zobrazen¡ obsahu na adres ch

         dec       cx
         test      cx,3
         jnz       list3
         call      outcr                    ; od© dkov n¡
         jmp       short list4
list3:   call      outspc                   ; oddˆlovac¡ mezera
         call      outspc                   ; oddˆlovac¡ mezera
list4:   add       di,4                     ; zv˜¨en¡ ukazatele v pamˆti
         or        cx,cx
         jnz       list1                    ; zobrazen¡ dal¨¡ho p©eru¨en¡
         ret

; -----------------------------------------------------------------------------
public   cmoslist
cmoslist:                                 ;* zobrazen¡ obsahu pamˆti CMOS

         xor       ax,ax
         call      readcmos                 ; kontrola instalace CMOS
         jnc       cmoslst0                 ; CMOS naistalov na OK

         mov       si,offset txterrc
         call      outtxt
         ret

cmoslst0:mov       si,offset nadpcmos       ; nadpis
         call      outtxt

         xor       ah,ah                    ; po‡ te‡n¡ adresa CMOS
cmoslst1:                                 ;* zobrazen¡ adresy
         push      ax
         mov       al,ah                    ; adresa bajtu
         call      outbyte                  ; zobrazen¡ bajtu
         mov       al,"="
         call      outch
         pop       ax

         call      rcmos                    ; ‡ten¡ obsahu z CMOS
         call      outbyte

         inc       ah                       ; zv˜¨en¡
         test      ah,7
         jnz       cmoslst4

         call      outcr
         jmp       short cmoslst5
cmoslst4:
         call      outspc
cmoslst5:
         cmp       ah,80h
         jb        cmoslst1

         mov       si,offset txtchsc
         call      outtxt

         call      chscmos                  ; je spr vn˜ kontroln¡ sou‡et ?
         pushf
         mov       ax,dx                    ; vypo‡ten˜ kontroln¡ sou‡et
         call      outwordh                 ; zobrazen¡ kontroln¡ho sou‡tu
         popf
         mov       si,offset txtchsc1       ; text OK
         jc        cmoslst6
         je        cmoslst7
cmoslst6:mov       si,offset txtchsc2       ; text chyby
cmoslst7:call      outtxt
         ret
; -----------------------------------------------------------------------------
public   winlist
winlist:                                  ;* zavadˆ‡ pevn‚ho disku DL

                                          ;* kontrola ‡¡sla disku
         push      dx                       ; £schova ‡¡sla disku
         mov       ah,8
         int       13h                      ; poskytnut¡ po‡tu disk–
         mov       al,dl                    ; po‡et pevn˜ch disk–
         pop       dx                       ; n vrat ‡¡sla disku
         or        al,80h
         cmp       al,dl                    ; je neplatn˜ disk ?
         ja        winlist1                 ; platn‚ ‡¡slo disku
winlist0:ret                                ; je neplatn‚ ‡¡slo disku
                                          ;* na‡ten¡ sektoru do pamˆti
winlist1:mov       cx,11                    ; po‡et pokus– o ‡ten¡
winlist2:dec       cx
         jcxz      winlist0                 ; sektor nelze na‡¡st
         push      dx                       ; £schova ‡¡sla disku
         push      cx
         mov       ax,201h                  ; 1 sektor ke ‡ten¡
         mov       cx,1                     ; v lec 0, sektor 1
         xor       dh,dh                    ; hlava 0
         push      cs
         pop       es
         mov       bx,offset diskbuf        ; diskov˜ buffer
         int       13h                      ; na‡ten¡ sektoru do pamˆti
         pop       cx
         pop       dx
         jc        winlist2                 ; dal¨¡ pokus o ‡ten¡ sektoru
;         dec       al                       ; je na‡ten 1 sektor ?
;         jnz       winlist2                 ; nen¡ na‡ten 1 sektor - znovu
                                          ;* zobrazen¡ sektoru
         and       dl,1                     ; ponech  ‡¡slo disku
         add       dl,"0"                   ; korekce na ‡¡slici ASCII
         mov       ds:[txthd2],dl           ; ulo‘en¡ ‡¡sla disku
         mov       si,offset txthd1
         call      outtxt                   ; zobrazen¡ nadpisu

winsekt:                                  ;* zobrazen¡ sektoru disku

         mov       cx,32                    ; po‡et © dk– k zobrazen¡
         mov       si,offset diskbuf        ; buffer se sektorem
winlist3:push      cx                     ;* zobrazen¡ adresy © dku
         mov       ax,si                    ; adresa v bufferu
         sub       ax,offset diskbuf        ; offset v bufferu
         call      outword                  ; zobrazen¡ slova HEX
         mov       al,":"
         call      outch                    ; oddˆlova‡
         call      outspc
                                          ;* zobrazen¡ ‡ sti HEX
         push      si
         mov       cx,16
winlist4:lodsb
         call      outbyte                  ; zobrazen¡ bajtu HEX
         call      outspc
         dec       cx
         jz        winlist7                 ; byl posledn¡ bajt
         cmp       cx,8                     ; je polovina ?
         jne       winlist5                 ; nen¡ polovina
         mov       al,"-"
         call      outch
winlist5:test      cx,3
         jne       winlist6
         call      outspc
winlist6:jmp       short winlist4           ; dal¨¡ bajt
winlist7:pop       si
                                          ;* zobrazen¡ ‡ sti ASCII
         mov       al,'"'
         call      outch
         mov       cx,16
winlist8:lodsb
         cmp       al,7fh
         je        winlistb
         cmp       al," "
         jae       winlist9
winlistb:mov       al,"."
winlist9:call      outch
         loop      winlist8
         mov       al,'"'
         call      outch

         pop       cx
         call      outcr
         loop      winlist3                 ; dal¨¡ © dek

winlista:ret
; -----------------------------------------------------------------------------
public   bootlist
bootlist:                                 ;* zobrazen¡ zavadˆ‡e disku

         mov       cx,5                     ; po‡et pokus– o na‡ten¡ sektoru
bootlst2:dec       cx
         jz        bootlst5                 ; pokus se nepovedl
         push      cx
         push      dx
         mov       al,dl                    ; ‡¡slo disku
         push      cs
         pop       ds
         mov       bx,offset diskbuf        ; diskov˜ buffer
         mov       cx,1                     ; po‡et sektor– = 1
         xor       dx,dx                    ; po‡ te‡n¡ sektor = 0
         int       25h                      ; ‡ten¡ sektoru z disku
         inc       sp
         inc       sp                       ; zru¨en¡ registru p©¡znak– v z sob.
         pop       dx
         pop       cx
         jc        bootlst2                 ; chyba - dal¨¡ pokus o ‡ten¡

bootlst4:                                 ;* zobrazen¡ nadpisu
         add       dl,"A"                   ; korekce na znak ASCII
         mov       ds:[txtboot2],dl         ; ulo‘en¡ ozna‡en¡ disku
         mov       si,offset txtboot1
         call      outtxt                   ; zobrazen¡ nadpisu

         jmp       winsekt                  ; zobrazen¡ sektoru

bootlst5:                                 ;* ‡ten¡ pomoc¡ paketu
         mov       cx,cs
         mov       cs:[bootpaks],cx         ; segment pro ulo‘en¡ dat
         mov       cx,5                     ; po‡et pokus– o na‡ten¡ sektoru
bootlst6:dec       cx
         jz        bootlst7                 ; pokus se nepovedl
         push      cx
         push      dx
         mov       al,dl                    ; ‡¡slo disku
         push      cs
         pop       ds
         mov       bx,offset bootpak        ; adresa paketu pro zavadˆ‡
         mov       cx,0ffffh                ; identifikace paketu
         xor       dx,dx                    ; po‡ te‡n¡ sektor = 0
         int       25h                      ; ‡ten¡ sektoru z disku
         inc       sp
         inc       sp                       ; zru¨en¡ registru p©¡znak– v z sob.
         pop       dx
         pop       cx
         jc        bootlst6                 ; chyba - dal¨¡ pokus o ‡ten¡
         jmp       short bootlst4           ; zobrazen¡ sektoru
bootlst7:ret
; -----------------------------------------------------------------------------
;                               Znakov‚ v˜stupy
; -----------------------------------------------------------------------------
public   outspcx

outspcx:                                 ;* zobrazen¡ oddˆlovac¡ch mezer
                                            ; VSTUP: AL=po‡et mezer
         push      ax
         push      cx
         xor       cx,cx
         mov       cl,al                    ; po‡et mezer
         jcxz      outspc2
outspc1: mov       al," "
         call      outch                    ; zobrazen¡ mezery
         loop      outspc1                  ; dal¨¡ mezera
outspc2: pop       cx
         pop       ax
         ret

; -----------------------------------------------------------------------------
;        mˆ©en¡ frekvence procesoru
; -----------------------------------------------------------------------------
;þ
TstCpu   PROC      NEAR

         mov       al,ds:[Procesor]
         cmp       al,7
         jae       TstCpu1                  ; je vy¨¨¡ typ procesoru OK

         ret

; ------ nastaven¡ dˆli‡ky reproduktoru

TstCpu1: cli
         in        al,[61h]
         and       al,not 3
         mov       bl,al                    ; £schova stavu portu
         out       [61h],al                 ; vypnut¡ v˜stupu na reproduktor
         mov       al,0b4h
         out       [43h],al                 ; povel pro nastaven¡ frekvence
         mov       al,0
         out       [42h],al                 ; dˆlic¡ konstanta LOW
         jmp       $+2
         out       [42h],al                 ; dˆlic¡ konstanta HIGH
         sti

; ------ p©¡prava k mˆ©en¡ frekvence procesoru

         mov       di,8000h                 ; pomocn  konstanta
         mov       cx,10                    ; po‡et pr–chod– smy‡kou
         cmp       byte ptr ds:[Procesor],8 ; 80386 a v˜¨e
         jae       TstCpu2
         mov       cl,1                     ; jinak 1 pr–chod

; ------ start mˆ©en¡ rychlosti

TstCpu2: mov       ah,0
         mov       al,bl                    ; stav portu 61h
         or        al,1                     ; hradlo ‡¡ta‡e zapnuto
         cli                                ; mus¡ b˜t z kaz p©eru¨en¡
         EVEN                               ; zarovn n¡ na sudou adresu
         out       [61h],al                 ; zapnut¡ ‡¡ta‡e
         mov       al,0

; ------ proveden¡ po‘adovan‚ho mno‘stv¡ instrukc¡ MUL

TstCpu3: rept      1000                     ; 1 smy‡ka celkem asi 1200000 takt–
         mul       di                       ; 1 instrukce asi 10 a‘ 120 takt–
         endm
         dec       cx
         jz        TstCpu4                  ; 10 smy‡ek -> 100000 a‘ 1200000 takt–
         jmp       TstCpu3

; ------ vypnut¡ ‡¡ta‡e reproduktoru

TstCpu4: mov       al,bl                    ; p–vodn¡ stav portu
         out       [61h],al                 ; vypnut¡ reproduktoru

; ------ ‡ten¡ dosa‘en‚ho stavu ‡¡ta‡e

         in        al,[42h]                 ; konstanta LOW
         mov       ah,al                    ; £schova konstanty LOW
         in        al,[42h]                 ; konstanta HIGH
         xchg      al,ah                    ; oprava po©ad¡ bajt–
         neg       ax                       ; ubˆhl˜ po‡et takt– hodin

; ------ n vrat nastaven¡ ‡¡ta‡e

         push      ax
         mov       al,0b6h                  ; povel
         out       [43h],al
         mov       al,5                     ; konstanta asi tak 920 Hz
         out       [42h],al                 ; konstanta - n¡‘¨¡ bajt
         out       [42h],al                 ; konstanta - vy¨¨¡ bajt
         pop       ax

         sti                                ; p©eru¨en¡ opˆt povoleno

                                            ; celkem doba asi 0.1 a‘ 50 ms
; frekvence 1193182 Hz (0.8381 us na impuls)
; - tj. asi 2000 a‘ 60000 impuls–
; v˜po‡et zhruba  f[MHz]= 1200000 / (impulsu_hodin * 0.8381us)
;                             ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÙ
;                                   doba mereni v [us]
;
;    f[MHz*10] = 1 430 000 / impulsu_hodin

         cmp       ax,100
         jb        TstCpu9

         xor       bx,bx
         mov       bl,ds:[procesor]         ; procesor
         shl       bx,1
         shl       bx,1
         mov       cx,ds:[hodproc2+bx]      ; dˆlic¡ konstanta pro hodiny
         mov       dx,ds:[hodProc2+bx+2]

         test      word ptr ds:[prepinac],8000h ; je cejchov n¡ hodin ?
         jnz       TstCpu8                  ; je cejchov n¡ hodin

         xchg      ax,cx

         cmp       dx,cx
         jae       TstCpu9

         div       cx                       ; p©epo‡et na kmito‡et

         mov       ds:[Hodiny],ax

         xor       ax,ax
         div       cx
         mov       cx,100
         mul       cx
         add       ax,7f60h
         adc       dx,0
         cmp       dl,100
         jb        TstCpu7
         sub       dl,100
         inc       word ptr ds:[Hodiny]
TstCpu7: mov       ds:[hodinyd],dl
         ret

TstCpu8: mov       ds:[hodiny],ax           ; hodiny procesoru
         mov       ds:[hodinyd],dl          ; vy¨¨¡ slovo hodin

TstCpu9:
         ret

TstCpu   ENDP


; -----------------------------------------------------------------------------
;                              D A T A
; -----------------------------------------------------------------------------
identclk db        'TESTCLK'                ; identifikace testu hodin

prepinac dw        1                      ;* p©ep¡na‡e
                                            ;   bit 0: v¨eobecn‚ informace "/S"
                                            ;   bit 1: obsah pamˆti DOS "/M"
                                            ;   bit 2: vektory p©eru¨en¡ "/I"
                                            ;   bit 3: obsah pamˆti CMOS "/C"
                                            ;   bit 4: zavadˆ‡ winchester– "/W"
                                            ;   bit 5: zavadˆ‡ disku "/B"

                                            ;   bit 15: cejchov n¡ hodin "/$"

bootdisk db        0                        ; disk k zobrazen¡ zavadˆ‡e

texthlp  db        'Volby: /S  - vseobecne informace',13,10
         db        '       /M  - obsah pameti DOS',13,10
         db        '       /I  - vektory preruseni',13,10
         db        '       /C  - obsah pameti CMOS',13,10
         db        '       /W  - hlavni zavadec pevneho disku',13,10
         db        '       /Bx - zavadec disku; x=A,B...',13,10
         db        0

nadpis   db        'Systemove informace V1.31; (c) Miroslav Nemecek',13,10
         db        '-----------------------------------------------',0

txttyp   db        '       Typ pocitace:',0
txtdos   db        '    Operacni system:',0
txtbios  db        '  Datum vzniku BIOS:',0
txtproc  db        '    Hlavni procesor:',0
txtkopr  db        'Matemat. koprocesor:',0
txthod   db        '   Hodiny procesoru:',0
txtmem   db        '  Celkova pamet RAM:',0
txtrom   db        '         Moduly ROM:',0
txtserp  db        '      Seriove porty:',0
txtparp  db        '    Paralelni porty:',0
txtcard  db        '     Graficka karta:',0
txtdisk  db        'Disketove mechaniky:',0
txtwin   db        '        Pevne disky:',0
txtlogd  db        '      Logicke disky:',0

zaver    db        0

tabtyp   label     byte                   ;* tabulka typ– po‡¡ta‡–
         db        'PC',0                   ; 0 (0ffh)
         db        'PC XT',0                ; 1 (0feh)
         db        'PCjr',0                 ; 2 (0fdh)
         db        'AT',0                   ; 3 (0fc00h, 0fc01h)
         db        'XT-286',0               ; 4 (0fc02h)
         db        'XT',0                   ; 5 (0fbh)
         db        'konvertabilni PC',0     ; 6 (0f9h)
         db        'neznamy typ',0          ; 7 nezn m˜ typ

tabdos   label     byte                   ;* tabulka opera‡n¡ch syst‚m–
         db        'DOS',0                  ; 0

tabmes   label     byte                   ;* tabulka mˆs¡c– v roce
         db        0                        ; 0
         db        'ledna',0                ; 1
         db        'unora',0                ; 2
         db        'brezna',0               ; 3
         db        'dubna',0                ; 4
         db        'kvetna',0               ; 5
         db        'cervna',0               ; 6
         db        'cervence',0             ; 7
         db        'srpna',0                ; 8
         db        'zari',0                 ; 9
         db        'rijna',0                ; 10
         db        'listopadu',0            ; 11
         db        'prosince',0             ; 12

tabproc  label     byte                   ;* tabulka procesor–
         db        '????',0                 ; 0
         db        'Intel 8088',0           ; 1
         db        'Intel 8086',0           ; 2
         db        'NEC V20',0              ; 3
         db        'NEC V30',0              ; 4
         db        'Intel 80188',0          ; 5
         db        'Intel 80186',0          ; 6
         db        'Intel 80286',0          ; 7
         db        'Intel 80386',0          ; 8
         db        'Intel 80386 SX',0       ; 9
         db        'Intel 80486',0          ; 10
         db        'Intel 80486+',0         ; 11
         db        'Intel PENTIUM',0        ; 12

tabprocm db        ' model ',0

tabkopr  label     byte                   ;* tabulka koprocesor–
         db        '????',0                 ; 0
         db        'nenainstalovan',0       ; 1
         db        'Intel 8087',0           ; 2
         db        'Intel 80287',0          ; 3
         db        'Intel 80387',0          ; 4
         db        'interni 80486',0        ; 5
         db        'interni Pentium',0      ; 6


tabcard  label     byte                   ;* tabulka grafick˜ch karet

         db        '????',0                 ; 0
         db        'CGA',0                  ; 1
         db        'MCGA',0                 ; 2
         db        'EGA',0                  ; 3
         db        'EGA64',0                ; 4
         db        'EGA Mono',0             ; 5
         db        'IBM8514',0              ; 6
         db        'Hercules',0             ; 7
         db        'ATT400',0               ; 8
         db        'VGA',0                  ; 9
         db        'PC3270',0               ; 10
         db        'VESA SVGA ',0           ; 11


tabega   label     byte                   ;* tabulka velikost¡ EGA/VGA
         db        '64 KB',0                ; 0
         db        '128 KB',0               ; 1
         db        '192 KB',0               ; 2
         db        '256 KB a vice',0        ; 3

tabdisk  label     byte                   ;* tabulka disketov˜ch mechanik
         db        'neznama',0              ; 0
         db        '5.25"/360KB',0          ; 1
         db        '5.25"/1.2MB',0          ; 2
         db        '3.5"/720KB',0           ; 3
         db        '3.5"/1.44MB',0          ; 4
         db        '3.5"/2.88MB',0          ; 5

txtmb    label     byte
txtwin1  db        ' MB',0
txtwin2  db        ' (typ ',0
txtkb    db        ' KB',0
txtmhz   db        ' MHz',0

public   txtnadp
txtnadp  db        13,10,'Segment Vlastnik  Jmeno  Velikost   Typ',13,10
         db        '------------------------------------------',13,10,0
vlsys    db        '--------',0
typvol   db        '--volny--',0
typprog  db        '<PROGRAM>',0
typenv   db        'prostredi',0
typdat   db        'data',0
typsys   db        'system',0
txtcelk  db        'Celkova pamet: ',0
txtvoln  db        'Volna pamet  : ',0
txtspoj  db        'Volna spojita: ',0
txtcomm  db        'COMMAND',0
txtumb   db        '  =================[ UMB ]================',13,10,0

public   nadpint
nadpint  db        13,10,'            Adresy vektoru preruseni',13,10
         db        '------------------------------------------------------'
         db        13,10,0

public   nadpcmos
nadpcmos db        13,10,'             Obsah pameti CMOS',13,10
         db        '-----------------------------------------------',13,10,0

public   txterrc
txterrc  db        13,10,'Pamet CMOS neni nainstalovana !',13,10,0

public   txtchsc
txtchsc  db        '   Kontrolni soucet pameti CMOS: ',0
txtchsc1 db        ' (OK)',13,10,0
txtchsc2 db        ' (Chyba !)',13,10,0

txthd1   db        13,10,'                  Hlavni zavadeci sektor pevneho disku '
txthd2   db        '0',13,10
         db        '-----------------------------------------------'
         db        '-----------------------------',13,10,0

txtboot1 db        13,10,'                     Zavadeci sektor logickeho disku '
txtboot2 db        'A',13,10
         db        '-----------------------------------------------'
         db        '-----------------------------',13,10,0

bootpak  dd        0                        ; ‡¡slo po‡ te‡n¡ho sektoru
         dw        1                        ; po‡et sektor–
         dw        offset diskbuf           ; adresa bufferu
bootpaks dw        0                        ; segment adresy bufferu

citnum   db        0                        ; ‡¡ta‡ ‡¡slice
citdek   db        0                        ; ‡¡ta‡ zobrazen˜ch ‡¡slic
pozice   db        0                        ; ‡¡ta‡ pozice na © dku
poztab   db        0                        ; pozice tabel toru

         db        23 DUP(0)


smodel   db        0ffh                     ; typ po‡¡ta‡e - syst‚mov˜ model
submodel db        0                        ; typ podmodelu po‡¡ta‡e

verze    dw        0                        ; verze a podverze oper. syst‚mu

biosden  db        1                        ; den vzniku BIOS
biosmes  db        1                        ; mesic vzniku BIOS
biosrok  dw        1980                     ; rok vzniku BIOS

procesor db        0                      ;* hlavn¡ procesor
                                            ;   0=nezn m˜
                                            ;   1=Intel 8088
                                            ;   2=Intel 8086
                                            ;   3=NEC V20
                                            ;   4=NEC V30
                                            ;   5=Intel 80188
                                            ;   6=Intel 80186
                                            ;   7=Intel 80286
                                            ;   8=Intel 80386
                                            ;   9=Intel 80386 SX
                                            ;  10=Intel 80486
                                            ;  11=Intel 80486+
                                            ;  12=Intel PENTIUM

modelcpu dw        0                        ; model CPU (0=nen¡ zn m)

vendrcpu db        13 dup(0)                ; Vendor ©etˆzec CPU

koproc   db        0                      ;* koprocesor
                                            ;   0=nezn m˜
                                            ;   1=‘ dn˜
                                            ;   2=8087
                                            ;   3=80287
                                            ;   4=80387
                                            ;   5=intern¡ 80486
                                            ;   6=intern¡ Pentium

hodproc  label     word                   ;* tabulka konstant pro hodiny
         dw        675      ;?              ;   0=nezn m˜
         dw        600      ;?              ;   1=Intel 8088
         dw        675                      ;   2=Intel 8086
         dw        700      ;?              ;   3=NEC V20
         dw        900      ;?              ;   4=NEC V30
         dw        800      ;?              ;   5=Intel 80188
         dw        900      ;?              ;   6=Intel 80186
         dw        1533 ;1551               ;   7=80286
         dw        3000     ;?              ;   8=80386
         dw        1817                     ;   9=80386 SX
         dw        6097 ;(7119)             ;  10=80486
         dw        6097                     ;  11=80486+
         dw        13572                    ;  12=Pentium

hodproc2 label     word                   ;* tabulka konstant pro hodiny 2
         dd        10000                    ;   0=nezn m˜
         dd        10000                    ;   1=Intel 8088
         dd        10000                    ;   2=Intel 8086
         dd        11000                    ;   3=NEC V20
         dd        13000                    ;   4=NEC V30
         dd        13000                    ;   5=Intel 80188
         dd        13000                    ;   6=Intel 80186
         dd        25254                    ;   7=80286
         dd        298890                   ;   8=80386
         dd        298700                   ;   9=80386 SX
         dd        310374                   ;  10=80486
         dd        71800                    ;  11=80486+
         dd        131940                   ;  12=Pentium

VideoCard db        0                     ;* grafick  karta
                                            ;     0=dosud nedetekov no
                                            ;     1=CGA
                                            ;     2=MCGA
                                            ;     3=EGA
                                            ;     4=EGA 64kB
                                            ;     5=EGA Mono
                                            ;    (6=IBM8514)
                                            ;     7=Hercules
                                            ;    (8=ATT400)
                                            ;     9=VGA
                                            ;    10=PC3270
                                            ;    11=VESA SVGA

VesaMEM  dw        0                        ; pamˆŸ VESA (v KB)

disket   db        0                      ;* po‡et disket. mechanik
disket1  db        0                      ;* typ mechaniky 1
disket2  db        0                      ;* typ mechaniky 2
disket3  db        0                      ;* typ mechaniky 3
disket4  db        0                      ;* typ mechaniky 4
                                            ;   0=???
                                            ;   1=5.25"/360 KB
                                            ;   2=5.25"/1.2 MB
                                            ;   3=5.25"/720 KB
                                            ;   4=3.5"/1.44 MB
                                            ;   5=3.5"/2.88 B

winchs0  dw        0                        ; velikost disku 0 v MB
wincht0  db        0                        ; typ pevn‚ho disku 0
winchs1  dw        0                        ; velikost disku 1 v MB
wincht1  db        0                        ; typ pevn‚ho disku 1

memory   dw        0                        ; celkov  pamˆŸ v KB

hodinyd  db        0                        ; desetiny kmito‡tu hodin
hodiny   dw        0                        ; hodiny procesoru (v MHz)
hodinyh  dw        0                        ; vy¨¨¡ slovo hodin

cit08    dw        0                        ; ‡¡ta‡ pro p©eru¨en¡ INT 08h

diskbuf  label     byte                     ; diskov˜ buffer

konec    label     byte                     ; konec programu MEMORY

code     ENDS
         END       sysinfo
