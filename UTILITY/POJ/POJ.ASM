
Code     SEGMENT
         ASSUME    cs:Code,ds:Code
         ORG       100h

KODJMP   EQU       0E9h                     ; k¢d instrukce JMP NEAR
KODPAX   EQU       50h                      ; k¢d instrukce PUSH AX
KODPBX   EQU       53h                      ; k¢d instrukce PUSH BX
KODPCX   EQU       51h                      ; k¢d instrukce PUSH CX
KODPDX   EQU       52h                      ; k¢d instrukce PUSH DX
OffsEXE  EQU       16                       ; offset adresy skoku v zavadˆ‡i EXE
OffsCOM  EQU       5                        ; offset adresy skoku v zavadˆ‡i COM

; ------ zobrazen¡ £vodn¡ho textu

Start:   mov       dx,offset UvTxt          ; £vodn¡ text
         mov       ah,9
         int       21h                      ; zobrazen¡ £vodn¡ho textu

; ------ p©edefinov n¡ z sobn¡ku

         cmp       sp,offset Zasob
         jae       Start2                   ; pamˆŸ je OK

; ------ chyba - nedostatek pamˆti

Start1:  mov       dx,offset MemTxt         ; text - nedostatek pamˆti
Chyba:   mov       ah,9
         int       21h                      ; zobrazen¡ textu n povˆdy
INT23:
Chyba2:  call      ClosErr                  ; uzav©en¡ soubor– p©i chybˆ
         mov       ax,4c01h
         int       21h

; ------ inicializace pamˆŸov˜ch buffer–

Start2:  mov       sp,offset Zasob          ; nov˜ konec z sobn¡ku
         call      InitMem                  ; inicializace pamˆti
         jc        Start1                   ; chyba pamˆti

; ------ p©edefinov n¡ INT 23h

         mov       dx,offset INT23
         mov       ax,2523h
         int       21h

; ------ rozbor p©¡kazov‚ho © dku

         call      Rozbor                   ; rozbor p©¡kazov‚ho © dku
         mov       dx,offset HelpTxt        ; text n povˆdy
Chyba3:  jc        Chyba                    ; chyba zad n¡ parametr–

; ------ otev©en¡ vstupn¡ho souboru

         call      OpenIn                   ; otev©en¡ vstupn¡ho souboru
         jc        Chyba

; ------ sestaven¡ jm‚na v˜stupn¡ho souboru

         call      InitOut                  ; sestaven¡ jm‚na v˜stup. souboru

; ------ p©i testu se v˜stupn¡ soubor nevytv ©¡

         test      byte ptr ds:[Param],10h  ; po‘aduje se test souboru ?
         jnz       Start4                   ; je pouze test - nen¡ v˜stup. soub.

; ------ test, zda v˜stupn¡ soubor ji‘ existuje

         mov       dx,offset Soubor2        ; jm‚no v˜stupn¡ho souboru
         mov       ah,4eh
         mov       cx,3fh                   ; atributy
         int       21h                      ; test v˜stupn¡ho souboru
         jc        Start3                   ; soubor nenalezen

; ------ dotaz, zda se m  soubor p©epsat

         mov       dx,offset ExTxt          ; text - existuje
         mov       ah,9
         int       21h                      ; zobrazen¡ textu

; ------ ‡ek n¡ na zad n¡ znaku

         mov       ax,0c01h
         int       21h                      ; vstup znaku z konzoly
         push      ax
         mov       dx,offset CRTxt
         mov       ah,9
         int       21h
         pop       ax
         cmp       al,"a"
         je        Start3
         cmp       al,"A"
         jne       Chyba2                   ; p©eru¨en¡ programu

; ------ vytvo©en¡ v˜stupn¡ho souboru

Start3:  mov       dx,offset Soubor2        ; jm‚no v˜stupn¡ho souboru
         mov       ah,3ch
         xor       cx,cx                    ; atributy
         int       21h                      ; vytvo©en¡ souboru
         mov       dx,offset CreatTxt       ; text - nelze vytvo©it
Chyba4:  jc        Chyba3                   ; chyba - soubor nelze vytvo©it
         mov       ds:[SouborI2],ax         ; identifik tor souboru

; ------ test, zda je soubor typu EXE

Start4:  call      TestExe                  ; test souboru, zda je EXE
         jc        Chyba4                   ; chyba ‡ten¡ ze souboru

; ------ test, zda je program ji‘ chr nˆn pojistkou

         call      TestPoj                  ; test souboru, zda je pojistka
         jc        Chyba4                   ; chyba ‡ten¡ ze souboru

; ------ rozli¨en¡ operac¡ pro EXE a COM

         test      byte ptr ds:[Param],2    ; je program typu EXE ?
         jnz       Start6                   ; je typu EXE

; ------ odstranˆn¡ pojistky COM

         test      byte ptr ds:[Param],8
         jz        Start52
         call      DInsC                    ; odstranˆn¡ pojistky COM
         jmp       short Start58

; ------ test pojistky COM

Start52: test      byte ptr ds:[Param],10h
         jz        Start53
         call      TestC                    ; test pojistky COM
         jmp       short Start58

; ------ opraven¡ programu COM

Start53: test      byte ptr ds:[Param],20h
         jz        Start54
         call      OprC                     ; opraven¡ programu COM
         jmp       short Start58

; ------ instalace pojistky COM

Start54: call      InstC                    ; instalace pojistky COM
Start58: jc        Chyba4                   ; nˆjak  chyba
         jmp       short Start7

; ------ inicializace parametr– vstupn¡ho souboru EXE

Start6:  call      InitXPar                 ; inicializace parametr– EXE

; ------ odstranˆn¡ pojistky EXE

         test      byte ptr ds:[Param],8
         jz        Start62
         call      DInsX                    ; odstranˆn¡ pojistky EXE
         jmp       short Start68

; ------ test pojistky EXE

Start62: test      byte ptr ds:[Param],10h
         jz        Start63
         call      TestX                    ; test pojistky EXE
         jmp       short Start68

; ------ opraven¡ programu EXE

Start63: test      byte ptr ds:[Param],20h
         jz        Start64
         call      OprX                     ; opraven¡ programu EXE
         jmp       short Start68

; ------ instalace pojistky EXE

Start64: call      InstX                    ; instalace pojistky EXE
Start68: jc        Start58                  ; nˆjak  chyba

; ------ nastaven¡ data a ‡asu souboru

Start7:  test      byte ptr ds:[Param],10h  ; je pouze test ?
         jnz       Start72                  ; je pouze test

         mov       bx,ds:[SouborI2]         ; identifik tor souboru
         or        bx,bx
         jz        Start72
         mov       cx,ds:[FileTime]         ; ‡as souboru
         mov       dx,ds:[FileDate]         ; datum souboru
         mov       ax,5701h
         int       21h                      ; nastaven¡ data a ‡asu souboru

; ------ uzav©en¡ soubor–

Start72: mov       bx,ds:[SouborI1]         ; vstupn¡ soubor
         or        bx,bx
         jz        Start73
         mov       ah,3eh
         int       21h                      ; uzav©en¡ souboru
         mov       word ptr ds:[SouborI1],0

Start73: test      byte ptr ds:[Param],10h
         jnz       Start74                  ; je pouze test
         mov       bx,ds:[SouborI2]         ; v˜stupn¡ soubor
         or        bx,bx
         jz        Start74
         mov       ah,3eh
         int       21h                      ; uzav©en¡ v˜stupn¡ho souboru
         mov       word ptr ds:[SouborI2],0

; ------ nastaven¡ atribut– v˜stupn¡ho souboru

Start74: test      byte ptr ds:[Param],10h
         jnz       Start75                  ; je pouze test
         mov       dx,offset Soubor2        ; jm‚no v˜stupn¡ho souboru
         mov       cx,ds:[FileAtr]          ; atributy souboru
         mov       ax,4301h
         int       21h                      ; nastaven¡ atribut– souboru

; ------ p©¡padn‚ zru¨en¡ vstupn¡ho programu a p©ejmenov n¡ p©echodn‚ho souboru

Start75: test      byte ptr ds:[Param],11h  ; zad n v˜stupn¡ soubor nebo test ?
         jnz       Start8                   ; je v˜stupn¡ soubor nebo jen test

         mov       dx,offset Soubor1        ; jm‚no vstupn¡ho souboru
         mov       ah,41h
         int       21h                      ; zru¨en¡ vstupn¡ho souboru
         jnc       Start76

         mov       dx,offset Soubor2        ; v˜stupn¡ soubor
         mov       ah,41h
         int       21h                      ; zru¨en¡ v˜stupn¡ho souboru

         mov       dx,offset DelTxt         ; text - nelze nahradit
         jmp       Chyba

Start76: mov       ah,56h
         mov       dx,offset Soubor2        ; jm‚no v˜stupn¡ho souboru
         push      ds
         pop       es
         mov       di,offset Soubor1        ; jm‚no vstupn¡ho souboru
         int       21h                      ; p©ejmenov n¡ souboru

Start8:
Start9:  mov       ax,4c00h
         int       21h

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                         Obsluha souboru COM
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

;þ

; -----------------------------------------------------------------------------
;        odstranˆn¡ pojistky COM
; -----------------------------------------------------------------------------

DInsC    PROC      NEAR

; ------ test, zda je pojistka

         mov       dx,offset NoPojTxt       ; text - nen¡ pojistka
         test      byte ptr ds:[Param],4    ; je pojistka ?
         stc
         jz        DInsC9                   ; nen¡ pojistka - chyba

; ------ na‡ten¡ souboru do pamˆti

DInsC1:  call      ReadCOM                  ; na‡ten¡ souboru do pamˆti
         jnc       DInsC2
         ret                                ; chyba ‡ten¡ souboru

; ------ n vrat p–vodn¡ch 3 bajt–

DInsC2:  mov       si,es:[1]                ; offset skoku
         add       si,3-5                   ; za‡ tek pojistky
         mov       al,es:[si+2]
         mov       dx,es:[si+2+1]
         mov       es:[0],al
         mov       es:[1],dx

; ------ z pis souboru na disk

         mov       cx,si                    ; d‚lka programu
         xor       si,si
         call      WritFile                 ; z pis souboru na disk
DInsC9:  ret

DInsC    ENDP

; -----------------------------------------------------------------------------
;        test pojistky COM
; -----------------------------------------------------------------------------

TestC    PROC      NEAR

; ------ test, zda je pojistka

         mov       dx,offset NoPojTxt       ; text - nen¡ pojistka
         test      byte ptr ds:[Param],4    ; je pojistka ?
         stc
         jz        TestC9                   ; nen¡ pojistka - chyba

; ------ na‡ten¡ souboru do pamˆti

         call      ReadCOM                  ; na‡ten¡ souboru do pamˆti
         jnc       TestC2
         ret                                ; chyba ‡ten¡ souboru

; ------ kontroln¡ sou‡et souboru v pamˆti

TestC2:  mov       cx,ax                    ; d‚lka programu (bajt–)
         shr       cx,1                     ; d‚lka programu s pojist.(slov)
         xor       si,si                    ; po‡ te‡n¡ adresa programu
         mov       dx,cx                    ; v˜choz¡ hodnota CRC
         cld
TestC3:  mov       ax,es:[si]               ; na‡ten¡ slova
         inc       si
         inc       si
         rol       dx,1                     ; rotace o 1 bit vlevo
         xor       dx,ax                    ; p©i‡ten¡ slova
         loop      TestC3                   ; dal¨¡ slovo

; ------ vyhodnocen¡ v˜sledku testu souboru

TestC7:  jnz       TestC8                   ; kontroln¡ sou‡et nesouhlas¡

         mov       dx,offset CRCOkTxt       ; kontroln¡ sou‡et OK
         mov       ah,9
         int       21h
         clc
         ret

TestC8:  mov       dx,offset CRCErTxt       ; text - soubor po¨kozen
         stc                                ; p©¡znak chyby
TestC9:  ret

TestC    ENDP

; -----------------------------------------------------------------------------
;        oprava pojistky COM
; -----------------------------------------------------------------------------

OprC     PROC      NEAR

; ------ test, zda je pojistka

         test      byte ptr ds:[Param],4    ; je pojistka ?
         jz        OprC1                    ; nen¡ pojistka
         jmp       DInsC1                   ; bˆ‘n‚ odstranˆn¡ pojistky

; ------ na‡ten¡ souboru do pamˆti

OprC1:   call      ReadCOM                  ; na‡ten¡ souboru do pamˆti
         jnc       OprC2
         ret                                ; chyba ‡ten¡ souboru

; ------ pokus o nalezen¡ pojistky v programu

OprC2:   mov       dx,offset FndTxt         ; text - nenalezena
         mov       si,ax                    ; d‚lka programu
         sub       si,20                    ; asi tak za‡ tek k testu
         jc        OprC9                    ; nenalezeno
OprC3:   cmp       word ptr es:[si],"JP"    ; je pojistka ?
         jne       OprC4                    ; nen¡ pojistka
         cmp       word ptr es:[si+OffsCOM],KODPAX+KODPBX*256 ; je PUSH AX/BX ?
         jne       OprC4                    ; nen¡ pojistka
         cmp       word ptr es:[si+OffsCOM+2],KODPCX+KODPDX*256 ; je PUSH CX/DX ?
         je        OprC6                    ; je pojistka OK
OprC4:   dec       si                       ; sn¡‘en¡ ukazatele v souboru
         cmp       si,3                     ; minim ln¡ offset
         jae       OprC3                    ; dal¨¡ test
         jmp       short OprC9              ; nenalezeno

; ------ n vrat p–vodn¡ch 3 bajt–

OprC6:   mov       al,es:[si+2]
         mov       dx,es:[si+2+1]
         mov       es:[0],al
         mov       es:[1],dx

; ------ z pis souboru na disk

         mov       cx,si                    ; d‚lka programu
         xor       si,si
         call      WritFile                 ; z pis souboru na disk
         jc        OprC9

; ------ zobrazen¡ hl ¨en¡

         mov       dx,offset VirTxt
         mov       ah,9
         int       21h
         clc
OprC9:   ret

OprC     ENDP

; -----------------------------------------------------------------------------
;        instalace pojistky COM
; -----------------------------------------------------------------------------

InstC    PROC      NEAR

; ------ test, zda je pojistka

         mov       dx,offset PojTxt         ; text - je ji‘ pojistka
         test      byte ptr ds:[Param],4    ; je pojistka ?
         stc
         jnz       InstC1                   ; je pojistka - chyba

; ------ na‡ten¡ souboru do pamˆti

         call      ReadCOM                  ; na‡ten¡ souboru do pamˆti
         jnc       InstC2
InstC1:  ret                                ; chyba ‡ten¡ souboru

; ------ kontrola velikosti souboru

InstC2:  mov       dx,offset SizTxt         ; text - nelze ochr nit
         cmp       ax,offset(CojBeg-CojEnd)-100h ; asi tak maxim ln¡ velikost
         cmc
         jc        InstC1                   ; nelze ochr nit pojistkou

; ------ £schova za‡ tku programu

         mov       al,ds:[ExeHead]          ; prvn¡ bajt programu
         mov       dx,word ptr ds:[ExeHead+1] ; druh˜ a t©et¡ bajt programu
         mov       byte ptr ds:[PojOld],al  ; prvn¡ bajt programu
         mov       word ptr ds:[PojOld+1],dx ; 2. a 3. bajt programu
         mov       byte ptr ds:[CojOld],al  ; prvn¡ bajt programu
         mov       word ptr ds:[CojOld+1],dx ; 2. a 3. bajt programu

; ------ relokace adres v zavadˆ‡i

         mov       ax,ds:[SoubNum]          ; velikost souboru
         mov       cx,ax                    ; adresa konce
         mov       byte ptr es:[0],KODJMP   ; k¢d instrukce JMP NEAR
         add       cx,OffsCOM-3
         mov       word ptr es:[1],cx       ; offset skoku
         mov       cx,ax                    ; adresa konce

         sub       ax,offset(PojIdn-Start)  ; korekce adres
         mov       di,offset(PojEnd-PojBeg) ; d‚lka zavadˆ‡e ve slovech
         mov       si,offset PojBeg         ; bˆ‘n  pojistka

         test      byte ptr ds:[Param],40h  ; je pokra‡ov n¡ ?
         jz        InstC3                   ; nen¡ pokra‡ov n¡

         sub       ax,offset(CojIdn-Start) - offset(PojIdn-Start) ; korekce
         mov       di,offset(CojEnd-CojBeg) ; d‚lka zavadˆ‡e ve slovech
         mov       si,offset CojBeg         ; pojistka s pokra‡ov n¡m

InstC3:  add       cx,di                    ; p©i‡ten¡ d‚lky zavadˆ‡e
         inc       cx
         and       cx,not 1                 ; zaokrouhlen¡
         mov       word ptr ds:[PojDelk2+1],cx ; d‚lka programu v bajtech
         mov       word ptr ds:[CojDelk2+1],cx ; d‚lka programu v bajtech
         shr       cx,1                     ; d‚lka programu ve slovech
         mov       word ptr ds:[PojDelk1+1],cx ; d‚lka programu ve slovech
         mov       word ptr ds:[CojDelk1+1],cx ; d‚lka programu ve slovech

         add       word ptr ds:[PojS2+1],ax
         add       word ptr ds:[PojS7+3],ax
         add       word ptr ds:[PojS8+1],ax
         sub       word ptr ds:[PojJMPSt+1],ax

         add       word ptr ds:[CojS2+1],ax
         add       word ptr ds:[CojS7+3],ax
         add       word ptr ds:[CojS8+1],ax
         sub       word ptr ds:[CojJMPSt+1],ax

; ------ p©enesen¡ zavadˆ‡e do bufferu

         cld
         mov       cx,di                    ; d‚lka zavadˆ‡e
         mov       di,ds:[SoubNum]          ; velikost
         rep       movsb                    ; p©enos zavadˆ‡e do bufferu
         mov       al,0
         stosb
         and       di,not 1

; ------ v˜po‡et kontroln¡ho sou‡tu souboru

InstC4:  push      ds
         mov       cx,di                    ; velikost souboru
         mov       ds,ds:[SoubSegm]
         xor       si,si
         shr       cx,1                     ; velikost souboru ve slovech
         mov       dx,cx                    ; v˜choz¡ hodnota CRC
         dec       cx                       ; kromˆ posledn¡ho slova
         cld
InstC5:  lodsw                              ; na‡ten¡ slova
         rol       dx,1                     ; rotace o 1 bit vlevo
         xor       dx,ax                    ; p©i‡ten¡ slova
         loop      InstC5                   ; dal¨¡ slovo
         rol       dx,1                     ; rotace
         mov       ds:[si],dx               ; kontroln¡ sou‡et
         pop       ds

; ------ ulo‘en¡ souboru na disk

         xor       si,si
         mov       cx,di                    ; velikost souboru
         call      WritFile                 ; z pis bloku na disk

InstC9:  ret

InstC    ENDP

; -----------------------------------------------------------------------------
;        na‡ten¡ souboru COM do pamˆti
; -----------------------------------------------------------------------------

ReadCOM  PROC      NEAR

         mov       es,ds:[SoubSegm]         ; datov˜ buffer
         xor       si,si                    ; offset v bufferu
         mov       cx,0ffffh                ; maxim ln¡ velikost
         xor       bx,bx                    ; offset v souboru LOW
         xor       dx,dx                    ; offset v souboru HIGH
         call      ReadFile                 ; na‡ten¡ souboru do bufferu
         mov       ds:[SoubNum],ax          ; po‡et bajt– v bufferu
         ret

ReadCOM  ENDP


; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                         Obsluha souboru EXE
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

;þ

; -----------------------------------------------------------------------------
;        odstranˆn¡ pojistky EXE
; -----------------------------------------------------------------------------

DInsX    PROC      NEAR

; ------ test, zda je pojistka

         test      byte ptr ds:[Param],4    ; je pojistka ?
         jnz       DInsX2                   ; je pojistka - OK
         mov       dx,offset NoPojTxt       ; text - nen¡ pojistka
DInsX1:  stc
         ret

; ------ ur‡en¡ nov‚ vnit©n¡ velikosti programu

DInsX2:  mov       ax,ds:[ExeCS]
         mov       cx,16
         mul       cx
         mov       cx,ds:[ExeIP]
         sub       cx,OffsEXE
         add       ax,cx
         adc       dx,0                     ; p–vodn¡ velikost programu
         mov       word ptr ds:[ProgNSiz],ax ; p–vodn¡ velikost programu
         mov       word ptr ds:[ProgNSiz+2],dx

; ------ adresa reloka‡n¡ tabulky

         mov       si,offset OldPoj
         mov       cx,ds:[ExeIP]
         sub       cx,OffsEXE               ; po‡ tek pojistky
         mov       bx,ds:[si+PojXRelT-PojXBeg] ; offset reloka‡n¡ tabulky
         sub       bx,cx
         add       ax,bx
         adc       dx,0
         add       ax,word ptr ds:[HeadSize]
         adc       dx,word ptr ds:[HeadSize+2]
         mov       word ptr ds:[RelAdr],ax
         mov       word ptr ds:[RelAdr+2],dx

; ------ p–vodn¡ velikost z hlav¡ programu

         mov       ax,ds:[si+PojXRel-PojXBeg] ; p–vodn¡ po‡et reloka‡n¡ch pol.
         mov       ds:[ExeRNum],ax          ; n vrat po‡tu reloka‡n¡ch polo‘ek
         shl       ax,1
         shl       ax,1                     ; p–vodn¡ velikost relok. tabulky
         mov       ds:[RelokSiz],ax         ; velikost reloka‡n¡ tabulky
         xor       dx,dx
         add       ax,ds:[ExeXRel]          ; + offset reloka‡n¡ tabulky
         adc       dx,0
         mov       cx,ax
         and       ax,not 0fh               ; zarovn n¡ na odstavec
         or        dx,dx
         jnz       DInsX3
         cmp       ax,word ptr ds:[Head2Adr]
         jae       DInsX3
         xor       cx,cx
         mov       ax,word ptr ds:[Head2Adr]
DInsX3:  add       ax,word ptr ds:[Head2Siz] ; p©i‡ten¡ 2. ‡ sti z hlav¡
         adc       dx,word ptr ds:[Head2Siz+2]
         mov       word ptr ds:[HeadNSiz],ax ; nov  velikost z hlav¡
         mov       word ptr ds:[HeadNSiz+2],dx

; ------ oprava adresy druh‚ ‡ sti hlavi‡ky

         and       cx,0fh
         sub       word ptr ds:[Head2Siz],cx
         sbb       word ptr ds:[Head2Siz+2],0
         add       word ptr ds:[Head2Adr],cx
         adc       word ptr ds:[Head2Adr+2],0

; ------ nov  zav dˆc¡ velikost programu

         add       ax,word ptr ds:[ProgNSiz]
         adc       dx,word ptr ds:[ProgNSiz+2]
         mov       word ptr ds:[LoadNSiz],ax ; nov  zav dˆc¡ velikost programu
         mov       word ptr ds:[LoadNSiz+2],dx

; ------ p©epo‡et zav dˆc¡ velikosti na str nky

         mov       cx,ax
         and       cx,1ffh
         mov       ds:[ExeLast],cx          ; po‡et bajt– v posledn¡ str nce
         add       ax,511
         adc       dx,0
         mov       cx,512
         and       dx,1ffh
         div       cx
         mov       ds:[ExePage],ax          ; po‡et str nek 512 bajt–

; ------ nastaven¡ nov‚ velikosti z hlav¡

         mov       ax,word ptr ds:[HeadNSiz]
         mov       dx,word ptr ds:[HeadNSiz+2]
         add       ax,0fh
         adc       dx,0
         mov       cx,16
         and       dx,0fh
         div       cx
         mov       ds:[ExeHSize],ax         ; velikost z hlav¡

; ------ n vrat p–vodn¡ch parametr– programu

         mov       ax,ds:[si+PojXSP-PojXBeg] ; p–vodn¡ registr SP
         mov       ds:[ExeSP],ax            ; n vrat registru SP
         mov       ax,ds:[si+PojXSS-PojXBeg] ; p–vodn¡ registr SS
         mov       ds:[ExeSS],ax            ; n vrat registru SS
         mov       ax,ds:[si+PojXIP-PojXBeg] ; p–vodn¡ registr IP
         mov       ds:[ExeIP],ax            ; n vrat registru IP
         mov       ax,ds:[si+PojXCS-PojXBeg] ; p–vodn¡ registr CS
         mov       ds:[ExeCS],ax            ; n vrat registru CS
         mov       ax,ds:[si+PojXMin-PojXBeg] ; p–vodn¡ minim ln¡ pamˆŸ
         mov       ds:[ExeMin],ax           ; n vrat minim ln¡ pamˆti

; ------ z pis nov‚ho z hlav¡ na disk

         mov       si,offset ExeHead
         mov       cx,28                    ; d‚lka z hlav¡
         push      ds
         pop       es
         call      WritFile                 ; z pis z hlav¡ na disk
         jnc       DInsX5
DInsX4:  ret

; ------ kopie prvn¡ ‡ sti z hlav¡

DInsX5:  xor       dx,dx
         mov       bx,28
         xor       di,di
         mov       cx,ds:[ExeXRel]
         sub       cx,bx                    ; velikost prvn¡ ‡ sti z hlav¡
         call      CopyFile                 ; kopie prvn¡ ‡ sti z hlav¡
         jc        DInsX4

; ------ kopie reloka‡n¡ tabulky

         mov       bx,word ptr ds:[RelAdr]  ; adresa reloka‡n¡ tabulky
         mov       dx,word ptr ds:[RelAdr+2]
         mov       cx,ds:[RelokSiz]         ; velikost reloka‡n¡ tabulky
         xor       di,di
         call      CopyFile                 ; kopie reloka‡n¡ tabulky
         jc        DInsX4

; ------ kopie p©¡padn‚ho zbytku prvn¡ ‡ sti z hlav¡

         mov       bx,ds:[ExeXRel]
         xor       dx,dx
         add       bx,cx
         adc       dx,di
         mov       cx,word ptr ds:[Head2Adr]
         mov       di,word ptr ds:[Head2Adr+2]
         sub       cx,bx
         sbb       di,dx
         jc        DInsX6
         call      CopyFile
         jc        DInsX4

; ------ kopie druh‚ ‡ sti z hlav¡

DInsX6:  mov       bx,word ptr ds:[Head2Adr] ; adresa druh‚ ‡ sti hlavi‡ky
         mov       dx,word ptr ds:[Head2Adr+2]
         mov       cx,word ptr ds:[Head2Siz] ; velikost druh‚ ‡ sti hlavi‡ky
         mov       di,word ptr ds:[Head2Siz+2]
         call      CopyFile                 ; kopie druh‚ ‡ sti hlavi‡ky
         jc        DInsX4

; ------ kopie tˆla programu

         mov       bx,word ptr ds:[HeadSize] ; adresa za hlavi‡kou
         mov       dx,word ptr ds:[HeadSize+2]
         mov       cx,word ptr ds:[ProgNSiz] ; nov  velikost programu
         mov       di,word ptr ds:[ProgNSiz+2]
         call      CopyFile                 ; kopie tˆla programu
         jc        DInsX4

; ------ kopie dat za programem

         mov       bx,word ptr ds:[LoadSize]
         mov       dx,word ptr ds:[LoadSize+2]
         mov       cx,word ptr ds:[OvlSize]
         mov       di,word ptr ds:[OvlSize+2]
         call      CopyFile                 ; zkop¡rov n¡ dat za programem
         jc        DInsX9

; ------ hl ¨en¡ o odstranˆn¡ pojistky

         test      byte ptr ds:[Param],20h
         jz        DInsX9                   ; nebyla oprava
         mov       dx,offset VirTxt
         mov       ah,9
         int       21h
         clc
DInsX9:  ret

DInsX    ENDP

; -----------------------------------------------------------------------------
;        test pojistky EXE
; -----------------------------------------------------------------------------

TestX    PROC      NEAR

; ------ test, zda je pojistka

         test      byte ptr ds:[Param],4    ; je pojistka ?
         jnz       TestX2                   ; je pojistka - OK
         mov       dx,offset NoPojTxt       ; text - nen¡ pojistka
         stc
TestX1:  ret

; ------ p©¡prava k testu

TestX2:  mov       di,word ptr ds:[ProgSize+2]; velikost HIGH
         mov       bp,word ptr ds:[ProgSize] ; velikost LOW
         mov       bx,word ptr ds:[HeadSize] ; po‡ te‡n¡ offset programu
         mov       dx,word ptr ds:[HeadSize+2]
         xor       ax,ax                    ; v˜choz¡ hodnota CRC
         mov       es,ds:[SoubSegm]         ; segment dat

; ------ ur‡en¡ velikosti bloku dat

TestX4:  mov       cx,0f000h
         or        di,di
         jnz       TestX5
         cmp       cx,bp
         jbe       TestX5
         mov       cx,bp
TestX5:  jcxz      TestX8                   ; konec dat

; ------ na‡ten¡ bloku programu

         xor       si,si                    ; offset v bufferu
         call      ReadFil0                 ; na‡ten¡ bloku dat
         jc        TestX1                   ; chyba ‡ten¡

; ------ posuny ukazatel–

         add       bx,cx
         adc       dx,0
         sub       bp,cx
         sbb       di,0

; ------ kontroln¡ sou‡et bloku programu

         shr       cx,1                     ; d‚lka programu s pojist.(slov)
TestX6:  rol       ax,1                     ; rotace o 1 bit vlevo
         xor       ax,es:[si]
         inc       si
         inc       si
         loop      TestX6                   ; dal¨¡ slovo
         jmp       short TestX4

; ------ vyhodnocen¡ v˜sledku testu souboru

TestX8:  or        ax,ax                    ; je v˜sledek = 0 ?
         jmp       TestC7                   ; vyhodnocen¡ v˜sledku

;         jz        TestX9                   ; kontroln¡ sou‡et je OK
;         mov       dx,offset CRCErTxt       ; text - soubor po¨kozen
;         stc                                ; p©¡znak chyby
;TestX9:  ret

TestX    ENDP

; -----------------------------------------------------------------------------
;        oprava pojistky EXE
; -----------------------------------------------------------------------------

OprX     PROC      NEAR

; ------ test, zda je pojistka

         test      byte ptr ds:[Param],4    ; je pojistka ?
         jz        OprX1                    ; nen¡ pojistka
         xor       byte ptr ds:[Param],28h  ; zmˆna na po‘adavek odpojistkov n¡
         jmp       DInsX                    ; bˆ‘n‚ odstranˆn¡ pojistky

; ------ p©¡prava k vyhled n¡ pojistky

OprX1:   mov       bp,word ptr ds:[ProgSize] ; vnit©n¡ velikost programu
         mov       di,word ptr ds:[ProgSize+2]
         mov       bx,word ptr ds:[HeadSize] ; velikost z hlav¡
         mov       dx,word ptr ds:[HeadSize+2]
         add       bx,bp                    ; adresa konce za programem
         adc       dx,di

; ------ velikost bloku k prohled n¡

         mov       cx,0ff00h
         or        di,di
         jnz       OprX2
         cmp       cx,bp
         jb        OprX2
         mov       cx,bp                    ; omezen¡ velikosti bloku
OprX2:   sub       bx,cx
         sbb       dx,0

; ------ na‡ten¡ bloku do pamˆti

         mov       es,ds:[SoubSegm]
         xor       si,si
         call      ReadFil0                 ; na‡ten¡ souboru do pamˆti
         jc        OprX9

; ------ prohled n¡ bloku v pamˆti

         mov       si,cx
         sub       si,100
         jbe       OprX8
OprX3:   cmp       word ptr es:[si],"JP"    ; je pojistka ?
         jne       OprX4                    ; nen¡ pojistka
         cmp       word ptr es:[si+OffsEXE],KODPAX+KODPBX*256 ; je PUSH AX/BX ?
         jne       OprX4                    ; nen¡ pojistka
         cmp       word ptr es:[si+OffsEXE+2],KODPCX+KODPDX*256 ; je PUSH CX/DX ?
         je        OprX6                    ; je pojistka OK
OprX4:   dec       si                       ; sn¡‘en¡ ukazatele v souboru
         jne       OprX3
         jmp       short OprX8

; ------ pojistka nalezena - nastaven¡ ukazatel–

OprX6:   add       bx,si                    ; offset v souboru
         adc       dx,0
         push      dx
         mov       ax,bx
         sub       ax,word ptr ds:[HeadSize] ; ode‡ten¡ z hlav¡
         sbb       dx,word ptr ds:[HeadSize+2]
         mov       cx,16
         and       dx,0fh
         div       cx                       ; p©epo‡et na segment:offset
         add       dx,OffsEXE
         mov       ds:[ExeCS],ax
         mov       ds:[ExeIP],dx
         pop       dx

; ------ na‡ten¡ za‡ tku pojistky

         or        byte ptr ds:[Param],4    ; p©¡znak nalezen¡ pojistky
         mov       cx,32                    ; velikost bufferu
         push      ds
         pop       es
         mov       si,offset OldPoj         ; buffer k na‡ten¡ star‚ pojistky
         call      ReadFil0                 ; na‡ten¡ za‡ tku pojistky
         jc        OprX9                    ; chyba ‡ten¡
         jmp       DInsX2                   ; odinstalov n¡ pojistky

; ------ pojistka nenalezena

OprX8:   mov       dx,offset FndTxt
         stc
OprX9:   ret

OprX     ENDP

; -----------------------------------------------------------------------------
;        instalace pojistky EXE
; -----------------------------------------------------------------------------
;þ
InstX    PROC      NEAR

; ------ test, zda je pojistka

         test      byte ptr ds:[Param],4    ; je pojistka ?
         jz        InstX2                   ; nen¡ pojistka - OK
         mov       dx,offset PojTxt         ; text - je ji‘ pojistka
InstX0:  stc
InstX1:  ret                                ; chyba ‡ten¡ souboru

; ------ £schova p–vodn¡ch parametr– programu

InstX2:  mov       ax,ds:[ExeSP]
         mov       ds:[PojXSP],ax           ; registr SP
         mov       ds:[CojXSP],ax           ; registr SP
         mov       ax,ds:[ExeSS]
         mov       ds:[PojXSS],ax           ; registr SS
         mov       ds:[CojXSS],ax           ; registr SS
         mov       ax,ds:[ExeIP]
         mov       ds:[PojXIP],ax           ; registr IP
         mov       ds:[CojXIP],ax           ; registr IP
         mov       ax,ds:[ExeCS]
         mov       ds:[PojXCS],ax           ; registr CS
         mov       ds:[CojXCS],ax           ; registr CS
         mov       ax,ds:[ExeMin]
         mov       ds:[PojXMin],ax          ; minim ln¡ pamˆŸ
         mov       ds:[CojXMin],ax          ; minim ln¡ pamˆŸ
         mov       ax,ds:[ExeRNum]          ; po‡et polo‘ek reloka‡n¡ tabulky
         mov       ds:[PojXRel],ax          ; po‡et polo‘ek relok. tabulky
         mov       ds:[CojXRel],ax          ; po‡et polo‘ek relok. tabulky

; ------ kontrola reloka‡n¡ tabulky

         mov       word ptr ds:[PojXRel1+1],ax ; po‡et relok. polo‘ek
         mov       word ptr ds:[CojXRel1+1],ax ; po‡et relok. polo‘ek
         add       word ptr ds:[PojXRel1+1],2
         add       word ptr ds:[CojXRel1+1],2
         cmp       ax,4000h-100h            ; asi tak maxim ln¡ po‡et polo‘ek
         jbe       InstX22
InstX21: mov       dx,offset SizTxt         ; text - nelze ochr nit
         jmp       short InstX0

; ------ adresa po‡ tku pojistky v programu

InstX22: mov       ax,word ptr ds:[ProgSize] ; velikost programu
         mov       dx,word ptr ds:[ProgSize+2]
         mov       cx,16                    ; dˆlitel pro p©evod
         div       cx                       ; v˜po‡et adresy pojistky
         mov       word ptr ds:[PojAdr],dx  ; offset adresy pojistky
         mov       word ptr ds:[PojAdr+2],ax ; segment adresy pojistky

; ------ nov  velikost z hlav¡ programu

         mov       ax,word ptr ds:[Head2Siz] ; velikost druh‚ ‡ sti hlavi‡ky LOW
         mov       dx,word ptr ds:[Head2Siz+2]
         add       ax,ds:[Head1Siz]         ; p©i‡ten¡ 1. ‡ sti hlavi‡ky
         adc       dx,0                     ; p©enos
         and       dx,0fh                   ; poji¨tˆn¡
         mov       cx,16                    ; dˆlitel
         div       cx                       ; p©epo‡et velikosti na odstavce
         mov       ds:[ExeHSize],ax         ; nov  velikost z hlav¡ programu

; ------ nov˜ po‡et polo‘ek reloka‡n¡ tabulky

         mov       word ptr ds:[ExeRNum],0  ; nen¡ ‘ dn  reloka‡n¡ polo‘ka

; ------ velikost pojistky s reloka‡n¡ tabulkou + CRC

         mov       ax,offset(PojXEnd-PojXBeg) ; z kladn¡ velikost pojistky
         test      byte ptr ds:[Param],40h  ; je pokra‡ov n¡ ?
         jz        InstX32                  ; nen¡ pokra‡ov n¡
         mov       ax,offset(CojXEnd-CojXBeg) ; pojistka s pokra‡ov n¡m
InstX32: add       ax,ds:[RelokSiz]         ; p©i‡ten¡ reloka‡n¡ tabulky
         mov       ds:[PojSize],ax          ; velikost pojistky s rel. tab.

; ------ nov  velikost programu

         xor       dx,dx
         inc       ax                       ; p©¡prava pro zaokrouhlen¡
         add       ax,word ptr ds:[ProgSize] ; p©i‡ten¡ velikosti programu
         adc       dx,word ptr ds:[ProgSize+2]
         and       ax,not 1                 ; zaokrouhlen¡ na sudou velikost
         mov       word ptr ds:[ProgNSiz],ax ; nov  velikost programu LOW
         mov       word ptr ds:[ProgNSiz+2],dx ; nov  velikost programu HIGH

; ------ nov  zav dˆc¡ velikost programu

         add       ax,ds:[Head1Siz]         ; p©i‡ten¡ 1. ‡ sti hlavi‡ky
         adc       dx,0
         add       ax,word ptr ds:[Head2Siz] ; p©i‡ten¡ 2. ‡ sti hlavi‡ky
         adc       dx,word ptr ds:[Head2Siz+2]
         mov       word ptr ds:[LoadNSiz],ax ; nov  zav dˆc¡ velikost
         mov       word ptr ds:[LoadNSiz+2],dx

; ------ nov  velikost souboru

         add       ax,word ptr ds:[OvlSize] ; p©i‡ten¡ datov‚ ‡ sti
         adc       dx,word ptr ds:[OvlSize+2]
         mov       word ptr ds:[SoubNSiz],ax ; nov  velikost souboru
         mov       word ptr ds:[SoubNSiz+2],dx
         mov       word ptr ds:[PojXDlk3+1],ax ; velikost programu
         mov       word ptr ds:[CojXDlk3+1],ax ; velikost programu

; ------ v˜po‡et zav dˆc¡ velikosti ve str nk ch

         mov       ax,word ptr ds:[LoadNSiz] ; nov  zav dˆc¡ velikost
         mov       dx,word ptr ds:[LoadNSiz+2]
         mov       cx,ax
         and       cx,1ffh                  ; velikost v posledn¡ str nce
         mov       ds:[ExeLast],cx          ; velikost posledn¡ str nky
         add       ax,511                   ; zaokrouhlen¡ na 512 str nky
         adc       dx,0
         and       dx,1ffh                  ; pojistka
         mov       cx,512
         div       cx                       ; v˜po‡et po‡tu str nek 512 B
         mov       ds:[ExePage],ax          ; po‡et str nek 512 B

; ------ startovac¡ adresa pojistky

         mov       ax,word ptr ds:[PojAdr]  ; offset pojistky
         mov       dx,word ptr ds:[PojAdr+2] ; segment pojistky
         mov       ds:[ExeCS],dx            ; segment CS
         add       ax,OffsEXE               ; offset za‡ tku pojistky
         mov       ds:[ExeIP],ax            ; offset IP

; ------ nastaven¡ segmentov˜ch adres v pojistce

         mov       word ptr ds:[PojXAdr4+4],dx ; kontrolovan  adresa CS
         mov       word ptr ds:[PojXRSS+2],dx ; reloka‡n¡ adresa SS
         mov       word ptr ds:[PojXRCS+2],dx ; reloka‡n¡ adresa CS

         mov       word ptr ds:[CojXAdr4+4],dx ; kontrolovan  adresa CS
         mov       word ptr ds:[CojXRSS+2],dx ; reloka‡n¡ adresa SS
         mov       word ptr ds:[CojXRCS+2],dx ; reloka‡n¡ adresa CS

; ------ adresa z sobn¡ku

         mov       ax,ds:[PojSize]          ; velikost pojistky
         mov       cl,4
         shr       ax,cl                    ; p©evod na odstavce
         add       ax,10h                   ; trochu rezerva
         mov       bx,ax                    ; £schova velikosti pojistky
         add       ax,word ptr ds:[PojAdr+2] ; segment z sobn¡ku
         mov       ds:[ExeSS],ax            ; adresa z sobn¡ku
         mov       word ptr ds:[ExeSP],400h ; offset z sobn¡ku

; ------ stanoven¡ minim ln¡ po‘adovan‚ pamˆti

         mov       ax,ds:[ExeMin]           ; p–vodn¡ rezerva m¡sta
         sub       ax,bx                    ; ode‡ten¡ pojistky
         jnc       InstX42
         xor       ax,ax
InstX42: cmp       ax,40h                   ; je m¡sto pro z sobn¡k ?
         jae       InstX43                  ; je dost m¡sta
         mov       ax,40h                   ; minim lnˆ pro z sobn¡k
InstX43: mov       ds:[ExeMin],ax           ; nov  po‘adovan  pamˆŸ

; ------ relokace adres v pojistce

         mov       si,offset PojXTab
         cld
         mov       dx,word ptr ds:[PojAdr]  ; offset po‡ tku pojistky
         mov       cx,(offset(CojXTab0-PojXTab))/2 ; po‡et polo‘ek v tabulce
InstX46: lodsw                              ; adresa polo‘ky k relokaci
         mov       di,ax                    ; adresa polo‘ky
         add       word ptr ds:[di],dx      ; relokace adresy
         loop      InstX46                  ; dal¨¡ polo‘ka

; ------ registry pro v˜po‡et kontroln¡ho sou‡tu

         mov       ax,word ptr ds:[ProgNSiz] ; skute‡n  velikost programu
         mov       dx,word ptr ds:[ProgNSiz+2]
         mov       cx,ax                    ; £schova velikosti
         add       ax,0ffffh
         adc       dx,0                     ; zaokrouhlen  velikost
         mov       word ptr ds:[PojXDel1+1],dx ; po‡et blok– 64 KB (i necel˜ch)
         mov       word ptr ds:[CojXDel1+1],dx ; po‡et blok– 64 KB (i necel˜ch)
         shr       cx,1                     ; velikost posledn¡ho bloku
         jnz       instX47
         mov       cx,8000h
InstX47: mov       word ptr ds:[PojXDel2+1],cx ; velikost dat v posledn¡m bloku
         mov       word ptr ds:[CojXDel2+1],cx ; velikost dat v posledn¡m bloku

; ====== ulo‘en¡ souboru na disk

; ------ z pis za‡ tku z hlav¡ do souboru

         push      ds
         pop       es
         mov       si,offset ExeHead        ; z hlav¡
         mov       cx,28                    ; d‚lka z hlav¡
         call      WritFile                 ; z pis z hlav¡ do souboru
         jnc       InstX49                  ; operace OK
InstX48: ret

; ------ kopie prvn¡ ‡ sti hlavi‡ky

InstX49: mov       bx,cx                    ; offset po‡ tku z hlav¡ v souboru
         xor       dx,dx
         mov       cx,ds:[Head1Siz]         ; velikost prvn¡ ‡ sti z hlav¡
         sub       cx,bx                    ; ode‡ten¡ ji‘ na‡ten‚ho z hlav¡
         xor       di,di
         call      CopyFile                 ; zkop¡rov n¡ za‡ tku z hlav¡
         jc        InstX48

; ------ kopie druh‚ ‡ sti hlavi‡ky

         mov       bx,word ptr ds:[Head2Adr] ; offset za‡ tku 2. ‡ sti hlavi‡ky
         mov       dx,word ptr ds:[Head2Adr+2]
         mov       cx,word ptr ds:[Head2Siz] ; velikost 2. ‡ sti hlavi‡ky
         mov       di,word ptr ds:[Head2Siz+2]
         call      CopyFile                 ; zkop¡rov n¡ 2. ‡ sti hlavi‡ky
         jc        InstX48

; ------ zkop¡rov n¡ samotn‚ho programu

         xor       ax,ax                    ; v˜choz¡ hodnota kontr. sou‡tu
         add       bx,cx                    ; offset za‡ tku programu
         adc       dx,di
         mov       cx,word ptr ds:[ProgSize] ; velikost programu
         mov       di,word ptr ds:[ProgSize+2]
         and       cx,not 1                 ; kromˆ lich‚ho bajtu
         call      CopyFile                 ; kopie samotn‚ho programu
         jc        InstX48                  ; chyba

; ------ na‡ten¡ lich‚ho bajtu do bufferu

         xor       si,si                    ; po‡ te‡n¡ adresa v bufferu
         mov       es,ds:[SoubSegm]         ; buffer
         test      byte ptr ds:[ProgSize],1 ; je lich  velikost programu ?
         jz        InstX51                  ; je sud  velikost programu
         add       bx,cx
         adc       dx,di                    ; offset lich‚ho bajtu
         mov       cx,1                     ; ‡ten¡ 1 bajtu
         xor       di,di
         call      ReadFil0                 ; na‡ten¡ 1 bajtu
         jc        InstX48
         inc       si                       ; po‡ te‡n¡ adresa = 1

; ------ p©enos zavadˆ‡e do bufferu

InstX51: mov       di,si                    ; ukl dac¡ adresa v bufferu
         mov       si,offset PojXBeg        ; zavadˆ‡
         mov       cx,offset(PojXCRC-PojXBeg) ; d‚lka zavadˆ‡e (po CRC)
         test      byte ptr ds:[Param],40h  ; je s pokra‡ov n¡m ?
         jz        InstX512
         mov       si,offset CojXBeg        ; zavadˆ‡
         mov       cx,offset(CojXCRC-CojXBeg) ; d‚lka zavadˆ‡e (po CRC)
InstX512:cld
         rep       movsb                    ; p©enos zavadˆ‡e do bufferu

; ------ na‡ten¡ reloka‡n¡ tabulky

         mov       si,di                    ; adresa k na‡ten¡ tabulky
         mov       bx,ds:[ExeXRel]          ; offset reloka‡n¡ tabulky
         xor       dx,dx
         mov       cx,ds:[RelokSiz]         ; velikost reloka‡n¡ tabulky
         call      ReadFil0                 ; na‡ten¡ reloka‡n¡ tabulky
         jc        InstX528                 ; chyba

; ------ adresa konce zavadˆ‡e

         add       si,cx                    ; adresa konce dat v bufferu
         mov       byte ptr es:[si],0       ; p©¡padn  lich  0
         inc       si                       ; p©¡prava pro zaokrouhlen¡
;         and       si,not 1                 ; zarovn n¡ na sudou adresu

; ------ v˜po‡et kontroln¡ho sou‡tu zavadˆ‡e (s reloka‡n¡ tabulkou)

         mov       cx,si                    ; velikost zavadˆ‡e (bajt–)
         shr       cx,1                     ; velikost souboru ve slovech
         xor       si,si                    ; po‡ te‡n¡ adresa
         cld
InstX52: rol       ax,1                     ; rotace o 1 bit vlevo
         xor       ax,es:[si]               ; p©i‡ten¡ slova
         inc       si
         inc       si
         loop      InstX52                  ; dal¨¡ slovo
         rol       ax,1                     ; rotace
         mov       es:[si],ax               ; kontroln¡ sou‡et
         inc       si
         inc       si                       ; adresa konce

; ------ ulo‘en¡ zavadˆ‡e do souboru

         mov       cx,si                    ; d‚lka
         xor       si,si                    ; po‡ te‡n¡ adresa
         call      WritFile                 ; z pis do souboru
         jc        InstX528                 ; chyba z pisu

; ------ kopie dat za programem

         mov       cx,word ptr ds:[OvlSize] ; velikost dat LOW
         mov       di,word ptr ds:[OvlSize+2] ; velikost dat HIGH
         mov       bx,word ptr ds:[LoadSize] ; offset dat LOW
         mov       dx,word ptr ds:[LoadSize+2] ; offset dat HIGH
         call      CopyFile                 ; zkop¡rov n¡ dat za programem
InstX528:ret

InstX    ENDP

; -----------------------------------------------------------------------------
;        inicializace parametr– programu EXE
; -----------------------------------------------------------------------------

InitXPar PROC      NEAR

; ------ velikost reloka‡n¡ tabulky

         mov       ax,ds:[ExeRNum]          ; po‡et polo‘ek reloka‡n¡ tabulky
         shl       ax,1
         shl       ax,1                     ; velikost rel. tab. (bajt–)
         mov       ds:[RelokSiz],ax         ; velikost reloka‡n¡ tabulky

; ------ velikost hlavi‡ky programu

         mov       ax,ds:[ExeHSize]         ; velikost z hlav¡ (odstavc–)
         cmp       ax,2
         jae       InitXP2
         mov       ax,2                     ; omezen¡ na minim lnˆ 2 odstavce
InitXP2: mov       cx,16
         mul       cx                       ; p©epo‡et na bajty
         mov       word ptr ds:[HeadSize],ax ; velikost z hlav¡ LOW
         mov       word ptr ds:[HeadSize+2],dx ; velikost z hlav¡ HIGH

; ------ velikost zav dˆn‚ ‡ sti

         mov       bx,ds:[ExeLast]          ; d‚lka posledn¡ str nky 512 B
         mov       ax,ds:[ExePage]          ; po‡et str nek 512 bajt–
         or        bx,bx                    ; jsou data v posledn¡ str nce ?
         jz        InitXP3                  ; nejsou data v posledn¡ str nce
         dec       ax                       ; kromˆ posledn¡ str nky
InitXP3: mov       dx,512                   ; d‚lka 1 str nky
         mul       dx                       ; p©epo‡et str nek na bajty
         add       ax,bx                    ; p©i‡ten¡ posledn¡ str nky
         adc       dx,0                     ; p©enos
         mov       word ptr ds:[LoadSize],ax ; zav dˆn  velikost souboru LOW
         mov       word ptr ds:[LoadSize+2],dx ; zav dˆn  velikost souboru HIGH

; ------ velikost zav dˆn‚ho programu

         sub       ax,word ptr ds:[HeadSize] ; ode‡ten¡ z hlav¡
         sbb       dx,word ptr ds:[HeadSize+2]
         jnc       InitXP4
         xor       ax,ax
         xor       dx,dx
InitXP4: mov       word ptr ds:[ProgSize],ax ; velikost programu
         mov       word ptr ds:[ProgSize+2],dx

; ------ celkov  velikost souboru

         xor       cx,cx
         xor       dx,dx
         mov       bx,ds:[SouborI1]         ; vstupn¡ soubor
         mov       ax,4202h
         int       21h                      ; vystaven¡ ukazatele na konec
         mov       word ptr ds:[SoubSize],ax ; velikost souboru LOW
         mov       word ptr ds:[SoubSize+2],dx ; velikost souboru HIGH

; ------ velikost dat za programem

         sub       ax,word ptr ds:[LoadSize] ; ode‡ten¡ zav dˆn‚ ‡ sti
         sbb       dx,word ptr ds:[LoadSize+2]
         jnc       InitXP5
         xor       ax,ax
         xor       dx,dx
InitXP5: mov       word ptr ds:[OvlSize],ax
         mov       word ptr ds:[OvlSize+2],dx

; ------ kontrola offsetu reloka‡n¡ tabulky

         mov       ax,ds:[ExeRelok]         ; adresa reloka‡n¡ tabulky
         cmp       ax,28
         jae       InitXP6                  ; adresa OK
         mov       ax,28
InitXP6: cmp       word ptr ds:[HeadSize+2],0
         jne       InitXP7
         cmp       ax,word ptr ds:[HeadSize]
         jbe       InitXP7
         mov       ax,word ptr ds:[HeadSize]
InitXP7: mov       ds:[ExeXRel],ax          ; opraven˜ offset reloka‡n¡ tabulky

; ------ velikost prvn¡ ‡ sti hlavi‡ky (po reloka‡n¡ tabulku)

         mov       cx,ax                    ; £schova adresy
         add       ax,15                    ; zaokrouhlen¡ nahoru
         and       ax,not 0fh               ; zarovn n¡ na odstavce
         jnz       InitXP8
         mov       ax,0fff0h
InitXP8: mov       ds:[Head1Siz],ax         ; velikost prvn¡ ‡ sti hlavi‡ky

; ------ adresa druh‚ ‡ sti hlavi‡ky

         xor       bx,bx
         add       cx,ds:[RelokSiz]         ; p©i‡ten¡ reloka‡n¡ tabulky
         adc       bx,0                     ; p©enos
         and       cx,not 0fh               ; zaokrouhlen¡ 2. ‡ sti dol–
         or        bx,bx
         jnz       InitXP9
         cmp       cx,ax
         jae       InitXP9
         mov       cx,ax                    ; omezen¡ za‡ tku p©i m lo polo‘k ch
InitXP9: mov       word ptr ds:[Head2Adr],cx ; adresa druh‚ ‡ sti relok. tab.
         mov       word ptr ds:[Head2Adr+2],bx

; ------ velikost druh‚ ‡ sti hlavi‡ky

         mov       ax,word ptr ds:[HeadSize] ; velikost hlavi‡ky LOW
         mov       dx,word ptr ds:[HeadSize+2] ; velikost hlavi‡ky HIGH
         sub       ax,cx
         sbb       dx,bx                    ; ode‡ten¡ po‡ tku druh‚ ‡ sti hlav.
         jnc       InitXPA
         xor       ax,ax
         xor       dx,dx
InitXPA: mov       word ptr ds:[Head2Siz],ax ; velikost druh‚ ‡ sti hlavi‡ky LOW
         mov       word ptr ds:[Head2Siz+2],dx

         ret

InitXPar ENDP

; -----------------------------------------------------------------------------
;        inicializace pamˆti (CY=nedostatek pamˆti)
; -----------------------------------------------------------------------------

InitMem  PROC      NEAR

; ------ zmen¨en¡ velikosti programu na minimum

         mov       bx,(offset(Zasob-Start)+10fh)/16 ; d‚lka programu
         mov       ah,4ah
         int       21h                      ; zmen¨en¡ velikosti programu
         jc        InitMem9                 ; chyba pamˆti

; ------ vytvo©en¡ datov‚ho bufferu

         mov       bx,1000h                 ; velikost bloku 64 KB
         mov       ah,48h
         int       21h                      ; vytvo©en¡ datov‚ho bufferu
         jc        InitMem9                 ; chyba - nedostatek pamˆti
         mov       ds:[SoubSegm],ax         ; adresa datov‚ho bufferu

InitMem9:ret

InitMem  ENDP

; -----------------------------------------------------------------------------
;        sestaven¡ jm‚na p©echodn‚ho souboru
; -----------------------------------------------------------------------------

InitOut  PROC      NEAR

; ------ test, zda byl v˜stupn¡ soubor zad n

         test      byte ptr ds:[Param],1    ; byl v˜stupn¡ soubor zad n ?
         jnz       InitOut9                 ; v˜stupn¡ soubor byl zad n

; ------ n hrada p©¡pony p©echodnou p©¡ponou "$$$"

         mov       si,ds:[SouborE2]         ; adresa p©¡pony jm‚na souboru
         mov       word ptr ds:[si],"$$"
InitOut1:mov       word ptr ds:[si+2],"$"   ; n hrada p©¡ponou "$$$"

; ------ test, zda p©echodn˜ soubor existuje

InitOut2:mov       dx,offset Soubor2        ; jm‚no v˜stupn¡ho souboru
         mov       ah,4eh
         mov       cx,3fh                   ; atributy
         int       21h                      ; test, zda soubor existuje
         jc        InitOut3                 ; soubor neexistuje

; ------ zv˜¨en¡ ‡¡sla p©¡pony

         push      si
InitOu22:cmp       byte ptr ds:[si+2],"$"
         jne       InitOu21
         mov       byte ptr ds:[si+2],"0"
InitOu21:inc       byte ptr ds:[si+2]
         cmp       byte ptr ds:[si+2],"9"
         jne       InitOu24
         mov       byte ptr ds:[si+2],"0"
         dec       si
         jmp       short InitOu22
InitOu24:pop       si
         jmp       short InitOut2

InitOut3:

InitOut9:ret

InitOut  ENDP

; -----------------------------------------------------------------------------
;        test souboru, zda je typu EXE (CY=chyba ‡ten¡ ze souboru)
; -----------------------------------------------------------------------------

TestExe  PROC      NEAR

; ------ na‡ten¡ z hlav¡ ze souboru

         push      ds
         pop       es
         xor       dx,dx                    ; offset v souboru HIGH
         xor       bx,bx                    ; offset v souboru LOW
         mov       si,offset ExeHead        ; za‡ tek z hlav¡
         mov       cx,offset(ExeHead0-ExeHead) ; d‚lka z hlav¡
         call      ReadFile                 ; na‡ten¡ z hlav¡ souboru
         jc        TestExe3                 ; nˆjak  chyba

; ------ test, zda je to program EXE

         cmp       ax,cx                    ; po‡et na‡ten˜ch bajt–
         jb        TestExe2                 ; nen¡ program EXE
         cmp       word ptr ds:[ExeIdnt],"MZ" ; je to EXE ?
         je        TestExe1                 ; je to EXE
         cmp       word ptr ds:[ExeIdnt],"ZM"
         jne       TestExe2                 ; nen¡ to EXE

TestExe1:or        byte ptr ds:[Param],2    ; p©¡znak programu EXE

TestExe2:clc
TestExe3:ret

TestExe  ENDP

; -----------------------------------------------------------------------------
;        test, zda je program ji‘ chr nˆn pojistkou
; -----------------------------------------------------------------------------

TestPoj  PROC      NEAR

; ------ rozli¨en¡, zda je soubor COM nebo EXE

         test      byte ptr ds:[Param],2    ; je program typu EXE ?
         jnz       TestPoj4                 ; program je typu EXE

; ------ test programu COM, zda za‡¡n  instrukc¡ JMP NEAR

         cmp       byte ptr ds:[ExeHead],KODJMP ; je instrukce JMP NEAR ?
         jne       TestPoj8                 ; nen¡ instrukce JMP NEAR

; ------ ur‡en¡ offset za‡ tku p©¡padn‚ pojistky

         mov       bx,word ptr ds:[ExeHead+1] ; offset instrukce JMP
         add       bx,3-OffsCOM             ; korekce offsetu v souboru
         xor       dx,dx                    ; offset v souboru HIGH
         mov       di,OffsCOM               ; offset instrukc¡ PUSH AX/BX
         jmp       short TestPoj6           ; test pojistky

; ------ ur‡en¡ adresy za‡ tku pojistky v programu EXE

TestPoj4:mov       ax,ds:[ExeCS]            ; registr CS
         add       ax,ds:[ExeHSize]         ; p©i‡ten¡ velikosti z hlav¡
         mov       cx,16
         mul       cx                       ; p©epo‡et na offset
         add       ax,ds:[ExeIP]            ; p©i‡ten¡ offsetu
         adc       dx,0                     ; p©enos HIGH
         sub       ax,OffsEXE               ; ode‡ten¡ z hlav¡ pojistky
         sbb       dx,0                     ; p©enos HIGH
         mov       bx,ax                    ; offset LOW
         mov       di,OffsEXE               ; offset instrukc¡ PUSH AX/BX

; ------ na‡ten¡ za‡ tku p©¡padn‚ pojistky

TestPoj6:mov       cx,32                    ; velikost bufferu
         push      ds
         pop       es
         mov       si,offset OldPoj         ; buffer k na‡ten¡ star‚ pojistky
         call      ReadFile                 ; na‡ten¡ za‡ tku pojistky
         jc        TestPoj9                 ; chyba ‡ten¡

; ------ kontrola, zda je to pojistka

         mov       bx,di                    ; offset instrukc¡ PUSH AX/BX
         cmp       word ptr ds:[si],"JP"    ; je pojistka ?
         jne       TestPoj8                 ; nen¡ pojistka
         cmp       word ptr ds:[si+bx],KODPAX+KODPBX*256 ; je PUSH AX/BX ?
         jne       TestPoj8                 ; nen¡ pojistka
         cmp       word ptr ds:[si+bx+2],KODPCX+KODPDX*256 ; je PUSH CX/DX ?
         jne       TestPoj8                 ; nen¡ pojistka
         or        byte ptr ds:[Param],4    ; p©¡znak, ‘e je ji‘ pojistka
TestPoj8:clc
TestPoj9:ret

TestPoj  ENDP

; -----------------------------------------------------------------------------
;        uzav©en¡ soubor– p©i chybˆ
; -----------------------------------------------------------------------------

ClosErr  PROC      NEAR

; ------ uzav©en¡ vstupn¡ho souboru

         mov       bx,ds:[SouborI1]
         or        bx,bx
         jz        ClosErr2
         mov       ah,3eh
         int       21h                      ; uzav©en¡ vstupn¡ho souboru

; ------ uzav©en¡ v˜stupn¡ho souboru

ClosErr2:mov       bx,ds:[SouborI2]
         or        bx,bx
         jz        ClosErr3
         mov       ah,3eh
         int       21h                      ; uzav©en¡ v˜stupn¡ho souboru

; ------ zru¨en¡ v˜stupn¡ho souboru

         mov       dx,offset Soubor2        ; v˜stupn¡ soubor
         mov       ah,41h
         int       21h                      ; zru¨en¡ v˜stupn¡ho souboru

ClosErr3:
         ret

ClosErr  ENDP

; -----------------------------------------------------------------------------
;        ‡ten¡ dat s kontrolou po‡tu bajt–
; -----------------------------------------------------------------------------

ReadFil0 PROC      NEAR

         push      ax
         call      ReadFile                 ; na‡ten¡ bloku dat
         jc        ReadFl02                 ; chyba ‡ten¡
         cmp       ax,cx                    ; bylo na‡teno v¨e ?
         je        ReadFl02
         stc
ReadFl02:pop       ax
         jnc       ReadFl03                 ; operace ‡ten¡ OK
         mov       dx,offset ReadTxt        ; text - chyba ‡ten¡
ReadFl03:ret

ReadFil0 ENDP

; -----------------------------------------------------------------------------
;        ‡ten¡ dat ze souboru
; -----------------------------------------------------------------------------
; VSTUP: DX:BX=offset v souboru
;        CX=po‡et bajt– ke ‡ten¡
;        ES:SI=adresa bufferu k na‡ten¡ dat
;        DS=datov˜ segment
; VSTUP:AX=po‡et skute‡nˆ na‡ten˜ch bajt–
; -----------------------------------------------------------------------------

ReadFile PROC      NEAR

; ------ £schova registr–

         push      bx
         push      dx
         push      ds

; ------ nastaven¡ ukazatele v souboru

         push      cx
         mov       cx,dx                    ; offset HIGH
         mov       dx,bx                    ; offset LOW
         mov       bx,ds:[SouborI1]         ; identifik tor souboru
         mov       ax,4200h
         int       21h                      ; nastaven¡ ukazatele na za‡ tek
         pop       cx
         jc        ReadFil8                 ; chyba

; ------ na‡ten¡ dat ze souboru

         mov       dx,si                    ; offset adresy bufferu
         push      es
         pop       ds                       ; DS <- segment adresy bufferu
         mov       ah,3fh
         int       21h                      ; na‡ten¡ z hlav¡ souboru

; ------ n vrat registr–

ReadFil8:pop       ds
         pop       dx
         pop       bx
         jnc       ReadFil9                 ; operace OK
         mov       dx,offset ReadTxt        ; text - chyba ‡ten¡
         mov       ax,0
ReadFil9:ret

ReadFile ENDP

; -----------------------------------------------------------------------------
;        ulo‘en¡ bloku ES:SI/CX do souboru (sekven‡nˆ)
; -----------------------------------------------------------------------------

WritFile PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      dx
         push      ds

; ------ z pis bloku na disk

         mov       bx,ds:[SouborI2]         ; identifik tor souboru
         push      es
         pop       ds
         mov       dx,si                    ; adresa
         mov       ah,40h
         int       21h                      ; z pis bloku na disk
         jc        WritFil2                 ; chyba z pisu
         cmp       ax,cx                    ; bylo zaps no v¨e ?
         je        WritFil2
         stc                                ; p©¡znak chyby z pisu

; ------ n vrat registr–

WritFil2:pop       ds
         pop       dx
         pop       bx
         pop       ax
         jnc       WritFil9                 ; operace OK
         mov       dx,offset WritTxt        ; text - chyba z pisu
WritFil9:ret

WritFile ENDP

; -----------------------------------------------------------------------------
;        zkop¡rov n¡ ‡ sti souboru
; -----------------------------------------------------------------------------
; VSTUP: DX:BX=offset po‡ tku dat ve vstupn¡m souboru
;        DI:CX=po‡et bajt– ke zkop¡rov n¡
;        AX=vstupn¡ hodnota kontroln¡ho sou‡tu
; VSTUP:AX=v˜stupn¡ hodnota kontroln¡ho sou‡tu
;        CY=chyba operace (v DX je chybov˜ text)
; -----------------------------------------------------------------------------

CopyFile PROC      NEAR

; ------ £schova registr–

         push      bx
         push      dx
         push      cx
         push      si
         push      di
         push      bp
         push      es

; ------ p©¡prava registr–

         mov       es,ds:[SoubSegm]         ; datov˜ segment
         mov       bp,cx                    ; ‡¡ta‡ dat LOW

; ------ ur‡en¡ velikosti bloku dat ke zkop¡rov n¡

CopyFil2:mov       cx,0f000h                ; p©ednastaven¡
         or        di,di                    ; je je¨tˆ v¡ce ne‘ 64 KB ?
         jnz       CopyFil3                 ; je je¨tˆ v¡ce ne‘ 64 KB
         cmp       cx,bp                    ; zb˜v  ji‘ m‚nˆ dat ne‘ 0f000h ?
         jbe       CopyFil3
         mov       cx,bp                    ; omezen¡ po‡tu bajt–
CopyFil3:clc                                ; p©¡znak operace OK
         jcxz      CopyFil8                 ; konec operace

; ------ na‡ten¡ bloku dat

         xor       si,si                    ; offset v bufferu k na‡ten¡ dat
         call      ReadFil0                 ; ‡ten¡ bloku dat
         jc        CopyFil8                 ; chyba ‡ten¡

; ------ ulo‘en¡ bloku dat

         call      WritFile                 ; z pis bloku dat
         jc        CopyFil8                 ; chyba z pisu

; ------ posun ukazatele v souboru a ‡¡ta‡e dat

         add       bx,cx
         adc       dx,0
         sub       bp,cx
         sbb       di,0

; ------ kontroln¡ sou‡et bloku dat

         shr       cx,1                     ; velikost bloku ve slovech
         jcxz      CopyFil6
CopyFil5:rol       ax,1                     ; rotace o 1 bit vlevo
         xor       ax,es:[si]               ; p©i‡ten¡ slova
         inc       si
         inc       si                       ; zv˜¨en¡ adresy
         loop      CopyFil5                 ; dal¨¡ slovo
CopyFil6:jmp       short CopyFil2           ; dal¨¡ blok dat

; ------ n vrat registr–

CopyFil8:pop       es
         pop       bp
         pop       di
         pop       si
         pop       cx
         jc        CopyFil9                 ; nˆjak  chyba - ponech  DX
         pop       dx
         pop       bx
         ret

CopyFil9:pop       bx
         pop       bx
         ret

CopyFile ENDP

; -----------------------------------------------------------------------------
;        otev©en¡ vstupn¡ho souboru
; -----------------------------------------------------------------------------

OpenIn   PROC      NEAR

; ------ £schova atribut– souboru

         mov       ax,4300h
         mov       dx,offset Soubor1        ; jm‚no vstupn¡ho souboru
         int       21h                      ; poskytnut¡ atribut– souboru
         jc        OpenIn8
         mov       ds:[FileAtr],cx          ; atributy souboru
         test      byte ptr ds:[Param],1    ; je zad n v˜stupn¡ soubor ?
         jnz       OpenIn2                  ; je zad n v˜stupn¡ soubor
         mov       dx,offset ROTxt
         test      cl,1                     ; je R/O ?
         stc
         jnz       OpenIn9                  ; chyba - je atribut R/O

; ------ otev©en¡ souboru

OpenIn2: mov       dx,offset Soubor1        ; jm‚no vstupn¡ho souboru
         mov       ax,3d00h
         int       21h                      ; otev©en¡ vstupn¡ho souboru
         jc        OpenIn8                  ; chybn‚ zad n¡ souboru
         mov       ds:[SouborI1],ax         ; identifik tor souboru

; ------ £schova data a ‡asu souboru

         mov       bx,ax
         mov       ax,5700h
         int       21h                      ; poskytnut¡ data a ‡asu souboru
         jc        OpenIn8
         mov       ds:[FileDate],dx         ; datum
         mov       ds:[FileTime],cx         ; ‡as

OpenIn8: mov       dx,offset InpTxt         ; text - soubor nenalezen
OpenIn9: ret

OpenIn   ENDP


; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                       Rozbor p©¡kazov‚ho © dku
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

; -----------------------------------------------------------------------------
;        rozbor p©¡kazov‚ho © dku (CY=chyba zad n¡)
; -----------------------------------------------------------------------------

Rozbor   PROC      NEAR

; ------ p©¡prava k dek¢dov n¡ p©¡kazov‚ho © dku

         mov       si,81h                   ; za‡ tek textu parametru
         mov       bl,ds:[si-1]             ; po‡et znak– zadan‚ho parametru
         and       bl,7fh                   ; ochrana proti chybˆ
         mov       bh,0
         mov       byte ptr ds:[si+bx],0    ; ozna‡en¡ konce textu v bufferu

; ------ rozbor p©ep¡na‡–

         call      ParPar                   ; rozbor zad n¡ p©ep¡na‡–
         jc        Rozbor9                  ; chyba zad n¡

; ------ rozbor jm‚na vstupn¡ho souboru

         mov       di,offset Soubor1        ; buffer jm‚na vstupn¡ho soubrou
         call      ParFile                  ; rozbor zad n¡ vstupn¡ho souboru
         jc        Rozbor9                  ; chyba - soubor nezad n

; ------ kopie do jm‚na v˜stupn¡ho souboru

         push      si
         mov       si,offset Soubor1
         mov       di,offset Soubor2
         mov       cx,128+10
         cld
         rep       movsb                    ; kopie jm‚na souboru
         add       bx,offset(Soubor2-Soubor1) ; korekce adresy p©¡pony
         mov       ds:[SouborE2],bx         ; p©¡pona jm‚na p©echodn‚ho souboru
         pop       si

; ------ rozbor p©ep¡na‡–

         call      ParPar                   ; rozbor zad n¡ p©ep¡na‡–
         jc        Rozbor9                  ; chyba zad n¡

; ------ rozbor jm‚na v˜stupn¡ho souboru

         mov       di,offset Soubor2        ; buffer jm‚na v˜stupn¡ho souboru
         call      ParFile                  ; rozbor zad n¡ v˜stupn¡ho souboru
         jc        Rozbor7                  ; v˜stupn¡ soubor nen¡ zad n
         mov       ds:[SouborE2],bx         ; p©¡pona jm‚na souboru
         or        byte ptr ds:[Param],1    ; p©¡znak zad n¡ v˜stupn¡ho souboru

; ------ rozbor p©ep¡na‡–

         call      ParPar                   ; rozbor zad n¡ p©ep¡na‡–
         jc        Rozbor9                  ; chyba zad n¡

; ------ test, zda je¨tˆ nˆco n sleduje

Rozbor7: call      ParSpc                   ; nesm¡ nic n sledovat
Rozbor8: cmc
Rozbor9: ret

Rozbor   ENDP

; -----------------------------------------------------------------------------
;        rozbor zad n¡ p©ep¡na‡– (CY=chyba)
; -----------------------------------------------------------------------------

ParPar   PROC      NEAR

; ------ nalezen¡ za‡ tku parametr–

ParPar1: call      ParSpc                   ; nalezen¡ za‡ tku textu
         jc        ParPar8                  ; nen¡ ‘ dn˜ dal¨¡ text
         cmp       al,"/"                   ; je p©¡znak parametru ?
         jne       ParPar8                  ; nen¡ parametr

; ------ na‡ten¡ znaku parametru

         call      ParChr                   ; na‡ten¡ p©¡znaku parametru "/"
         call      ParChr                   ; na‡ten¡ znaku parametru
         jc        ParPar9                  ; nen¡ zad n - to je chyba

; ------ parametr "/X" - odstranˆn¡ pojistky z programu

         cmp       al,"X"                   ; je parametr "/X" ?
         jne       ParPar2                  ; nen¡ parametr "/X"
         test      byte ptr ds:[Param],10h+20h ; jsou jin‚ p©ep¡na‡e ?
         jnz       ParPar7                  ; jsou jin‚ p©ep¡na‡e - chyba
         or        byte ptr ds:[Param],8    ; p©¡znak parametru "/X"
         jmp       short ParPar1            ; dal¨¡ parametr

; ------ parametr "/T" - test neporu¨enosti programu

ParPar2: cmp       al,"T"                   ; je parametr "/T" ?
         jne       ParPar3                  ; nen¡ parametr "/T"
         test      byte ptr ds:[Param],8+20h ; jsou jin‚ p©ep¡na‡e ?
         jnz       ParPar7                  ; jsou jin‚ p©ep¡na‡e - chyba
         or        byte ptr ds:[Param],10h  ; p©¡znak parametru "/T"
         jmp       short ParPar1            ; dal¨¡ parametr

; ------ parametr "/!" - oprava zavirovan‚ho programu

ParPar3: cmp       al,"!"                   ; je parametr "/!" ?
         jne       ParPar4                  ; nen¡ parametr "/!"
         test      byte ptr ds:[Param],8+10h ; jsou jin‚ p©ep¡na‡e ?
         jnz       ParPar7                  ; jsou jin‚ p©ep¡na‡e - chyba
         or        byte ptr ds:[Param],20h  ; p©¡znak parametru "/!"
         jmp       short ParPar1            ; dal¨¡ parametr

; ------ parametr "/C" - pojistka s pokra‡ov n¡m

ParPar4: cmp       al,"C"                   ; je parametr "/C" ?
         jne       ParPar7                  ; nen¡ parametr "/C"
         or        byte ptr ds:[Param],40h  ; p©¡znak pojistky s pokra‡ov n¡m
         jmp       short ParPar1            ; dal¨¡ parametr

; ------ chyba zad n¡ parametr–

ParPar7: stc                                ; p©¡znak chyby zad n¡
         ret

ParPar8: clc
ParPar9: ret

ParPar   ENDP

; -----------------------------------------------------------------------------
;        rozbor jm‚na souboru (ES:DI=buffer -> BX=p©¡pona, DX=jm‚no)
; -----------------------------------------------------------------------------

ParFile  PROC      NEAR

; ------ nalezen¡ za‡ tku jm‚na souboru

         call      ParSpc                   ; nalezen¡ za‡ tku jm‚na souboru
         jc        ParFile9                 ; konec textu
         cmp       al,"/"                   ; oddˆlova‡ parametr– ?
         stc                                ; p©¡znak chyby
         je        ParFile9                 ; oddˆlova‡ parametr– - nen¡ jm‚no

; ------ dek¢dov n¡ jm‚na souboru

         mov       dx,di                    ; £schova za‡ tku jm‚na
         mov       bx,di                    ; £schova za‡ tku p©¡pony
ParFile2:call      ParChr                   ; vstup znaku z p©¡kazov‚ho © dku
         cld
         jc        ParFile7                 ; konec p©¡kazov‚ho © dku
         je        ParFile6                 ; oddˆlova‡ - konec jm‚na
         cmp       al,"/"                   ; oddˆlova‡ parametr– ?
         je        ParFile6                 ; oddˆlova‡ parametr– - konec jm‚na

; ------ ulo‘en¡ znaku

         stosb                              ; ulo‘en¡ znaku do bufferu

; ------ test za‡ tku jm‚na

         cmp       al,":"                   ; oddˆlova‡ disku ?
         je        ParFile3                 ; oddˆlova‡ disku - za‡ tek jm‚na
         cmp       al,"\"                   ; oddˆlova‡ cesty ?
         jne       ParFile4                 ; nen¡ za‡ tek jm‚na
ParFile3:mov       dx,di                    ; £schova za‡ tku jm‚na

; ------ test za‡ tku p©¡pony

ParFile4:cmp       al,"."                   ; oddˆlova‡ p©¡pony ?
         jne       ParFile2                 ; nen¡ p©¡pona
         mov       bx,di                    ; £schova za‡ tku p©¡pony
         jmp       short ParFile2           ; dek¢dov n¡ dal¨¡ho znaku

; ------ korekce adresy p©¡pony jm‚na

ParFile6:dec       si                       ; n vrat znaku oddˆlova‡e
ParFile7:cmp       bx,dx                    ; byla p©¡pona zad na ?
         ja        ParFile8                 ; p©¡pona byla zad na OK
         mov       bx,di                    ; p©¡pona na konci jm‚na

; ------ ukon‡en¡ jm‚na souboru

ParFile8:mov       al,0                     ; ukon‡ovac¡ bajt 0
         stosb                              ; ukon‡en¡ jm‚na souboru
         clc                                ; p©¡znak operace OK

ParFile9:ret

ParFile  ENDP

; -----------------------------------------------------------------------------
;        vypu¨tˆn¡ mezer z p©¡kazov‚ho © dku
; -----------------------------------------------------------------------------

ParSpc   PROC      NEAR

         call      ParChr                   ; na‡ten¡ znaku z p©¡kazov‚ho © dku
         jc        ParSpc3                  ; nen¡ dal¨¡ znak
         je        ParSpc                   ; mezera - vypu¨tˆn¡
         dec       si                       ; n vrat posledn¡ho znaku
ParSpc3: ret

ParSpc   ENDP

; -----------------------------------------------------------------------------
;        vstup znaku z p©¡kazov‚ho © dku DS:SI
; -----------------------------------------------------------------------------

ParChr   PROC      NEAR

; ------ na‡ten¡ znaku z p©¡kazov‚ho © dku

         cld
         lodsb                              ; na‡ten¡ znaku z p©¡kazov‚ho © dku

; ------ n hrada tabel toru mezerou

         cmp       al,9
         jne       ParChr1                  ; nen¡ znak tabel toru
         mov       al," "                   ; n hrada tabel toru mezerou

; ------ konverze na velk‚ p¡smeno

ParChr1: cmp       al,"a"
         jb        ParChr2
         cmp       al,"z"
         ja        ParChr2
         sub       al,32                    ; konverze na velk‚ p¡smeno

; ------ test, zda je konec © dku

ParChr2: cmp       al," "                   ; je platn˜ znak ?
         jae       ParChr4                  ; je platn˜ znak nebo mezera
         dec       si                       ; n vrat ukazatele textu

ParChr4: ret

ParChr   ENDP

; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                                   data
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

Param    db        0                        ; parametry, p©ep¡na‡e
                                            ;    bit 0: 1=zad n v˜stupn¡ soubor
                                            ;    bit 1: 1=program je typu EXE
                                            ;    bit 2: 1=nalezena pojistka
                                            ;    bit 3: 1=po‘ad. odpojistkov n¡
                                            ;    bit 4: 1=test programu
                                            ;    bit 5: 1=po‘ad. opraven¡ prog.
                                            ;    bit 6: 1=pojistka s pokra‡ov n¡m

; ------ texty a hl ¨en¡

UvTxt    db        'POJ V1.21 - antivirova pojistka programu; (c) Miroslav Nemecek',13,10,'$'
HelpTxt  db        'Zadejte: POJ  vstupni_soubor  [vystupni_soubor]  [/C | /X | /T | /!]',13,10
         db        '              vstupni_soubor .... jmeno programu k ochraneni pojistkou',13,10
         db        '              vystupni_soubor ... jmeno vystupniho souboru',13,10
         db        '              /C ................ pouzit pojistku s moznosti pokracovani',13,10
         db        '              /T ................ test programu na neporusenost',13,10
         db        '              /X ................ odstraneni pojistky z programu',13,10
         db        '              /! ................ odstraneni pojistky s odvirovanim programu',13,10
         db        '$'

MemTxt   db        'Nedostatek pameti !',13,10,'$'
PojTxt   db        'Program je jiz chranen pojistkou !',13,10,'$'
SizTxt   db        'Program nelze ochranit pojistkou !',13,10,'$'

InpTxt   db        'Chybne zadani souboru k ochraneni - soubor nenalezen !',13,10,'$'
CreatTxt db        'Chyba - vystupni soubor nelze vytvorit !',13,10,'$'
ReadTxt  db        'Chyba cteni ze vstupniho souboru !',13,10,'$'
WritTxt  db        'Chyba zapisu do vystupniho souboru !'
CRTxt    db        13,10,'$'
ExTxt    db        'Vystupni soubor jiz existuje - chcete jej prepsat (A=ano) ? $'

NoPojTxt db        'Program neni chranen pojistkou (nebo je napaden virem) !',13,10,'$'
CRCErTxt db        'Program je POSKOZEN (zrejme napaden virem) !',13,7,10,'$'

CRCOkTxt db        'Kontrolni soucet souboru souhlasi.',13,10,'$'

FndTxt   db        'Pojistka v programu nenalezena !',13,10,'$'
VirTxt   db        'Pojistka z programu odstranena. Funkcnost programu NEZARUCENA !',13,10,'$'

DelTxt   db        'Puvodni soubor nelze nahradit novym souborem !',13,10,'$'
ROTxt    db        'Soubor nesmi mit nastavenu ochranu proti zapisu !',13,10,'$'

; ------ vstupn¡ soubor

Soubor1  db        128+10 dup(0)            ; jm‚no vstupn¡ho souboru
SouborI1 dw        0                        ; identifik. vstup. souboru (0=nen¡)

; ------ v˜stupn¡ soubor

Soubor2  db        128+10 dup(0)            ; jm‚no v˜stupn¡ho souboru
SouborE2 dw        0                        ; adresa p©¡pony jm‚na souboru
SouborI2 dw        0                        ; identifik. v˜stup.souboru (0=nen¡)

; ------ datov˜ buffer

SoubSegm dw        0                        ; segment se souborem
SoubNum  dw        0                        ; po‡et bajt– v bufferu

; ------ parametry vstupn¡ho souboru EXE

RelokSiz dw        0                        ; velikost reloka‡n¡ tabulky
HeadSize dd        0                        ; velikost z hlav¡ souboru
LoadSize dd        0                        ; velikost zav dˆn‚ ‡ sti programu
ProgSize dd        0                        ; velikost vnit©n¡ho programu
SoubSize dd        0                        ; p–vodn¡ celkov  velikost souboru
OvlSize  dd        0                        ; velikost dat za programem

ExeXRel  dw        0                        ; opraven˜ offset reloka‡n¡ tabulky

Head1Siz dw        0                        ; velikost prvn¡ ‡ sti hlavi‡ky
Head2Adr dd        0                        ; adresa po‡ tku 2. ‡ sti hlavi‡ky
Head2Siz dd        0                        ; velikost druh‚ ‡ sti hlavi‡ky

PojAdr   dd        0                        ; adresa pojistky v programu
RelAdr   dd        0                        ; adresa reloka‡n¡ tabulky v progr.

FileAtr  dw        0                        ; atributy souboru
FileDate dw        0                        ; datum souboru
FileTime dw        0                        ; ‡as souboru

; ------ parametry v˜stupn¡ho souboru EXE

SoubNSiz dd        0                        ; nov  velikost souboru EXE
LoadNSiz dd        0                        ; nov  zav dˆc¡ velikost programu
ProgNSiz dd        0                        ; nov  vnit©n¡ velikost programu
HeadNSiz dd        0                        ; nov  velikost hlavi‡ky programu
PojSize  dw        0                        ; velikost pojistky

; ------ z hlav¡ programu EXE (COM) - sem se na‡te prvn¡ch 28 bajt– souboru

ExeHead  label     byte                     ; z hlav¡ souboru EXE
ExeIdnt  db        'MZ'                     ; identifik tor souboru EXE
ExeLast  dw        0                        ; d‚lka posledn¡ str nky 512 B
ExePage  dw        0                        ; po‡et str nek 512 bajt– souboru
ExeRNum  dw        0                        ; po‡et polo‘ek reloka‡n¡ tabulky
ExeHSize dw        0                        ; velikost z hlav¡ (odstavc– 16 B)
ExeMin   dw        0                        ; minim ln¡ pamˆŸ
ExeMax   dw        0                        ; maxim ln¡ pamˆŸ
ExeSS    dw        0                        ; registr SS
ExeSP    dw        0                        ; registr SP
ExeSum   dw        0                        ; kontroln¡ sou‡et souboru
ExeIP    dw        0                        ; registr IP
ExeCS    dw        0                        ; registr CS
ExeRelok dw        0                        ; offset reloka‡n¡ tabulky
ExeOvl   dw        0                        ; po‡et modul– OVL
ExeHead0 label     byte

; ------ za‡ tek star‚ pojistky v souboru

OldPoj   db        32 dup(0)                ; asi tak 32 bajt– ze star‚ pojistky

;þ

; *****************************************************************************
;
;                              Pojistka COM
;                        (mus¡ m¡t sudou d‚lku !)
;
; *****************************************************************************

         EVEN

PojBeg   label     byte                     ; za‡ tek pojistky

PojIdn   db        'PJ'                     ; identifik tor pojistky

; !!!!!!!!
PojOld   db        0,0,0                    ; p–vodn¡ za‡ tek programu

; ------ £schova registr–

PojStart:push      ax                       ;Ä¿tyto 4 instrukce = identifikace !
         push      bx                       ; ³
         push      cx                       ; ³
         push      dx                       ;ÄÙ
         push      si
         push      di
         pushf

         mov       di,100h                  ; konstanta 100h

; ------ kontroln¡ sou‡et programu s pojistkou

; !!!!!!!
PojDelk1:mov       cx,offset(PojEnd-PojBeg)/2 ; d‚lka programu s pojist.(slov)
         mov       si,di                    ; po‡ te‡n¡ adresa programu
         mov       dx,cx                    ; v˜choz¡ hodnota CRC
         cld
PojS1:   lodsw                              ; na‡ten¡ slova
         rol       dx,1                     ; rotace o 1 bit vlevo
         xor       dx,ax                    ; p©i‡ten¡ slova
         loop      PojS1                    ; dal¨¡ slovo
         jz        PojS3                    ; kontroln¡ sou‡et je OK

; ------ chyba - program je po¨kozen

; !!!!!!!
PojS2:   mov       dx,offset PojText1       ; text - program je po¨kozen
         mov       ah,9
         int       21h
         mov       ax,4cfeh
         int       21h                      ; n vrat programu s chybou

; ------ kontrola verze syst‚mu

PojS3:   mov       ah,30h
         int       21h                      ; poskytnut¡ verze syst‚mu
         cmp       al,3                     ; je verze syst‚mu alespo¤ 3.00 ?
         jb        PojS7                    ; je n¡zk  verze syst‚mu

; ------ p©¡prava k nalezen¡ cesty

         mov       cx,ds:[2ch]              ; segment prost©ed¡
         jcxz      PojS7                    ; nen¡ prost©ed¡
         push      ds                       ; £schova DS
         mov       ds,cx                    ; segment prost©ed¡
         xor       ax,ax                    ; hledan‚ slovo
         xor       si,si                    ; za‡ tek prost©ed¡

; ------ nalezen¡ textu jm‚na souboru

PojS4:   inc       si                       ; zv˜¨en¡ ukazatele v prost©ed¡
         cmp       ds:[si-1],ax             ; konec prost©ed¡ ?
         jne       PojS4                    ; nalezen¡ konce prost©ed¡
         mov       cx,3                     ; po‡et bajt– ke ‡ten¡
         add       si,cx                    ; za‡ tek jm‚na souboru

; ------ otev©en¡ souboru

         mov       dx,si                    ; adresa textu jm‚na souboru
         mov       ah,3dh                   ; zde je AL=0
         int       21h                      ; otev©en¡ souboru
         pop       ds                       ; n vrat DS
         jc        PojS7                    ; chyba otev©en¡ souboru
         xchg      ax,bx                    ; identifik tor souboru

; ------ na‡ten¡ za‡ tku souboru

         mov       ah,3fh
         mov       dx,di                    ; za‡ tek programu 100h
         int       21h                      ; na‡ten¡ za‡ tku souboru

; ------ stanoven¡ velikosti souboru

         mov       ax,4202h
         xor       cx,cx
         xor       dx,dx
         int       21h                      ; vystaven¡ ukazatele na konec

; ------ kontrola velikosti souboru

; !!!!!!!!
PojDelk2:cmp       ax,offset(PojEnd-PojBeg) ; d‚lka programu s pojistkou
         jne       PojS2                    ; velikost nesouhlas¡

; ------ uzav©en¡ souboru

PojS6:   mov       ah,3eh
         int       21h                      ; uzav©en¡ souboru

; ------ kontrola za‡ tku souboru

; !!!!!!!!
PojS7:   cmp       word ptr ds:[di+1],offset PojStart-103h ; souhlas¡ offs. skoku ?
         jne       PojS2                    ; nesouhlas¡ offset skoku

; ------ n vrat za‡ tku programu

; !!!!!!!!
PojS8:   mov       si,offset PojOld         ; p–vodn¡ za‡ tek programu
         cld
         movsb
         movsw

; ------ n vrat registr–

         popf
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax

; !!!!!!!!
PojJMPSt:jmp       near ptr Start           ; start programu

PojText1 db        13,'Program POSKOZEN !',13,7,10,'$'

         EVEN

PojCRC   dw        0                        ; kontroln¡ sou‡et programu+pojistka

PojEnd   label     byte                     ; konec pojistky

; *****************************************************************************
;
;                         Cojistka COM s dotazem
;                        (mus¡ m¡t sudou d‚lku !)
;
; *****************************************************************************

         EVEN

CojBeg   label     byte                     ; za‡ tek pojistky

CojIdn   db        'PJ'                     ; identifik tor pojistky

; !!!!!!!!
CojOld   db        0,0,0                    ; p–vodn¡ za‡ tek programu

; ------ £schova registr–

CojStart:push      ax                       ;Ä¿tyto 4 instrukce = identifikace !
         push      bx                       ; ³
         push      cx                       ; ³
         push      dx                       ;ÄÙ
         push      si
         push      di
         pushf

         mov       di,100h                  ; konstanta 100h

; ------ kontroln¡ sou‡et programu s pojistkou

; !!!!!!!
CojDelk1:mov       cx,offset(CojEnd-CojBeg)/2 ; d‚lka programu s pojist.(slov)
         mov       si,di                    ; po‡ te‡n¡ adresa programu
         mov       dx,cx                    ; v˜choz¡ hodnota CRC
         cld
CojS1:   lodsw                              ; na‡ten¡ slova
         rol       dx,1                     ; rotace o 1 bit vlevo
         xor       dx,ax                    ; p©i‡ten¡ slova
         loop      CojS1                    ; dal¨¡ slovo
         jz        CojS3                    ; kontroln¡ sou‡et je OK

; ------ chyba - program je po¨kozen

; !!!!!!!
CojS2:   mov       dx,offset CojText1       ; text - program je po¨kozen
         mov       ah,9
         int       21h
         mov       ax,0c08h
         int       21h                      ; vstup znaku z kl vesnice
         cmp       al,13                    ; je ENTER ?
         je        CojS8                    ; je ENTER - pokra‡ov n¡
         mov       ax,4cfeh
         int       21h                      ; n vrat programu s chybou

; ------ kontrola verze syst‚mu

CojS3:   mov       ah,30h
         int       21h                      ; poskytnut¡ verze syst‚mu
         cmp       al,3                     ; je verze syst‚mu alespo¤ 3.00 ?
         jb        CojS7                    ; je n¡zk  verze syst‚mu

; ------ p©¡prava k nalezen¡ cesty

         mov       cx,ds:[2ch]              ; segment prost©ed¡
         jcxz      CojS7                    ; nen¡ prost©ed¡
         push      ds                       ; £schova DS
         mov       ds,cx                    ; segment prost©ed¡
         xor       ax,ax                    ; hledan‚ slovo
         xor       si,si                    ; za‡ tek prost©ed¡

; ------ nalezen¡ textu jm‚na souboru

CojS4:   inc       si                       ; zv˜¨en¡ ukazatele v prost©ed¡
         cmp       ds:[si-1],ax             ; konec prost©ed¡ ?
         jne       CojS4                    ; nalezen¡ konce prost©ed¡
         mov       cx,3                     ; po‡et bajt– ke ‡ten¡
         add       si,cx                    ; za‡ tek jm‚na souboru

; ------ otev©en¡ souboru

         mov       dx,si                    ; adresa textu jm‚na souboru
         mov       ah,3dh                   ; zde je AL=0
         int       21h                      ; otev©en¡ souboru
         pop       ds                       ; n vrat DS
         jc        CojS7                    ; chyba otev©en¡ souboru
         xchg      ax,bx                    ; identifik tor souboru

; ------ na‡ten¡ za‡ tku souboru

         mov       ah,3fh
         mov       dx,di                    ; za‡ tek programu 100h
         int       21h                      ; na‡ten¡ za‡ tku souboru

; ------ stanoven¡ velikosti souboru

         mov       ax,4202h
         xor       cx,cx
         xor       dx,dx
         int       21h                      ; vystaven¡ ukazatele na konec

; ------ kontrola velikosti souboru

; !!!!!!!!
CojDelk2:cmp       ax,offset(CojEnd-CojBeg) ; d‚lka programu s pojistkou
         jne       CojS2                    ; velikost nesouhlas¡

; ------ uzav©en¡ souboru

CojS6:   mov       ah,3eh
         int       21h                      ; uzav©en¡ souboru

; ------ kontrola za‡ tku souboru

; !!!!!!!!
CojS7:   cmp       word ptr ds:[di+1],offset CojStart-103h ; souhlas¡ offs. skoku ?
         jne       CojS2                    ; nesouhlas¡ offset skoku

; ------ n vrat za‡ tku programu

; !!!!!!!!
CojS8:   mov       si,offset CojOld         ; p–vodn¡ za‡ tek programu
         cld
         movsb
         movsw

; ------ n vrat registr–

         popf
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax

; !!!!!!!!
CojJMPSt:jmp       near ptr Start           ; start programu

CojText1 db        13,'Program POSKOZEN !',13,10
         db           'Pokracovat (ENTER=ANO) ?',13,7,10,'$'

         EVEN

CojCRC   dw        0                        ; kontroln¡ sou‡et programu+pojistka

CojEnd   label     byte                     ; konec pojistky


PojXTab  label     word                     ; tabulka adres k relokaci
         dw        PojXRelT
         dw        PojXT1+1
         dw        PojXS2+1
         dw        PojXT2+1
;         dw        PojXDlk3+1
         dw        PojXT3+2
         dw        PojXT3+4                 ; (POZOR = je to BAJT !)
         dw        PojXAdr4+2
         dw        PojXT5+3
         dw        PojXT6+3
         dw        PojXT7+3
         dw        PojXRCS
         dw        PojXRSS
PojXTab0 label     word

; ------ NSLEDUJE !!!

CojXTab  label     word                     ; tabulka adres k relokaci
         dw        CojXRelT
         dw        CojXT1+1
         dw        CojXS2+1
         dw        CojXT2+1
;         dw        CojXDlk3+1
         dw        CojXT3+2
         dw        CojXT3+4                 ; (POZOR = je to BAJT !)
         dw        CojXAdr4+2
         dw        CojXT5+3
         dw        CojXT6+3
         dw        CojXT7+3
         dw        CojXRCS
         dw        CojXRSS
CojXTab0 label     word

; *****************************************************************************
;
;                             Pojistka EXE
;                        (mus¡ m¡t sudou d‚lku !)
;
; *****************************************************************************
;þ
         EVEN

PojXBeg  label     byte                     ; za‡ tek pojistky (offset = 0)

PojXIdn  db        'PJ'                     ; identifik tor pojistky

; !!!!!!!!
PojXSP   dw        0                        ; p–vodn¡ SP programu
PojXSS   dw        0                        ; p–vodn¡ SS programu
PojXIP   dw        0                        ; p–vodn¡ IP programu Ä¿ po©ad¡ !!!
PojXCS   dw        0                        ; p–vodn¡ CS programu ÄÙ zachovat !
PojXRel  dw        0                        ; po‡et reloka‡n¡ch polo‘ek
PojXRelT dw        offset(PojXCRC-PojXBeg)  ; offset reloka‡n¡ tabulky
PojXMin  dw        0                        ; minim ln¡ pamˆŸ za programem

; ------ £schova registr–

PojXStrt:push      ax                       ;Ä¿tyto 4 instrukce = identifikace !
         push      bx                       ; ³
         push      cx                       ; ³
         push      dx                       ;ÄÙ
         push      si
         push      di
         push      ds
         pushf

; ------ inicializace segmentu po‡ tku programu

         mov       bx,ds                    ; segment PSP
         add       bx,10h                   ; segment za‡ tku programu
         push      bx                       ; £schova reloka‡n¡ adresy
         mov       ds,bx                    ; po‡ tek programu

; ------ v˜po‡et kontroln¡ho sou‡tu dat

         cld
         xor       si,si                    ; v˜choz¡ offset dat
PojXDel1:mov       di,1234h                 ; po‡et blok– 64 KB (i necel˜ch)
         xor       dx,dx                    ; v˜choz¡ hodnota CRC
PojXDel2:mov       cx,1234h                 ; d‚lka posledn¡ho bloku 0 a‘ 8000h
         dec       di                       ; ‡¡ta‡ blok– 64 KB
         jz        PojXSum2                 ; je posledn¡ blok
         js        PojXSum6                 ; konec dat
         mov       cx,8000h                 ; d‚lka 64 KB
PojXSum2:lodsw                              ; na‡ten¡ slova
         rol       dx,1                     ; rotace o 1 bit vlevo
         xor       dx,ax                    ; p©i‡ten¡ slova
         loop      PojXSum2                 ; dal¨¡ slovo
         add       bx,1000h                 ; zv˜¨en¡ segmentu adresy
         mov       ds,bx                    ; nov˜ segment adresy
         jmp       short PojXDel2

; ------ relokace programu

PojXSum6:pop       bx                       ; reloka‡n¡ adresa
         push      cs
         pop       ds                       ; DS <- CS
PojXRel1:mov       cx,1234h                 ; po‡et reloka‡n¡ch polo‘ek v tab.
PojXT1:  mov       si,offset(PojXRTab-PojXBeg) ; reloka‡n¡ tabulka
PojXRl2: push      ds
         lodsw                              ; offset relokovan‚ polo‘ky
         mov       di,ax                    ; offset polo‘ky
         lodsw                              ; segment relokovan‚ polo‘ky
         add       ax,bx                    ; segment adresy polo‘ky
         mov       ds,ax                    ; segment adresy polo‘ky
         add       ds:[di],bx               ; relokace polo‘ky
         pop       ds
         loop      PojXRl2                  ; relokace dal¨¡ polo‘ky

; ------ test v˜sledku kontroln¡ho sou‡tu

         or        dx,dx                    ; je kontroln¡ sou‡et OK ?
         jz        PojXS3                   ; kontroln¡ sou‡et je OK

; ------ chyba - program je po¨kozen

PojXS2:  mov       dx,offset(PojXTxt1-PojXBeg) ; text - program je po¨kozen
         mov       ah,9
         int       21h
         mov       ax,4cfeh
         int       21h                      ; n vrat programu s chybou

; ------ kontrola verze syst‚mu

PojXS3:  mov       ah,30h
         int       21h                      ; poskytnut¡ verze syst‚mu
         cmp       al,3                     ; je verze syst‚mu alespo¤ 3.00 ?
         jb        PojXS8                   ; je n¡zk  verze syst‚mu

; ------ p©¡prava k nalezen¡ cesty

         mov       cx,es:[2ch]              ; segment prost©ed¡
         jcxz      PojXS8                   ; nen¡ prost©ed¡
         push      ds                       ; £schova DS
         mov       ds,cx                    ; segment prost©ed¡
         xor       ax,ax                    ; hledan‚ slovo
         xor       si,si                    ; za‡ tek prost©ed¡

; ------ nalezen¡ textu jm‚na souboru

PojXS4:  inc       si                       ; zv˜¨en¡ ukazatele v prost©ed¡
         cmp       ds:[si-1],ax             ; konec prost©ed¡ ?
         jne       PojXS4                   ; nalezen¡ konce prost©ed¡
         add       si,3                     ; za‡ tek jm‚na souboru

; ------ otev©en¡ souboru

         mov       dx,si                    ; adresa textu jm‚na souboru
         mov       ah,3dh                   ; zde je AL = 0
         int       21h                      ; otev©en¡ souboru
         pop       ds                       ; n vrat DS
         jc        PojXS8                   ; chyba otev©en¡ souboru
         xchg      ax,bx                    ; identifik tor souboru

; ------ na‡ten¡ za‡ tku souboru

         mov       cx,18h                   ; d‚lka dat k na‡ten¡
         mov       ah,3fh
PojXT2:  mov       dx,offset(PojXRTab-PojXBeg) ; buffer k na‡ten¡ z hlav¡
         int       21h                      ; na‡ten¡ za‡ tku souboru

; ------ stanoven¡ velikosti souboru

         mov       ax,4202h
         xor       cx,cx
         xor       dx,dx
         int       21h                      ; vystaven¡ ukazatele na konec

; ------ kontrola velikosti souboru (LOW)

; !!!!!!!!
PojXDlk3:cmp       ax,offset(PojXEnd-PojXBeg) ; d‚lka programu celkem LOW
         jne       PojXS2                    ; velikost nesouhlas¡

; ------ uzav©en¡ souboru

         mov       ah,3eh
         int       21h                      ; uzav©en¡ souboru

; ------ kontrola startovac¡ adresy programu

PojXT3:  cmp       word ptr ds:[PojXRTab-PojXBeg+14h],offset(PojXStrt-PojXBeg) ; offset ?
         jne       PojXS2                   ; nesouhlas¡ offset skoku
; !!!!!!!!
PojXADR4:cmp       word ptr ds:[PojXRTab-PojXBeg+16h],1234h ; souhlas¡ segment ?
         jne       PojXS2                   ; nesouhlas¡

; ------ n vrat registr–

PojXS8:  popf
         pop       ds
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax

; ------ skok do programu

PojXT5:  mov       ss,cs:[PojXSS-PojXBeg]
PojXT6:  mov       sp,cs:[PojXSP-PojXBeg]
PojXT7:  jmp       dword ptr cs:[PojXIP-PojXBeg]

PojXTxt1 db        13,'Program POSKOZEN !',13,7,10,'$'

         EVEN
PojXRTab label     word                     ; tabulka adres relok. polo‘ek
                                            ; - na‡¡t  se sem t‚‘ z hlav¡ EXE !

PojXRCS  dd        offset(PojXCS-PojXBeg)   ; relokace registru CS
PojXRSS  dd        offset(PojXSS-PojXBeg)   ; relokace registru SS

PojXCRC  dw        0                        ; kontroln¡ sou‡et programu+pojistka
                                            ; - posouv  se za konec tabulky rel.
                                            ;   polo‘ek !!!

                                            ; (n sleduje z sobn¡k)

PojXEnd  label     byte                     ; konec pojistky (+tabulka rek.pol.)

; *****************************************************************************
;
;                      Pojistka EXE s pokra‡ov n¡m
;                        (mus¡ m¡t sudou d‚lku !)
;
; *****************************************************************************
;þ
         EVEN

CojXBeg  label     byte                     ; za‡ tek Pojistky (offset = 0)

CojXIdn  db        'PJ'                     ; identifik tor Pojistky

; !!!!!!!!
CojXSP   dw        0                        ; p–vodn¡ SP programu
CojXSS   dw        0                        ; p–vodn¡ SS programu
CojXIP   dw        0                        ; p–vodn¡ IP programu Ä¿ po©ad¡ !!!
CojXCS   dw        0                        ; p–vodn¡ CS programu ÄÙ zachovat !
CojXRel  dw        0                        ; po‡et reloka‡n¡ch polo‘ek
CojXRelT dw        offset(CojXCRC-CojXBeg)  ; offset reloka‡n¡ tabulky
CojXMin  dw        0                        ; minim ln¡ pamˆŸ za programem

; ------ £schova registr–

CojXStrt:push      ax                       ;Ä¿tyto 4 instrukce = identifikace !
         push      bx                       ; ³
         push      cx                       ; ³
         push      dx                       ;ÄÙ
         push      si
         push      di
         push      ds
         pushf

; ------ inicializace segmentu po‡ tku programu

         mov       bx,ds                    ; segment PSP
         add       bx,10h                   ; segment za‡ tku programu
         push      bx                       ; £schova reloka‡n¡ adresy
         mov       ds,bx                    ; po‡ tek programu

; ------ v˜po‡et kontroln¡ho sou‡tu dat

         cld
         xor       si,si                    ; v˜choz¡ offset dat
CojXDel1:mov       di,1234h                 ; po‡et blok– 64 KB (i necel˜ch)
         xor       dx,dx                    ; v˜choz¡ hodnota CRC
CojXDel2:mov       cx,1234h                 ; d‚lka posledn¡ho bloku 0 a‘ 8000h
         dec       di                       ; ‡¡ta‡ blok– 64 KB
         jz        CojXSum2                 ; je posledn¡ blok
         js        CojXSum6                 ; konec dat
         mov       cx,8000h                 ; d‚lka 64 KB
CojXSum2:lodsw                              ; na‡ten¡ slova
         rol       dx,1                     ; rotace o 1 bit vlevo
         xor       dx,ax                    ; p©i‡ten¡ slova
         loop      CojXSum2                 ; dal¨¡ slovo
         add       bx,1000h                 ; zv˜¨en¡ segmentu adresy
         mov       ds,bx                    ; nov˜ segment adresy
         jmp       short CojXDel2

; ------ relokace programu

CojXSum6:pop       bx                       ; reloka‡n¡ adresa
         push      cs
         pop       ds                       ; DS <- CS
CojXRel1:mov       cx,1234h                 ; po‡et reloka‡n¡ch polo‘ek v tab.
CojXT1:  mov       si,offset(CojXRTab-CojXBeg) ; reloka‡n¡ tabulka
CojXRl2: push      ds
         lodsw                              ; offset relokovan‚ polo‘ky
         mov       di,ax                    ; offset polo‘ky
         lodsw                              ; segment relokovan‚ polo‘ky
         add       ax,bx                    ; segment adresy polo‘ky
         mov       ds,ax                    ; segment adresy polo‘ky
         add       ds:[di],bx               ; relokace polo‘ky
         pop       ds
         loop      CojXRl2                  ; relokace dal¨¡ polo‘ky

; ------ test v˜sledku kontroln¡ho sou‡tu

         or        dx,dx                    ; je kontroln¡ sou‡et OK ?
         jz        CojXS3                   ; kontroln¡ sou‡et je OK

; ------ chyba - program je po¨kozen

CojXS2:  mov       dx,offset(CojXTxt1-CojXBeg) ; text - program je po¨kozen
         mov       ah,9
         int       21h
         mov       ax,0c08h
         int       21h                      ; vstup znaku z kl vesnice
         cmp       al,13                    ; je ENTER ?
         je        CojXS8                   ; je ENTER - pokra‡ov n¡
         mov       ax,4cfeh
         int       21h                      ; n vrat programu s chybou

; ------ kontrola verze syst‚mu

CojXS3:  mov       ah,30h
         int       21h                      ; poskytnut¡ verze syst‚mu
         cmp       al,3                     ; je verze syst‚mu alespo¤ 3.00 ?
         jb        CojXS8                   ; je n¡zk  verze syst‚mu

; ------ p©¡prava k nalezen¡ cesty

         mov       cx,es:[2ch]              ; segment prost©ed¡
         jcxz      CojXS8                   ; nen¡ prost©ed¡
         push      ds                       ; £schova DS
         mov       ds,cx                    ; segment prost©ed¡
         xor       ax,ax                    ; hledan‚ slovo
         xor       si,si                    ; za‡ tek prost©ed¡

; ------ nalezen¡ textu jm‚na souboru

CojXS4:  inc       si                       ; zv˜¨en¡ ukazatele v prost©ed¡
         cmp       ds:[si-1],ax             ; konec prost©ed¡ ?
         jne       CojXS4                   ; nalezen¡ konce prost©ed¡
         add       si,3                     ; za‡ tek jm‚na souboru

; ------ otev©en¡ souboru

         mov       dx,si                    ; adresa textu jm‚na souboru
         mov       ah,3dh                   ; zde je AL = 0
         int       21h                      ; otev©en¡ souboru
         pop       ds                       ; n vrat DS
         jc        CojXS8                   ; chyba otev©en¡ souboru
         xchg      ax,bx                    ; identifik tor souboru

; ------ na‡ten¡ za‡ tku souboru

         mov       cx,18h                   ; d‚lka dat k na‡ten¡
         mov       ah,3fh
CojXT2:  mov       dx,offset(CojXRTab-CojXBeg) ; buffer k na‡ten¡ z hlav¡
         int       21h                      ; na‡ten¡ za‡ tku souboru

; ------ stanoven¡ velikosti souboru

         mov       ax,4202h
         xor       cx,cx
         xor       dx,dx
         int       21h                      ; vystaven¡ ukazatele na konec

; ------ kontrola velikosti souboru (LOW)

; !!!!!!!!
CojXDlk3:cmp       ax,offset(CojXEnd-CojXBeg) ; d‚lka programu celkem LOW
         jne       CojXS2                    ; velikost nesouhlas¡

; ------ uzav©en¡ souboru

         mov       ah,3eh
         int       21h                      ; uzav©en¡ souboru

; ------ kontrola startovac¡ adresy programu

CojXT3:  cmp       word ptr ds:[CojXRTab-CojXBeg+14h],offset(CojXStrt-CojXBeg) ; offset ?
         jne       CojXS2                   ; nesouhlas¡ offset skoku
; !!!!!!!!
CojXADR4:cmp       word ptr ds:[CojXRTab-CojXBeg+16h],1234h ; souhlas¡ segment ?
         jne       CojXS2                   ; nesouhlas¡

; ------ n vrat registr–

CojXS8:  popf
         pop       ds
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax

; ------ skok do programu

CojXT5:  mov       ss,cs:[CojXSS-CojXBeg]
CojXT6:  mov       sp,cs:[CojXSP-CojXBeg]
CojXT7:  jmp       dword ptr cs:[CojXIP-CojXBeg]

CojXTxt1 db        13,'Program POSKOZEN !',13,10
         db           'Pokracovat (ENTER=ANO) ?',13,7,10,'$'

         EVEN
CojXRTab label     word                     ; tabulka adres relok. polo‘ek
                                            ; - na‡¡t  se sem t‚‘ z hlav¡ EXE !

CojXRCS  dd        offset(CojXCS-CojXBeg)   ; relokace registru CS
CojXRSS  dd        offset(CojXSS-CojXBeg)   ; relokace registru SS

CojXCRC  dw        0                        ; kontroln¡ sou‡et programu+Pojistka
                                            ; - posouv  se za konec tabulky rel.
                                            ;   polo‘ek !!!

                                            ; (n sleduje z sobn¡k)

CojXEnd  label     byte                     ; konec Cojistky (+tabulka rek.pol.)

         dw        200h dup(?)              ; z sobn¡k
Zasob    label     word

Code     ENDS
         END       Start
