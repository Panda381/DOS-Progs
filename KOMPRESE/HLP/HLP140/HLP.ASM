
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°
;
;                      Prohl¡‘e‡ soubor– n povˆdy
;
; °°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°°

BUFFSIZE EQU       2000h                    ; velikost vstupn¡ho bufferu
MAXFND   EQU       15                       ; maxim ln¡ d‚lka textu k hled n¡

; ------ definice k¢dovac¡ tabulky

MAXLENX  EQU       25                       ; min. d‚lka dlouh‚ho ©etˆzce
MAXLEN   EQU       MAXLENX+254              ; maxim ln¡ d‚lka ©etˆzce

SUBSTLEN EQU       7                        ; d‚lka nahrazen  dlouh˜m k¢dem


Code     SEGMENT
         ASSUME    cs:Code,ds:Code
         ORG       100h

; ------ zmen¨en¡ bloku programu na minimum

Start:   mov       dx,offset MemTxt         ; chyba - nedostatek pamˆti
         cmp       sp,offset Zasob          ; je pamˆŸ OK ?
         jb        Start3                   ; chyba pamˆti
         mov       sp,offset Zasob          ; p©edefinov n¡ ukazatele z sobn¡ku
         mov       bx,offset(ViewEnd-Start+10fh)/16 ; konec programu
         mov       ah,4ah
         int       21h                      ; zmen¨en¡ velikosti bloku programu
         jc        Start3                   ; nepravdˆpodobn  chyba

; ------ vytvo©en¡ vstupn¡ho bufferu

         mov       bx,BUFFSIZE/16           ; velikost vstupn¡ho bufferu
         mov       ah,48h
         int       21h                      ; vytvo©en¡ vstupn¡ho bufferu
         jc        Start3                   ; chyba - nedostatek pamˆti
         mov       ds:[InpSegm],ax          ; adresa vstupn¡ho bufferu

; ------ vytvo©en¡ datov‚ho bufferu

         mov       ah,48h
         mov       bx,0ffffh                ; velikost buffer–
         int       21h                      ; vytvo©en¡ datov‚ho bufferu
         mov       ah,48h
         int       21h                      ; vytvo©en¡ men¨¡ho bufferu
         jc        Start3                   ; nedostatek pamˆti

; ------ inicializace ukazatel– buffer–

         cmp       bx,8                     ; minim ln¡ velikost pamˆti
         jb        Start3                   ; chyba - nedostatek pamˆti
         mov       ds:[DatSegm],ax          ; adresa datov‚ho segmentu
         mov       word ptr ds:[TopADr+2],ax ; adresa po‡ te‡n¡ho © dku
         mov       ds:[DatSize],bx          ; velikost datov‚ho bufferu
         mov       dx,bx                    ; velikost datov‚ho bufferu
         and       dx,0f000h                ; maxim ln¡ segment
         add       dx,ax                    ; maxim ln¡ segment
         mov       cl,4                     ; po‡et rotac¡
         shl       bx,cl                    ; maxim ln¡ offset
         mov       ds:[MaxOffs],bx          ; maxim ln¡ offset
         mov       ds:[MaxSegm],dx          ; maxim ln¡ segment

; ------ inicializace konfigura‡n¡ho souboru

         call      InitCNF                  ; inicializace konfigurace

; ------ rozbor p©¡kazov‚ho © dku

         call      Rozbor                   ; rozbor p©¡kazov‚ho © dku
         jnc       Start4                   ; zad n¡ OK

; ------ chyba zad n¡

Start2:  mov       dx,offset HelpTxt        ; text n povˆdy
Start3:  push      cs
         pop       ds
         push      dx
         mov       dx,offset UvTxt
         mov       ah,9
         int       21h
         pop       dx
         mov       ah,9
         int       21h                      ; zobrazen¡ n povˆdy
         int       20h                      ; konec textu

; ------ otev©en¡ vstupn¡ho souboru

Start4:  mov       dx,offset Soubor1        ; jm‚no vstupn¡ho souboru
         mov       ax,3d00h
         int       21h                      ; otev©en¡ souboru pro ‡ten¡
Start62: mov       dx,offset SoubTxt
         jc        Start3                   ; chyba - soubor nenalezen
         mov       ds:[SoubIdnt],ax         ; identifik tor souboru

; ------ £schova data a ‡asu souboru

         mov       bx,ax                    ; identifik tor souboru
         mov       ax,5700h
         int       21h                      ; poskytnut¡ data a ‡asu souboru
         mov       ds:[FileDate],dx         ; datum souboru
         mov       ds:[FileTime],cx         ; ‡as souboru

; ------ £schova atribut– souboru

         mov       ax,4300h
         mov       dx,offset Soubor1        ; jm‚no souboru
         int       21h                      ; poskytnut¡ atribut– souboru
         mov       ds:[FileAtr],cx          ; atributy souboru

; ------ test, zda je zkomprimovan˜ soubor

         mov       es,ds:[DatSegm]          ; datov˜ segment
         xor       di,di                    ; ukl dac¡ adresa do bufferu
         mov       ds,ds:[InpSegm]          ; segment vstupn¡ho bufferu
         xor       si,si                    ; ‡tec¡ adresa z bufferu
         cld                                ; smˆr nahoru

         call      ReadByte                 ; na‡ten¡ bajtu
         cmp       dh,27                    ; prvn¡ identifika‡n¡ bajt
         jne       Start7                   ; nen¡ komprimovan˜ soubor

         call      ReadByte                 ; na‡ten¡ bajtu
         cmp       dh,"X"                   ; druh˜ identifika‡n¡ bajt
         jne       Start7                   ; nen¡ komprimovan˜ soubor

         call      ReadByte                 ; na‡ten¡ bajtu
         cmp       dh,"C"                   ; t©et¡ identifika‡n¡ bajt
         jne       Start7                   ; nen¡ komprimovan˜ soubor

         call      ReadByte                 ; na‡ten¡ bajtu
         cmp       dh,"D"                   ; ‡tvrt˜ identifika‡n¡ bajt
         je        Start8                   ; je komprimovan˜ soubor OK

; ------ nen¡ komprimace - na‡ten¡ souboru

Start7:  ;or        byte ptr cs:[Prepin],3   ; je k¢d IBM
         xor       si,si                    ; za‡ tek dat v souboru
Start71: call      ReadByte                 ; na‡ten¡ bajtu
         jz        Start9                   ; konec souboru
         call      WritByte                 ; ulo‘en¡ do bufferu
         jnz       Start71                  ; dal¨¡ bajt
         jmp       short Start86            ; soubor nen¡ na‡ten cel˜

; ------ dekomprese dat

Start8:  call      ReadByte
         jz        Start9
         mov       byte ptr cs:[Prepin],dh
         call      ReadByte
         jz        Start9
         mov       byte ptr cs:[Prepin+1],dh

         call      ZadHesl                  ; p©¡padn‚ zad n¡ hesla
         jnc       Start82
         int       20h                      ; p©eru¨en¡ programu

Start82: call      DeKomp                   ; dekomprese dat

; ------ test, zda je ji‘ konec souboru

Start88: call      ReadByte                 ; test, zda je dal¨¡ bajt
         jz        Start9                   ; je konec souboru - OK

; ------ soubor nebyl na‡ten cel˜

Start86: push      cs
         pop       ds
         mov       dx,offset CelyTxt
         mov       ah,9
         int       21h                      ; zobrazen¡ chybov‚ho hl ¨en¡
         mov       ax,0c01h
         int       21h                      ; ‡ek n¡ na zad n¡ kl vesy

; ------ nastaven¡ ukazatele konce dat v bufferu

Start9:  push      cs
         pop       ds
         mov       word ptr ds:[MaxOffs],di ; maxim ln¡ offset v bufferu
         mov       word ptr ds:[MaxSegm],es ; maxim ln¡ segment v bufferu

; ------ test, zda byl v˜stupn¡ soubor zad n

         test      byte ptr ds:[Param],8    ; byl v˜stupn¡ soubor zad n ?
         jnz       View201                  ; v˜stupn¡ soubor byl zad n

; ------ odstranˆn¡ koncov‚ho EOF souboru

         or        di,di                    ; jsou nˆjak  data ?
         jz        View2                    ; je po‡ tek segmentu
         dec       di                       ; adresa posledn¡ho bajtu
         cmp       byte ptr es:[di],26      ; je EOF ?
         jne       View2                    ; nen¡ EOF
         mov       word ptr ds:[MaxOffs],di ; odstranˆn¡ EOF z bufferu
View2:   jmp       View21

; ------ test, zda v˜stupn¡ soubor ji‘ existuje

View201: mov       dx,offset Soubor2        ; jm‚no v˜stupn¡ho souboru
         mov       ax,3d02h
         int       21h                      ; test, zda soubor existuje
         jc        View204                  ; neexistuje
         mov       si,dx                    ; jm‚no souboru

; ------ v˜stupn¡ soubor existuje

         mov       bx,ax                    ; identifik tor souboru
         mov       ah,3eh
         int       21h                      ; uzav©en¡ souboru

         mov       dx,offset Exis1Txt
         mov       ah,9
         int       21h                      ; text "Vystupni soubor"

View2031:cld
         lodsb
         cmp       al,0
         je        View2032
         mov       dl,al
         mov       ah,2
         int       21h
         jmp       short View2031

View2032:mov       dx,offset Exis2Txt
         mov       ah,9
         int       21h

         mov       ah,1
         int       21h

         call      UpCase                   ; konverze na velk‚ p¡smeno

         push      ax
         mov       dx,offset CRTxt
         mov       ah,9
         int       21h
         pop       ax

         cmp       al,"A"
         je        View204

         int       20h

; ------ otev©en¡ v˜stupn¡ho souboru

View204: mov       dx,offset Soubor2        ; jm‚no souboru
         mov       ah,3ch
         xor       cx,cx
         int       21h                      ; vytvo©en¡ v˜stupn¡ho souboru
         jnc       View206                  ; operace OK

         mov       dx,offset VystTxt
View205: jmp       Start3                   ; chyba

; ------ z pis souboru na disk

View206: mov       bx,ax                    ; identifik tor souboru

         mov       si,ds:[MaxSegm]          ; maxim ln¡ segment
         sub       si,ds:[DatSegm]          ; velikost v segmentech
         xor       di,di
         shl       si,1
         rcl       di,1
         shl       si,1
         rcl       di,1
         shl       si,1
         rcl       di,1
         shl       si,1
         rcl       di,1
         add       si,ds:[MaxOffs]
         adc       di,0                     ; DI:SI=‡¡ta‡ dat k z pisu

         mov       bp,ds:[DatSegm]

View207: mov       cx,0f000h
         or        di,di
         jnz       View208
         cmp       cx,si
         jbe       View208
         mov       cx,si                    ; omezen¡ velikosti

View208: xor       dx,dx
         mov       ah,40h
         mov       ds,bp
         int       21h                      ; z pis bloku na disk
         mov       dx,offset WritTxt        ; text - chyba z pisu
         jc        View205                  ; chyba z pisu
         cmp       ax,cx
         jne       View205                  ; chyba - disk pln˜

         add       bp,0f00h
         sub       si,0f000h
         sbb       di,0
         jnc       View207

         push      cs
         pop       ds

; ------ nastaven¡ data a ‡asu souboru

         mov       ax,5701h
         mov       dx,ds:[FileDate]         ; datum souboru
         mov       cx,ds:[FileTime]         ; ‡as souboru
         int       21h                      ; nastaven¡ data a ‡asu souboru

; ------ uzav©en¡ souboru

         mov       ah,3eh
         int       21H

; ------ nastaven¡ atribut– souboru

         mov       dx,offset Soubor2        ; jm‚no souboru
         mov       cx,ds:[FileAtr]          ; p–vodn¡ atributy souboru
         mov       ax,4301h
         int       21h                      ; nastaven¡ atribut– souboru

         int       20h




; ------ inicializace videom¢du

View21:  mov       ah,0fh
         int       10h
         cmp       al,7
         je        View22
         mov       word ptr ds:[AdrVRAM+3],0b8h
         mov       byte ptr ds:[Barva],1bh  ; barva - barevn˜ videom¢d
         mov       byte ptr ds:[Barva1],30h ; barva nadpisu
         mov       byte ptr ds:[Barva2],30h ; barva nadpisu
         cmp       al,2
         je        View23
         cmp       al,3
         je        View23
         mov       al,3
View22:  mov       ah,0
         int       10h                      ; inicializace videom¢du

; ------ stanoven¡ parametr– videom¢du

View23:  mov       ah,12h
         mov       bx,0c310h
         int       10h                      ; test, zda je karta EGA/VGA

         push      ds
         xor       ax,ax
         mov       ds,ax

         cmp       bh,2
         ja        View25                   ; nen¡ EGA/VGA
         cmp       bl,5
         ja        View25
         mov       bl,ds:[484h]             ; posledn¡ © dek na displeji
         cmp       bl,15                    ; je je¨tˆ rozumn‚ ‡¡slo © dku ?
         jb        View25                   ; neplatn˜ po‡et © dk–
         mov       cs:[MaxRow],bl           ; posledn¡ © dek na displeji
         inc       bx
         mov       cs:[Radku],bl            ; po‡et © dk– displeje
View25:  mov       ax,ds:[44eh]             ; adresa videostr nky
         pop       ds
         mov       word ptr ds:[AdrVRAM],ax ; adresa videopamˆti

; ------ £schova ‡¡sla aktivn¡ str nky

         mov       ah,0fh
         int       10h                      ; poskytnut¡ videostr nk
         mov       ds:[AktPage],bh          ; aktivn¡ videostr nka

; ------ definice INT 23h a INT 24h

         mov       dx,offset INT23
         mov       ax,2523h
         int       21h
         mov       dx,offset INT24
         mov       ax,2524h
         int       21h

; ------ zmen¨en¡ velikosti datov‚ho bloku

         mov       cl,4
         mov       es,ds:[DatSegm]          ; datov˜ segment
         mov       ax,ds:[MaxOffs]          ; maxim ln¡ offset
         add       ax,0fh
         shr       ax,cl                    ; p©epo‡et na odstavce
         mov       bx,ds:[MaxSegm]          ; maxim ln¡ segment
         sub       bx,ds:[DatSegm]          ; po‡et blok– 64 KB
         add       bx,ax                    ; velikost datov‚ho bloku
         mov       ah,4ah
         int       21h                      ; zmen¨en¡ velikosti dat. bloku

; ------ vytvo©en¡ datov‚ho bloku displeje

         mov       al,160
         mul       byte ptr ds:[Radku]      ; po‡et © dk– na displej
         add       ax,0fh
         shr       ax,cl                    ; velikost bloku v odstavc¡ch
         jnz       View27
         mov       ax,0ffffh
View27:  mov       bx,ax
         mov       ah,48h
         int       21h                      ; vytvo©en¡ datov‚ho bloku
         mov       word ptr ds:[InpNum],0   ; p©ednastaven¡ - nic nen¡
         jnc       View28
         mov       ah,48h
         int       21h                      ; druh˜ pokus
         jc        View29                   ; ani tak se nepovedlo
View28:  mov       ds:[InpSegm],ax          ; adresa segmentu
         shl       bx,1
         shl       bx,1
         shl       bx,1                     ; velikost ve slovech
         mov       cx,bx
         mov       word ptr ds:[InpNum],bx  ; velikost ve slovech

; ------ £schova obsahu obrazovky

         push      ds
         mov       es,ax                    ; adresa segmentu
         xor       di,di                    ; za‡ tek bufferu
         lds       si,ds:[AdrVRAM]          ; adresa videopamˆti
         cld
         rep       movsw                    ; £schova obsahu obrazovky
         pop       ds

; ------ £schova pozice kurzoru

View29:  mov       ah,3
         call      Int10P                   ; ‡ten¡ pozice kurzoru
         mov       ds:[OldKurz],dx          ; £schova pozice kurzoru

; ------ inicializace konverzn¡ch tabulek

         call      InitTab                  ; prvn¡ inicializace tabulek

; ------ zobrazen¡ nadpisu

View30:

; ------ zobrazen¡ aktivn¡ str nky

View3:
         call      DispUkaz                 ; zobrazen¡ ukazatele polohy v soub.

         call      DispNadp                 ; zobrazen¡ nadpisu

         call      Disp                     ; zobrazen¡ aktivn¡ str nky
         call      KurzOff                  ; vypnut¡ kurzoru

; ------ obsluha kl vesnice

View32:  mov       ah,0
         int       16h                      ; ‡ek n¡ na stisk kl vesy
         cmp       al,0
         je        View33
         mov       ah,0
View33:  or        ax,ax
         jz        INT23                    ; p©eru¨en¡ Ctrl-Break
         cmp       al,27
         je        INT23                    ; kone ESC

; ------ skok na funkci

         mov       di,offset TabKey
         mov       cx,offset(TabKey0-TabKey)/4
View4:   cmp       ax,ds:[di]
         je        View5
         add       di,4
         loop      View4
         jmp       short View32

View5:   mov       si,word ptr ds:[TopADr]  ; adresa str nky - offset
         mov       dx,word ptr ds:[TopAdr+2] ; adresa str nky - segment
         mov       bp,ds:[TopLine]          ; ‡¡slo po‡ te‡n¡ho © dku str nky
         mov       bx,ds:[RadkuTxt]         ; po‡et © dk– celkem
         sub       bx,1                     ; ‡¡slo posledn¡ho © dku
;         sub       bx,ds:[DispRow]          ; ‡¡slo posledn¡ho © dku
         jnc       View53
         xor       bx,bx                    ; ‡¡slo posledn¡ho © dku
View53:  call      word ptr ds:[di+2]       ; obsluha funkce kl vesy
         mov       word ptr ds:[TopAdr+2],dx ; adresa str nky - segment
         mov       word ptr ds:[TopAdr],si  ; adresa str nky - offset
         mov       ds:[TopLine],bp          ; nov‚ ‡¡slo prvn¡ho © dku str nky
         jmp       short View3

; ------ vymaz n¡ videostr nky

INT23:   push      cs
         pop       ds
         mov       al,80                    ; po‡et znak– na © dek
         mul       byte ptr ds:[Radku]      ; po‡et znak– na videostr nku
         mov       cx,ax                    ; po‡et znak– na videostr nku
         les       di,ds:[AdrVRAM]          ; adresa videopamˆti
         mov       ax,720h                  ; mazac¡ slovo
         cld
         rep       stosw                    ; vymaz n¡ obrazovky

; ------ navr cen¡ obsahu obrazovky

         push      ds
         mov       di,word ptr ds:[AdrVRAM] ; adresa videopamˆti
         mov       cx,ds:[InpNum]           ; po‡et slov v bufferu
         mov       ds,ds:[InpSegm]          ; adresa segmentu
         cld
         xor       si,si                    ; za‡ tek bufferu
         rep       movsw                    ; £schova obsahu obrazovky
         pop       ds

; ------ nastaven¡ pozice kurzoru

         mov       dx,ds:[OldKurz]          ; p–vodn¡ pozice kurzoru
         mov       ah,2
         call      Int10P                   ; nastaven¡ pozice kurzoru

         call      StorCNF                  ; £schova konfigurace

         int       20h

INT24    PROC      FAR

         mov       al,0
         iret

INT24    ENDP

; -----------------------------------------------------------------------------
;        inicializace konfigurace
; -----------------------------------------------------------------------------
;þ
InitCNF  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      es

; ------ kontrola verze syst‚mu

         push      ds
         pop       es
         mov       ah,30h
         int       21h                      ; poskytnut¡ verze syst‚mu
         mov       di,offset HLPBuff        ; buffer jm‚na souboru
         cmp       al,3
         jb        InitCNF6                 ; je n¡zk  verze syst‚mu

; ------ adresa prost©ed¡

         push      ds
         mov       cx,ds:[2ch]              ; segment prost©ed¡
         jcxz      InitCNF5                 ; neplatn‚ prost©ed¡
         mov       ds,cx                    ; segment prost©ed¡

; ------ nalezen¡ textu cesty

         xor       si,si                    ; za‡ tek prost©ed¡
         mov       cx,7fffh                 ; max. d‚lka prost©ed¡
         xor       ax,ax                    ; hledan‚ slovo
InitCNF2:inc       si                       ; zv˜¨en¡ ukazatele v prost©ed¡
         cmp       word ptr ds:[si-1],ax    ; je konec prost©ed¡ ?
         loopne    InitCNF2                 ; dal¨¡ bajt prost©ed¡

; ------ p©enesen¡ textu cesty

         add       si,3                     ; za‡ tek textu cesty
         cld
InitCNF3:mov       dx,di                    ; £schova adresy k ulo‘en¡ jm‚na
InitCNF4:lodsb                              ; na‡ten¡ znaku
         stosb                              ; ulo‘en¡ znaku
         cmp       al,"\"                   ; byl to oddˆlova‡ cesty ?
         je        InitCNF3                 ; byl oddˆlova‡ cesty - £schova
         cmp       al,0                     ; konec textu ?
         jne       InitCNF4                 ; dal¨¡ znak
         mov       di,dx                    ; adresa k ulo‘en¡ jm‚na souboru
InitCNF5:pop       ds

; ------ p©id n¡ jm‚na souboru

InitCNF6:mov       si,offset HLPName        ; jm‚no souboru
         mov       cx,8                     ; d‚lka jm‚na souboru
         cld
         rep       movsb                    ; p©id n¡ jm‚na souboru

; ------ otev©en¡ souboru

         mov       dx,offset HLPBuff        ; jm‚no souboru
         mov       ax,3d00h
         int       21h                      ; otev©en¡ pro ‡ten¡
         jc        InitCNF7                 ; soubor nen¡
         mov       bx,ax                    ; identifik tor souboru

; ------ na‡ten¡ konfigura‡n¡ho souboru

         mov       dx,offset HLPCnf0        ; konfigura‡n¡ buffer
         mov       cx,offset(HLPCnf0-HLPCnf) ; d‚lka konfigurace
         mov       ah,3fh
         int       21h                      ; na‡ten¡ souboru do bufferu

; ------ uzav©en¡ souboru

         mov       ah,3eh
         int       21h                      ; uzav©en¡ souboru

; ------ kontrola, zda je to platn˜ soubor

InitCNF7:mov       si,offset HLPCnf         ; vzorek
         mov       di,offset HLPCnf0        ; na‡ten˜ soubor
         mov       cx,7                     ; d‚lka z hlav¡
         cld
         repe      cmpsb                    ; porovn n¡ z hlav¡ souboru

; ------ £schova souboru nebo plat¡ star˜ soubor

         mov       si,offset HLPCnf         ; star  konfigurace
         mov       di,offset HLPCnf0        ; nov  konfigurace
         mov       cx,offset(HLPCnf0-HLPCnf) ; d‚lka konfigurace
         jne       InitCNF8                 ; soubor nen¡ platn˜
         xchg      si,di                    ; jinak se uschov  nov  konfigurace
InitCNF8:rep       movsb                    ; zdvojen¡ konfigurace


; ------ n vrat registr–

         pop       es
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

InitCNF  ENDP

; -----------------------------------------------------------------------------
;        ulo‘en¡ konfigurace
; -----------------------------------------------------------------------------

StorCNF  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      es

; ------ kontrola, zda byla konfigurace zmˆnˆna

         push      ds
         pop       es
         mov       si,offset HLPCnf         ; sou‡asn  konfigurace
         mov       di,offset HLPCnf0        ; star  konfigurace
         mov       cx,offset(HLPCnf0-HLPCnf) ; d‚lka konfigurace
         cld
         repe      cmpsb                    ; porovn n¡ konfigurac¡
         je        StorCNF9                 ; nebyla zmˆnˆna

; ------ vytvo©en¡ konfigura‡n¡ho souboru

         mov       dx,offset HLPBuff        ; buffer jm‚na souboru
         xor       cx,cx
         mov       ah,3ch
         int       21h                      ; vytvo©en¡ souboru
         jc        StorCNF9                 ; soubor nelze vytvo©it
         mov       bx,ax                    ; identifik tor souboru

; ------ z pis konfigurace do souboru

         mov       cx,offset(HLPCnf0-HLPCnf) ; d‚lka konfigurace
         mov       dx,offset HLPCnf         ; sou‡asn  konfigurace
         mov       ah,40h
         int       21h                      ; ulo‘en¡ konfigurace

; ------ uzav©en¡ souboru

         mov       ah,3eh
         int       21h                      ; uzav©en¡ souboru

; ------ n vrat registr–

StorCNF9:pop       es
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

StorCNF  ENDP

; *****************************************************************************
;
;                        Ovl d n¡ programu
;
; *****************************************************************************

TabKey   dw        5000h,Dolu               ; kurzor dol–
         dw        4800h,Nahoru             ; kurzor nahoru
         dw        5100h,PgDn               ; str nka dol–
         dw        4900h,PgUp               ; str nka nahoru
         dw        4f00h,EndK               ; konec textu
         dw        4700h,Home               ; za‡ tek textu
         dw        0020h,EnterK             ; MEZERA filtr
         dw        000dh,Hledej             ; ENTER hled n¡
         dw        000ch,HledC              ; ^L hled n¡ d le
         dw        3c00h,FKam               ; F2 filtr Kamenick˜ch
         dw        3d00h,FLat               ; F3 filtr Latin 2
         dw        3e00h,FKOI               ; F4 filtr KOI 8
         dw        3f00h,Blok               ; F5 z pis bloku na disk
         dw        4000h,Tisk               ; F6 tisk bloku na tisk rnu
         dw        3b00h,Napov              ; F1 n povˆda
         dw        4b00h,Vlevo              ; kurzor vlevo
         dw        4d00h,Vpravo             ; kurzor vpravo
         dw        7300h,CVlevo             ; ^kurzor vlevo
         dw        7400h,CVpravo            ; ^kurzor vpravo

TabKey0  label     word

; -----------------------------------------------------------------------------
;        z pis bloku na disk, tisk na tisk rnu
; -----------------------------------------------------------------------------
;þ
Tisk:    or        byte ptr ds:[Param],4    ; p©¡znak tisku
         jmp       short Blok101


Blok     PROC      NEAR

; ------ £schova registr–

;         push      si                       ; adresa str nky - offset
;         push      dx                       ; adresa str nky - segment
;         push      bp                       ; ‡¡slo prvn¡ho © dku str nky
         and       byte ptr ds:[Param],not 4 ; zru¨en¡ p©¡znaku tisku

Blok101: push      di
         push      word ptr ds:[FindAdr]
         push      word ptr ds:[FindAdr+2]
         push      word ptr ds:[FindLine]

; ------ nastaven¡ kurzoru asi tak do st©edu obrazovky

         mov       cx,ds:[DispRow]
         shr       cx,1                     ;
Blok1:   call      NextLine                 ; nalezen¡ dal¨¡ho © dku
         loop      BLok1

; ------ ozna‡en¡ za‡ tku bloku

         mov       di,offset ZacTxt         ; v˜zva k ozna‡en¡ za‡ tku bloku
         call      BlokF                    ; obsluha kurzoru bloku
         jc        Blok9                    ; p©eru¨en¡ operace

; ------ £schova parametr– za‡ tku bloku

         mov       word ptr ds:[Blok1Adr],si
         mov       word ptr ds:[Blok1Adr+2],dx
         mov       ds:[Blok1Lin],bp
         or        byte ptr ds:[Param],20h  ; p©¡znak zobrazen¡ bloku

; ------ ozna‡en¡ konce bloku

         mov       di,offset KonTxt         ; v˜zva k ozna‡en¡ konce bloku
         call      BlokF                    ; obsluha kurzoru bloku
         jc        Blok9                    ; p©eru¨en¡ operace

; ------ ulo‘en¡ bloku na disk

         call      UlozBlk                  ; ulo‘en¡ bloku na disk

; ------ n vrat registr–

Blok9:   and       byte ptr ds:[Param],not 20h ; zru¨en¡ p©¡znaku zobraz. bloku
         pop       word ptr ds:[FindLine]
         pop       word ptr ds:[FindAdr+2]
         pop       word ptr ds:[FindAdr]
         pop       di
         mov       si,word ptr ds:[TopAdr]
         mov       dx,word ptr ds:[TopAdr+2]
         mov       bp,ds:[TopLine]
;         pop       bp
;         pop       dx
;         pop       si
         ret

Blok     ENDP

; -----------------------------------------------------------------------------
;        obsluha kurzoru bloku (DI=text k zobrazen¡, CY=p©eru¨en¡ ESC)
; -----------------------------------------------------------------------------

BlokF    PROC      NEAR

         call      DispHlas                 ; zobrazen¡ hl ¨en¡

; ------ ‡ek n¡ na stisk kl vesy

BlokF1:  mov       word ptr ds:[Blok2Adr],si ; ozna‡en¡ konce bloku
         mov       word ptr ds:[Blok2Adr+2],dx
         mov       ds:[Blok2Lin],bp
         call      HledNrm0                 ; ulo‘en¡ a normalizace parametr–
         call      Disp                     ; zobrazen¡ aktivn¡ str nky
         call      KurzOff                  ; vypnut¡ kurzoru
         mov       ah,0
         int       16h

; ------ p©eru¨en¡ operace ESC, Ctrl-BREAK

         or        ax,ax
         jz        BlokF2                   ; p©eru¨en¡ Ctrl-BREAK
         cmp       al,27
         jne       BlokF4                   ; nen¡ p©eru¨en¡
BlokF2:  stc                                ; p©¡znak p©eru¨en¡ operace
BLokF3:  ret

; ------ ukon‡en¡ volby ENTER

BlokF4:  cmp       al,0dh
         je        BlokF3                   ; ukon‡en¡ volby ENTER

; ------ posun kurzoru dol–

         cmp       ax,5000h
         jne       BlokF5                   ; nen¡ kurzor dol–
         call      NextLine                 ; nalezen¡ dal¨¡ho © dku

; ------ posun kurzoru nahoru

BlokF5:  cmp       ax,4800h
         jne       BlokF6                   ; nen¡ kurzor nahoru
         call      PredLine                 ; nalezen¡ p©edchoz¡ho © dku

; ------ posun o str nku dol–

BlokF6:  cmp       ax,5100h
         jne       BlokF7                   ; nen¡ str nka dol–
         mov       cx,ds:[DispRow]          ; po‡et zobrazen˜ch © dk–
         dec       cx                       ; o 1 © dek m‚nˆ
BlokF62: call      NextLine                 ; posun o © dek dol–
         loop      BlokF62                  ; posun o dal¨¡ © dek

; ------ posun o str nku nahoru

BlokF7:  cmp       ax,4900h
         jne       BlokF8                   ; nen¡ str nka nahoru
         mov       cx,ds:[DispRow]          ; po‡et zobrazen˜ch © dk–
         dec       cx                       ; o 1 © dek m‚nˆ
BlokF72: call      PredLine                 ; posun o © dek nahoru
         loop      BlokF72

; ------ konec textu

BlokF8:  cmp       ax,4f00h
         jne       BlokF9
         call      HledZap
BlokF82: call      NextLine
         jnc       BlokF82
         call      HledVyp

; ------ za‡ tek textu

BlokF9:  cmp       ax,4700h
         jne       BlokF1
         call      HledZap
BlokF92: call      PredLine
         jnc       BlokF92
         call      HledVyp
         jmp       short BlokF1

BlokF    ENDP

; -----------------------------------------------------------------------------
;        ulo‘en¡ bloku na disk
; -----------------------------------------------------------------------------

UlozBlk  PROC      NEAR

; ------ test, zda je nˆjak˜ © dek

         cmp       word ptr ds:[RadkuTxt],0 ; je alespo¤ 1 © dek ?
         jne       UlozBlk1                 ; je alespo¤ 1 © dek textu
         ret                                ; jinak se nic neukl d 

; ------ zobrazen¡ textu v˜zvy

UlozBlk1:mov       di,offset ZapisTxt
         test      byte ptr ds:[Param],4    ; je tisk ?
         jz        UlozBl11                 ; nen¡ tisk
         mov       di,offset TiskTxt
UlozBl11:call      DispHlas

; ------ otev©en¡ souboru pro z pis

         mov       dx,offset BlokSoub
         test      byte ptr ds:[Param],4    ; je tisk ?
         jz        UlozBl13
         mov       dx,offset BlokTisk
UlozBl13:mov       ax,3d01h                 ; otev©en¡ pro z pis
         int       21h                      ; otev©en¡ souboru pro z pis
         jnc       UlozBlk2                 ; soubor £spˆ¨nˆ otev©en
         mov       ah,3ch
         xor       cx,cx
         int       21h                      ; vytvo©en¡ nov‚ho souboru
         jnc       UlozBlk2
         jmp       UlozBlk7                 ; chyba - soubor nelze vytvo©it
UlozBlk2:mov       bx,ax                    ; identifik tor souboru

; ------ vystaven¡ ukazatele na konec souboru

         test      byte ptr ds:[Param],4
         jnz       UlozBl22
         xor       cx,cx
         xor       dx,dx
         mov       ax,4202h
         int       21h                      ; vystaven¡ ukazatele na konec soub.

; ------ stanoven¡ po‡ tku bloku a po‡et © dk– k z pisu

UlozBl22:mov       si,word ptr ds:[Blok1Adr] ; offset po‡ tku
         mov       dx,word ptr ds:[Blok1Adr+2] ; segment po‡ tku
         mov       bp,ds:[Blok1Lin]         ; © dek po‡ tku
         mov       cx,ds:[Blok2Lin]         ; © dek konce
         sub       cx,bp                    ; po‡et © dk– k z pisu - 1
         jae       UlozBlk3                 ; konec je za za‡ tkem OK
         neg       cx                       ; oprava rozd¡lu © dk–
         mov       si,word ptr ds:[Blok2Adr] ; offset po‡ tku
         mov       dx,word ptr ds:[Blok2Adr+2] ; segment po‡ tku
         mov       bp,ds:[Blok2Lin]         ; © dek po‡ tku
UlozBlk3:inc       cx                       ; + 1 © dek

; ------ £schova registr– p©ed z pisem

UlozBlk4:push      cx                       ; ‡¡ta‡ © dk–
         push      si                       ; offset © dku
         push      dx                       ; segment © dku
         push      ds                       ; datov˜ segment

; ------ normalizace adresy po‡ tku © dku

         mov       di,si                    ; £schova offset po‡ tku © dku
         mov       ax,si                    ; offset po‡ tku © dku
         and       si,0fh                   ; offset v odstavci
         mov       cl,4                     ; po‡et rotac¡
         shr       ax,cl                    ; p©evod offsetu na segment
         add       dx,ax                    ; segment adresy
         mov       ds,dx                    ; segment adresy © dku
         mov       dx,si                    ; offset po‡ tku © dku

; ------ d‚lka posledn¡ho © dku

         mov       cx,cs:[MaxOffs]          ; offset konce bufferu
         sub       cx,di                    ; d‚lka p©¡padn‚ho posledn¡ho © dku
         inc       bp                       ; zv˜¨en¡ ‡¡sla © dku
         cmp       bp,cs:[RadkuTxt]         ; je to posledn¡ © dek ?
         je        UlozBl55                 ; je to posledn¡ © dek

; ------ nalezen¡ konce © dku

         xor       cx,cx                    ; ‡¡ta‡ d‚lky © dku
         mov       ah,10                    ; hledan˜ znak LF
         cld
UlozBlk5:inc       cx                       ; zv˜¨en¡ ‡¡ta‡e d‚lky © dku
         jnz       UlozBl52
         dec       cx                       ; n vrat 0ffffh p©i p©ete‡en¡
UlozBl52:lodsb                              ; na‡ten¡ znaku z © dku
         cmp       al,ah                    ; je to konec © dku ?
         jne       UlozBlk5                 ; nen¡ konec - dal¨¡ znak

; ------ z pis © dku do souboru

UlozBl55:dec       bp                       ; n vrat ‡¡sla © dku
         mov       ah,40h
         int       21h                      ; z pis © dku do souboru
         jc        UlozBl58                 ; chyba z pisu © dku
         cmp       ax,cx                    ; bylo zaps no v¨e OK ?

; ------ n vrat registr– po z pisu

UlozBl58:pop       ds
         pop       dx
         pop       si
         pop       cx
         jc        UlozBlk6                 ; byla chyba z pisu

; ------ test p©eru¨en¡ operace

         mov       ah,1
         int       16h
         or        ax,ax
         jz        UlozBlk6                 ; p©eru¨en¡ Ctrl-Break
         cmp       al,1bh
         je        UlozBlk6                 ; p©eru¨en¡ ESC

; ------ p©¡prava pro dal¨¡ © dek

         call      NextLine                 ; adresa dal¨¡ho © dku
         loop      UlozBlk4                 ; z pis dal¨¡ho © dku
         clc                                ; operace probˆhla OK

; ------ uzav©en¡ souboru

UlozBlk6:pushf
         mov       ah,3eh
         int       21h                      ; uzav©en¡ souboru

; ------ vypr zdnˆn¡ bufferu kl vesnice

UlozBl62:mov       ah,1
         int       16h
         jz        UlozBl64
         mov       ah,0
         int       16h
         jmp       short UlozBl62
UlozBl64:popf
         jnc       UlozBlkA                 ; operace probˆhla OK

; ------ chyba z pisu

UlozBlk7:mov       di,offset ZapErTxt
         test      byte ptr ds:[Param],4
         jz        UlozBl72
         mov       di,offset TiskETxt
UlozBl72:call      DispHlas
UlozBlk8:mov       ah,1
         int       16h
         jz        UlozBlk9
         mov       ah,0
         int       16h
         jmp       short UlozBlk8
UlozBlk9:mov       ah,0
         int       16h
UlozBlkA:ret

UlozBlk  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ hl ¨en¡ DS:DI
; -----------------------------------------------------------------------------

DispHlas PROC      NEAR

; ------ £schova registr–

         push      ax
         push      cx
         push      si
         push      di
         push      es

; ------ zobrazen¡ hl ¨en¡

         mov       si,di                    ; adresa textu hl ¨en¡
         mov       ah,ds:[Barva2]           ; barva nadpisu
         les       di,ds:[AdrVRAM]          ; adresa videopamˆti
         mov       cx,80                    ; d‚lka textu
         call      DispAsc                  ; zobrazen¡ textu

; ------ n vrat registr–

         pop       es
         pop       di
         pop       si
         pop       cx
         pop       ax
         ret

DispHlas ENDP

; -----------------------------------------------------------------------------
;        n povˆda
; -----------------------------------------------------------------------------

Napov    PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      es

; ------ p©¡prava registr–

         mov       si,offset MHelpTxt       ; text n povˆdy
         mov       dx,offset(MHelpTx1-MHelpTxt)/offset(MHelpTx1-MHelpTx0)
         les       di,ds:[AdrVRAM]          ; adresa videopamˆti
         mov       ah,ds:[MaxRow]
         sub       ah,offset(MHelpTx1-MHelpTxt)/offset(MHelpTx1-MHelpTx0)
         shr       ah,1                     ; po‡ te‡n¡ © dek
         inc       ah
         mov       al,160                   ; bajt– na © dek
         mul       ah                       ; offset © dku
         add       di,ax                    ; © dek
         add       di,(80-offset(MHelpTx1-MHelpTx0)) AND not 1 ; pozice
         mov       ah,ds:[Barva3]           ; barva n povˆdy

; ------ rozli¨en¡ KOI 8

         mov       bh,0
         mov       al,ds:[ParamC]
         and       al,3
         cmp       al,2                     ; je KOI 8 ?
         je        Napov4                   ; je KOI 8

; ------ zobrazen¡ textu n povˆdy

         cld
Napov2:  push      di
         mov       cx,offset(MHelpTx1-MHelpTx0) ; d‚lka jednoho © dku
Napov3:  lodsb
         stosw
         loop      Napov3
         mov       byte ptr es:[di+160+3],7
         mov       byte ptr es:[di+160+1],7
         pop       di
         add       di,160
         dec       dx                       ; ‡¡ta‡ © dk–
         jnz       Napov2                   ; dal¨¡ © dek
         jmp       short Napov8

; ------ je k¢d KOI 8

Napov4:  push      di
         mov       cx,offset(MHelpTx1-MHelpTx0) ; d‚lka jednoho © dku
Napov5:  lodsb
         cmp       al,128
         jb        Napov6
         mov       bl,al
         mov       al,ds:[bx+TbKamKOI-128]
Napov6:  stosw
         loop      Napov5
         mov       byte ptr es:[di+160+3],7
         mov       byte ptr es:[di+160+1],7
         pop       di
         add       di,160
         dec       dx                       ; ‡¡ta‡ © dk–
         jnz       Napov4                   ; dal¨¡ © dek

; ------ st¡n

Napov8:  mov       cx,offset(MHelpTx1-MHelpTx0)-2
Napov9:  mov       byte ptr es:[di+5],7
         inc       di
         inc       di
         loop      Napov9

; ------ ‡ek n¡ na stisk kl vesy

         mov       ah,0
         int       16h

; ------ n vrat registr–

         pop       es
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

Napov    ENDP

; -----------------------------------------------------------------------------
;        filtr Kamenick˜ch
; -----------------------------------------------------------------------------

FKam     PROC      NEAR

         and       byte ptr ds:[ParamC],not 7
         jmp       short EnterK2

FKam     ENDP

; -----------------------------------------------------------------------------
;        filtr Latin 2
; -----------------------------------------------------------------------------

FLat     PROC      NEAR

         and       byte ptr ds:[ParamC],not 7
         or        byte ptr ds:[ParamC],1
         jmp       short EnterK2

FLat     ENDP

; -----------------------------------------------------------------------------
;        filtr KOI 8
; -----------------------------------------------------------------------------

FKOI     PROC      NEAR

         and       byte ptr ds:[ParamC],not 7
         or        byte ptr ds:[ParamC],2
         jmp       short EnterK2

FKOI     ENDP

; -----------------------------------------------------------------------------
;        filtr font–
; -----------------------------------------------------------------------------

EnterK   PROC      NEAR

         xor       byte ptr ds:[ParamC],4   ; zmˆna p©¡znaku filtru
EnterK2: call      InitTab                  ; inicializace tabulek
         call      DispNadp                 ; zobrazen¡ nadpisu
         ret

EnterK   ENDP

; -----------------------------------------------------------------------------
;        posun kurzoru o znak vpravo/vlevo
; -----------------------------------------------------------------------------

Vpravo   PROC      NEAR

         inc       word ptr ds:[TopPoz]
         jns       Vpravo3
Vlevo:   dec       word ptr ds:[TopPoz]
         jns       Vpravo3
         inc       word ptr ds:[TopPoz]
Vpravo3: ret

Vpravo   ENDP

; -----------------------------------------------------------------------------
;        posun kurzoru o 10 znak– vlevo
; -----------------------------------------------------------------------------

CVlevo   PROC      NEAR

         mov       cx,10
CVlevo1: call      Vlevo
         loop      CVlevo1
         ret

CVlevo   ENDP

; -----------------------------------------------------------------------------
;        posun kurzoru o 10 znak– vpravo
; -----------------------------------------------------------------------------

CVpravo  PROC      NEAR

         mov       cx,10
CVpravo1:call      Vpravo
         loop      CVpravo1
         ret

CVpravo  ENDP

; -----------------------------------------------------------------------------
;        posun kurzoru o © dek dol–
; -----------------------------------------------------------------------------

Dolu     PROC      NEAR

         cmp       bp,bx                    ; je ji‘ maxim ln¡ © dek ?
         cmc
         jc        Dolu1                    ; je ji‘ maxim ln¡ © dek
         call      NextLine                 ; nalezen¡ dal¨¡ho © dku
Dolu1:   ret

Dolu     ENDP


; -----------------------------------------------------------------------------
;        posun kurzoru o © dek nahoru
; -----------------------------------------------------------------------------

Nahoru   PROC      NEAR

         or        bp,bp                    ; je ji‘ prvn¡ © dek ?
         stc
         jz        Nahoru1                  ; je ji‘ prvn¡ © dek
         call      PredLine                 ; nalezen¡ p©edchoz¡ho © dku
Nahoru1: ret

Nahoru   ENDP

; -----------------------------------------------------------------------------
;        posun o str nku dol–
; -----------------------------------------------------------------------------

PgDn     PROC      NEAR

         mov       cx,ds:[DispRow]          ; po‡et zobrazen˜ch © dk–
         dec       cx                       ; o 1 © dek m‚nˆ
PgDn1:   call      Dolu                     ; posun o © dek dol–
         jc        PgDn2
         loop      PgDn1                    ; posun o dal¨¡ © dek
PgDn2:   ret

PgDn     ENDP

; -----------------------------------------------------------------------------
;        konec textu
; -----------------------------------------------------------------------------

EndK     PROC      NEAR

         call      HledZap
EndK1:   call      Dolu
         jnc       EndK1
         call      HledVyp
;         ret

EndK     ENDP

; -----------------------------------------------------------------------------
;        posun o str nku nahoru
; -----------------------------------------------------------------------------

PgUp     PROC      NEAR

         mov       cx,ds:[DispRow]          ; po‡et zobrazen˜ch © dk–
         dec       cx                       ; o 1 © dek m‚nˆ
PgUp1:   call      Nahoru                   ; posun o © dek nahoru
         jc        PgUp2
         loop      PgUp1
PgUp2:   ret

PgUp     ENDP

; -----------------------------------------------------------------------------
;        pokra‡ov n¡ v hled n¡
; -----------------------------------------------------------------------------

HledC    PROC      NEAR

         call      KonvFND
         mov       si,word ptr ds:[FindAdr]
         mov       dx,word ptr ds:[FindAdr+2]
         mov       bp,ds:[FindLine]

         cmp       bp,-1                    ; byla inicializace ?
         je        HledC9                   ; nebyla inicializace

         cmp       byte ptr ds:[FndNum],0
         je        HledC9                   ; nen¡ nic v bufferu

         call      HledZap                  ; zapnut¡ indik toru

         test      byte ptr ds:[Param],80h  ; hled n¡ zpˆt ?
         jz        HledC4                   ; hled n¡ vp©ed

         call      HledPred                 ; nalezen¡ p©edchoz¡ho © dku
         jmp       short HledC5

HledC4:  call      HledNxt                  ; nalezen¡ dal¨¡ho © dku

HledC5:  jnc       HledC6
         call      FlushKey                 ; vypr zdnˆn¡ bufferu kl vesnice
         jmp       short HledC9

HledC6:  mov       word ptr ds:[FindAdr],si
         mov       word ptr ds:[FindAdr+2],dx
         mov       ds:[FindLine],bp

         call      HledNorm                 ; normalizace kurzoru

HledC9:  mov       si,word ptr ds:[TopAdr]
         mov       dx,word ptr ds:[TopAdr+2]
         mov       bp,ds:[TopLine]
         call      HledVyp                  ; vypnut¡ indik toru
         ret

HledC    ENDP

; -----------------------------------------------------------------------------
;        za‡ tek textu
; -----------------------------------------------------------------------------

Home     PROC      NEAR

         call      HledZap
Home1:   call      Nahoru
         jnc       Home1
         call      HledVyp
         ret

Home     ENDP

; -----------------------------------------------------------------------------
;        nalezen¡ dal¨¡ho © dku DX:SI, ‡¡slo BP (CY=nen¡)
; -----------------------------------------------------------------------------

NextLine PROC      NEAR

; ------ kontrola ‡¡sla © dku

         inc       bp                       ; zv˜¨en¡ ‡¡sla © dku
         cmp       bp,ds:[RadkuTxt]         ; je ji‘ posledn¡ © dek ?
         jae       NextLin8                 ; je ji‘ posledn¡ © dek

; ------ nalezen¡ dal¨¡ho © dku

         push      ds
         push      ax
         mov       ds,dx                    ; segment adresy
         mov       ah,10                    ; hledan˜ znak LF
         cld
NextLin1:lodsb                              ; na‡ten¡ znaku z bufferu
         or        si,si                    ; je p©ete‡en¡ bufferu ?
         jz        NextLin3                 ; je p©ete‡en¡ bufferu
NextLin2:cmp       al,ah                    ; je LF ?
         jne       NextLin1                 ; nalezen¡ znaku LF
         pop       ax
         pop       ds
         ret

; ------ zv˜¨en¡ segmentu adresy

NextLin3:add       dx,1000h                 ; zv˜¨en¡ segmentu adresy
         mov       ds,dx                    ; nov˜ segment adresy
         jmp       short NextLin2

; ------ nen¡ dal¨¡ © dek

NextLin8:dec       bp                       ; n vrat ‡¡sla © dku
         stc                                ; p©¡znak, ‘e nen¡ dal¨¡ © dek
         ret

NextLine ENDP

; -----------------------------------------------------------------------------
;        nalezen¡ p©edchoz¡ho © dku DX:SI, ‡¡slo BP (CY=nen¡)
; -----------------------------------------------------------------------------

PredLine PROC      NEAR

; ------ kontrola ‡¡sla © dku

         or        bp,bp                    ; je ji‘ prvn¡ © dek ?
         jz        PredLin9                 ; je ji‘ prvn¡ © dek

; ------ nalezen¡ p©edchoz¡ho © dku

         dec       bp                       ; sn¡‘en¡ ‡¡sla © dku
         push      ds
         push      ax
         mov       ds,dx                    ; segment adresy © dku
         mov       ah,10                    ; hledan˜ znak LF

PredLin1:or        si,si                    ; je p©ete‡en¡ segmentu ?
         jz        PredLin6                 ; je p©ete‡en¡ segmentu
PredLin2:dec       si                       ; adresa minul‚ho znaku LF

PredLin3:or        si,si
         jz        PredLin7                 ; p©ete‡en¡ segmentu
PredLin4:dec       si                       ; sn¡‘en¡ ukazatele v bufferu

         cmp       byte ptr ds:[si],ah      ; je to znak LF ?
         jne       PredLin3                 ; nen¡ LF - nalezen¡ dal¨¡ho LF

         inc       si                       ; n vrat posledn¡ho znaku
         jz        PredLin8                 ; p©ete‡en¡ segmentu

PredLin5:clc                                ; p©¡znak operace OK
         pop       ax
         pop       ds
         ret

; ------ podte‡en¡ segmentu

PredLin6:cmp       dx,cs:[DatSegm]          ; je ji‘ po‡ te‡n¡ segment ?
         je        PredLin5                 ; je ji‘ po‡ te‡n¡ segment
         sub       dx,1000h                 ; posun adresy segmentu
         mov       ds,dx                    ; nov˜ segment adresy
         jmp       short PredLin2

; ------ podte‡en¡ segmentu

PredLin7:cmp       dx,cs:[DatSegm]          ; je ji‘ po‡ te‡n¡ segment ?
         je        PredLin5                 ; je ji‘ po‡ te‡n¡ segment
         sub       dx,1000h                 ; posun adresy segmentu
         mov       ds,dx                    ; nov˜ segment adresy
         jmp       short PredLin4

; ------ p©ete‡en¡ segmentu

PredLin8:add       dx,1000h                 ; posun adresy segmentu
         jmp       short PredLin5

; ------ nen¡ dal¨¡ © dek

PredLin9:stc                                ; p©¡znak, ‘e nen¡ dal¨¡ © dek
         ret

PredLine ENDP

; -----------------------------------------------------------------------------
;        hled n¡ textu (DX:SI=za‡ tek str nky, BP=‡¡slo © dku za‡ tku str nky)
; -----------------------------------------------------------------------------

Hledej   PROC      NEAR

         or        byte ptr ds:[Param],40h  ; p©¡znak hled n¡ textu

; ------ test, zda je kurzor zobrazen

         mov       ax,ds:[TopLine]          ; ‡¡slo po‡ te‡n¡ho © dku
         cmp       ax,ds:[FindLine]         ; je kurzor mimo ?
         ja        Hledej01                 ; kurzor je mimo
         add       ax,ds:[DispRow]          ; ‡¡slo posledn¡ho © dku
         cmp       ax,ds:[FindLine]         ; je kurzor mimo ?
         ja        Hledej09                 ; kurzor je OK

; ------ nastaven¡ kurzoru

Hledej01:push      dx
         push      si
         push      bp

         mov       si,word ptr ds:[TopAdr]
         mov       dx,word ptr ds:[TopAdr+2]
         mov       bp,ds:[TopLine]
         mov       cx,ds:[DispRow]          ; po‡et zobrazen˜ch © dk–
         shr       cx,1                     ; polovina
Hledej02:call      NextLine
         loop      Hledej02
         mov       word ptr ds:[FindAdr],si
         mov       word ptr ds:[FindAdr+2],dx
         mov       ds:[FindLine],bp

         pop       bp
         pop       si
         pop       dx


Hledej09:
         mov       byte ptr ds:[FndNum],0

         call      DispNad2                 ; zobrazen¡ nadpisu hled n¡

Hledej1: call      KonvFND                  ; konverze textu
         call      Disp                     ; zobrazen¡ obrazovky
         call      DispFnd                  ; zobrazen¡ hledan‚ho textu
         call      DispUkaz                 ; zobrazen¡ ukazatele polohy v soub.

         push      dx
         mov       dh,0
         mov       dl,byte ptr ds:[FndNum]  ; po‡et znak– textu
         add       dl,offset(Nadpis20-Nadpis2)
         mov       ah,2
         call      Int10P                   ; nastaven¡ kurzoru
         pop       dx

         mov       ah,0
         int       16h

; ------ p©eru¨en¡ funkce hled n¡

         cmp       al,27
         je        Hledej18
         or        ax,ax
         jnz       Hledej19
Hledej18:jmp       Hledej9

; ------ vymaz n¡ znaku

Hledej19:cmp       ax,4b00h
         je        Hledej2                  ; kurzor vlevo
         cmp       ax,5300h
         je        Hledej2                  ; DELETE
         cmp       al,8
         jne       Hledej3
Hledej2: cmp       byte ptr ds:[FndNum],0
         je        Hledej1
         dec       byte ptr ds:[FndNum]
Hledej11:jmp       short Hledej1

; ------ obnoven¡ znaku - kurzor vpravo

Hledej3: cmp       ax,4d00h
         jne       Hledej4
         mov       bl,byte ptr ds:[FndNum]
         cmp       bl,MAXFND
         je        Hledej1                  ; buffer je ji‘ pln˜
         inc       byte ptr ds:[FndNum]     ; zv˜¨en¡ ‡¡ta‡e znak–
         jmp       short Hledej11

; ------ nalezen¡ ©etˆzce vp©ed

Hledej4:
         mov       si,word ptr ds:[FindAdr]
         mov       dx,word ptr ds:[FindAdr+2]
         mov       bp,ds:[FindLine]

         cmp       ax,5000h                 ; kurzor dol–
         jne       Hledej5

         call      HledZap                  ; zapnut¡ indik toru hled n¡
Hledej42:call      HledNxt                  ; nalezen¡ dal¨¡ho © dku

; ------ nastaven¡ kurzoru

Hledej44:jnc       Hledej45
         call      FlushKey                 ; vypr zdnˆn¡ bufferu kl vesnice
         jmp       short Hledej12

Hledej45:mov       word ptr ds:[FindAdr],si
         mov       word ptr ds:[FindAdr+2],dx
         mov       ds:[FindLine],bp

         call      HledNorm                 ; normalizace kurzoru

Hledej12:call      HledVyp                  ; vypnut¡ indik toru
         jmp       short Hledej11

; ------ nalezen¡ ©etˆzce zpˆt

Hledej5: cmp       ax,4800h                 ; kurzor nahoru
         jne       Hledej6

         call      HledZap                  ; zapnut¡ indik toru hled n¡
Hledej52:call      HledPred                 ; nalezen¡ p©edchoz¡ho © dku
         jmp       short Hledej44           ; normalizace kurzoru

; ------ nalezen¡ prvn¡ho v˜skytu ©etˆzce ENTER

Hledej6: cmp       al,13
         jne       Hledej68                 ; nen¡ ENTER
         cmp       byte ptr ds:[FndNum],0
         je        Hledej66                 ; nen¡ nic zad no
         call      HledZap                  ; zapnut¡ indik toru
Hledej62:call      PredLine                 ; nastaven¡ na p©ede¨l˜ © dek
         jnc       Hledej62                 ; nalezen¡ prvn¡ho © dku
         call      HledTst
         jnc       Hledej63                 ; prvn¡ © dek vyhovuje
         call      HledNxt                  ; nalezen¡ dal¨¡ho © dku
Hledej63:call      HledVyp                  ; vypnut¡ indik toru
         jc        Hledej66                 ; ©etˆzec nenalezen
         mov       word ptr ds:[FindAdr],si
         mov       word ptr ds:[FindAdr+2],dx
         mov       ds:[FindLine],bp

         call      HledNorm                 ; normalizace kurzoru
Hledej66:jmp       Hledej9                  ; n vrat z obsluhy

; ------ nalezen¡ prvn¡ho v˜skytu ©etˆzce HOME

Hledej68:cmp       ax,4700h                 ; HOME
         jne       Hledej7
         call      HledZap                  ; zapnut¡ indik toru
Hledej69:call      PredLine                 ; nalezen¡ p©edchoz¡ho © dku
         jnc       Hledej69
         call      HledTst
         jnc       Hledej44                 ; prvn¡ © dek OK
         jmp       short Hledej42

; ------ nalezen¡ posledn¡ho v˜skytu ©etˆzce END

Hledej7: cmp       ax,4f00h
         jne       Hledej72
         call      HledZap                  ; zapnut¡ indik toru
Hledej71:call      NextLine                 ; nastaven¡ na dal¨¡ © dek
         jnc       Hledej71                 ; nalezen¡ posledn¡ho © dku
         call      HledTst
         jnc       Hledej44                 ; posledn¡ © dek OK
         jmp       short Hledej52           ; nalezen¡ posledn¡ho ©etˆzce

; ------ posun o str nku zpˆt

Hledej72:cmp       ax,4900h                 ; PAGE UP
         jne       Hledej74
         mov       cx,ds:[DispRow]
         dec       cx
Hledej73:call      PredLine                 ; nalezen¡ p©edchoz¡ho © dku
         loop      Hledej73
;         call      HledZap                  ; zapnut¡ indik toru
;         mov       cx,bp
;         sub       cx,ds:[DispRow]          ; minim ln¡ © dek k dosa‘en¡
;         jnc       Hledj722
;         xor       cx,cx
;Hledj722:call      HledPred                 ; nalezen¡ p©edchoz¡ho ©etˆzce
;         jc        Hledej73                 ; nenalezen
;         cmp       bp,cx
;         ja        Hledj722                 ; nen¡ je¨tˆ po‘adovan˜ © dek
         clc                                ; p©¡znak operace OK
Hledj444:jmp       Hledej44

; ------ posun o str nku vp©ed

Hledej74:cmp       ax,5100h                 ; PAGE DOWN
         jne       Hledej8
         mov       cx,ds:[DispRow]
         dec       cx
Hledej75:call      NextLine                 ; nalezen¡ dal¨¡ho © dku
         loop      Hledej75
;         call      HledZap                  ; zapnut¡ indik toru
;         mov       cx,bp
;         add       cx,ds:[DispRow]
;         cmp       cx,ds:[RadkuTxt]
;         jb        Hledej75
;         mov       cx,ds:[RadkuTxt]
;         dec       cx
;Hledej75:call      HledNxt                  ; nalezen¡ dal¨¡ho ©etˆzce
;         jc        Hledej76                 ; nenalezen
;         cmp       bp,cx
;         jb        Hledej75                 ; nen¡ je¨tˆ po‘adovan˜ © dek
         clc                                ; p©¡znak operace OK
         jmp       short Hledj444

; ------ vlo‘en¡ znaku

Hledej8: cmp       al,0
         je        Hledej13                 ; neplatn˜ znak
         mov       bx,ds:[FndNum]
         cmp       bl,MAXFND
         je        Hledej13                 ; buffer je ji‘ pln˜
         mov       ds:[FndTxt+bx],al        ; ulo‘en¡ znaku do bufferu
         inc       byte ptr ds:[FndNum]     ; zv˜¨en¡ ‡¡ta‡e znak–
Hledej13:jmp       Hledej1

; ------ n vrat z obsluhy hled n¡

Hledej9: and       byte ptr ds:[Param],not 40h ; zru¨en¡ p©¡znaku hled n¡
         call      DispNadp

         mov       si,word ptr ds:[TopAdr]
         mov       dx,word ptr ds:[TopAdr+2]
         mov       bp,ds:[TopLine]
         ret

Hledej   ENDP

; -----------------------------------------------------------------------------
;        nastaven¡ kurzoru pro hled n¡ a normalizace obrazovky
; -----------------------------------------------------------------------------

HledNrm0:mov       word ptr ds:[FindAdr],si
         mov       word ptr ds:[FindAdr+2],dx
         mov       ds:[FindLine],bp

; -----------------------------------------------------------------------------
;        normalizace po‡ tku obrazovky p©i hled n¡
; -----------------------------------------------------------------------------

HledNorm PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      bp

; ------ p©¡prava ukazatel–

         mov       si,word ptr ds:[FindAdr]
         mov       dx,word ptr ds:[FindAdr+2]
         mov       bp,ds:[FindLine]

; ------ test, zda je podte‡en¡ kurzoru

         mov       cx,ds:[DispRow]          ; po‡et zobrazen˜ch © dk–
         shr       cx,1
         shr       cx,1                     ; 1/4 obrazovky
         mov       ax,ds:[TopLine]          ; ‡¡slo po‡ te‡n¡ho © dku
         add       ax,cx                    ; okraj naho©e
         cmp       ax,bp                    ; je podte‡en¡ po‡ tku ?
         jbe       HledNrm3                 ; po‡ tek je OK

; ------ posun kurzoru, je-li podte‡en¡

HledNrm2:call      PredLine
         loop      HledNrm2
         jmp       short HledNrm8           ; nastaven¡ po‡ tku obrazovky

; ------ test, zda je p©ete‡en¡ kurzoru

HledNrm3:mov       ax,cx
         shl       cx,1
         add       cx,ax                    ; 3/4 obrazovky
         mov       ax,ds:[TopLine]
         add       ax,cx
         cmp       ax,bp                    ; je p©ete‡en¡ konce ?
         jae       HledNrm9                 ; kurzor je OK

; ------ posun kurzoru, je-li p©ete‡en¡

         mov       bx,ds:[RadkuTxt]
         sub       bx,ds:[DispRow]          ; maxim ln¡ prvn¡ © dek
         jnc       HledNrm4
         xor       bx,bx
HledNrm4:call      PredLine
         loop      HledNrm4
         inc       cx
         cmp       bp,bx
         ja        HledNrm4

; ------ nov  adresa po‡ tku obrazovky

HledNrm8:mov       word ptr ds:[TopAdr],si
         mov       word ptr ds:[TopAdr+2],dx
         mov       ds:[TopLine],bp

; ------ n vrat registr–

HledNrm9:pop       bp
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

HledNorm ENDP

; -----------------------------------------------------------------------------
;        nalezen¡ n sleduj¡c¡ho © dku s textem
; -----------------------------------------------------------------------------
; VSTUP: DX:SI=ukazatel za‡ tku © dku
;        BP=‡¡slo © dku
; VSTUP:DX:SI=ukazatel nalezen‚ho © dku
;        BP=‡¡slo nalezen‚ho © dku
;        CY=text nenalezen
; -----------------------------------------------------------------------------

HledNxt  PROC      NEAR

; ------ £schova registr–

         and       byte ptr ds:[Param],not 80h ; p©¡znak hled n¡ vp©ed
         push      dx
         push      si
         push      bp

; ------ nastaven¡ ukazatele na n sleduj¡c¡ © dek

HledNxt1:call      NextLine                 ; nalezen¡ dal¨¡ho © dku
         jc        HledNxt9                 ; nen¡ dal¨¡ © dek

; ------ test, zda © dek obsahuje hledan˜ text

         call      HledTst                  ; test © dku
         jc        HledNxt1                 ; text nenalezen - dal¨¡ © dek

; ------ text nalezen - registry jsou platn‚

         add       sp,6                     ; zru¨en¡ registr– v z sobn¡ku
         clc                                ; p©¡znak operace OK
         ret

; ------ text nenalezen - n vrat registr–

HledNxt9:pop       bp
         pop       si
         pop       dx
         stc                                ; p©¡znak, ‘e text nebyl nalezen
         ret

HledNxt  ENDP

; -----------------------------------------------------------------------------
;        nalezen¡ p©edchoz¡ho © dku s textem
; -----------------------------------------------------------------------------
; VSTUP: DX:SI=ukazatel za‡ tku © dku
;        BP=‡¡slo © dku
; VSTUP:DX:SI=ukazatel nalezen‚ho © dku
;        BP=‡¡slo nalezen‚ho © dku
;        CY=text nenalezen
; -----------------------------------------------------------------------------

HledPred PROC      NEAR

; ------ £schova registr–

         or        byte ptr ds:[Param],80h  ; p©¡znak hled n¡ zpˆt
         push      dx
         push      si
         push      bp

; ------ nastaven¡ ukazatele na p©edchoz¡ © dek

HledPrd1:call      PredLine                 ; nalezen¡ dal¨¡ho © dku
         jc        HledPrd9                 ; nen¡ dal¨¡ © dek

; ------ test, zda © dek obsahuje hledan˜ text

         call      HledTst                  ; test © dku
         jc        HledPrd1                 ; text nenalezen - dal¨¡ © dek

; ------ text nalezen - registry jsou platn‚

         add       sp,6                     ; zru¨en¡ registr– v z sobn¡ku
         clc                                ; p©¡znak operace OK
         ret

; ------ text nenalezen - n vrat registr–

HledPrd9:pop       bp
         pop       si
         pop       dx
         stc                                ; p©¡znak, ‘e text nebyl nalezen
         ret

HledPred ENDP

; -----------------------------------------------------------------------------
;        test, zda © dek DX:SI/BP obsahuje hledan˜ text (CY=neobsahuje)
; -----------------------------------------------------------------------------

HledTst  PROC      NEAR

; ------ nen¡-li text, © dek vyhovuje OK

         cmp       byte ptr ds:[FndNum],0   ; je zad n nˆjak˜ text ?
         jne       HledTst0                 ; je nˆco zad no
         ret                                ; nen¡ nic zad no - © dek vyhovuje

; ------ £schova registr–

HledTst0:push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         push      ds
         push      es

; ------ p©¡prava registr–

         mov       ax,ds
         mov       es,ax                    ; ES <- datov˜ segment
         mov       bp,ds:[MaxOffs]          ; maxim ln¡ offset
         mov       ah,ds:[FndTxt0]          ; prvn¡ znak textu
         cld
         mov       ds,dx                    ; segment s daty
         mov       bh,0

; ------ test, zda je ji‘ konec textu

HledTst1:cmp       si,bp                    ; je maxim ln¡ offset ?
         je        HledTst6                 ; je maxim ln¡ offset

; ------ na‡ten¡ jednoho znaku

HledTs11:lodsb                              ; na‡ten¡ znaku
         or        si,si                    ; je p©ete‡en¡ segmentu ?
         jz        HledTst5                 ; je p©ete‡en¡ segmentu

; ------ test, zda je ji‘ konec © dku

HledTst2:cmp       al,10                    ; je konec © dku ?
         je        HledTst7                 ; je konec © dku

; ------ p©evod na velk‚ p¡smeno

         mov       bl,al                    ; znak ke konverzi
         mov       bl,cs:[bx+Filtr]         ; konverze na znak displeje
         mov       al,cs:[bx+FiltrU]        ; p©evod na velk‚ p¡smeno

; ------ test, zda se prvn¡ znak shoduje

         cmp       al,ah                    ; shoduje se prvn¡ znak ?
         jne       HledTst1                 ; znak se neshoduje - dal¨¡ znak

; ------ porovn n¡, zda se shoduj¡ ostatn¡ znaky textu

         mov       cx,cs:[FndNum]           ; d‚lka zadan‚ho textu
         dec       cx                       ; d‚lka textu - 1
         jcxz      HledTst8                 ; je zad n pouze 1 znak - OK

         push      si
         push      dx
         mov       di,offset FndTxt0+1      ; buffer zadan‚ho textu

; ------ na‡ten¡ znaku

HledTs42:lodsb                              ; na‡ten¡ znaku
         or        si,si                    ; je p©ete‡en¡ segmentu ?
         jz        HledTs48                 ; je p©ete‡en¡ segmentu

; ------ p©evod na velk‚ p¡smeno

HledTs43:mov       bl,al                    ; znak ke konverzi
         mov       bl,cs:[bx+Filtr]         ; konverze na znak displeje
         mov       al,cs:[bx+FiltrU]        ; p©evod na velk‚ p¡smeno

; ------ test, zda se znak shoduje

         scasb                              ; shoduje se znak ?
         loope     HledTs42                 ; test dal¨¡ho znaku

; ------ n vrat registr–

         pop       dx
         pop       si
         mov       ds,dx                    ; n vrat DS
         jne       HledTst1                 ; text se neshoduje - dal¨¡ znak
         jmp       short HledTst8           ; text se shoduje OK

; ------ p©ete‡en¡ segmentu

HledTs48:add       dx,1000h
         mov       ds,dx
         jmp       short HledTs43

; ------ p©ete‡en¡ segmentu

HledTst5:add       dx,1000h                 ; posun adresy segmentu
         mov       ds,dx                    ; nov˜ segment
         jmp       short HledTst2

; ------ test, zda je maxim ln¡ segment

HledTst6:cmp       dx,cs:[MaxSegm]          ; je maxim ln¡ segment ?
         jne       HledTs11                 ; nen¡ maxim ln¡ segment

; ------ text nenalezen

HledTst7:stc                                ; p©¡znak nenalezen¡ textu

; ------ n vrat registr–

HledTst8:pop       es
         pop       ds
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

HledTst  ENDP

; -----------------------------------------------------------------------------
;        zapnut¡ indik toru hled n¡
; -----------------------------------------------------------------------------

HledZap  PROC      NEAR

         push      ds
         push      si
         call      KurzOff                  ; vypnut¡ kurzoru
         lds       si,ds:[AdrVRAM]
         mov       byte ptr ds:[si],"*"     ; indik tor hled n¡
         pop       si
         pop       ds
         ret

HledZap  ENDP

; -----------------------------------------------------------------------------
;        vypnut¡ indik toru hled n¡
; -----------------------------------------------------------------------------

HledVyp  PROC      NEAR

         push      ds
         push      si
         lds       si,ds:[AdrVRAM]
         mov       byte ptr ds:[si]," "     ; indik tor hled n¡
         pop       si
         pop       ds
         ret

HledVyp  ENDP

; -----------------------------------------------------------------------------
;        vypr zdnˆn¡ bufferu kl vesnice
; -----------------------------------------------------------------------------

FlushKey PROC      NEAR

         push      ax
FlushK1: mov       ah,1
         int       16h
         jz        FlushK2
         mov       ah,0
         int       16h
         jmp       short FlushK1
FlushK2: pop       ax
         stc
         ret

FlushKey ENDP

; *****************************************************************************
;
;                           Dekomprese souboru
;
; *****************************************************************************

;þ

; -----------------------------------------------------------------------------
;        dekomprese souboru (DS:SI -> ES:DI)
; -----------------------------------------------------------------------------

DeKomp   PROC      NEAR

; ------ p©¡prava k dek¢dov n¡

         cld                                ; smˆr nahoru
         mov       ax,1                     ; p©¡znak, ‘e nen¡ dal¨¡ bit

; ------ prvn¡ p©¡znakov˜ bit

DeKomp1: call      DekBit                   ; prvn¡ p©¡znakov˜ bit
         jz        DeKomp2                  ; konec bufferu
         jc        DeKomp3                  ; je komprese ©etˆzce

; ------ p©esun jednoho bajtu dat bez komprese

         call      ReadByte                 ; na‡ten¡ bajtu
         jz        DeKomp2                  ; konec dat
         call      WritByte                 ; z pis bajtu
         jnz       DeKomp1                  ; nen¡ p©ete‡en¡ bufferu
DeKomp2: ret

; ------ p©¡prava k opakov n¡ ©etˆzce

DeKomp3: xor       bx,bx                    ; BX <- 0 offset ©etˆzce
         xor       cx,cx                    ; d‚lka ©etˆzce k opakov n¡

; -----------------------------------
;        stanoven¡ d‚lky ©etˆzce
; -----------------------------------

; ------ prvn¡ bit d‚lky

         mov       dl,1                     ; po‡et bit– ke ‡ten¡
         call      DekBitC                  ; ‡ten¡ prvn¡ho a druh‚ho bitu d‚lky
         jz        DeKomp2                  ; p©ete‡en¡ bufferu
                                            ; (zde je stav 0 a‘ 1)

; ------ zv˜¨en¡ ‡¡ta‡e d‚lky

DeKomp32:inc       cx
         inc       cx

; ------ na‡ten¡ p©¡znaku konce k¢du

         call      DekBit                   ; p©¡znakov˜ bit
         jz        DeKomp2                  ; p©ete‡en¡ bufferu
         jnc       DeKomp38                 ; konec k¢du

; ------ p©¡padn‚ pokra‡ov n¡ k¢du

         cmp       cl,MAXLENX-4+1           ; je ji‘ maxim ln¡ d‚lka ?
         jb        DeKomp32                 ; pokra‡ov n¡ k¢du
         inc       cx
         inc       cx                       ; korekce posledn¡ch 2 k¢d–

; ------ korekce pro n hradn¡ k¢d

DeKomp38:cmp       cl,SUBSTLEN              ; je n hradn¡ d‚lka ?
         jb        DeKomp7                  ; d‚lka je OK
         ja        DeKomp39                 ; je dlouh  d‚lka

; ------ dlouh‚ opakov n¡

         call      ReadByte                 ; na‡ten¡ bajtu
DeKomp51:jz        DeKomp2                  ; konec
         mov       cl,dh                    ; d‚lka (stav 0 a‘ 255)
         cmp       cl,255                   ; konec dek¢dov n¡ ?
         je        DeKomp51                 ; je konec
         add       cx,MAXLENX+1             ; zv˜¨en¡ d‚lky

; ------ korekce d‚lky pro dlouh˜ k¢d

DeKomp39:dec       cx

; ------------------------------------
;        stanoven¡ offsetu ©etˆzce
; ------------------------------------

; ------ d‚lka 2 nem  offset HIGH

DeKomp7: cmp       cx,2                     ; je d‚lka 2 ?
         je        DeKomp9                  ; je d‚lka 2

; ------ prvn¡ p©¡znakov˜ bit - p©¡znak offsetu HIGH=0

         call      DekBit                   ; ‡ten¡ p©¡znakov‚ho bitu
         jz        DeKomp51                 ; p©ete‡en¡ bufferu
         jc        DeKomp9                  ; offset HIGH = 0

; ------ 3 bity offsetu HIGH

         mov       dl,3                     ; na‡tou se 3 bity
         call      DekBitB                  ; na‡ten¡ 3 bit– offsetu
         jz        DeKomp51                 ; p©ete‡en¡ bufferu

; ------ stav 0,1: offset HIGH = 1,2

         cmp       bh,1                     ; je stav 0,1 ?
         jbe       DeKomp8                  ; offset HIGH = 1,2

; ------ ‡tvrt˜ bit offsetu

         call      DekBitB                  ; na‡ten¡ 1 bitu offsetu
         jz        DeKomp51                 ; p©ete‡en¡ bufferu

; ------ stav 4,7: offset HIGH=3 a‘ 6

         dec       bh                       ; stav 3 a‘ 14
         cmp       bh,6                     ; je stav 3 a‘ 6 ?
         jbe       DeKomp9                  ; je offset 3 a‘ 6
                                            ; jinak stav 7 a‘ 14

; ------ p t˜ bit offsetu

         call      DekBitB                  ; na‡ten¡ 1 bitu offsetu
         jz        DeKomp51                 ; p©ete‡en¡ bufferu

; ------ stav 14 a‘ 23: offset HIGH=7 a‘ 13

         sub       bh,7                     ; stav 7 a‘ 22
         cmp       bh,13
         jbe       DeKomp9                  ; je offset 7 a‘ 13
                                            ; jinak stav 14 a‘ 22

; ------ ¨est˜ bit offsetu: offset HIGH=14 a‘ 31

         call      DekBitB                  ; na‡ten¡ 1 bitu offsetu
         jz        DeKomp51                 ; p©ete‡en¡ bufferu
         sub       bh,15                    ; stav 13 a‘ 30

; ------ offset ©etˆzce LOW

DeKomp8: inc       bh                       ; korekce BH
DeKomp9: call      ReadByte                 ; na‡ten¡ offsetu d‚lky LOW
         mov       bl,dh                    ; offset d‚lky LOW
         jz        DeKomp51                 ; p©ete‡en¡ bufferu

; ------ p©enos ©etˆzce

         push      si                       ; £schova SI
         mov       bp,ds                    ; £schova DS
         push      es
         pop       ds                       ; DS <- ES
         mov       si,di                    ; sou‡asn˜ ukazatel
         sub       si,bx                    ; posun za‡ tku ©etˆzce
         jc        DeKompC                  ; podte‡en¡ bufferu
DeKompA: mov       dh,ds:[si]
         inc       si
         jz        DeKompD
DeKompAB:call      WritByte                 ; ulo‘en¡ bajtu do bufferu
         loopnz    DeKompA
DeKompAA:mov       ds,bp                    ; n vrat DS
         pop       si
         jz        DeKomp51
DeKompB: jmp       DeKomp1                  ; dal¨¡ dek¢dov n¡

; ------ podte‡en¡ bufferu

DeKompC: push      ax
         mov       ax,ds
         sub       ax,1000h
         mov       ds,ax
         pop       ax
         jmp       short DeKompA

; ------ p©ete‡en¡ bufferu

DeKompD: push      ax
         mov       ax,ds
         add       ax,1000h
         mov       ds,ax
         pop       ax
         jmp       short DeKompAB

DeKomp   ENDP

; -----------------------------------------------------------------------------
;        poskytnut¡ bitu ze stavov‚ho slova do registru BH
; -----------------------------------------------------------------------------
; VSTUP: AX=stavov‚ slovo (nastaven nejvy¨¨¡ bit jako p©¡znak konce slova)
;        DS:SI=ukazatel zkomprimovan˜ch dat
;        BH=p–vodn¡ stav registru BH
;        DL=po‡et bit– k na‡ten¡ do registru BH (nesm¡ b˜t = 0 !)
; VSTUP:ZY=p©ete‡en¡ bufferu
;        BH=nov˜ stav registru BH
;        DL=1 (p©ipraveno pro ‡ten¡ 1 bitu)
; -----------------------------------------------------------------------------

DekBitB  PROC      NEAR

         call      DekBit                   ; ‡ten¡ bitu stavov‚ho slova
         jz        DekBitB9                 ; p©ete‡en¡ bufferu
         rcl       bh,1                     ; rotace bitu do registru BH
         dec       dl                       ; sn¡‘en¡ ‡¡ta‡e bit– DL
         jnz       DekBitB                  ; ‡ten¡ dal¨¡ho bitu
         inc       dx                       ; nastaven¡ p©¡znaku NZ
DekBitB9:ret

DekBitB  ENDP

; -----------------------------------------------------------------------------
;        poskytnut¡ bitu ze stavov‚ho slova do registru CX
; -----------------------------------------------------------------------------
; VSTUP: AX=stavov‚ slovo (nastaven nejvy¨¨¡ bit jako p©¡znak konce slova)
;        DS:SI=ukazatel zkomprimovan˜ch dat
;        CX=p–vodn¡ stav registru CX
;        DL=po‡et bit– k na‡ten¡ do registru CX (nesm¡ b˜t = 0 !)
; VSTUP:ZY=p©ete‡en¡ bufferu
;        CX=nov˜ stav registru CX
;        DL=1 (p©ipraveno pro ‡ten¡ 1 bitu)
; -----------------------------------------------------------------------------

DekBitC  PROC      NEAR

         call      DekBit                   ; ‡ten¡ bitu stavov‚ho slova
         jz        DekBitC9                 ; p©ete‡en¡ bufferu
         rcl       cx,1                     ; rotace bitu do registru CX
         dec       dl                       ; sn¡‘en¡ ‡¡ta‡e bit– DL
         jnz       DekBitC                  ; ‡ten¡ dal¨¡ho bitu
         inc       dx                       ; nastaven¡ p©¡znaku NZ
DekBitC9:ret

DekBitC  ENDP

; -----------------------------------------------------------------------------
;        poskytnut¡ bitu ze stavov‚ho slova
; -----------------------------------------------------------------------------
; VSTUP: AX=stavov‚ slovo (nastaven nejvy¨¨¡ bit jako p©¡znak konce slova)
;        DS:SI=ukazatel zkomprimovan˜ch dat
; VSTUP:CF=bit ze stavov‚ho slova
;        ZY=p©ete‡en¡ bufferu
; -----------------------------------------------------------------------------

DekBit   PROC      NEAR

; ------ poskytnut¡ jednoho bitu stavov‚ho slova

         shr       ax,1                     ; poskytnut¡ bitu ze stav. slova
         jnz       DekBit3                  ; nen¡ je¨tˆ konec slova

; ------ na‡ten¡ dal¨¡ho stavov‚ho slova

         call      ReadByte                 ; na‡ten¡ bajtu
         jz        DekBit3                  ; konec bufferu
         mov       al,dh                    ; ni‘¨¡ bajt stavov‚ho slova
         call      ReadByte                 ; na‡ten¡ bajtu
         mov       ah,dh                    ; konec bufferu
         stc
         rcr       ax,1                     ; prvn¡ bit stavov‚ho slova
DekBit3: ret

DekBit   ENDP

; -----------------------------------------------------------------------------
;     ‡ten¡ bajtu ze souboru (ukazatel DS:SI, ZY=konec souboru, DH=bajt)
; -----------------------------------------------------------------------------

ReadByte PROC      NEAR

; ------ test, zda jsou dal¨¡ data v bufferu

         cmp       si,cs:[InpNum]           ; je konec dat v bufferu ?
         jae       ReadByt2                 ; je konec bufferu

; ------ na‡ten¡ bajtu z bufferu

ReadByt8:mov       dh,ds:[si]               ; na‡ten¡ bajtu z bufferu
         inc       si                       ; nastaven¡ p©¡znaku NZ
ReadByt9:ret

; ------ na‡ten¡ dal¨¡ho bloku dat ze souboru

ReadByt2:push      ax
         push      bx
         push      cx
         push      dx

         xor       si,si                    ; ukazatel na za‡ tek bufferu
         mov       ah,3fh
         mov       bx,cs:[SoubIdnt]         ; identifik tor souboru
         mov       cx,BUFFSIZE              ; velikost bufferu
         xor       dx,dx                    ; za‡ tek k na‡ten¡ bloku dat
         int       21h                      ; na‡ten¡ bloku dat ze souboru

         jnc       ReadByt3                 ; operace ‡ten¡ OK
         xor       ax,ax                    ; jinak jako konec souboru
ReadByt3:mov       cs:[InpNum],ax           ; po‡et bajt– v bufferu

         call      Dekod                    ; dek¢dov n¡ dat

         or        ax,ax                    ; je konec souboru ?

         pop       dx
         pop       cx
         pop       bx
         pop       ax
         jnz       ReadByt8                 ; jsou dal¨¡ data
         ret                                ; jinak konec souboru

ReadByte ENDP

; -----------------------------------------------------------------------------
;        dek¢dov n¡ dat na‡ten˜ch do bufferu
; -----------------------------------------------------------------------------

Dekod    PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      ds

; ------ test, zda m  b˜t soubor dek¢dov n

         test      byte ptr cs:[Prepin],4   ; je heslo ?
         jz        Dekod9                   ; nek¢duje se

; ------ p©¡prava registr–

         mov       ds,cs:[InpSegm]          ; segment vstupn¡ho bufferu
         xor       si,si                    ; ukazatel v bufferu
         mov       dx,cs:[InpNum]           ; po‡et bajt– v bufferu
         or        dx,dx                    ; jsou nˆjak  data ?
         jz        Dekod9                   ; nejsou ‘ dn  data
         xor       bx,bx
         mov       bl,byte ptr cs:[InpSize] ; ukazatel f ze
         add       word ptr cs:[InpSize],dx ; ‡¡ta‡ velikosti souboru
         adc       word ptr cs:[InpSize+2],0

; ------ dek¢dov n¡ dat

Dekod3:  mov       cx,word ptr cs:[bx+Klic]
         mov       al,ds:[si]               ; bajt k zak¢dov n¡
         and       cl,7
         xor       al,ch                    ; zak¢dov n¡ bajtu
         ror       al,cl
         mov       ds:[si],al
         inc       si
         inc       bl
         dec       dx
         jnz       Dekod3                   ; dal¨¡ bajt dat

; ------ n vrat registr–

Dekod9:  pop       ds
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

Dekod    ENDP

; -----------------------------------------------------------------------------
;      ulo‘en¡ bajtu do bufferu (ukazatel ES:DI, ZY=konec bufferu, DH=bajt)
; -----------------------------------------------------------------------------

WritByte PROC      NEAR

; ------ test, zda se offset shoduje s koncem

         cmp       di,cs:[MaxOffs]          ; shoduje se offset ?
         je        WritByt3                 ; offset se shoduje s koncem

; ------ test, zda je dal¨¡ © dek (znak LF)

WritByt1:cmp       dh,10                    ; je znak LF ?
         je        WritByt4                 ; je nov˜ © dek - znak LF

; ------ ulo‘en¡ bajtu do bufferu

         mov       es:[di],dh               ; ulo‘en¡ bajtu do bufferu
         inc       di                       ; zv˜¨en¡ ukazatele v bufferu
         jz        WritByt2                 ; je p©ete‡en¡ segmentu
         ret                                ; jinak n vrat NZ

; ------ posun adresy segmentu

WritByt2:push      ax
         mov       ax,es                    ; segment
         add       ax,1000h                 ; posun adresy segmentu
         mov       es,ax                    ; nov˜ segment
         pop       ax
         ret                                ; n vrat se stavem NZ

; ------ test, zda se shoduje segment s koncem

WritByt3:push      ax
         mov       ax,es                    ; segment
         cmp       ax,cs:[MaxSegm]          ; je koncov˜ segment ?
         pop       ax
         jne       WritByt1                 ; nen¡ koncov˜ segment
         ret                                ; jinak n vrat ZY

; ------ nov˜ © dek - zv˜¨en¡ ‡¡ta‡e © dk–

WritByt4:inc       word ptr cs:[RadkuTxt]   ; zv˜¨en¡ ‡¡ta‡e © dk– celkem

; ------ ulo‘en¡ znaku do bufferu

         mov       es:[di],dh               ; ulo‘en¡ bajtu do bufferu
         inc       di                       ; zv˜¨en¡ ukazatele v bufferu
         jz        WritByt2                 ; je p©ete‡en¡ segmentu
         ret                                ; jinak n vrat NZ

WritByte ENDP

; *****************************************************************************
;
;                               Zobrazen¡ textu
;
; *****************************************************************************
;þ

; -----------------------------------------------------------------------------
;        zobrazen¡ hledan‚ho textu
; -----------------------------------------------------------------------------

DispFnd  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      si
         push      di
         push      es

; ------ p©¡prava registr–

         cld
         mov       si,offset FndTxt0        ; buffer hledan‚ho textu
         les       di,ds:[AdrVRAM]          ; adresa videopamˆti
         add       di,2*offset(Nadpis20-Nadpis2)
         mov       ah,ds:[Barva2]           ; barva nadpisu
         mov       cx,MAXFND                ; maxim ln¡ d‚lka textu
         mov       bx,ds:[FndNum]           ; po‡et znak– v bufferu

; ------ zobrazen¡ hledan‚ho textu

DispFnd1:lodsb
         dec       bx                       ; ‡¡ta‡ platn˜ch znak–
         jns       DispFnd2                 ; je platn˜ znak
         mov       al," "                   ; n hrada znaku mezerou
DispFnd2:stosw                              ; zobrazen¡ znaku
         loop      DispFnd1

; ------ n vrat registr–

         pop       es
         pop       di
         pop       si
         pop       cx
         pop       bx
         pop       ax
         ret

DispFnd  ENDP

; -----------------------------------------------------------------------------
;        dek¢dov n¡ ‡¡sla AX od adresy ES:DI dol–
; -----------------------------------------------------------------------------

DekNum   PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      dx

; ------ dek¢dov n¡ jedn‚ ‡¡slice

         mov       bx,10                    ; dˆlitel
DekNum1: xor       dx,dx
         div       bx                       ; v˜po‡et jedn‚ ‡¡slice
         add       dl,"0"                   ; korekce na ASCII znak

; ------ ulo‘en¡ jedn‚ ‡¡slice

         dec       di                       ; sn¡‘en¡ ukazatele
         mov       es:[di],dl               ; ulo‘en¡ znaku

; ------ test, zda je dal¨¡ ‡¡slo

         or        ax,ax                    ; je konec ‡¡sla ?
         jnz       DekNum1                  ; nen¡ konec ‡¡sla

; ------ n vrat registr–

         pop       dx
         pop       bx
         pop       ax
         ret

DekNum   ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ ukazatele polohy v souboru
; -----------------------------------------------------------------------------

DispUkaz PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      di
         push      bp
         push      es

; ------ dek¢dov n¡ ‡¡seln‚ho ukazatele

         push      ds
         pop       es
         mov       di,offset NadpCis-11     ; za‡ tek bufferu
         mov       al," "                   ; znak k ulo‘en¡
         cld
         mov       cx,11                    ; d‚lka bufferu
         rep       stosb                    ; vymaz n¡ bufferu
         mov       ax,ds:[RadkuTxt]         ; celkov˜ po‡et © dk–
         call      DekNum                   ; dek¢dov n¡ ‡¡sla
         dec       di                       ; sn¡‘en¡ ukazatele
         mov       byte ptr es:[di],"/"     ; oddˆlova‡
         mov       ax,ds:[TopLine]          ; po‡ te‡n¡ © dek
         inc       ax
         call      DekNum                   ; dek¢dov n¡ po‡ te‡n¡ho © dku

; ------ p©¡prava registr–

         les       di,ds:[AdrVRAM]          ; adresa videopamˆti
         add       di,2*(offset(Nadpis0-Nadpis)+13+1) ; po‡ te‡n¡ pozice k zobr.
         mov       bp,80-offset(Nadpis0-Nadpis)-13-2 ; celkov  d‚lka ukazatele
         cld

; ------ stanoven¡ d‚lky kurzoru

         mov       ax,ds:[RadkuTxt]         ; celkov˜ po‡et © dk–
         sub       ax,ds:[TopLine]          ; po‡et zbyl˜ch © dk–
         ja        DispUk04
         mov       ax,1                     ; omezen¡ p©i podte‡en¡
DispUk04:cmp       ax,ds:[DispRow]          ; je ji‘ konec souboru ?
         jbe       DispUk05                 ; je ji‘ konec souboru
         mov       ax,ds:[DispRow]          ; omezen¡ na po‡et zobrazen˜ch © dk–
DispUk05:mul       bp                       ; po‡et © dk– * koeficient d‚lky
         div       word ptr ds:[RadkuTxt]   ; ¨¡©ka kurzoru relativnˆ
         or        ax,ax                    ; je d‚lka 0 ?
         jnz       DispUk1
         inc       ax                       ; d‚lka = 1
DispUk1: cmp       ax,bp                    ; p©ekro‡ila se d‚lka ukazatele ?
         jbe       DispUk2                  ; d‚lka je OK
         mov       ax,bp                    ; omezen¡ d‚lky kurzoru
DispUk2: mov       bx,ax                    ; £schova d‚lky kurzoru

; ------ stanoven¡ po‡ te‡n¡ pozice kurzoru

         mov       ax,ds:[TopLine]          ; po‡ te‡n¡ © dek
         mul       bp                       ; po‡ te‡n¡ © dek * koeficient d‚lky
         div       word ptr ds:[RadkuTxt]   ; po‡ te‡n¡ pozice relativnˆ
         mov       cx,ax                    ; £schova po‡ te‡n¡ pozice
         add       ax,bx                    ; koncov  pozice
         cmp       ax,bp                    ; p©ekro‡ena d‚lka ukazatele ?
         jbe       DispUk3                  ; d‚lka je OK
         mov       cx,bp                    ; konec ukazatele
         sub       cx,bx                    ; po‡ te‡n¡ pozice

; ------ oprava konce kurzoru

DispUk3: mov       ax,ds:[TopLine]          ; po‡ te‡n¡ © dek
         add       ax,ds:[DispRow]          ; ‡¡slo koncov‚ho © dku
         cmp       ax,ds:[RadkuTxt]         ; zobrazen konec ?
         jb        DispUk4                  ; nen¡ zobrazen konec
         mov       cx,bp                    ; konec ukazatele
         sub       cx,bx                    ; po‡ te‡n¡ pozice ukazatele

; ------ zobrazen¡ ukazatele

DispUk4: sub       bp,cx                    ; ode‡ten¡ po‡ tku ukazatele
         sub       bp,bx                    ; ode‡ten¡ d‚lky ukazatele
         mov       al,ds:[GRAFX3]           ; po‡ te‡n¡ znak ukazatele
         mov       ah,ds:[Barva2]           ; barva nadpisu
         rep       stosw                    ; zobrazen¡ po‡ tku ukazatele
         mov       al,ds:[GRAFX4]           ; ukazatel
         mov       cx,bx                    ; d‚lka ukazatele
         rep       stosw                    ; zobrazen¡ ukazatele
         mov       cx,bp                    ; konec ukazatele
         mov       al,ds:[GRAFX3]           ; znak konce ukazatele
         rep       stosw                    ; zobrazen¡ konce ukazatele

         mov       al," "
         stosw

; ------ n vrat registr–

         pop       es
         pop       bp
         pop       di
         pop       cx
         pop       bx
         pop       ax
         ret

DispUkaz ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ nadpisu hled n¡
; -----------------------------------------------------------------------------

DispNad2 PROC      NEAR

         push      ax
         push      cx
         push      dx
         push      si
         push      di
         push      es

; ------ p©¡prava registr–

         mov       ah,ds:[Barva2]           ; barva nadpisu
         les       di,ds:[AdrVRAM]

; ------ zobrazen¡ n povˆdy

         mov       si,offset Nadpis2
         mov       cx,offset(Nadpis0-Nadpis)+13 ; d‚lka textu
         call      DispAsc                  ; zobrazen¡ textu

         pop       es
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       ax
         ret

DispNad2 ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ bˆ‘n‚ho nadpisu
; -----------------------------------------------------------------------------

DispNadp PROC      NEAR

         push      ax
         push      cx
         push      dx
         push      si
         push      di
         push      es

; ------ p©¡prava registr–

         mov       ah,ds:[Barva2]           ; barva nadpisu
         les       di,ds:[AdrVRAM]

; ------ zobrazen¡ jm‚na souboru

         cld
         mov       al," "
         stosw                              ; £vodn¡ mezera
         mov       si,ds:[AdrSoub]
         mov       cx,13
         call      DispAsc

; ------ zobrazen¡ n povˆdy

         mov       si,offset Nadpis
         mov       cx,offset(Nadpis0-Nadpis) ; d‚lka textu
         call      DispAsc

         pop       es
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       ax
         ret

DispNadp ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ ©etˆzce ASCIIZ (d‚lka CX)
; -----------------------------------------------------------------------------

DispAsc  PROC      NEAR

         cld
DispAsc1:lodsb
         cmp       al,0
         jne       DispAsc2
         mov       al," "
         dec       si
DispAsc2:stosw
         loop      DispAsc1
         ret

DispAsc  ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ aktivn¡ str nky
; -----------------------------------------------------------------------------

Disp     PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         push      ds
         push      es

; ------ p©¡prava registr– k zobrazen¡

         les       di,cs:[AdrVRAM]          ; adresa videopamˆti
         add       di,160                   ; adresa druh‚ho © dku na displeji
         lds       si,cs:[TopAdr]           ; adresa po‡ tku str nky
         mov       bp,word ptr cs:[MaxOffs] ; maxim ln¡ offset
         mov       dh,cs:[MaxRow]           ; po‡et © dk– k zobrazen¡
         mov       dl,cs:[ParamC]           ; parametry a p©ep¡na‡e
         xor       bx,bx
         mov       ax,cs:[FindLine]
         sub       ax,cs:[TopLine]          ; offset od po‡ tku obrazovky
         inc       ax                       ; p©ednastaven¡
         mov       cs:[FindLin0],ax         ; ‡¡ta‡ k zobrazen¡ nalezen‚ho © dku
         cld

; ------ p©¡prava k zobrazen¡ bloku

         mov       word ptr cs:[Blok1Cit],-1 ; blok vypnut
         mov       word ptr cs:[Blok2Cit],-1 ; blok vypnut
         and       byte ptr cs:[Param],not 10h ; vypnut¡ bloku
         test      byte ptr cs:[Param],20h  ; je blok vypnut ?
         jz        Disp1                    ; blok je vypnut

         mov       ax,cs:[Blok1Lin]         ; © dek po‡ tku bloku
         mov       cx,cs:[Blok2Lin]         ; © dek konce bloku
         cmp       ax,cx                    ; je konec za za‡ tkem ?
         jbe       Disp02                   ; po©ad¡ je OK
         xchg      ax,cx                    ; oprava po©ad¡
Disp02:  sub       ax,cs:[TopLine]          ; offset od po‡ tku
         jae       Disp03                   ; nen¡ podte‡en¡
         xor       byte ptr cs:[Param],10h  ; zmˆna p©¡znaku
Disp03:  inc       ax
         mov       cs:[Blok1Cit],ax         ; ‡¡ta‡ po‡ tku
         inc       cx
         sub       cx,cs:[TopLine]          ; offset od po‡ tku
         jae       Disp04                   ; nen¡ podte‡en¡
         xor       byte ptr cs:[Param],10h  ; zmˆna p©¡znaku
Disp04:  inc       cx
         mov       cs:[Blok2Cit],cx         ; ‡¡ta‡ konce

; ------ obsluha zobrazen¡ bloku

Disp1:   dec       word ptr cs:[Blok1Cit]   ; ‡¡ta‡ po‡ tku bloku
         jnz       Disp11                   ; nen¡ po‡ tek bloku
         xor       byte ptr cs:[Param],10h  ; p©¡znak © dku uvnit© bloku
Disp11:  dec       word ptr cs:[Blok2Cit]   ; ‡¡ta‡ konce bloku
         jnz       Disp14                   ; nen¡ konec bloku
         xor       byte ptr cs:[Param],10h  ; p©¡znak © dku uvnit© bloku

; ------ stanoven¡ barvy © dku, p©¡prava k zobrazen¡ © dku

Disp14:  mov       ah,cs:[Barva1]           ; barva © dku s nalezen˜m textem
         dec       word ptr cs:[FindLin0]   ; ‡¡ta‡ nalezen‚ho © dku
         jz        Disp2                    ; je to nalezen˜ © dek
         test      byte ptr cs:[Param],10h  ; je to © dek uvnit© bloku ?
         jnz       Disp2                    ; je to © dek uvnit© bloku
         mov       ah,cs:[Barva]            ; barva bˆ‘n‚ho © dku
Disp2:   mov       cx,cs:[TopPoz]           ; po‡ te‡n¡ pozice © dku
         mov       cs:[TopPoz0],cx          ; ‡¡ta‡ po‡ te‡n¡ pozice © dku
         mov       cx,80                    ; po‡et znak– na © dek

; ------ zobrazen¡ textu © dku

Disp3:   cmp       si,bp                    ; je koncov˜ offset ?
         je        Disp36                   ; je koncov˜ offset
Disp300: lodsb                              ; na‡ten¡ znaku
         or        si,si                    ; je p©ete‡en¡ segmentu ?
         jz        Disp31                   ; je p©ete‡en¡ segmentu
Disp30:  cmp       al,10                    ; je konec © dku LF ?
         je        Disp4                    ; je konec © dku LF
         cmp       al,13                    ; je CR ?
         je        Disp3                    ; CR se ignoruje
         jcxz      Disp3                    ; je ji‘ konec © dku
         cmp       al,9                     ; je tabel tor ?
         je        Disp32                   ; zobrazen¡ tabel toru

; ------ filtr znak–

         dec       word ptr cs:[TopPoz0]    ; ‡¡ta‡ po‡ te‡n¡ pozice
         jns       Disp3                    ; znak se nezobraz¡
         mov       bl,al                    ; znak ke konverzi
         mov       al,cs:[bx+Filtr]         ; konverze znaku
         stosw                              ; ulo‘en¡ znaku
         dec       cx
         jmp       short Disp3

; ------ posun segmentu adresy

Disp31:  push      ax
         mov       ax,ds                    ; segment adresy
         add       ax,1000h                 ; posun segmentu adresy
         mov       ds,ax                    ; nov˜ segment adresy
         pop       ax
         jmp       short Disp30

; ------ zobrazen¡ tabel toru

Disp32:  mov       al," "                   ; zobraz¡ se mezera
Disp33:  dec       word ptr cs:[TopPoz0]    ; ‡¡ta‡ po‡ te‡n¡ pozice
         jns       Disp34                   ; znak se nezobraz¡
         stosw                              ; ulo‘en¡ znaku mezery
         dec       cx                       ; sn¡‘en¡ ‡¡ta‡e znak–
         jz        Disp3
Disp34:  push      ax
         mov       al,byte ptr cs:[TopPoz]
         sub       al,byte ptr cs:[TopPoz0]
         test      al,7
         pop       ax
         jnz       Disp33                   ; nen¡ tabela‡n¡ pozice - dal¨¡ znak
         jmp       short Disp3


; ------ je koncov˜ offset - test, zda je t‚‘ koncov˜ segment

Disp36:  push      ax
         mov       ax,ds                    ; segment adresy
         cmp       ax,cs:[MaxSegm]          ; je koncov˜ segment ?
         pop       ax
         jne       Disp300                  ; nen¡ koncov˜ segment

; ------ vymaz n¡ zbytku © dku

Disp4:   mov       al," "                   ; znak mezery k vymaz n¡
         rep       stosw                    ; vymaz n¡ zbytku © dku

; ------ p©¡prava k zobrazen¡ dal¨¡ho © dku

         dec       dh                       ; ‡¡ta‡ © dk– k zobrazen¡
         jz        Disp6
         jmp       Disp1                    ; zobrazen¡ dal¨¡ho © dku

; ------ n vrat registr–

Disp6:   pop       es
         pop       ds
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

Disp     ENDP

; -----------------------------------------------------------------------------
;        vypnut¡ kurzoru
; -----------------------------------------------------------------------------

KurzOff  PROC      NEAR

         push      ax
         push      dx

         mov       dl,0
         mov       dh,cs:[Radku]
         mov       ah,2
         call      Int10P

         pop       dx
         pop       ax
         ret

KurzOff  ENDP

; -----------------------------------------------------------------------------
;        obsluha INT 10h
; -----------------------------------------------------------------------------

Int10P   PROC      NEAR

         push      bx
         push      si
         push      di
         push      bp
         push      ds
         push      es

         mov       bh,cs:[AktPage]
         int       10h

         pop       es
         pop       ds
         pop       bp
         pop       di
         pop       si
         pop       bx
         ret

Int10P   ENDP

; *****************************************************************************
;
;                   Dek¢dov n¡ p©¡kazov‚ho © dku
;
; *****************************************************************************

; -----------------------------------------------------------------------------
;        rozbor p©¡kazov‚ho © dku (CY=chyba zad n¡)
; -----------------------------------------------------------------------------

Rozbor   PROC      NEAR

; ------ p©¡prava p©¡kazov‚ho © dku

         mov       si,81h                   ; za‡ tek p©¡kazov‚ho © dku
         mov       bl,ds:[si-1]             ; d‚lka textu p©¡kazov‚ho © dku
         and       bl,7fh                   ; pojistka
         mov       bh,0
         mov       byte ptr ds:[bx+si],0    ; ozna‡en¡ konce textu
         push      ds
         pop       es

; ------ rozbor parametr– na za‡ tku

         call      ParPar                   ; rozbor parametr–
         jc        Rozbor9                  ; chyba zad n¡

; ------ rozbor jm‚na vstupn¡ho souboru

         mov       di,offset Soubor1        ; jm‚no vstupn¡ho souboru
         call      ParFil                   ; rozbor jm‚na vstupn¡ho souboru
         jc        Rozbor9                  ; nen¡ zad n - chyba
         mov       ds:[ADrSoub],bx          ; £schova adresy jm‚na souboru

; ------ rozbor parametr–

         call      ParPar                   ; rozbor parametr–
         jc        Rozbor9                  ; chyba zad n¡

; ------ rozbor jm‚na v˜stupn¡ho souboru

         mov       di,offset Soubor2        ; buffer jm‚na v˜stupn¡ho souboru
         call      ParFil                   ; rozbor jm‚na v˜stupn¡ho souboru
         jc        Rozbor8                  ; v˜stupn¡ soubor nen¡ zad n
         or        byte ptr ds:[Param],8    ; p©¡znak zad n¡ v˜stupn¡ho souboru

; ------ rozbor parametr–

         call      ParPar                   ; rozbor parametr–
         jc        Rozbor9                  ; chyba zad n¡

Rozbor8: clc
Rozbor9: ret

Rozbor   ENDP

; -----------------------------------------------------------------------------
;        parametry (CY=chyba zad n¡)
; -----------------------------------------------------------------------------

ParPar   PROC      NEAR

; ------ nalezen¡ za‡ tku parametr–

         call      ParSpc
         jc        ParPar8
         cmp       al,"/"
         jne       ParPar8

; ------ na‡ten¡ znaku parametru

         call      ParChr
         call      ParChr
         cmp       al,"G"
         jne       ParPar5

; ------ nalezen¡ za‡ tku textu hesla

         call      ParSpc
         jc        ParPar8
         cmp       al,'"'
         jne       ParPar8
         call      ParChr

; ------ p©enesen¡ textu hesla

         push      di
         mov       di,offset Heslo
ParPar2: call      ParChr
         jc        ParPar4
         cmp       al,'"'
         je        ParPar4
         cmp       di,offset Heslo+40
         jae       ParPar3
         mov       ds:[di],al
         inc       di
ParPar3: jmp       short ParPar2
ParPar4: sub       di,offset Heslo
         mov       ds:[HesloN],di
         pop       di
         jmp       short ParPar             ; dal¨¡ parametr

; ------ zad n¡ n rodn¡ho k¢du

ParPar5: mov       dl,0
         cmp       al,"K"
         je        ParPar6                  ; je k¢d Kamenick˜ch
         inc       dx
         cmp       al,"L"
         je        ParPar6                  ; je k¢d Latin 2
         inc       dx
         cmp       al,"I"
         jne       ParPar7                  ; nen¡ ani k¢d KOI 8 - chyba zad n¡
ParPar6: mov       byte ptr ds:[Prepin],dl  ; p©ep¡na‡ n rodn¡ho k¢du
         jmp       short ParPar             ; dal¨¡ parametr

; ------ chyba zad n¡

ParPar7: stc
         ret

ParPar8: clc
ParPar9: ret

ParPar   ENDP

; -----------------------------------------------------------------------------
;        dek¢dov n¡ jm‚na souboru ES:DI (BX=adresa jm‚na)
; -----------------------------------------------------------------------------

ParFil   PROC      NEAR

; ------ nalezen¡ za‡ tku jm‚na souboru

         call      ParSpc
         jc        ParFil9
         cmp       al,"/"
         stc
         je        ParFil9

; ------ p©enesen¡ jm‚na souboru do bufferu

ParFil1: mov       bx,di                    ; adresa jm‚na souboru
ParFil2: call      ParChr
         cld
         jc        ParFil4
         je        ParFil3
         cmp       al,"/"
         je        ParFil3
         stosb
         cmp       al,":"
         je        ParFil1
         cmp       al,"\"
         je        ParFil1
         jmp       short ParFil2

; ------ ukon‡en¡ jm‚na

ParFil3: dec       si
parFil4: mov       al,0
         stosb
         clc

ParFil9: ret

ParFil   ENDP

; -----------------------------------------------------------------------------
;        vypu¨tˆn¡ mezer z p©¡kazov‚ho © dku
; -----------------------------------------------------------------------------

ParSpc   PROC      NEAR

         call      ParChr                   ; vstup znaku z p©¡kazov‚ho © dku
         jc        ParSpc2                  ; konec textu
         je        ParSpc                   ; mezera - vypu¨tˆn¡
         dec       si                       ; n vrat posledn¡ho znaku
ParSpc2: ret

ParSpc   ENDP

; -----------------------------------------------------------------------------
;        dek¢dov n¡ p©¡kazov‚ho © dku
; -----------------------------------------------------------------------------

ParChr   PROC      NEAR

; ------ na‡ten¡ znaku z p©¡kazov‚ho © dku

         cld
         lodsb                              ; na‡ten¡ znaku z p©¡kazov‚ho © dku

; ------ n hrada znaku velk˜m p¡smenem

         call      UpCase

; ------ n hrada tabel toru mezerou

         cmp       al,9
         jne       ParChr2
         mov       al," "                   ; n hrada tabel toru mezerou

; ------ test, zda je konec textu

ParChr2: cmp       al," "                   ; je platn˜ znak ?
         jae       ParChr3                  ; nen¡ konec textu
         dec       si                       ; n vrat posledn¡ho znaku

ParChr3: ret

ParChr   ENDP

; -----------------------------------------------------------------------------
;        inicializace tabulek font–
; -----------------------------------------------------------------------------

InitTab  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      si
         push      di
         push      es

; ------ konverze vlastn¡ch text–

         mov       al,"|"
         mov       bl,"-"
         mov       bh,"#"
         test      byte ptr ds:[ParamC],4   ; vypnuta ‡e¨tina ?
         jnz       InitTb00                 ; vypnuta ‡e¨tina

         mov       al,ds:[ParamC]
         and       al,3
         cmp       al,2                     ; je k¢d KOI 8 ?

         mov       al,179                   ; ‡ ra Kamenick˜ch
         mov       bl,176                   ; kurzor 1
         mov       bh,178                   ; kurzor 2

         jne       InitTb00                 ; nen¡ KOI 8

         mov       al,231                   ; ‡ ra KOI 8
         mov       bl,141                   ; kurzor 1
         mov       bh,159                   ; kurzor 2

InitTb00:mov       byte ptr ds:[Nadpis],al  ;
         mov       byte ptr ds:[GRAFX1],al
         mov       byte ptr ds:[GRAFX2],al
         mov       byte ptr ds:[GRAFX3],bl  ; kurzor 1
         mov       byte ptr ds:[GRAFX4],bh  ; kurzor 2
         mov       byte ptr ds:[GRAFX5],al

; ------ tabulka pro p©evod na velk  p¡smena

         mov       bl,byte ptr ds:[ParamC]  ; k¢d displeje
         and       bx,3                     ; k¢d displeje

;                                          ;* oprava KOI 8 podle displeje
;         mov       ax,offset TbKOICS0       ; k¢d s KOI 8
;         cmp       bl,2                     ; je KOI 8 ?
;         je        InitTb08                 ; je KOI 8
;         mov       ax,offset TbKOICS1       ; je k¢d bez KOI 8
;InitTb08:mov       ds:[TXK1],ax
;         mov       ds:[TXK2],ax

         mov       al,byte ptr ds:[Prepin]  ; p©ep¡na‡e ze souboru
         and       al,3                     ; p©ep¡na‡e
         cmp       al,3                     ; je k¢d IBM ?
         jne       InitTb01                 ; nen¡ k¢d IBM
         or        bl,3                     ; pro k¢d IBM se nek¢duje
InitTb01:shl       bl,1                     ; offset v tabulce adres
         mov       si,ds:[bx+TabUpC]        ; adresa konverzn¡ tabulky

; ------ £schova tabulky pro velk  p¡smena

         push      ds
         pop       es
         mov       di,offset FiltrU0
         mov       cx,128
         cld
         rep       movsb                    ; £schova tabulky

; ------ tabulka pro odstranˆn¡ diakritiky p©i zad n¡

         test      byte ptr ds:[ParamC],4   ; je filtr ‡e¨tiny ?
         jnz       InitTb02                 ; je filtr ‡e¨tiny
         or        bl,6                     ; nen¡ filtr - nekonvertuje se
InitTb02:mov       si,ds:[bx+TabCS0]        ; filtr odstranˆn¡ diakritiky

; ------ £schova tabulky pro odstranˆn¡ diakritiky p©i zad n¡

         mov       di,offset FiltrZ0
         mov       cx,128
         rep       movsb                    ; £schova tabulky

; ------ tabulka pro zobrazen¡

         mov       bl,byte ptr ds:[Prepin]  ; p©ep¡na‡e ze souboru
         and       bx,3                     ; k¢d souboru
         mov       al,ds:[ParamC]           ; sou‡asn‚ nastaven¡ displeje
         test      al,4                     ; je vypnuta ‡e¨tina ?
         jz        InitTab1                 ; nen¡ vypnuta
         or        al,3                     ; k¢d vypnut¡
InitTab1:and       al,3                     ; k¢d zobrazen¡
         shl       bl,1                     ; offset v tabulce adres
         shl       bl,1
         or        bl,al
         shl       bl,1

; ------ £schova tabulky pro zobrazen¡

         mov       si,ds:[bx+TabDisp]       ; adresa konverzn¡ tabulky
         mov       di,offset Filtr0
         mov       cx,128
         rep       movsb                    ; £schova tabulky

; ------ n vrat registr–

         pop       es
         pop       di
         pop       si
         pop       cx
         pop       bx
         pop       ax
         call      KonvFND                  ; konverze textu pro hled n¡
         ret

InitTab  ENDP

; -----------------------------------------------------------------------------
;        konverze zad van‚ho textu
; -----------------------------------------------------------------------------

KonvFND  PROC      NEAR

; ------ £schova registr–

         push      ax
         push      bx
         push      cx
         push      si

; ------ konverze textu

         xor       bx,bx
         mov       cx,MAXFND                ; d‚lka textu
         mov       si,offset FndTxt         ; buffer hledan‚ho textu
         cld
KonvFND1:lodsb                              ; na‡ten¡ znaku
         mov       bl,al                    ; offset v tabulce
         mov       bl,ds:[bx+FiltrZ]        ; odstranˆn¡ diakritiky
         mov       al,ds:[bx+FiltrU]        ; p©evod na velk‚ p¡smeno
         mov       ds:[si+FndTxt0-FndTxt-1],al ; ulo‘en¡ znaku do tabulky
         loop      KonvFND1                 ; dal¨¡ znak

; ------ n vrat registr–

         pop       si
         pop       cx
         pop       bx
         pop       ax
         ret

KonvFND  ENDP

; -----------------------------------------------------------------------------
;        zad n¡ hesla k dek¢dov n¡ souboru (CY=p©eru¨en¡ operace)
; -----------------------------------------------------------------------------
;þ
ZadHesl  PROC      NEAR

; ------ test, zda m  b˜t heslo zad no

         test      byte ptr cs:[Prepin],4   ; je soubor k¢dov n heslem ?
         jnz       ZadHesl1                 ; soubor je k¢dov n heslem
         ret

; ------ £schova registr–

ZadHesl1:push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      ds

         push      cs
         pop       ds

; ------ test, zda bylo heslo ji‘ zad no

         cmp       byte ptr ds:[HesloN],0   ; bylo heslo ji‘ zad no ?
         jne       ZadHesl5                 ; heslo ji‘ bylo zad no

; ------ zobrazen¡ v˜zvy k zad n¡ hesla

         mov       dx,offset HesloTxt
         mov       ah,9
         int       21h                      ; zobrazen¡ v˜zvy k zad n¡ hesla

; ------ p©¡prava k zad v n¡ hesla

         mov       si,offset Heslo          ; buffer k zad v n¡ hesla

; ------ vstup znaku z kl vesnice

ZadHesl2:mov       ah,0
         int       16h

; ------ ukon‡en¡ zad v n¡ hesla

         cmp       al,0dh
         je        ZadHesl4

; ------ p©eru¨en¡ operace ESC, Ctrl-Break

         or        ax,ax
         jz        ZadHes22
         cmp       al,1bh
         jne       ZadHes23
ZadHes22:stc
         jmp       short ZadHesl9

; ------ zru¨en¡ jednoho znaku

ZadHes23:cmp       al,8
         je        ZadHes24
         cmp       ax,0e7fh
         jne       ZadHes25
ZadHes24:cmp       si,offset Heslo
         jbe       ZadHesl2
         call      DelCh
         dec       si
         jmp       short ZadHesl2

; ------ zad n¡ jednoho znaku

ZadHes25:cmp       al," "
         jb        ZadHesl2
         cmp       si,offset Heslo + 40
         jae       ZadHesl2
         call      UpCase
         mov       ds:[si],al
         inc       si
         mov       al,"*"
         call      DispCh
         jmp       short ZadHesl2

; ------ £schova d‚lky zadan‚ho textu

ZadHesl4:sub       si,offset Heslo
         jbe       ZadHes22                 ; nen¡ nic zad no - p©eru¨en¡
         mov       ds:[HesloN],si           ; d‚lka hesla

; ------ vymaz n¡ textu

         mov       cx,si                    ; d‚lka textu hesla
         add       cx,offset(HesloTx0-HesloTxt)-1
ZadHes42:call      DelCh
         loop      ZadHes42

; ------ sestaven¡ k¢dovac¡ho kl¡‡e

ZadHesl5:mov       cx,ds:[HesloN]           ; d‚lka textu hesla
         mov       di,offset Klic           ; k¢dovac¡ kl¡‡
         mov       bx,257                   ; d‚lka k¢dovac¡ho kl¡‡e
         mov       dx,-1                    ; v˜choz¡ hodnota k¢du

; ------ v˜po‡et k¢dovac¡ho kl¡‡e

ZadHesl7:mov       si,offset Heslo          ; buffer s textem hesla
         call      CheckSum                 ; kontroln¡ sou‡et kl¡‡e
         mov       ds:[di],dl               ; ni‘¨¡ bajt kontroln¡ho sou‡tu
         inc       di                       ; zv˜¨en¡ ukl dac¡ adresy v kl¡‡i
         dec       bx                       ; ‡¡ta‡ d‚lky kl¡‡e
         jnz       ZadHesl7                 ; dal¨¡ znak kl¡‡e

         call      Dekod                    ; prvn¡ dek¢dov n¡ bufferu
         clc

; ------ n vrat registr–

ZadHesl9:pop       ds
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

ZadHesl  ENDP

; -----------------------------------------------------------------------------
;        Kontroln¡ sou‡et CRC bloku dat
; -----------------------------------------------------------------------------
; VSTUP: DS:SI=za‡ tek bloku dat
;        CX=po‡et bajt–
;        DX=vstupn¡ hodnota kontroln¡ho sou‡tu
; VSTUP:DX=v˜stupn¡ hodnota kontroln¡ho sou‡tu
; -----------------------------------------------------------------------------

CheckSum PROC      NEAR

         push      ax
         push      cx
         push      di
         jcxz      CheckSm2                 ; nejsou ‘ dn  data
         mov       di,cx                    ; po‡et bajt– pro kontroln¡ sou‡et
         cld                                ; smˆr nahoru

CheckSm1:lodsb                              ; a = fgetc(fp);
         xor       al,dh                    ; d = a ^= d;
         mov       dh,al
         mov       cl,4
         shr       al,cl                    ; a >>= 4;
         xor       al,dh                    ; d = a ^= d;
         mov       dh,al
         mov       cl,3
         ror       al,cl                    ; a = (a >> 3) | (a << 5);
         mov       ch,al                    ; f = a;
         and       al,1fh                   ; a &= 0x1f;
         xor       al,dl                    ; e = a ^= e;
         mov       dl,al
         mov       al,ch                    ; a = f;
         ror       al,1                     ; a = (a >> 1) | (a << 7);
         and       al,0f0h                  ; a &= 0xf0;
         xor       al,dl                    ; e = a ^= e;
         mov       dl,al
         mov       al,ch                    ; a = f & 0xe0;
         and       al,0e0h
         xor       al,dh                    ; a ^= d;
         mov       dh,dl                    ; d = e;
         mov       dl,al                    ; e = a;

         dec       di
         jnz       CheckSm1                 ; dal¨¡ bajt

CheckSm2:pop       di
         pop       cx
         pop       ax
         ret

CheckSum ENDP

; -----------------------------------------------------------------------------
;        zru¨en¡ znaku DOS
; -----------------------------------------------------------------------------

DelCh    PROC      NEAR

         push      ax
         mov       al,8
         call      DispCh
         mov       al," "
         call      DispCh
         mov       al,8
         call      DispCh
         pop       ax
         ret

DelCh    ENDP

; -----------------------------------------------------------------------------
;        zobrazen¡ znaku DOS
; -----------------------------------------------------------------------------

DispCh   PROC      NEAR

         push      ax
         push      dx
         mov       dl,al
         mov       ah,2
         int       21h
         pop       dx
         pop       ax
         ret

DispCh   ENDP

; -----------------------------------------------------------------------------
;        konverze znaku na velk‚ p¡smeno
; -----------------------------------------------------------------------------

UpCase   PROC      NEAR

         cmp       al,"a"
         jb        UpCase2
         cmp       al,"z"
         ja        UpCase2
         sub       al,32
UpCase2: ret

UpCase   ENDP

; *****************************************************************************
;
;                                data
;
; *****************************************************************************

Blok1Adr dd        0                        ; adresa za‡ tku bloku
Blok1Lin dw        0                        ; © dek za‡ tku bloku
Blok1Cit dw        0                        ; ‡¡ta‡ © dku za‡ tku bloku

Blok2Adr dd        0                        ; adresa konce bloku
Blok2Lin dw        0                        ; © dek konce bloku
Blok2Cit dw        0                        ; ‡¡ta‡ © dku konce bloku

Param    db        0                        ; parametry
                                            ;  bit 2: 1=vyti¨tˆn¡ bloku
                                            ;  bit 3: 1=zad n v˜stupn¡ soubor
                                            ;  bit 4: 1=© dek je uvnit© bloku
                                            ;  bit 5: 1=zobrazen¡ kurzoru bloku
                                            ;  bit 6: 1=re‘im hled n¡ textu
                                            ;  bit 7: 1=smˆr hled n¡ zpˆt

Prepin   dw        3                        ; p©ep¡na‡e ze souboru
                                            ;   bit 0,1: 0=k¢d Kamenick˜ch
                                            ;            1=k¢d Latin 2
                                            ;            2=k¢d KOI 8
                                            ;            3=k¢d IBM
                                            ;   bit 2: 1=soubor zak¢dov n heslem

                                          ;* tabulky konverz¡ pro displej
TabDisp  dw        Tb00                     ; KAM -> KAM
         dw        TbKamLat                 ; KAM -> LAT
         dw        TbKamKOI                 ; KAM -> KOI
         dw        TbKamCS0                 ; KAM -> 0

         dw        TbLatKam                 ; LAT -> KAM
         dw        Tb00                     ; LAT -> LAT
         dw        TbLatKOI                 ; LAT -> KOI
         dw        TbLatCS0                 ; LAT -> 0

         dw        TbKOIKam                 ; KOI -> KAM
         dw        TbKOILat                 ; KOI -> LAT
         dw        Tb00                     ; KOI -> KOI
TXK1     dw        TbKOICS0                 ; KOI -> 0

         dw        Tb00                     ; IBM -> KAM
         dw        Tb00                     ; IBM -> LAT
         dw        Tb00                     ; IBM -> KOI
         dw        Tb00                     ; IBM -> 0

                                          ;* tabulky velk˜ch p¡smen displeje
TabUpC   dw        TbKamUpC                 ; Kamenick˜ch
         dw        TbLatUpC                 ; Latin 2
         dw        TbKOIUpC                 ; KOI 8
         dw        Tb00                     ; IBM

                                          ;* tabulky odstranˆn¡ diakritiky
TabCS0   dw        TbKamCS0                 ; KAM -> 0
         dw        TbLatCS0                 ; LAT -> 0
TXK2     dw        TbKOICS0                 ; KOI -> 0
         dw        Tb00                     ; IBM -> 0

; ------ filtr znak– pro zobrazen¡ (z vnit©n¡ch font– na fonty displeje)

Filtr    db          0,  1,  2,  3,  4,  5,  6,  7,  8,  9, 10, 11, 12, 13, 14, 15
         db         16, 17, 18, 19, 20, 21, 22, 23, 24, 25, 26, 27, 28, 29, 30, 31
         db         32, 33, 34, 35, 36, 37, 38, 39, 40, 41, 42, 43, 44, 45, 46, 47
         db         48, 49, 50, 51, 52, 53, 54, 55, 56, 57, 58, 59, 60, 61, 62, 63
         db         64, 65, 66, 67, 68, 69, 70, 71, 72, 73, 74, 75, 76, 77, 78, 79
         db         80, 81, 82, 83, 84, 85, 86, 87, 88, 89, 90, 91, 92, 93, 94, 95
         db         96, 97, 98, 99,100,101,102,103,104,105,106,107,108,109,110,111
         db        112,113,114,115,116,117,118,119,120,121,122,123,124,125,126,127

Filtr0   db        128 dup(0)               ; konverzn¡ tabulka 128 a‘ 255

; ------ filtr velk˜ch p¡smen (podle k¢du displeje)

FiltrU   db          0,  1,  2,  3,  4,  5,  6,  7,  8,  9, 10, 11, 12, 13, 14, 15
         db         16, 17, 18, 19, 20, 21, 22, 23, 24, 25, 26, 27, 28, 29, 30, 31
         db         32, 33, 34, 35, 36, 37, 38, 39, 40, 41, 42, 43, 44, 45, 46, 47
         db         48, 49, 50, 51, 52, 53, 54, 55, 56, 57, 58, 59, 60, 61, 62, 63
         db         64, 65, 66, 67, 68, 69, 70, 71, 72, 73, 74, 75, 76, 77, 78, 79
         db         80, 81, 82, 83, 84, 85, 86, 87, 88, 89, 90, 91, 92, 93, 94, 95
         db         96, 65, 66, 67, 68, 69, 70, 71, 72, 73, 74, 75, 76, 77, 78, 79
         db         80, 81, 82, 83, 84, 85, 86, 87, 88, 89, 90,123,124,125,126,127

FiltrU0  db        128 dup(0)               ; konverzn¡ tabulka 128 a‘ 255

; ------ odstranˆn¡ diakritiky p©i zad v n¡ textu (podle k¢du displeje)

FiltrZ   db          0,  1,  2,  3,  4,  5,  6,  7,  8,  9, 10, 11, 12, 13, 14, 15
         db         16, 17, 18, 19, 20, 21, 22, 23, 24, 25, 26, 27, 28, 29, 30, 31
         db         32, 33, 34, 35, 36, 37, 38, 39, 40, 41, 42, 43, 44, 45, 46, 47
         db         48, 49, 50, 51, 52, 53, 54, 55, 56, 57, 58, 59, 60, 61, 62, 63
         db         64, 65, 66, 67, 68, 69, 70, 71, 72, 73, 74, 75, 76, 77, 78, 79
         db         80, 81, 82, 83, 84, 85, 86, 87, 88, 89, 90, 91, 92, 93, 94, 95
         db         96, 97, 98, 99,100,101,102,103,104,105,106,107,108,109,110,111
         db        112,113,114,115,116,117,118,119,120,121,122,123,124,125,126,127

FiltrZ0  db        128 dup(0)               ; konverzn¡ tabulka 128 a‘ 255

; ------ tabulka bez konverze

Tb00     db        128,129,130,131,132,133,134,135,136,137,138,139,140,141,142,143
         db        144,145,146,147,148,149,150,151,152,153,154,155,156,157,158,159
         db        160,161,162,163,164,165,166,167,168,169,170,171,172,173,174,175
         db        176,177,178,179,180,181,182,183,184,185,186,187,188,189,190,191
         db        192,193,194,195,196,197,198,199,200,201,202,203,204,205,206,207
         db        208,209,210,211,212,213,214,215,216,217,218,219,220,221,222,223
         db        224,225,226,227,228,229,230,231,232,233,234,235,236,237,238,239
         db        240,241,242,243,244,245,246,247,248,249,250,251,252,253,254,255

; ------ konverzn¡ tabulka z k¢du Kamenick˜ch na velk  p¡smena

TbKamUpC label     byte
         db        '€š…Ž…†€‰‰Š‹œŠŽ'
         db        '’’§™•¦—™š›œž†'
         db        '‹•—¥¥¦§›ž««',172,173,174,175
         db        176,177,178,179,180,181,182,183,184,185,186,187,188,189,190,191
         db        192,193,194,195,196,197,198,199,200,201,202,203,204,205,206,207
         db        208,209,210,211,212,213,214,215,216,217,218,219,220,221,222,223
         db        224,225,226,227,228,229,230,231,232,233,234,235,236,237,238,239
         db        240,241,242,243,244,245,246,247,248,249,250,251,252,253,254,255

;; ------ konverzn¡ tabulka z k¢du Kamenick˜ch na mal  p¡smena
;
;TbKamDnC label     byte
;         db        '‡‚ƒ„ƒŸ‡ˆˆ¡Œ„ '
;         db        '‚‘‘“”¢–£˜”¨Œ˜©Ÿ'
;         db        ' ¡¢£¤¤–“¨©ªª',172,173,174,175
;         db        176,177,178,179,180,181,182,183,184,185,186,187,188,189,190,191
;         db        192,193,194,195,196,197,198,199,200,201,202,203,204,205,206,207
;         db        208,209,210,211,212,213,214,215,216,217,218,219,220,221,222,223
;         db        224,225,226,227,228,229,230,231,232,233,234,235,236,237,238,239
;         db        240,241,242,243,244,245,246,247,248,249,250,251,252,253,254,255

; ------ konverzn¡ tabulka z k¢du Latin 2 na velk  p¡smena

TbLatUpC label     byte
         db        128,154,144,182,142,222,143,128,157,211,138,138,215,141,142,143
         db        144,145,145,226,153,149,149,151,151,153,154,155,155,157,158,172
         db        181,214,224,233,164,164,166,166,168,168,170,141,172,184,174,175
         db        176,177,178,179,180,181,182,183,184,185,186,187,188,189,189,191
         db        192,193,194,195,196,197,198,198,200,201,202,203,204,205,206,207
         db        209,209,210,211,210,213,214,215,183,217,218,219,220,221,222,223
         db        224,225,226,227,227,213,230,230,232,233,232,235,237,237,221,239
         db        240,241,242,243,244,245,246,247,248,249,250,235,252,252,254,255

;; ------ konverzn¡ tabulka z k¢du Latin 2 na mal  p¡smena
;
;TbLatDnC label     byte
;
;         db        135,129,130,131,132,133,134,135,136,137,139,139,140,171,132,134
;         db        130,146,146,147,148,150,150,152,152,148,129,156,156,136,158,159
;         db        160,161,162,163,165,165,167,167,169,169,170,171,159,173,174,175
;         db        176,177,178,179,180,160,131,216,173,185,186,187,188,190,190,191
;         db        192,193,194,195,196,197,199,199,200,201,202,203,204,205,206,207
;         db        208,208,212,137,212,229,161,140,216,217,218,219,220,238,133,223
;         db        162,225,147,228,228,229,231,231,234,163,234,251,236,236,238,239
;         db        240,241,242,243,244,245,246,247,248,249,250,251,253,253,254,255

; ------ konverzn¡ tabulka z k¢du KOI 8 na velk  p¡smena

TbKOIUpC label     byte
         db        128,129,130,131,132,133,134,135,136,137,138,139,140,141,142,143
         db        144,145,146,147,148,149,150,151,152,153,154,155,156,157,158,159
         db        160,161,162,163,164,165,166,167,168,169,170,171,172,173,174,175
         db        176,177,178,179,180,181,182,183,184,185,186,187,188,189,190,191
         db        192,225,194,227,228,229,230,199,232,233,234,235,236,237,238,239
         db        240,241,242,243,244,245,214,247,248,249,250,219,220,221,222,223
         db        224,225,226,227,228,229,230,231,232,233,234,235,236,237,238,239
         db        240,241,242,243,244,245,246,247,248,249,250,251,252,253,254,255

;; ------ konverzn¡ tabulka z k¢du KOI 8 na mal  p¡smena
;
;TbKOIDnC label     byte
;         db        128,129,130,131,132,133,134,135,136,137,138,139,140,141,142,143
;         db        144,145,146,147,148,149,150,151,152,153,154,155,156,157,158,159
;         db        160,161,162,163,164,165,166,167,168,169,170,171,172,173,174,175
;         db        176,177,178,179,180,181,182,183,184,185,186,187,188,189,190,191
;         db        192,193,194,195,196,197,198,199,200,201,202,203,204,205,206,207
;         db        208,209,210,211,212,213,214,215,216,217,218,219,220,221,222,223
;         db        224,193,226,195,196,197,198,231,200,201,202,203,204,205,206,207
;         db        208,209,210,211,212,213,246,215,216,217,218,251,252,253,254,255
;
; ------ konverzn¡ tabulka z k¢du Kamenick˜ch na k¢d bez diakritiky

TbKamCS0 label     byte
         db        'CuedaDTceELIllAA'
         db        'EzZooOuUyOUSLYRt'
         db        'aiounNUOsrrR',172,173,174,175
         db        176,177,178,179,180,181,182,183,184,185,186,187,188,189,190,191
         db        192,193,194,195,196,197,198,199,200,201,202,203,204,205,206,207
         db        208,209,210,211,212,213,214,215,216,217,218,219,220,221,222,223
         db        224,225,226,227,228,229,230,231,232,233,234,235,236,237,238,239
         db        240,241,242,243,244,245,246,247,248,249,250,251,252,253,254,255

; ------ konverzn¡ tabulka z k¢du Latin 2 na k¢d bez diakritiky

TbLatCS0 label     byte
         db        'CueaauccleOoiZAC'
         db        'ELlooLlSsOUTtLxc'
         db        'aiouAaZzEerzCs',174,175
         db        176,177,178,179,180,'AAES',185,186,187,188,'Zz',191
         db        192,193,194,195,196,197,'Aa',200,201,202,203,204,205,206,207
         db        'dDDEdNIIe',217,218,219,220,'TU',223
         db        'O',225,'ONnnSsRUrUyYt',239
         db        240,241,242,243,244,245,246,247,248,249,250,'uRr',254,255

;; ------ konverzn¡ tabulka z k¢du KOI 8 na k¢d bez diakritiky - k¢d IBM
;
;TbKoiCS0 label     byte
;         db        128,129,130,131,132,133,134,135,136,137,138,139,140,141,142,143
;         db        144,145,146,147,148,149,150,151,152,153,154,155,156,157,158,159
;         db        160,161,162,163,164,165,166,167,168,169,170,171,172,173,174,175
;         db        176,177,178,179,180,181,182,183,184,185,186,187,188,189,190,191
;         db        'Ú','a','À','cder','Ä','uiullono'
;         db        'oarstu','Ã','eayz','Â',220,221,222,'Å'
;         db        '¿','A','Ù','CDER','³','UIULLONO'
;         db        'OARSTU','´','EAYZ','Á',252,253,254,255

; ------ konverzn¡ tabulka z k¢du KOI 8 na k¢d bez diakritiky

TbKoiCS0 label     byte
         db        128,129,130,131,132,133,134,135,136,137,138,139,140,141,142,143
         db        144,145,146,147,148,149,150,151,152,153,154,155,156,157,158,159
         db        160,161,162,163,164,165,166,167,168,169,170,171,172,173,174,175
         db        176,177,178,179,180,181,182,183,184,185,186,187,188,189,190,191
         db        192,'a',194,'cder',199,'uiullono'
         db        'oarstu',214,'eayz',219,220,221,222,223
         db        224,'A',226,'CDER',231,'UIULLONO'
         db        'OARSTU',246,'EAYZ',251,252,253,254,255

; ------ konverzn¡ tabulka z k¢du Latin 2 na k¢d Kamenick˜ch

TbLatKam label     byte
         db        'C‚a„–ccle™”iZŽC'
         db        'Š“”œŒSs™š†ŸLx‡'
         db        ' ¡¢£Aa’‘Ee¿z€s®¯'
         db        176,177,178,179,180,'A‰S',185,186,187,188,'Zz',191
         db        192,193,194,195,196,197,'Aa',200,201,202,203,204,205,206,'x'
         db        'dD…Eƒ¥‹Iˆ',217,218,219,220,'T¦',223
         db        '•',225,'§Nn¤›¨«—ªš˜t',39
         db        '-".""­ö,ø"',250,'ž©',254,255

; ------ konverzn¡ tabulka z k¢du KOI 8 na k¢d Kamenick˜ch

TbKoiKam label     byte
         db        128,129,130,131,132,133,134,135,136,137,138,139,140,141,142,143
         db        144,145,146,147,148,149,150,151,152,153,154,155,156,157,158,159
         db        160,161,162,163,164,165,166,167,168,169,170,171,172,173,174,175
         db        176,177,178,179,180,181,182,183,184,185,186,187,188,189,190,191
         db        'Ú À‡ƒˆªÄ¡–Œ”¤¢'
         db        '“„©¨Ÿ£Ã‚ ˜‘Â',220,39,248,'Å'
         db        '¿Ù€…‰«³š‹¦Šœ™¥•'
         db        '§Žž›†—´’Á',252,'^',254,255

; ------ konverzn¡ tabulka z k¢du Kamenick˜ch na k¢d Latin 2

TbKamLat label     byte
         db        172,'‚',212,'„',210,155,159,216,183,145,214,150,146,'Ž',181
         db        '',167,166,'“”',224,133,233,236,'™š',230,149,237,252,156
         db        ' ¡¢£',229,213,222,226,231,253,234,232,'/',245,174,175
         db        176,177,178,179,180,179,186,187,191,185,186,187,188,188,217,191
         db        192,193,194,195,196,197,179,186,200,201,202,203,204,205,206,205
         db        188,205,196,200,192,218,201,186,205,217,218,219,220,219,219,223
         db        'a',225,'G','p','S','s','m','t','f','T','O','d','0','/','E','N'
         db        '=','+','>','<',218,217,246,'=',248,250,250,'V','n','2',254,255

; ------ konverzn¡ tabulka z k¢du KOI 8 na k¢d Latin 2

TbKoiLat label     byte
         db        128,129,130,131,132,133,134,135,136,137,138,139,140,141,142,143
         db        144,145,146,147,148,149,150,151,152,153,154,155,156,157,158,159
         db        160,161,162,163,164,165,166,167,168,169,170,171,172,173,174,175
         db        176,177,178,179,180,181,182,183,184,185,186,187,188,189,190,191
         db        'Ú',' ','À',159,212,216,234,'Ä',129,'¡',133,146,150,148,229,'¢'
         db        147,132,253,231,156,'£','Ã','‚',' ',236,167,'Â',243, 39,248,'Å'
         db        '¿',181,'Ù',172,210,183,232,'³','š',214,222,145,149,'™',213,224
         db        226,142,252,230,155,233,'´','',181,237,166,'Á',249,'^',254,255

; ------ konverzn¡ tabulka z k¢du Kamenick˜ch na k¢d KOI 8

TbKamKoi label     byte
         db        227,200,215,196,209,228,244,195,197,229,235,233,204,203,241,225
         db        247,218,250,208,205,239,202,245,217,237,232,243,236,249,242,212
         db        193,201,207,213,206,238,234,240,211,210,198,230,131,173,174,175
         db        176,177,178,231,246,246,246,224,224,246,231,224,226,226,226,224
         db        194,251,219,214,199,223,214,214,194,192,251,219,214,199,223,251
         db        251,219,219,194,194,192,192,223,223,226,192,219,220,221,222,223
         db        224,225,226,227,228,229,230,231,232,233,234,235,236,237,238,239
         db        240,241,242,243,244,245,246,247,248,249,250,251,252,253,254,255

; ------ konverzn¡ tabulka z k¢du Latin 2 na k¢d KOI 8

TbLatKoi label     byte
         db        128,200,215,'a',209,202,'c','c','l','e',237,208,'i','Z',241,'C'
         db        247,235,203,208,205,236,204,'S','s',237,232,244,212,'L','x',195
         db        193,201,207,213,'A','a',250,218,'E','e',224,'z',227,'s',174,175
         db        176,177,178,231,246,225,'A',229,'S',246,231,224,226,'Z','z',224
         db        194,251,219,214,199,223,'A','a',194,192,251,219,213,199,223,'x'
         db        'd','D',228,'E',196,238,233,'I',197,226,192,219,220,'T',234,223
         db        239,225,240,'N','n',206,243,211,230,245,198,232,217,249,'t', 39
         db        '-',252,'.',220,220,131,':',',',222,252,254,200,242,210,254,255

; ------ parametry textu

         EVEN
RadkuTxt dw        1                        ; celkov˜ po‡et © dk– textu
TopAdr   dd        0                        ; adresa po‡ tku str nky
TopLine  dw        0                        ; ‡¡slo po‡ te‡n¡ho © dku displeje
TopPoz   dw        0                        ; po‡ te‡n¡ pozice © dku
TopPoz0  dw        0                        ; ‡¡ta‡ po‡ te‡n¡ pozice © dku

FindAdr  dd        0                        ; adresa nalezen‚ho © dku
FindLine dw        -1                       ; ‡¡slo nalezen‚ho © dku
FindLin0 dw        0                        ; ‡¡ta‡ nalezen‚ho © dku k zobrazen¡

; ------ parametry displeje

Barva    db        7                        ; barva textu
Barva1   db        70h                      ; vysv¡cen˜ © dek
Barva2   db        70h                      ; barva nadpisu
Barva3   db        70h                      ; barva n povˆdy

DispRow  label     word                     ; po‡et zobrazen˜ch © dk–
MaxRow   db        24                       ; posledn¡ © dek na displeji
         db        0                        ; doplnˆn¡ na slovo

Radku    db        25                       ; po‡et © dk– displeje
AdrVRAM  dd        0b0000000h               ; adresa videopamˆti
AktPage  db        0                        ; aktivn¡ videostr nka
OldKurz  dw        0                        ; star  pozice kurzoru

GRAFX3   db        176                      ; kurzor 1
GRAFX4   db        178                      ; kurzor 2

; ------ texty

Nadpis   db        179,' ESC=konec; F1=napoveda '
GRAFX2   db        179,'             0/0'
NadpCis  label     byte
GRAFX5   db        179
Nadpis0  label     byte
         db        0

Nadpis2  db        ' ESC=navrat; ENTER,sipky=hledani '
GRAFX1   db        179,' text:'
Nadpis20 db        0

UvTxt    db        'HLP V1.40 - prohlizec souboru napovedy; (c) Miroslav Nemecek',13,10,'$'
HelpTxt  db        '    ÉÍÍ Zadejte jmeno souboru napovedy k zobrazeni ! ÍÍ»',13,10
         db        '    º Zadanim vstupniho i vystupniho jmena souboru se  º',13,10
         db        '    º   soubor napovedy dekoduje do textoveho tvaru.   º',13,10
         db        '    ÈÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼',13,10
         db        'Prepinace: /G "heslo" .. prednastaveni dekodovaciho hesla',13,10
         db        '           /K .......... nekomprimovany soubor v kodu Kamenickych',13,10
         db        '           /L .......... nekomprimovany soubor v kodu Latin 2',13,10
         db        '           /I .......... nekomprimovany soubor v kodu KOI 8',13,10
         db        '$'
SoubTxt  db        'Zadany soubor napovedy nenalezen !',13,10,'$'
MemTxt   db        'Chyba - nedostatek pameti !'
CRTxt    db        13,10,'$'
Exis1Txt db        'Vystupni soubor $'
Exis2Txt db        ' jiz existuje !',13,10
         db        'Chcete jej prepsat (A=ano) ? $'
VystTxt  db        'Chybne zadani vystupniho souboru !',13,10,'$'
WritTxt  db        'Chyba zapisu souboru na disk !',13,10,'$'
CelyTxt  db        'VAROVANI: Soubor se nenacetl do pameti cely ! ',7,13,10
         db        '          Stisknete libovolnou klavesu... ',13,10,'$'

ZacTxt   db        ' Nastavte kurzor na prvni radek bloku a stisknete ENTER (ESC=preruseni).',0
KonTxt   db        ' Nastavte kurzor na posledni radek bloku a stisknete ENTER (ESC=preruseni).',0
ZapisTxt db        ' Cekejte, pridavam blok k souboru BLOK.$$$ v aktivnim adresari...',0
ZapErTxt db        ' CHYBA - blok nelze ulozit na disk ! Stisknete libovolnou klavesu...',0

TiskTxt  db        ' Cekejte, tisknu oznaceny blok na tiskarnu...',0
TiskETxt db        ' CHYBA - oznaceny blok nelze vytisknout ! Stisknete libovolnou klavesu...',0

BlokSoub db        'BLOK.$$$',0             ; jm‚no souboru bloku
BlokTisk db        'PRN',0                  ; jm‚no tisk rny

; ------ velk  n povˆda

MHelpTxt db        'ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ HLP V1.40 ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿'
         db        '³ ESC ...... opusteni programu, navrat do systemu    ³'
         db        '³ ENTER .... vyhledavani textu                       ³'
         db        '³              ENTER = vyhledani textu od zacatku    ³'
         db        '³              sipka_nahoru/dolu = hledani zpet/vpred³'
         db        '³              sipka_vlevo, BS = zruseni 1 znaku     ³'
         db        '³              sipka_vpravo = obnoveni 1 znaku textu ³'
         db        '³              ESC = opusteni rezimu hledani         ³'
         db        '³              HOME = prvni vyskyt retezce           ³'
         db        '³              END = posledni vyskyt retezce         ³'
         db        '³              PAGE_UP/PAGE_DOWN = posun o stranku   ³'
         db        '³ MEZERA ... vypnuti diakritiky (bez hacku a carek)  ³'
         db        '³ F2 ....... zobrazeni textu v kodu Kamenickych      ³'
         db        '³ F3 ....... zobrazeni textu v kodu Latin 2          ³'
         db        '³ F4 ....... zobrazeni textu v kodu KOI 8            ³'
         db        '³ F5 ....... ulozeni bloku na disk (do "BLOK.$$$")   ³'
         db        '³ F6 ....... vytisteni bloku na tiskarnu             ³'
         db        '³ kurzorove klavesy ... posun o radek/pozici         ³'
         db        '³ Ctrl-vlevo/vpravo ... posun o 10 pozic vlevo/vpravo³'
         db        '³ PAGE_UP/PAGE_DOWN ... posun o stranku              ³'
         db        '³ HOME/END ... zacatek/konec textu                   ³'
MHelpTx0 db        'ÀÄÄÄÄÄÄÄÄÄÄ stisknete libovolnou klavesu ÄÄÄÄÄÄÄÄÄÄÄÄÙ'
MHelpTx1 label     byte

; ------ heslo

HesloN   dw        0                        ; d‚lka hesla
Heslo    db        40 dup(0)                ; text hesla

HesloTxt db        'Zadejte text hesla (ESC=preruseni): $'
HesloTx0 label     byte

Klic     db        257 dup(?)               ; k¢dovac¡ kl¡‡ hesla

; ------ konfigurace

HLPName  db        'HLP.CNF',0
HLPBuff  db        128 dup(0)               ; jm‚no domovsk‚ho programu

HLPCnf   db        'HLPCNF1'                ; identifik tor konfigurace
ParamC   db        0                        ; parametry konfigurace
                                            ;  bit 0,1: k¢d displeje
                                            ;           0=k¢d Kamenick˜ch
                                            ;           1=k¢d Latin 2
                                            ;           2=k¢d KOI 8
                                            ;  bit 2: 1=zapnut filtr ‡e¨tiny
                                            ;  bit 3: 1=vlastn¡ fonty CS
                                            ;  bit 4: 1=vypnut horn¡ © dek
         db        0                        ; rezerva

HLPCnf0  db        offset(HLPCnf0-HLPCnf) dup(0) ; uschovan  p–vodn¡ konfigurace

; ------ datov‚ buffery (buffer s textem je o 1/8 n¡‘e ne‘ vstupn¡ buffer !)

DatSegm  dw        0                        ; adresa bufferu s textem
DatSize  dw        0                        ; velikost bufferu s textem (odst.)
MaxOffs  dw        0                        ; maxim ln¡ offset v bufferu
MaxSegm  dw        0                        ; maxim ln¡ segment v bufferu

SoubIdnt dw        0                        ; identifik tor souboru
InpSegm  dw        0                        ; segment vstupn¡ho bufferu souboru
InpNum   dw        0                        ; po‡et bajt– ve vstupn¡m bufferu
InpSize  dd        0                        ; ‡¡ta‡ velikosti vstupn¡ho souboru

FileDate dw        0                        ; uschovan˜ datum souboru
FileTime dw        0                        ; uschovan˜ ‡as souboru
FileAtr  dw        0                        ; atributy souboru

AdrSoub  dw        0                        ; adresa jm‚na souboru
Soubor1  db        128 dup(0)               ; buffer jm‚na souboru 1
Soubor2  db        128 dup(0)               ; buffer jm‚na souboru 2

FndNum   dw        0                        ; d‚lka zadan‚ho textu
FndTxt   db        MAXFND dup(" ")          ; buffer hledan‚ho textu
FndTxt0  db        MAXFND dup(" ")          ; pracovn¡ buffer hledan‚ho textu

; *****************************************************************************
;
;                            Z sobn¡k
;
; *****************************************************************************

; ------ z sobn¡k

         dw        200h dup(?)              ; z sobn¡k
Zasob    label     near

ViewEnd  label     near                     ; konec programu

Code     ENDS
         END       Start
