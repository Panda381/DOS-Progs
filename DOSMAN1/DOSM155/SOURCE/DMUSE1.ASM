
; modul DMUSE1.ASM 

; *****************************************************************************
;
;                    
;
; *****************************************************************************


extrn    editnum:word,editseg:byte,darkcit:word
extrn    editmax:word,topseged:word,segend:word,topseg:word,seznseg:word
extrn    seznseg0:byte,bufwin:byte,bufwin2:byte
extrn    parnum:byte,citdek:byte,citnum:byte
extrn    upperkam:byte,upperlat:byte,prevcs:byte,config:byte
extrn    textvy11:byte,textvy12:byte,displ:byte,flags:byte,extent:byte
extrn    tabl:byte,tabr:byte,col1:byte,color:byte,mousedat:byte,citmouse:byte
extrn    edikey:word,keystat:byte,keyinp:byte,mousepoz:word,insmouse:byte
extrn    aktpage:byte,adrvram:dword,selbuf:byte,numsrc:byte,tabhlp0:word
extrn    txthlped:word,endline:near,conf2:byte
extrn    conf6:byte,windret:near,displw:byte,windretx:near,usesedit:near
extrn    palon:byte,paloff:byte,dark:word,vyzvy:near,ukazkey:dword
extrn    verze:word,rerdir:near,test1file:near,srcins0:near,parbreak:word
extrn    usesedit:near,getkurz:near,windret:near,storekur:near,storfile:near
extrn    settabf:near,window:near,aktdisk:near,aktfile:near,space:near
extrn    directory:near,endline:near,delpolf:near,col7:byte,outbuf:byte
extrn    srcins:near,pathhome:byte,parrez:byte
extrn    rozbnam:byte,bufcopy:byte,bufcop1:byte,bufcop2:byte,bufcop3:word
extrn    bufcop4:word,soucins:near,outbuf3:byte,flagsc:byte,bufnadp:byte
extrn    txtvar:byte,errdir1:byte,errdir2:byte,errdel2:byte,delvolb:byte
extrn    textvy2:byte,txtadr:byte,errdel1:byte,volbyh:near
extrn    transuk:dword,blokuk:dword,poziceuk:byte,delkauk:byte
extrn    hledpol0:near,hledpol:near,segvram:word
extrn    errcop1:byte,zobrchyb:near,rozbflg:byte,maxdbuf:word
extrn    xpush:near,xpop:near,zacrol:word,kurrol:word,delrol:byte
extrn    zobrfile:near,files:near,comlin:byte,kurzlin:near,obrlin:near
extrn    normat:byte,kurzon:near,kurzoff:near,radeknul:near,aktkurz:word
extrn    all:byte,errxc1:byte,errxc2:byte,erroper:byte,nadadr:byte
extrn    copyfk:near,txtcop5:byte,txtcop6:byte,testex:near,topret:near
extrn    msgcek:near,getoper:near,txtmov5:byte,delfk:near
extrn    errdr1:byte,errdr2:byte,storekall:near,prodldem:near
extrn    conf7:byte,storecnf:near,soubhis:byte,zachis:byte,konhis:byte

code     segment   public
         assume    cs:code,ds:code


; -----------------------------------------------------------------------------

public   ulozhist                         ;* ulo‘en¡ historie
ulozhist:

ifndef   demo
ifdef    upver

         or        byte ptr ds:[parrez],0ch ; ignor. p©eru¨. i chyby
         mov       word ptr ds:[parbreak],0
         test      byte ptr ds:[conf7],2    ; m  se uschovat historie ?
         jz        ulozhst1                 ; neuchov v  se historie
;         call      storecnf                 ; ulo‘en¡ konfigurace
         mov       si,offset soubhis
         mov       di,offset outbuf
         call      sethome                  ; sestaven¡ cesty k souboru
         mov       ah,3ch
         xor       cx,cx                    ; atributy - norm ln¡ soubor
         int       21h                      ; vytvo©en¡ souboru
         jc        ulozhst1                 ; chyba vytvo©en¡ souboru
         mov       bx,ax                    ; identifik tor souboru
         mov       ah,40h
         mov       cx,offset konhis
         mov       dx,offset zachis         ; za‡ tek historie
         sub       cx,dx                    ; d‚lka historie
         int       21h                      ; z pis historie do souboru
         mov       ah,3eh
         int       21h                      ; uzav©en¡ souboru

endif
endif

                                          ;* inicializace registr–
ulozhst1:ret

; -----------------------------------------------------------------------------
public   modiscr
modiscr:                                    ; modifikace oken pomoc¡ SCROLL LOCK

         pushf
         call      testscr                  ; je aktivn¡ funkce SCROLL LOCK ?
         jnz       modiscr0                 ; SCROLL LOCK je aktivn¡
modiscrx:popf
         ret

modiscr0:
ifdef    upver
         call      testaktw                 ; je okno aktivn¡ ?
         jc        modiscrx                 ; okno nen¡ aktivn¡
         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         push      ds
         push      es
         push      cs
         pop       ds
         push      cs
         pop       es

         or        byte ptr ds:[conf6],4    ; p©¡znak funkce SCROLL LOCK

         call      kurzout                  ; vypnut¡ kurzoru
         call      windret                  ; nov‚ zobrazen¡ oken

modiscr1:call      testscr                  ; je p©esmyka‡ SCROLL LOCK uvolnˆn ?
         jz        modiscr9                 ; p©esmyka‡ SCROLL LOCK je uvolnˆn
         call      testkey                  ; test, zda je kl vesa z kl vesnice
         jz        modiscr1                 ; nen¡ kl vesa z kl vesnice

                                          ;* oprava konce okna
         call      getendl                  ; poskytnut¡ max. © dku konce okna
         inc       dh
         cmp       dh,ds:[displw]           ; je za maxim ln¡m © dkem ?
         jae       modiscr5                 ; je povolen˜ konec okna
         mov       ds:[displw],dh           ; omezen¡ na maxim ln¡ © dek
modiscr5:
                                          ;* je kl vesa z kl vesnice
         call      inpkey                   ; vstup znaku z kl vesnice
         call      flushkey                 ; vypr zdnˆn¡ kl vesnice
                                          ;* kurzor nahoru
         cmp       ah,48h                   ; je kurzor nahoru ?
         jne       modiscr4                 ; nen¡ kurzor nahoru
         cmp       byte ptr ds:[displw],8   ; je ji‘ © dek 8 ?
         jbe       modiscr1                 ; je ji‘ © dek 8
         dec       byte ptr ds:[displw]     ; sn¡‘en¡ © dku okna
         jmp       short modiscr8           ; p©ekreslen¡ okna
                                          ;* kurzor dol–
modiscr4:cmp       ah,50h                   ; je kurzor dol– ?
         jne       modiscr6                 ; nen¡ kurzor dol–
         call      getendl                  ; poskytnut¡ max. © dku konce okna
         inc       dh
         cmp       dh,ds:[displw]           ; je ji‘ posledn¡ © dek okna ?
         jbe       modiscr1                 ; je ji‘ posledn¡ © dek okna
         inc       byte ptr ds:[displw]     ; posun konce dol–
         jmp       short modiscr8           ; p©ekreslen¡ okna

                                          ;* prvn¡ © dek
modiscr6:cmp       ah,47h                   ; je Home ?
         jne       modiscr7                 ; nen¡ Homr
         mov       byte ptr ds:[displw],8   ; prvn¡ povolen˜ © dek
         jmp       short modiscr8           ; p©ekreslen¡ okna

                                          ;* posledn¡ © dek
modiscr7:cmp       ah,4fh                   ; je End ?
         jne       modiscr1                 ; nen¡ End
         call      getendl                  ; poskytnut¡ max. © dku konce okna
         inc       dh
         mov       ds:[displw],dh           ; omezen¡ na maxim ln¡ © dek

modiscr8:call      windret                  ; nov‚ zobrazen¡ okna
         jmp       short modiscr1           ; ‡ek n¡ na nˆjakou akci

modiscr9:
         and       byte ptr ds:[conf6],not 4 ; zru¨en¡ funkce SCROLL LOCK

         call      getnakt
         xor       ax,ax
         call      usesedit                 ; normalizace
         call      getakt
         xor       ax,ax
         call      usesedit                 ; normalizace

         call      windretx                 ; n vrat zobrazen¡ oken

         pop       es
         pop       ds
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
endif
         popf
         ret
; -----------------------------------------------------------------------------
public   modihlpalt,outhelp,outedhlp,outthlp

modihlpalt:                               ;* modifikace vyhled v n¡ s Alt

         call      testalt                  ; test stisku kl vesy Alt
         jnz       modihla2                 ; kl vesa Alt je stisknuta
         cmp       byte ptr cs:[numsrc],0   ; je buffer ji‘ vynulovan˜ ?
         je        modihla2                 ; je ji‘ vynulovan˜
         mov       byte ptr cs:[numsrc],0   ; vynulov n¡ buff. rychl‚ho hled n¡
         call      endline                  ; zobrazen¡ koncov‚ho © dku
         call      obrkurz                  ; zobrazen¡ kurzoru povel. © dku
modihla2:ret


outhelp:                                  ;* zobrazen¡ z kladn¡ n povˆdy
         pushf
         push      si
         call      obrcomm                  ; zobrazen¡ p©¡kazov‚ho © dku
         lea       si,[tabhlp0]             ; tabulka z kladn¡ch n povˆd
         call      outakth                  ; zobrazen¡ textu n povˆdy
         call      obrkurz                  ; navr cen¡ zobrazen¡ kurzoru
         pop       si
         popf
         ret


outedhlp:                                 ;* zobrazen¡ n povˆdy pro editor
         pushf
         and       byte ptr cs:[flags],3fh  ; p©¡znak - nejsou p©esmyka‡e
         push      si
         lea       si,[txthlped]            ; adresa n povˆdy pro editor
         call      outthlp                  ; zobrazen¡ textu n povˆdy
         pop       si
         popf
         ret

public   modihlp

modihlp:                                  ;* modifikace n povˆdy
                                            ; VSTUP: SI=tabulka adres n povˆd

         push      bx
         push      si
         push      ds
                                          ;* zji¨tˆn¡ statutu p©esmyka‡–
         xor       bx,bx
         mov       ds,bx                    ; DS <- 0000
         mov       bh,ds:[0417h]            ; statu p©esmyka‡–
         push      cs
         pop       ds                       ; DS <- CS
         mov       bl,0c0h                  ; k¢d pro Alt-
         test      bh,8                     ; je p©esmyka‡ Alt- ?
         jnz       modihlp2                 ; je p©esmyka‡ Alt-
         mov       bl,80h                   ; k¢d pro Ctrl-
         test      bh,4                     ; je p©esmyka‡ Ctrl- ?
         jnz       modihlp2                 ; je p©esmyka‡ Ctrl-
         test      bh,3                     ; je p©esmyka‡ Shift- ?
         mov       bl,40h                   ; k¢d pro Shift-
         jnz       modihlp2                 ; je p©esmyka‡ Shift-
         xor       bl,bl                    ; k¢d - bez p©esmyka‡e
modihlp2:                                 ;* aktualizace stavu p©esmyka‡–
         mov       bh,ds:[flags]            ; sou‡asn‚ nastaven¡ p©esmyka‡–
         and       byte ptr ds:[flags],3fh  ; zru¨en¡ p©¡znaku p©esmyka‡–
         or        ds:[flags],bl            ; nov‚ nastaven¡ p©esmyka‡–
         and       bh,0c0h                  ; p–vodn¡ nastaven¡ p©esmyka‡–
         cmp       bh,bl                    ; byla zmˆna statutu p©esmyka‡– ?
         je        modihlp3                 ; nebyla zmˆna
         call      outakth                  ; zobrazen¡ nov‚ n povˆdy
modihlp3:pop       ds
         pop       si
         pop       bx
         ret

public   outakth
outakth:                                  ;* zobrazen¡ aktivn¡ n povˆdy
                                            ; VSTUP: SI=tabulka adres n povˆd

         push      bx
         push      si
         cmp       word ptr cs:[si],0       ; je tabulka definovan  ?
         je        outakth1                 ; tabulka nen¡ definovan 
         mov       bl,cs:[flags]            ; sou‡asn‚ nastaven¡ p©esmyka‡–
         and       bl,0c0h                  ; nastaven¡ p©esmyka‡–
         rol       bl,1
         rol       bl,1
         rol       bl,1                     ; p©evod na ‡¡slo
         xor       bh,bh
         mov       si,cs:[si+bx]            ; adresa n povˆdy
         call      outthlp                  ; zobrazen¡ n povˆdy SI
outakth1:pop       si
         pop       bx
         ret


outthlp:                                  ;* zobrazen¡ textu n povˆdy
                                            ; VSTUP: SI=adresa textu n povˆdy
         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      ds
         push      cs
         pop       ds
         call      testakth                 ; je n povˆda povolena ?
         jc        outthlp5                 ; n povˆda nen¡ povolena
         call      mouseoff                 ; vypnut¡ my¨i
         call      getdispl                 ; posledn¡ © dek obrazovky
         xor       dl,dl
;         mov       dx,24*256                ; pozice pro zobrazen¡ textu n pov.
         mov       cx,10                    ; ‡¡ta‡ funk‡n¡ch kl ves
         mov       bl,"1"                   ; ozna‡en¡ funk‡n¡ kl vesy
                                          ;* zobrazen¡ k¢du kl vesy
outthlp1:mov       al,9                     ; nastaven¡ barvy 9
         call      outch1                   ; nastaven¡ barvy pro ozna‡. kl vesy
         mov       al,bl                    ; ozna‡en¡ kl vesy
         cmp       al,"9"                   ; je ji‘ kl vesa 10 ?
         jbe       outthlp4                 ; nen¡
         mov       al,"1"
         call      outch1
         mov       al,"0"
outthlp4:call      outch1                   ; zobrazen¡ ‡¡sla kl vesy
         mov       al,3                     ; barva pro ozna‡en¡ textu n povˆdy
         call      outch1                   ; nastaven¡ barvy textu
                                          ;* zobrazen¡ textu kl vesy
         push      cx                       ; ‡¡ta‡ kl ves
         mov       cl,6                     ; po‡et znak– textu kl vesy
outthlp2:lodsb                              ; na‡ten¡ znaku
         or        al,al                    ; je ji‘ konec textu ?
         jz        outthlp3                 ; je konec textu
         call      outch1                   ; zobrazen¡ znaku textu
         loop      outthlp2                 ; zobrazen¡ dal¨¡ho znaku
outthlp3:mov       al," "                   ; mezera pro konec textu
         call      outch                    ; zobrazen¡ kocov˜ch mezer
         pop       cx
                                          ;* oddˆlovac¡ mezera
         mov       al,9                     ; nastaven¡ barvy 9
         call      outch1                   ; nastaven¡ barvy pro ozna‡. kl vesy
         mov       al," "                   ; oddˆlovac¡ mezera
         call      outch1                   ; zobrazen¡ oddˆlovac¡ mezery
         inc       bl                       ; zv˜¨en¡ ‡¡sla kl vesy
         loop      outthlp1                 ; dal¨¡ kl vesa
         call      mouseon                  ; zapnut¡ my¨i

outthlp5:pop       ds
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------
public   testscr

testscr:                                  ;* test p©esmyka‡e SCROLL LOCK
                                            ; VSTUP: ZN=SCROLL LOCK je aktivn¡

         push      ds
         push      ax
         xor       ax,ax
         mov       ds,ax
         test      byte ptr ds:[417h],10h   ; je aktivn¡ SCROLL LOCK ?
         pop       ax
         pop       ds
         ret

; -----------------------------------------------------------------------------
public   testalt

testalt:                                  ;* test stisku kl vesy Alt
                                            ; VSTUP: ZN=kl vesa je stisknuta

         push      ds
         push      ax
         xor       ax,ax
         mov       ds,ax
         test      byte ptr ds:[417h],8     ; je stisknuta kl vesa Alt ?
         pop       ax
         pop       ds
         ret

; -----------------------------------------------------------------------------
public   testshft

testshft:                                 ;* test stisku kl vesy Shift
                                            ; VSTUP: ZN=kl vesa je stisknuta

         push      ds
         push      ax
         test      byte ptr cs:[conf2],8    ; je nucen˜ SHIFT ?
         jnz       testshf1                 ; je nucen˜ SHIFT
         test      byte ptr cs:[conf2],10h  ; je povolen˜ re‘im se SHIFT ?
         jz        testshf1                 ; nen¡ povolen˜ SHIFT
         xor       ax,ax
         mov       ds,ax
         test      byte ptr ds:[417h],3     ; je stisknuta kl vesa Shift ?
testshf1:pop       ax
         pop       ds
         ret



; *****************************************************************************
;
;                    Znakov‚ vstupnˆ/v˜stupn¡ podprogramy
;
; *****************************************************************************
public   flushkey

flushkey:                                 ;* vypr zdnˆn¡ bufferu kl vesnice

         push      ax
         push      cx
         xor       cx,cx                    ; maxim ln¡ po‡et cykl–
flk1:    call      testkey                  ; test stavu kl vesnice
         jz        flk2                     ; nen¡ p©ipraven dal¨¡ znak
         call      inpkey                   ; vstup znaku z kl vesnice
         loop      flk1                     ; vypr zdnˆn¡ dal¨¡ho znaku
flk2:    pop       cx
         pop       ax
         ret

; -----------------------------------------------------------------------------
public   inpkeyf

inpkeyf:                                 ;* vstup znaku s vyprazd¤ov n¡m
                                            ; VSTUP: SI=tabulka n povˆdy
                                            ; VSTUP: AX=znak

         push      cx
         xor       ax,ax                    ; AX <- 0 nulov n¡ bufferu
         xchg      ax,cs:[edikey]           ; vyjmut¡ znaku z bufferu
         or        ax,ax                    ; je nˆjak  kl vesa ?
         jnz       inpkey5                  ; byla nˆjak  kl vesa
inpkey4: call      modihlp                  ; modifikace zobrazen¡ n povˆdy
;         call      testalt
;         jnz       inpkey8                  ; kl vesa ALT je stisknuta
;         mov       byte ptr cs:[numsrc],0   ; zru¨en¡ bufferu znak–
inpkey8: call      testkey                  ; je p©ipraven znak ?
         jz        inpkey4                  ; ‡ek n¡ na p©¡chod znaku
         call      inpkey                   ; vstup znaku z kl vesnice
                                          ;* zru¨en¡ stejn˜ch znak–
inpkey5: mov       cx,ax                    ; £schova znaku
         mov       ah,1
         call      inpkbuf                  ; je nˆjak˜ znak v buffer ?
         jnz       inpkey7                  ; je znak v bufferu - neru¨¡ se
inpkey6: call      testkey                  ; test p©ipraven‚ kl vesy
         jz        inpkey7                  ; nen¡ p©ipraven ‘ dn˜ znak
         cmp       ax,cx                    ; je stejn˜ znak ?
         jne       inpkey7                  ; nen¡ stejn˜ znak
         call      inpkey                   ; vypr zdnˆn¡ bufferu kl vesnice
         mov       cs:[edikey],ax           ; £schova znaku
         jmp       short inpkey6            ; dal¨¡ znak
inpkey7: mov       ax,cx                    ; n vrat znaku
         pop       cx
         ret

; -----------------------------------------------------------------------------
public   testkey

testkey:                                  ;* test kl vesnice, zda p©i¨el znak
                                            ; VSTUP: NZ=znak je p©ipraven
                                            ;         AX=znak

;         mov       ax,cs:[edikeym]
;         or        ax,ax
;         jnz       inpkey3                  ; je p©ipraven znak

         test      byte ptr cs:[conf6],8    ; je karta EGA ?
         jz        testkey2                 ; nen¡ karta EGA
         cmp       word ptr cs:[dark],0     ; je povoleno stm¡v n¡ ?
         je        testkey2                 ; stm¡v n¡ nen¡ povoleno
         cmp       word ptr cs:[segvram],0b000h ; je displej MDA ?
         je        testkey2                 ; je MDA - neztmavuje se
         cmp       word ptr cs:[darkcit],0  ; m  b˜t displej stmaven ?
         jnz       testkey1                 ; displej nem  b˜t stmaven
                                          ;* displej m  b˜t stmaven
         test      byte ptr cs:[conf6],10h  ; je displej stmaven ?
         jnz       testkey2                 ; displej je ji‘ stmaven
         call      darkon                   ; stmaven¡ displeje
         jmp       short testkey2
testkey1:                                 ;* displej nem  b˜t stmaven
         test      byte ptr cs:[conf6],10h  ; je displej stmaven ?
         jz        testkey2                 ; displej nen¡ stmaven
         call      darkoff                  ; rozsv¡cen¡ displeje
testkey2:
         sti
         call      vyzvy                    ; obsluha v˜zev od port– COM
         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         sti
         int       28h                      ; obsluha ‡ek n¡ na kl vesnici
         sti
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         mov       ah,cs:[keystat]          ; k¢d pro ‡ten¡ stavu kl vesnice
         jmp       short inpkey0            ; ‡ten¡ stavu kl vesnice

; -----------------------------------------------------------------------------
public   darkon
darkon:                                   ;* stmaven¡ displeje
         push      si
         mov       si,offset paloff
         call      setpal                   ; nastaven¡ palet displeje
         or        byte ptr cs:[conf6],10h  ; displej je stmaven
         pop       si
         ret
; -----------------------------------------------------------------------------
public   darkoff
darkoff:                                  ;* rozsv¡cen¡ displeje
         push      si
         mov       si,offset palon
         call      setpal                   ; nastaven¡ palet displeje
         and       byte ptr cs:[conf6],not 10h ; displej nen¡ stmaven
         pop       si
         ret
; -----------------------------------------------------------------------------
public   setpal
setpal:                                   ;* nastaven¡ palet displeje CS:SI
         push      ax
         push      cx
         push      dx
         cli
         mov       dx,3dah
         in        al,dx                    ; synchronizace
         mov       dx,3c0h
         mov       cx,16
         xor       ax,ax
setpal1: xchg      ah,al
         out       dx,al                    ; registr
         xchg      ah,al
         mov       al,cs:[si]
         inc       si
         out       dx,al                    ; data
         inc       ah
         loop      setpal1                  ; dal¨¡ registr
         mov       al,31h
         out       dx,al
         mov       al,cs:[si]
         inc       si
         out       dx,al                    ; okol¡
         sti
         pop       dx
         pop       cx
         pop       ax
         ret
; -----------------------------------------------------------------------------
public   inpkey

inpkey:                                  ;* vstup znaku z kl vesnice
                                           ; VSTUP: AX=znak

;         call      testkey                  ; je p©ipraven znak ?
;         je        inpkey                   ; ‡ek n¡ na p©¡chod znaku
;         xor       ax,ax
;         xchg      ax,cs:[edikeym]          ; znak od my¨i
;         or        ax,ax
;         jnz       inpkey3                  ; byl znak v bufferu my¨i
         and       byte ptr cs:[parrez],not 80h ; zru¨en¡ p©¡znaku p©eru¨en¡
         mov       ah,cs:[keyinp]           ; k¢d pro ‡ten¡ znaku z kl vesnice
inpkey0: call      inpkbuf                  ; vstup znaku z bufferu
         jnz       inpkey00                 ; byl nˆjak˜ znak
         sti
         int       16h                      ; ‡ten¡ znaku z kl vesnice
         sti
inpkey00:pushf
         or        ah,ah                    ; je k¢d kl vesy s Alt ?
         je        inpkey2                  ; je k¢d s Alt
         cmp       al,0e0h                  ; je ©¡dic¡ k¢d s E0 ?
         je        inpkey1                  ; je ©¡dic¡ k¢d s E0
         cmp       al,0f0h                  ; je ©¡dic¡ k¢d s F0 ?
         jne       inpkey2                  ; nen¡ ©¡dic¡ k¢d s F0
inpkey1: xor       al,al                    ; normalizace na k¢d 00
inpkey2: popf
inpkey3: ret


public   inpkbuf
inpkbuf:                                  ;* vstup znaku z bufferu
                                            ; VSTUP: AH=bit 0: 1=test
                                            ; VSTUP: AX=kl vesa
                                            ;         ZY=nen¡ kl vesa

         push      bx
         push      si
         push      ds
                                          ;* test, zda je buffer
         mov       bx,word ptr cs:[ukazkey+2]
         or        bx,word ptr cs:[ukazkey] ; je buffer ?
         jz        inpkbuf3                 ; nen¡ kl vesa
                                          ;* test, zda je znak
         lds       si,cs:[ukazkey]          ; ukazatel bufferu makrokl ves
         mov       bl,ds:[si]               ; ‡ten¡ p©ipraven‚ho znaku
         or        bl,bl                    ; je to platn˜ znak ?
         jz        inpkbuf2                 ; nen¡ dal¨¡ znak
                                          ;* zv˜¨en¡ ukazatele bufferu
         test      ah,1                     ; je jenom test vstupu ?
         mov       al,bl                    ; na‡ten˜ znak
         mov       ah,1                     ; je jako kl vesa
         jnz       inpkbuf3                 ; je jenom test vstupu
         inc       word ptr cs:[ukazkey]    ; zv˜¨en¡ ukazatele textu
         jmp       short inpkbuf3
                                          ;* v bufferu nen¡ znak
inpkbuf2:mov       word ptr cs:[ukazkey],0
         mov       word ptr cs:[ukazkey+2],0 ; zru¨en¡ ukazatele bufferu
inpkbuf3:pop       ds
         pop       si
         pop       bx
         ret

; -----------------------------------------------------------------------------
public   mouseon

mouseon:                                  ;* zapnut¡ zobrazen¡ my¨i

         push      ax
         mov       ax,1
mouseon0:pushf
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         push      ds
         push      es
         test      byte ptr cs:[config],40h ; je my¨ instalov na ?
         jz        mouseon1                 ; my¨ nen¡ instalov na
         sti
         int       33h                      ; zapnut¡ zobrazen¡ my¨i
         sti
mouseon1:pop       es
         pop       ds
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         popf
         pop       ax
         ret

; -----------------------------------------------------------------------------
public   mouseoff

mouseoff:                                 ;* vypnut¡ zobrazen¡ my¨i

         push      ax
         mov       ax,2
         jmp       short mouseon0

; -----------------------------------------------------------------------------
public   mouseget

; Pou‘it¡ tla‡¡tek:  lev‚=veden¡ kurzoru, prav‚=ESC,ozna‡en¡


mouseget:                                 ;* poskytnut¡ stavu my¨i
                                            ; VSTUP:BX=funkce
                                            ;           0=nen¡ ‘ dn‚ tla‡¡tko
                                            ;           1=lev‚ tla‡¡tko dr‘¡
                                            ;           2=prav‚ tla‡¡tko dr‘¡
                                            ;           3=st©edn¡ tla‡¡tko dr‘¡
                                            ;           4=lev‚ tla‡¡tko 1x
                                            ;           5=prav‚ tla‡¡tko 1x
                                            ;           6=st©edn¡ tla‡¡tko 1x
                                            ;           7=lev‚ tla‡¡tko 2x
                                            ;           8=prav‚ tla‡¡tko 2x
                                            ;           9=st©edn¡ tla‡¡tko 2x
                                            ;          10=uvolnˆn¡ lev‚ho tla‡.
                                            ;          11=uvolnˆn¡ prav‚ho tla‡.
                                            ;          12=uvolnˆn¡ prost©. tla‡.
                                            ;        ZY=nen¡ ‘ dn‚ tla‡¡tko

         xor       bx,bx
         test      byte ptr cs:[config],40h ; je my¨ instalov na ?
         jnz       mouseg10                 ; my¨ je nainstalov na

         jmp       mousegt1                 ; my¨ nen¡ instalov na

mouseg10:pushf
         push      ax
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         push      ds
         push      es

         mov       ax,3
         sti
         int       33h                      ; poskytnut¡ pozice my¨i
         sti
         shr       cx,1
         shr       cx,1
         shr       cx,1                     ; pozice / 8
         xchg      byte ptr cs:[mousepoz],cl ; pozice na displeji
         shr       dx,1
         shr       dx,1
         shr       dx,1                     ; © dek / 8
         xchg      byte ptr cs:[mousepoz+1],dl ; © dek s kurzorem my¨i
         mov       ch,dl                    ; © dek s kurzorem my¨i
         cmp       word ptr cs:[mousepoz],cx; byla zmˆna pozice my¨i ?
         je        mouseg13                 ; nen¡ zmˆna pozice my¨i

         mov       ax,cs:[dark]             ; konstanta pro stm¡va‡
         mov       cs:[darkcit],ax          ; nastaven¡ ‡¡ta‡e pro stm¡v n¡
         cmp       word ptr cs:[mousepoz],79 ; je prav˜ horn¡ roh ?
         jne       mouseg13                 ; nen¡ prav˜ horn¡ roh
         mov       word ptr cs:[darkcit],9  ; zhasnut¡ displeje za 1/2 sekundy

mouseg13:
                                          ;* zji¨tˆn¡ tla‡¡tek
         test      bl,4                     ; je st©edn¡ tla‡¡tko ?
         jz        mousegt2                 ; nen¡ st©edn¡ tla‡¡tko
         or        bl,3                     ; nastaven¡ obou tla‡¡tek
mousegt2:and       bl,3                     ; ponech n¡ stavu tla‡¡tek
         mov       bh,cs:[citmouse]         ; ‡¡ta‡ my¨i
         test      bl,2                     ; je prav‚ tla‡¡tko ?
         jnz       mousegt6                 ; je prav‚ tla‡¡tko
         or        byte ptr cs:[insmouse],1 ; p©¡znak, ‘e nastaven¡ nen¡ ur‡eno
mousegt6:                                 ;* byla zmˆna ?
         cmp       bl,cs:[mousedat]         ; je zmˆna tla‡¡tek ?
         je        mousegt3                 ; nen¡ zmˆna stavu tla‡¡tek
                                          ;* je zmˆna tla‡¡tek
         ja        mousegt4                 ; nen¡ uvolnˆn¡ tla‡¡tek
                                          ;* je uvolnˆn¡ tla‡¡tka
         xchg      bl,cs:[mousedat]         ; n vrat uvolnˆn‚ho tla‡¡tka
         add       bl,9                     ; korekce na k¢d uvolnˆn¡
         jmp       short mousegt3
mousegt4:                                 ;* je stisknuto dal¨¡ tla‡¡tko
         mov       ax,cs:[dark]             ; konstanta pro stm¡va‡
         mov       cs:[darkcit],ax          ; nastaven¡ ‡¡ta‡e pro stm¡v n¡

         mov       byte ptr cs:[citmouse],6 ; ‡¡ta‡ my¨i 1/3 sekundy
         mov       cs:[mousedat],bl         ; ulo‘en¡ nov‚ho stavu tla‡¡tek
         add       bl,3                     ; k¢dy pro prvn¡ stisk
                                          ;* test druh‚ho stisku
         or        bh,bh                    ; je prvn¡ stisk ?
         je        mousegt3                 ; je prvn¡ stisk
         add       bl,3                     ; k¢dy pro druh˜ stisk
mousegt3:xor       bh,bh                  ;* nen¡ zmˆna tla‡¡tek
         pop       es
         pop       ds
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       ax
         popf
mousegt1:
         ;cmp       bl,4
         ;jb        mousegt5
         ;cmp       bl,7
         ;jae       mousegt5
         ;cmp       byte ptr cs:[citmouse],3
         ;jb        mousegt5
         ;xor       bl,bl
;mousegt5:
         or        bx,bx
         ret

; -----------------------------------------------------------------------------
public   mousemen

mousemen:                                 ;* funk‡n¡ kl vesy s my¨¡
                                            ; VSTUP: BL=k¢d tla‡¡tek
                                            ; VSTUP: AX=k¢d funk‡n¡ kl vesy
                                            ;         CY=nen¡ funk‡n¡ kl vesa


         xor       ax,ax
         push      dx
         call      testakth                 ; je n povˆda aktivn¡ ?
         jc        mousemn1                 ; n povˆda nen¡ aktivn¡
         mov       dx,cs:[mousepoz]         ; pozice kurzoru my¨i
         cmp       dh,cs:[displ]            ; je © dek s n povˆdou ?
         jb        mousemn1                 ; nen¡ © dek s n povˆdou
         cmp       bl,11                    ; je stisk prav‚ho tla‡¡tka ?
         je        mousemn3                 ; je stisk prav‚ho tla‡¡tka
         mov       dh,40h                   ; p©¡znka n povˆdy se SHIFT
         cmp       bl,12                    ; je stisk prost©edn¡ho tla‡¡tka ?
         je        mousemn4                 ; je volba SHIFT
         cmp       bl,10                    ; je stisk lev‚ho tla‡¡tka ?
         stc
         jne       mousemn1

         mov       dh,cs:[flags]            ; p©¡znaky
         and       dh,0c0h                  ; stav p©esmyka‡–
         or        dh,dh
         mov       ax,3b00h                 ; k¢d kl vesy F1
         jz        mousemn2                 ; je z kladn¡ n povˆda
mousemn4:mov       ah,54h                   ; n povˆda se SHIFT
         cmp       dh,40h
         je        mousemn2                 ; je n povˆda se SHIFT
         mov       ah,5eh                   ; n povˆda s CTRL
         cmp       dh,80h
         je        mousemn2                 ; je n povˆda s CTRL
mousemn3:mov       ah,68h                   ; n povˆda s ALT
mousemn2:shr       dl,1
         shr       dl,1
         shr       dl,1                     ; pozice / 8
         add       ah,dl                    ; k¢d fuk‡n¡ kl vesy
mousemn1:pop       dx
         ret

; -----------------------------------------------------------------------------
public   kurzout

kurzout:                                  ;* vypnut¡ zobrazen¡ kurzoru

         pushf
         push      ax
         push      cx
         push      dx
         xor       al,al
         call      getdispl                 ; po‡et © dk– displeje
         inc       dh                       ; © dek za rohem
         xor       dl,dl
         call      outch1                   ; nastaven¡ pozice kurzoru
         pop       dx
         pop       cx
         pop       ax
         popf
         ret

; -----------------------------------------------------------------------------
public   outhexw,outhexb

outhexw:                                  ;* zobrazen¡ slova HEX
                                            ; VSTUP: AX=slovo k zobrazen¡
                                            ;        DX=pozice
         push      ax
         mov       al,ah
         call      outhexb                  ; zobrazen¡ vy¨¨¡ho bajtu
         pop       ax

outhexb:                                  ;* zobrazen¡ bajtu HEX
                                            ; VSTUP: AL=bajt k zobrazen¡
                                            ;        DX=pozice
         push      ax
         shr       al,1
         shr       al,1
         shr       al,1
         shr       al,1
         call      outhexch
         pop       ax

outhexch:push      ax
         and       al,0fh
         cmp       al,10
         jb        outhxch1
         add       al,7
outhxch1:add       al,"0"
         call      outch1
         pop       ax
         ret

; -----------------------------------------------------------------------------
public   outch1,outch10

outch1:                                   ;* v˜stup 1 znaku na displej
                                            ; VSTUP: DX=pozice kurzoru
                                            ;        AL=ASCII znak

         push      cx                       ; £schova CX
         mov       cx,1                     ; po‡et - 1 znak
         call      outch                    ; zobrazen¡ 1 znaku
         pop       cx                       ; n vrat CX
         ret

outch10:                                  ;* v˜stup 1 znaku na displej
                                            ; VSTUP: DX=pozice kurzoru
                                            ;        AL=ASCII znak

         push      cx                       ; £schova CX
         mov       cx,1                     ; po‡et - 1 znak
         call      outch0                   ; zobrazen¡ 1 znaku
         pop       cx                       ; n vrat CX
         ret

; -----------------------------------------------------------------------------
public   outch01,outch011

outch01:                                  ;* v˜stup znaku na displej VRAM
                                            ; VSTUP: DX=pozice kurzoru
                                            ;        CX=po‡et znak–
                                            ;        AL=ASCII znak
                                            ;        BL=atribut barvy

         push      cx
         push      dx
         push      es
         cmp       dl,79                    ; je p©ekro‡ena pozice ?
         ja        outch011                 ; pozice p©ekro‡ena
         cmp       dh,cs:[displ]            ; je p©ekro‡en © dek ?
         ja        outch011                 ; © dek p©ekro‡en
         les       di,dword ptr cs:[adrvram]; adresa za‡ tku VRAM
         push      ax
         mov       al,80
         mul       dh
         xor       dh,dh
         add       ax,dx
         add       ax,ax
         add       di,ax
         pop       ax
         mov       ah,bl
         rep       stosw
outch011:pop       es
         pop       dx
         pop       cx
         ret


public   outch02

outch02:                                  ;* v˜stup znaku na displej BIOS
                                            ; VSTUP: DX=pozice kurzoru
                                            ;        CX=po‡et znak–
                                            ;        AL=ASCII znak
                                            ;        BL=atribut barvy

         push      cx
         push      dx
         mov       bh,cs:[aktpage]          ; aktivn¡ str nka
         and       bh,1
         mov       ah,2                     ; funkce nastaven¡ pozice kurzoru
         push      ax
         push      cx
         push      bx
         int       10h                      ; nastaven¡ pozice kurzoru
         pop       bx
         pop       cx
         pop       ax
         jcxz      outch022
         mov       ah,9                     ; funkce z pisu znaku s atributem
         int       10h                      ; z pis znaku
outch022:pop       dx
         pop       cx
         ret


public   outch,outch0,setout

outch:                                    ;* v˜stup znaku na displej (- ©¡dic¡)
                                            ; VSTUP: DX=pozice kurzoru
                                            ;        CX=po‡et znak–
                                            ;        AL=ASCII znak
                                            ;        DS=datov˜ segment

         cmp       al,32                    ; je nastaven¡ atributu ?
         jae       outch0                   ; nen¡ nastaven¡ atributu - tisk
         push      ax                       ; £schova AX (znak k tisku)
         push      bx                       ; £schova BX
         push      cx
         push      dx
         push      si                       ; £schova SI
         push      di                       ; £schova DI
         push      bp                       ; £schova BP
         or        al,al                    ; je pouze nastaven¡ kurzoru ?
         jz        outch12                  ; je nastaven¡ kurzoru
         xor       ah,ah                    ; AH <- 0
         mov       si,offset col1-1         ; tabulka barev
         add       si,ax                    ; adresa barvy
         mov       al,cs:[si]               ; na‡ten¡ barvy
         mov       cs:[color],al            ; nastaven¡ atributu barvy
         jmp       short outch13            ; n vrat z obsluhy
                                          ;* nastaven¡ kurzoru
outch12: mov       bh,cs:[aktpage]          ; aktivn¡ videostr nka
         and       bh,1
         mov       ah,2                     ; funkce nastaven¡ pozice kurzoru
         int       10h                      ; nastaven¡ pozice kurzoru
outch13: pop       bp                       ; n vrat BP
         pop       di                       ; n vrat DI
         pop       si                       ; n vrat SI
         pop       dx
         pop       cx
         pop       bx                       ; n vrat BX
         pop       ax                       ; n vrat AX (znak k tisku)
         ret

outch0:                                   ;* v˜stup znaku na displej
                                            ; VSTUP: DX=pozice kurzoru
                                            ;        CX=po‡et znak–
                                            ;        AL=ASCII znak
                                            ;        DS=datov˜ segment

         push      es
         push      bx                       ; £schova BX
         push      si                       ; £schova SI
         push      di                       ; £schova DI
         push      bp                       ; £schova BP
         push      ax                       ; £schova AX (znak k tisku)
         mov       bl,cs:[color]            ; aktivn¡ barva (atribut)
setout:  call      outch02                  ; v˜stup znaku (nastaven˜ ovlada‡)
         add       dl,cl                    ; zv˜¨en¡ pozice kurzoru
         pop       ax                       ; n vrat AX (znak k tisku)
         pop       bp                       ; n vrat BP
         pop       di                       ; n vrat DI
         pop       si                       ; n vrat SI
         pop       bx                       ; n vrat BX
         pop       es
         ret




; -----------------------------------------------------------------------------
public   outtxt

outtxt:                                   ;* v˜stup textu na displej
                                            ; VSTUP: DX=pozice kurzoru
                                            ;        DS:SI=text k tisku
                                            ;        (DS=datov˜ segment)
         push      ax
outtxt0: lodsb                              ; na‡ten¡ znaku k tisku
         or        al,al                    ; je koncov˜ bajt 0 ?
         jz        outtx1                   ; je koncov˜ bajt 0 - konec
         cmp       al,31                    ; je konec © dku ?
         je        outtx1                   ; je konec © dku
         call      outch1                   ; tisk 1 znaku
         jmp       short outtxt0            ; tisk dal¨¡ho znaku
outtx1:  pop       ax
         ret

; -----------------------------------------------------------------------------
public   outtx0

outtx0:                                   ;* v˜stup textu na displej
                                            ; VSTUP: DX=pozice kurzoru
                                            ;        DS:SI=text k tisku
                                            ;        (DS=datov˜ segment)

         push      ax
outtx2:  lodsb                              ; na‡ten¡ znaku k tisku
         or        al,al                    ; je koncov˜ bajt 0 ?
         jz        outtx3                   ; je koncov˜ bajt 0 - konec
         cmp       al,31                    ; je konec © dku ?
         je        outtx3                   ; je konec © dku
         call      outch10                  ; tisk 1 znaku
         jmp       short outtx2             ; tisk dal¨¡ho znaku
outtx3:  pop       ax
         ret

; -----------------------------------------------------------------------------
public   wrolldown,wrollup

wrollup:                                  ;* rolov n¡ okna nahoru
                                            ; VSTUP: BP=ukazatel okna
                                            ;        AL=po‡et © dek k rolov n¡

         call      testaktw                 ; je okno aktivn¡ ?
         jnc       wrollup0                 ; okno je aktivn¡
         ret

wrolldown:                                ;* rolov n¡ okna dol–
                                            ; VSTUP: BP=ukazatel okna
                                            ;        AL=po‡et © dek k rolov n¡
         call      testaktw                 ; je okno aktivn¡ ?
         jnc       wrolldn0                 ; okno je aktivn¡
         ret

wrollup0:                                 ;* rolov n¡ okna nahoru
         push      ax
         mov       ah,6                     ; rolov n¡ okna nahoru
         jmp       short wrolldn1           ; rolov n¡ okna


wrolldn0:                                 ;* rolov n¡ okna dol–
         push      ax
         mov       ah,7                     ; rolov n¡ dol–
wrolldn1:call      wrolldnup                ; rolov n¡ okna
         pop       ax
         ret

public   wrolldnup
wrolldnup:                                ;* rolov n¡ okna
                                            ; VSTUP: AH=7 dol–
                                            ;        AH=6 nahoru

         push      bp
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         call      mouseoff                 ; vypnut¡ my¨i
         mov       cl,ds:[bp+3]             ; po‡ te‡n¡ pozice okna
         mov       dl,cl
         add       dl,39
         mov       bh,ds:[col1]
         mov       ch,4
         call      getendlw                 ; poskytnut¡ posledn¡ho © dku
         dec       dh
         int       10h
         call      mouseon                  ; zapnut¡ my¨i
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       bp
         ret


; -----------------------------------------------------------------------------
public   getdispcx

getdispcx:                                ;* ‡ten¡ ‡¡sla posledn¡ho © dku
                                            ; VSTUP: CX=‡¡slo posledn¡ho © dku
         xor       cx,cx
         mov       cl,cs:[displ]
         ret

; -----------------------------------------------------------------------------
public   getdisplw

getdisplw:                                ;* ‡ten¡ ‡¡sla posledn¡ho © dku
                                            ; VSTUP: DH=‡¡slo posledn¡ho © dku
         mov       dh,cs:[displw]
         ret

; -----------------------------------------------------------------------------
public   getdispl

getdispl:                                 ;* ‡ten¡ ‡¡sla posledn¡ho © dku
                                            ; VSTUP: DH=‡¡slo posledn¡ho © dku
         mov       dh,cs:[displ]
         ret

; -----------------------------------------------------------------------------
public   getpoclw

getpoclw:                                 ;* poskytnut¡ po‡tu © dk– bez n povˆdy
                                            ; VSTUP: CX=po‡et © dk–

         call      getpocl
         cmp       cl,cs:[displw]           ; je konec okna men¨¡ ?
         jbe       getpocl4
         mov       cl,cs:[displw]
getpocl4:ret

; -----------------------------------------------------------------------------
public   getpocl

getpocl:                                  ;* poskytnut¡ po‡tu © dk– bez n povˆdy
                                            ; VSTUP: CX=po‡et © dk–
         xor       cx,cx
         mov       cl,cs:[displ]
         call      testakth                 ; je n povˆda aktivn¡ ?
         jc        getpocl2                 ; n povˆda nen¡ aktivn¡
         dec       cx                       ; sn¡‘en¡ po‡tu © dk–
getpocl2:ret

; -----------------------------------------------------------------------------
public   getendlw
getendlw:                                 ;* posledn¡ © dek okna DH
         call      getendl                  ; maxim ln¡ © dek okna
         cmp       dh,cs:[displw]           ; je konec okna men¨¡ ?
         jb        getendl4
         mov       dh,cs:[displw]
         dec       dh
getendl4:ret
; -----------------------------------------------------------------------------
public   getendl

getendl:                                  ;* ‡ten¡ ‡¡sla © dku konce okna
                                            ; VSTUP: DH=‡¡slo © dku konce okna

         call      getdispl                 ; poskytnut¡ ‡¡sla posledn¡ho © dku
         dec       dh
         call      testakth                 ; je n povˆda aktivn¡ ?
         jc        getend2                  ; n povˆda nen¡ aktivn¡
         dec       dh                       ; sn¡‘en¡ ‡¡sla © dku
getend2: ret

; -----------------------------------------------------------------------------
public   getnumlw

getnumlw:                                 ;* ‡ten¡ po‡tu © dk– soubor–
                                            ; VSTUP: CX=po‡et © dk– se soubory

         push      dx
         call      getendlw                 ; ‡¡slo posledn¡ho © dku okna
         xor       cx,cx
         mov       cl,dh
         sub       cl,4                     ; ode‡ten¡ okraj– okna
         pop       dx
         ret

; -----------------------------------------------------------------------------
public   getnuml

getnuml:                                  ;* ‡ten¡ po‡tu © dk– soubor–
                                            ; VSTUP: CX=po‡et © dk– se soubory

         push      dx
         call      getendl                  ; ‡¡slo posledn¡ho © dku okna
         xor       cx,cx
         mov       cl,dh
         sub       cl,4                     ; ode‡ten¡ okraj– okna
         pop       dx
         ret

; -----------------------------------------------------------------------------
public   getnumfw

getnumfw:                                 ;* zji¨tˆn¡ po‡tu soubor– na displeji
                                            ; VSTUP: CX=po‡et soubor– v oknˆ

         call      getnumlw                 ; zji¨tˆn¡ po‡tu © dk– soubor–
         call      testsetw                 ; je pln‚ zobrazen¡ okna ?
         jz        getnumf3                 ; je pln‚ zobrazen¡ okna
         push      ax
         mov       ax,cx
         add       cx,ax
         add       cx,ax
         pop       ax
getnumf3:ret

; -----------------------------------------------------------------------------
public   getnumf

getnumf:                                  ;* zji¨tˆn¡ po‡tu soubor– na displeji
                                            ; VSTUP: CX=po‡et soubor– v oknˆ

         call      getnuml                  ; zji¨tˆn¡ po‡tu © dk– soubor–
         call      testsetw                 ; je pln‚ zobrazen¡ okna ?
         jz        getnumf1                 ; je pln‚ zobrazen¡ okna
         push      ax
         mov       ax,cx
         add       cx,ax
         add       cx,ax
         pop       ax
getnumf1:ret

; -----------------------------------------------------------------------------
public   testsetwn

testsetwn:                                ;* test nastaven¡ neaktivn¡ho okna
                                            ; VSTUP: BP=adresa tabulky okna
                                            ; VSTUP: ZY=je pln‚ zobrazen¡ okna
         push      bp
         call      getnakt
         call      testsetw
         pop       bp
         ret

; -----------------------------------------------------------------------------
public   testsetw

testsetw:                                 ;* test nastaven¡ okna
                                            ; VSTUP: BP=adresa tabulky okna
                                            ; VSTUP: ZY=je pln‚ zobrazen¡ okna

         test      byte ptr cs:[bp+29],2    ; zobrazuj¡ se pozn mky ?
         jz        teststw1                 ; pozn mky se nezobrazuj¡
         cmp       al,al                    ; nastaven¡ p©¡znaku ZY
         ret

teststw1:test      byte ptr cs:[bp+29],1    ; je trojsloupcov‚ zobrazen¡ ?
         ret

; -----------------------------------------------------------------------------
public   testakth

testakth:                                 ;* test, zda je n povˆda aktivn¡
                                            ; VSTUP: BP=adresa tabulky okna
                                            ; VSTUP: CY=n povˆda nen¡ aktivn¡

         test      byte ptr cs:[flags],4    ; je n povˆda aktivn¡ ?
         jnz       testakth1                ; n povˆda je aktivn¡
         stc                                ; p©¡znak - n povˆda nen¡ aktivn¡
testakth1:ret

; -----------------------------------------------------------------------------
public   testaktwn

testaktwn:                                ;* test, zda je neaktivn¡ okno zapnut‚
                                            ; VSTUP: CY=okno je vypnut‚
         push      bp
         call      getnakt                  ; poskytnut¡ neatkivn¡ho okna
         call      testaktw                 ; test, jestli je okno zapnut‚
         pop       bp
         ret

; -----------------------------------------------------------------------------
public   testaktw

testaktw:                                 ;* test, zda je okno zapnut‚
                                            ; VSTUP: BP=adresa tabulky okna
                                            ; VSTUP: CY=okno je vypnut‚

         test      byte ptr cs:[flags],2    ; jsou vypnuta obˆ okna ?
         jnz       testaktw1                ; obˆ okna jsou vypnuta
         test      byte ptr cs:[bp+0],1     ; p©¡znak okna
         jnz       testaktw2                ; okno je zapnut‚
testaktw1:stc                               ; p©¡znak - okno je vypnut‚
testaktw2:ret

; -----------------------------------------------------------------------------
public   getakt

getakt:                                   ;* poskytnut¡ adresy aktivn¡ho okna
                                            ; VSTUP: BP=adresa tabulky okna

         test      byte ptr cs:[flags],1    ; je aktivn¡ lev‚ okno ?
         lea       bp,[tabl]                ; adresa lev‚ho okna
         jnz       getakt1                  ; je lev‚ okno
         lea       bp,[tabr]                ; adresa prav‚ho okna
getakt1: ret

; -----------------------------------------------------------------------------
public   getnakt

getnakt:                                  ;* poskytnut¡ adresy neaktivn¡ho okna
                                            ; VSTUP: BP=adresa tabulky okna

         test      byte ptr cs:[flags],1    ; je aktivn¡ lev‚ okno ?
         lea       bp,[tabl]                ; adresa lev‚ho okna
         jz        getnakt1                 ; je prav‚ okno
         lea       bp,[tabr]                ; adresa prav‚ho okna
getnakt1:ret

; -----------------------------------------------------------------------------
public   testakt

testakt:                                  ;* test, zda jde o aktivn¡ okno
                                            ; VSTUP: BP=adresa tabulky okna
                                            ; VSTUP: ZY=je aktivn¡
         push      ax
         push      bp
         call      getakt                   ; nastaven¡ adresy aktivn¡ho okna
         mov       ax,bp                    ; adresa aktivn¡ho okna
         pop       bp
         cmp       ax,bp                    ; je to aktivn¡ okno ?
         pop       ax
         ret

; -----------------------------------------------------------------------------
public   testexe

testexe:                                  ;* test, zda je soubor EXE, COM, BAT
                                            ; VSTUP: DS:SI=adresa polo‘ky
                                            ; VSTUP: ZY=je spustiteln˜ program

         push      di
         push      es
         push      cs
         pop       es
         lea       di,[extent]              ; tabulka extent–
         call      testexe0                 ; test p©¡pony EXE
         je        testexe3                 ; p©¡pona nalezena
         call      testexe0                 ; test p©¡pony COM
         je        testexe3                 ; p©¡pona nalezena
         call      testexe0                 ; test p©¡pony BAT
testexe3:pop       es
         pop       di
         ret

testexe0:                                 ;* test, zda je p©¡pona ES:DI
                                            ; VSTUP: BP=adresa parametr– okna
                                            ;        DS:SI=adresa polo‘ky
                                            ;        ES:DI=adresa p©¡pony

         push      di
         push      si
         push      ds
         add       si,9                     ; adresa p©¡pony souboru
         cmpsw
         jne       testexe1
         cmpsb
testexe1:pop       ds
         pop       si
         pop       di
         pushf
         add       di,3                     ; adresa n sleduj¡c¡ p©¡pony
         popf
         ret

; -----------------------------------------------------------------------------
;                              konverze znak–
; -----------------------------------------------------------------------------
public   konvfnt
konvfnt:                                  ;* konverze fontu znak–
                                            ; VSTUP/VSTUP: AL=znak

         pushf
         push      bx
         push      ds
         push      cs
         pop       ds
         test      byte ptr ds:[config],3
         jz        konvfnt2                 ; nen¡ p©evod fontu
         cmp       al,128                   ; nejmen¨¡ znak
         jb        konvfnt2                 ; je men¨¡
         cmp       al,171                   ; nejvˆt¨¡ znak
         ja        konvfnt2                 ; je vˆt¨¡
         sub       al,128                   ; offset v tabulce
         add       al,al
         lea       bx,[prevcs]              ; tabulka pro p©evod
         test      byte ptr ds:[config],1   ; je vypnut¡ ‡e¨tiny ?
         jnz       konvfnt1                 ; je vypnut¡ ‡e¨tiny
         inc       bx                       ; je LATIN 2
konvfnt1:xlat                               ; p©evod znaku
konvfnt2:pop       ds
         pop       bx
         popf
         ret

; -----------------------------------------------------------------------------
public   lowcase0
lowcase0:                                 ; VSTUP: AL=znak k p©evodu

         cmp       al,"A"                   ; je znak men¨¡ ne‘ "A" ?
         jb        lowcas8                  ; nen¡ velk‚ p¡smeno
         cmp       al,"Z"                   ; je znak vˆt¨¡ ne‘ "Z" ?
         ja        lowcas8                  ; nen¡ velk‚ p¡smeno
         add       al,32                    ; korekce na mal‚ p¡smeno
lowcas8: ret


public   lowcase
lowcase:                                  ;* p©evod znaku na mal‚ p¡smeno
                                            ; VSTUP: AL=znak k p©evodu

         cmp       al,"A"                   ; je znak men¨¡ ne‘ "A" ?
         jb        lowca5                   ; nen¡ velk‚ p¡smeno
         cmp       al,"Z"                   ; je znak vˆt¨¡ ne‘ "Z" ?
         ja        lowca1                   ; nen¡ velk‚ p¡smeno
         add       al,32                    ; korekce na mal‚ p¡smeno
         ret

lowca1:  or        al,al
         jns       lowca5
         test      byte ptr cs:[config],1   ; je vypnut¡ ‡e¨tiny ?
         jnz       lowca4                   ; je vypnut¡ ‡e¨tiny
         push      bx
         lea       bx,[upperlat-2]          ; p©evodn¡ tabulka LATIN 2
         test      byte ptr cs:[config],2   ; je LATIN 2 ?
         jnz       lowca2                   ; je k¢d LATIN 2
         lea       bx,[upperkam-2]          ; p©evodn¡ tabulka KAMENICKCH
lowca2:  add       bx,2                     ; zv˜¨en¡ adresy v tabulce
         cmp       byte ptr cs:[bx],0       ; je konec tabulky ?
         je        lowca3                   ; je konec tabulky
         cmp       cs:[bx+1],al             ; je hledan˜ znak ?
         jne       lowca2                   ; test dal¨¡ho znaku
         mov       al,cs:[bx]               ; p©eveden˜ znak
lowca3:  pop       bx
lowca4:  or        al,al
lowca5:  ret

; -----------------------------------------------------------------------------
public   highcase0
highcase0:                                ;* korekce AL na velk‚ p¡smeno
                                            ; VSTUP: AL=znak k p©evodu

         cmp       al,"a"                   ; je znak men¨¡ ne‘ "a" ?
         jb        highcas8                 ; nen¡ mal‚ p¡smeno
         cmp       al,"z"                   ; je znak vˆt¨¡ ne‘ "z" ?
         ja        highcas8                 ; nen¡ mal‚ p¡smeno
         sub       al,32                    ; korekce na velk‚ p¡smeno
highcas8:ret


public   highcase
highcase:                                 ;* korekce AL na velk‚ p¡smeno
                                            ; VSTUP: AL=znak k p©evodu

         cmp       al,"a"                   ; je znak men¨¡ ne‘ "a" ?
         jb        highc5                   ; nen¡ mal‚ p¡smeno
         cmp       al,"z"                   ; je znak vˆt¨¡ ne‘ "z" ?
         ja        highc1                   ; nen¡ mal‚ p¡smeno
         sub       al,32                    ; korekce na velk‚ p¡smeno
         ret

highc1:  test      byte ptr cs:[config],1   ; je vypnut¡ ‡e¨tiny ?
         jnz       highc4                   ; je vypnut¡ ‡e¨tiny
         push      bx
         lea       bx,[upperlat-2]          ; p©evodn¡ tabulka LATIN 2
         test      byte ptr cs:[config],2   ; je LATIN 2 ?
         jnz       highc2                   ; je k¢d LATIN 2
         lea       bx,[upperkam-2]          ; p©evodn¡ tabulka KAMENICKCH
highc2:  add       bx,2                     ; zv˜¨en¡ adresy v tabulce
         cmp       byte ptr cs:[bx],0       ; je konec tabulky ?
         je        highc3                   ; je konec tabulky
         cmp       cs:[bx],al               ; je hledan˜ znak ?
         jne       highc2                   ; test dal¨¡ho znaku
         mov       al,cs:[bx+1]             ; p©eveden˜ znak
highc3:  pop       bx
highc4:  or        al,al
highc5:  ret

; -----------------------------------------------------------------------------
public   transtxt
transtxt:                                   ; p©enos textu ASCIIZ
                                            ; VSTUP: DS:SI -> ES:DI
                                            ; VSTUP: ES:DI - koncov  0

         push      ax                       ; £schova AX
transt1: lodsb                              ; na‡ten¡ znaku k p©enosu
         stosb                              ; ulo‘en¡ znaku na v˜stup
         or        al,al                    ; byl to ji‘ koncov˜ bajt 0 ?
         jnz       transt1                  ; nebyla je¨tˆ koncov  0 - dal¨¡
         dec       di                       ; n vrat ukazatele na posledn¡ 0
         pop       ax                       ; n vrat AX
         ret

; -----------------------------------------------------------------------------
public   comptxt
comptxt:                                    ; porovn n¡ text– ASCIIZ
                                            ; VSTUP: DS:SI -> ES:DI
                                            ; VSTUP: ZY=je shoda ©etˆzc–

         push      si
         push      di
         push      ax                       ; £schova AX
compt1:  cmpsb                              ; porovn n¡ znak–
         jne       compt2                   ; nen¡ shoda znak–
         cmp       byte ptr ds:[si-1],0     ; byl to ji‘ konec ?
         jne       compt1                   ; nen¡ konec - dal¨¡ znak
compt2:  pop       ax                       ; n vrat AX
         pop       di
         pop       si
         ret

; -----------------------------------------------------------------------------
public   setkonc
setkonc:                                    ; z pis koncovky "y", "–"
                                            ; VSTUP: DS:SI=text ‡¡sla
                                            ;        ES:DI=ukl dac¡ adresa
         push      ax
         call      testkonc                 ; test koncovky ‡¡sla
         or        al,al
         jz        endli3                   ; je ‡¡slo 1
         mov       si,offset textvy11       ; koncovka ‡¡sla 2,3,4
         dec       al
         jz        endli2                   ; je ‡¡slo 2,3,4
         mov       si,offset textvy12       ; kocovka ostatn¡ch ‡¡sel
endli2:  call      transtxt                 ; ulo‘en¡ koncovky
endli3:  pop       ax
         ret

public   testkonc
testkonc:                                   ; test koncovky ‡¡sla
                                            ; VSTUP: DS:SI=text ‡¡sla
                                            ; VSTUP: AL=0 je koncovka pro "1"
                                            ;         AL=1 koncovka pro 2,3,4
                                            ;         AL=2 ostatn¡ ‡¡sla

         push      dx
         push      si
         mov       dx,"00"                  ; posledn¡ 2 ‡¡slice
testkon1:call      readtest                 ; na‡ten¡ prvn¡/dal¨¡ ‡¡slice
         jc        testkon2                 ; nen¡ ji‘ dal¨¡ ‡¡slice
         mov       dh,dl                    ; p©edposledn¡ ‡¡slice
         mov       dl,al                    ; posledn¡ ‡¡slice


ifdef    ANGL
         call      readtest                 ; na‡ten¡ dal¨¡ ‡¡slice
         mov       al,2                     ; p©¡znak jin‚ho ‡¡sla
         jnc       testkon3                 ; je v¡ce ‡¡slic
else

IFDEF    slov
         call      readtest                 ; na‡ten¡ dal¨¡ ‡¡slice
         mov       al,2                     ; p©¡znak jin‚ho ‡¡sla
         jnc       testkon3                 ; je v¡ce ‡¡slic
ELSE
         jmp       short testkon1           ; na‡ten¡ dal¨¡ ‡¡slice
ENDIF

endif

testkon2:mov       al,2                     ; p©¡znak jin‚ho ‡¡sla
         cmp       dh,"1"                   ; byly des¡tky ?
         je        testkon3                 ; jsou mno‘n  ‡¡sla
         cmp       dl,"4"                   ; je ‡¡slice vˆt¨¡ ne‘ "4" ?
         ja        testkon3                 ; je ‡¡slice vˆt¨¡ ne‘ "4"
         cmp       dl,"0"                   ; je ‡¡slice "0" ?
         je        testkon3                 ; je ‡¡slice "0"
         dec       al                       ; p©¡znak ‡¡sla 2,3,4
         cmp       dl,"1"                   ; je ‡¡slice "1" ?
         jne       testkon3                 ; nen¡ "1" - je 2,3,4
         dec       al                       ; p©¡znak ‡¡slice "1"
testkon3:pop       si
         pop       dx
         ret

public   readtest
readtest:                                   ; ‡ten¡ ‡¡slice p©i testu
                                            ; VSTUP: CY=nen¡ platn  ‡¡slice

         lodsb                              ; na‡ten¡ dal¨¡ ‡¡slice
         cmp       al,"."                   ; je oddˆlovac¡ te‡ka ?
         je        readtest
         or        al,al                    ; je ji‘ konec textu ?
         stc
         jz        readtst1
         cmp       al,"0"                   ; je ‡¡slice ?
         jb        readtst1                 ; nen¡ ‡¡slice
         cmp       al,"9"+1                 ; je ‡¡slice ?
         cmc                                ; CY=nen¡ ‡¡slice
readtst1:ret

; -----------------------------------------------------------------------------
public   center
center:                                     ; centrov n¡ textu ASCIIZ
                                            ; VSTUP: DS:SI=adresa textu
                                            ;        CL=max. ¨¡©ka © dku
                                            ;        DL=pozice kurzoru na © dku
                                            ; VSTUP: CL=offset za‡ tku textu
                                            ;         DL=nov  pozice kurzoru

         push      ax                       ; £schova AX
         push      si                       ; £schova SI
center1: lodsb                              ; na‡ten¡ znaku z bufferu
         or        al,al                    ; je konec textu ?
         jz        center2                  ; je ji‘ konec textu
         cmp       al,31                    ; je konec © dku ?
         je        center2                  ; je konec © dku
         jb        center1                  ; je ©¡dic¡ znak <31 - dal¨¡ znak
         dec       cl                       ; sn¡‘en¡ ‡¡ta‡e znak–
         jnz       center1                  ; nen¡ je¨tˆ 0 - dal¨¡ znak
center2: shr       cl,1                     ; CL/2 - offset od za‡ tku
         add       dl,cl                    ; zv˜¨en¡ pozice kurzoru
         pop       si                       ; n vrat SI
         pop       ax                       ; n vrat AX
         ret
; -----------------------------------------------------------------------------
public   lensi0
lensi0:                                   ;* zji¨tˆn¡ d‚lky ©etˆzce ASCIIZ DS:SI
                                            ; VSTUP: DS:SI=adresa ©etˆzce
                                            ; VSTUP: AX=d‚lka ©etˆzce
                                            ;         ZY=©etˆzec je nulov˜

         push      si
         xor       ax,ax                    ; ‡¡ta‡ d‚lky ©etˆzce
lensi01: cmp       byte ptr ds:[si],0       ; je ji‘ konec ©etˆzce ?
         je        lensi04                  ; je konec ©etˆzce
         inc       si                       ; zv˜¨en¡ ukazatele textu
         inc       al                       ; zv˜¨en¡ ‡¡ta‡e d‚lky
         jnz       lensi01                  ; dal¨¡ znak
         dec       al                       ; omezen¡ na 255
lensi04: or        al,al
         pop       si
         ret
; -----------------------------------------------------------------------------
public   lensi
lensi:                                    ;* zji¨tˆn¡ d‚lky ©etˆzce ASCIIZ DS:SI
                                            ; VSTUP: DS:SI=adresa ©etˆzce
                                            ; VSTUP: AL=d‚lka ©etˆzce
                                            ;         CY=p©ete‡en¡ (AL=255)

         push      si
         xor       ax,ax                    ; nejvˆt¨¡ nalezen  d‚lka
lens1:   cmp       byte ptr ds:[si],0       ; je ji‘ konec ©etˆzce ?
         je        lens3                    ; je konec ©etˆzce
         inc       si                       ; zv˜¨en¡ ukazatele textu
         cmp       byte ptr ds:[si-1],31    ; je konec © dku ?
         jb        lens1                    ; je ©¡dic¡ znak - ignorov n¡
         jne       lens2                    ; nen¡ konec © dku
         cmp       ah,al                    ; je vˆt¨¡ ne‘ nalezen˜ © dek ?
         jbe       lens11                   ; nen¡ vˆt¨¡
         mov       al,ah                    ; nov  nalezen  d‚lka
lens11:  xor       ah,ah                    ; nov  hodnota ‡¡ta‡e d‚lky
         jmp       short lens1
lens2:   inc       ah                       ; zv˜¨en¡ ‡¡ta‡e znak– ©etˆzce
         jnz       lens1                    ; test dal¨¡ho znaku
         dec       ah                       ; AH <- 255 p©ete‡en¡ textu
         mov       al,ah
         stc                                ; p©¡znak p©ete‡en¡
lens3:   cmp       ah,al                    ; je vˆt¨¡ ne‘ nalezen˜ © dek ?
         jbe       lens4                    ; nen¡ vˆt¨¡
         mov       al,ah                    ; nov  nalezen  d‚lka
lens4:   pop       si
         ret
; -----------------------------------------------------------------------------
public   lendi

lendi:                                    ;* zji¨tˆn¡ d‚lky ©etˆzce ASCIIZ DS:DI
                                            ; VSTUP: ES:DI=adresa ©etˆzce
                                            ; VSTUP: AL=d‚lka ©etˆzce
                                            ;         CY=p©ete‡en¡ (AL=255)

         push      di
         xor       ax,ax                    ; nejvˆt¨¡ nalezen  d‚lka
lend1:   cmp       byte ptr ds:[di],0       ; je ji‘ konec ©etˆzce ?
         je        lend3                    ; je konec ©etˆzce
         inc       di                       ; zv˜¨en¡ ukazatele textu
         cmp       byte ptr ds:[di-1],31    ; je konec © dku ?
         jb        lend1                    ; je ©¡dic¡ znak - ignorov n¡
         jne       lend2                    ; nen¡ konec © dku
         cmp       ah,al                    ; je vˆt¨¡ ne‘ nalezen˜ © dek ?
         jbe       lend11                   ; nen¡ vˆt¨¡
         mov       al,ah                    ; nov  nalezen  d‚lka
lend11:  xor       ah,ah                    ; nov  hodnota ‡¡ta‡e d‚lky
         jmp       short lend1
lend2:   inc       ah                       ; zv˜¨en¡ ‡¡ta‡e znak– ©etˆzce
         jnz       lend1                    ; test dal¨¡ho znaku
         dec       ah                       ; AH <- 255 p©ete‡en¡ textu
         mov       al,ah
         stc                                ; p©¡znak p©ete‡en¡
lend3:   cmp       ah,al                    ; je vˆt¨¡ ne‘ nalezen˜ © dek ?
         jbe       lend4                    ; nen¡ vˆt¨¡
         mov       al,ah                    ; nov  nalezen  d‚lka
lend4:   pop       di
         ret
; -----------------------------------------------------------------------------
;                            Zobrazen¡ ‡¡sel
; -----------------------------------------------------------------------------
public   tisknm2s,tisknm20,tisknm21
tisknm2s:                                   ; tisk ‡¡sla 2 ‡¡slice s mezerami
                                            ; VSTUP: AL=‡¡slo
                                            ;        DX=pozice

         call      setspc                   ; nastaven¡ m¢du tisku mezer
         jmp       short tisknm21

tisknm20:                                   ; tisk ‡¡sla 2 ‡¡slice s nulami
                                            ; VSTUP: AL=‡¡slo
                                            ;        DX=pozice

         call      setnul                   ; nastaven¡ m¢du tisku mezer
tisknm21:push      di
         push      si
         push      bx
         push      cx
         push      ds
         push      es
         push      cs
         pop       ds
         push      cs
         pop       es
         push      dx                       ; pozice
         mov       di,offset bufwin2        ; tiskov˜ buffer
         xor       ah,ah
         call      deknum2                  ; dek¢dov n¡ ‡¡sla 2 ‡¡slice
         pop       dx                       ; pozice
         mov       si,offset bufwin2        ; tiskov˜ buffer
         call      outtxt                   ; zobrazen¡ textu
         call      setfre                   ; voln˜ form t tisku ‡¡sla
         pop       es
         pop       ds
         pop       cx
         pop       bx
         pop       si
         pop       di
         ret

; -----------------------------------------------------------------------------
public   tisknm0
tisknm0:                                    ; tisk ‡¡sla 10 ‡¡slic zarov. vpravo
                                            ; VSTUP: BX:AX=‡¡slo
                                            ;        DX=pozice

         push      di
         push      si
         push      ds
         push      es
         push      cs
         pop       ds
         push      cs
         pop       es
         push      dx                       ; pozice
         call      setspc                   ; nastaven¡ m¢du tisku mezer
         mov       dx,bx                    ; vy¨¨¡ slovo ‡¡sla
         mov       di,offset bufwin2        ; tiskov˜ buffer
         call      deknumx                  ; p©ek¢dov n¡ ‡¡sla s omezen¡m
         pop       dx                       ; pozice
         mov       si,offset bufwin2        ; tiskov˜ buffer
         call      outtxt                   ; zobrazen¡ textu
         call      setfre                   ; voln˜ form t tisku ‡¡sla
         pop       es
         pop       ds
         pop       si
         pop       di
         ret

; -----------------------------------------------------------------------------
public   setspc,setnul,setfre
setspc:                                   ;* zapnut¡ tisku mezer p©ed ‡¡slem

         and       byte ptr cs:[parnum],0fdh ; vypnut¡ tisku nul
         or        byte ptr cs:[parnum],2   ; zapnut¡ tisku mezer
         ret


setnul:                                   ;* zapnut¡ tisku nul p©ed ‡¡slem

         and       byte ptr cs:[parnum],0fbh ; vypnut¡ tisku mezer
         or        byte ptr cs:[parnum],4   ; zapnut¡ tisku nul
         ret


setfre:                                   ;* zapnut¡ voln‚ho form tu ‡¡sla

         and       byte ptr cs:[parnum],0f9h ; vypnut¡ tisku mezer a nul
         ret

; -----------------------------------------------------------------------------
public   deknumx,deknmx1,deknmx2,deknmx3,deknmx4
deknumx:                                    ; p©ek¢dov n¡ ‡¡sla s omezen¡m
                                            ; VSTUP: (DX:)AX=‡¡slo k p©ek¢dov n¡
                                            ;        ES:DI=adr. k ulo‘en¡ textu
                                            ; VSTUP: AL=po‡et znak– ©etˆzce
                                            ; zni‡en‚ registry: AX,BX,CX,DX

         cmp       dx,5f5h                  ; vy¨¨¡ slovo ‡¡sla 100 000 000
         jne       deknmx1                  ; nen¡ shoda
         cmp       ax,0e100h                ; ni‘¨¡ slovo ‡¡sla 100 000 000
deknmx1: jnb       deknmx2                  ; je ‡¡slo 100 000 000 nebo vy¨¨¡
         jmp       deknum8                  ; dek¢dov n¡ ‡¡sla 8 ‡¡slic

deknmx2: cmp       dx,3b9ah                 ; vy¨¨¡ slovo ‡¡sla 1000 000 000
         jne       deknmx3                  ; nen¡ shoda
         cmp       ax,0ca00h                ; ni‘¨¡ slovo ‡¡sla 1000 000 000
deknmx3: jnb       deknmx4                  ; je ‡¡slo 1000 000 000 nebo vy¨¨¡
         or        byte ptr cs:[parnum],10h ; z kaz tisku te‡ku tis¡c–
         call      deknum9                  ; dek¢dov n¡ ‡¡sla 9 ‡¡slic
         and       byte ptr cs:[parnum],0efh; povolen¡ tisku te‡ky tis¡c–
         ret

deknmx4: or        byte ptr cs:[parnum],1   ; z kaz tisku te‡ek
         call      deknum0                  ; dek¢dov n¡ ‡¡sla 10 ‡¡slic
         and       byte ptr cs:[parnum],0feh ; povolen¡ tisku te‡ek
         ret

; -----------------------------------------------------------------------------
public   deknum,deknum0,deknum9,deknum8,deknum7,deknum6,deknum5,deknum4
public   deknum41,deknum3,deknum2
deknum:                                   ;* p©evod ‡¡sla na dekadick‚ ‡¡slo
                                            ; VSTUP: (DX:)AX=‡¡slo k p©ek¢dov n¡
                                            ;        ES:DI=adr. k ulo‘en¡ textu
                                            ; VSTUP: AL=po‡et znak– ©etˆzce
                                            ; zni‡en‚ registry: AX,BX,CX,DX

deknum0:                                  ;* dek¢dov n¡ 10 ‡¡slic
         mov       bx,3b9ah                 ; vy¨¨¡ slovo ‡¡sla 1000 000 000
         mov       cx,0ca00h                ; ni‘¨¡ slovo ‡¡sla 1000 000 000
         call      deknm0                   ; dek¢dov n¡ 1 ‡¡slice
deknum9:                                  ;* dek¢dov n¡ 9 ‡¡slic
         mov       bx,5f5h                  ; vy¨¨¡ slovo ‡¡sla 100 000 000
         mov       cx,0e100h                ; ni‘¨¡ slovo ‡¡sla 100 000 000
         call      deknm0                   ; dek¢dov n¡ 1 ‡¡slice
deknum8:                                  ;* dek¢dov n¡ 8 ‡¡slic
         mov       bl,98h                   ; vy¨¨¡ slovo ‡¡sla 10 000 000
         mov       cx,9680h                 ; ni‘¨¡ slovo ‡¡sla 10 000 000
         call      deknm02                  ; dek¢dov n¡ 1 ‡¡slice
deknum7:                                  ;* dek¢dov n¡ 7 ‡¡slic
         mov       bl,0fh                   ; vy¨¨¡ slovo ‡¡sla 1 000 000
         mov       cx,4240h                 ; ni‘¨¡ slovo ‡¡sla 1 000 000
         call      deknm02                  ; dek¢dov n¡ 1 ‡¡slice
         call      deknm8                   ; oddˆlovac¡ te‡ka
deknum6:                                  ;* dek¢dov n¡ 6 ‡¡slic
         mov       bl,1                     ; vy¨¨¡ slovo ‡¡sla 100 000
         mov       cx,86a0h                 ; ni‘¨¡ slovo ‡¡sla 100 000
         call      deknm02                  ; dek¢dov n¡ 1 ‡¡slice
deknum5:                                  ;* dek¢dov n¡ 5 ‡¡slic
         mov       cx,10000                 ; ni‘¨¡ slovo ‡¡sla 10 000
         call      deknm01                  ; dek¢dov n¡ 1 ‡¡slice
deknum4:                                  ;* dek¢dov n¡ 4 ‡¡slic
         mov       cx,1000                  ; ni‘¨¡ slovo ‡¡sla 1 000
         call      deknm00                  ; dek¢dov n¡ 1 ‡¡slice
         test      byte ptr cs:[parnum],10h ; je z kaz tisku tis¡c– ?
         jz        deknum41                 ; nen¡ z kaz tisku tis¡c–
         or        byte ptr cs:[parnum],8   ; p©echodn˜ z kaz tisku te‡ky
deknum41:call      deknm8                   ; oddˆlovac¡ te‡ka
deknum3:                                  ;* dek¢dov n¡ 3 ‡¡slic
         mov       cx,100                   ; ni‘¨¡ slovo ‡¡sla 100
         call      deknm00                  ; dek¢dov n¡ 1 ‡¡slice
deknum2:                                  ;* dek¢dov n¡ 2 ‡¡slic
         mov       cx,10                    ; ni‘¨¡ slovo ‡¡sla 10
         call      deknm00                  ; dek¢dov n¡ 1 ‡¡slice
                                          ;* dek¢dov n¡ 1 ‡¡slice
         add       al,"0"                   ; korekce na ‡¡slici
         stosb                              ; ulo‘en¡ posledn¡ ‡¡slice
         xor       al,al
         stosb                              ; ulo‘en¡ ukon‡ovac¡ho bajtu 0
         dec       di                       ; ukazatel na posledn¡ bajt 0
         and       byte ptr cs:[parnum],0f7h; zru¨en¡ p©echodn‚ho z kazu te‡ky
         xchg      al,cs:[citdek]           ; vynulov n¡ ‡¡ta‡e ‡¡slic
         inc       al                       ; po‡et znak– dek¢dovan‚ho textu
         ret

public   deknm00,deknm01,deknm02,deknm0,deknm1,deknm2,deknm21,deknm3

                                          ;* dek¢dov n¡ ‡¡slice 32-bit. ‡¡sla
                                            ; VSTUP: DX:AX=‡¡slo
                                            ;        BX:CX=dˆlitel ‡¡sla
                                            ;        ES:DI=adr. k ulo‘en¡ textu

deknm00: xor       dx,dx                    ; DX <- 0
deknm01: xor       bl,bl                    ; BL <- 0
deknm02: xor       bh,bh                    ; BH <- 0
deknm0:  mov       byte ptr cs:[citnum],2fh ; inicializace ‡¡ta‡e ‡¡slice
deknm1:  inc       byte ptr cs:[citnum]     ; zv˜¨en¡ ‡¡ta‡e ‡¡slice
         sub       ax,cx                    ; ode‡ten¡ ni‘¨¡ho slova ‡¡sla
         sbb       dx,bx                    ; ode‡ten¡ vy¨¨¡ho slova ‡¡sla
         jnc       deknm1                   ; nen¡ je¨tˆ p©ete‡en¡
         add       ax,cx                    ; n vrat ni‘¨¡ho slova ‡¡sla
         adc       dx,bx                    ; n vrat vy¨¨¡ho slova ‡¡sla
         cmp       byte ptr cs:[citnum],"0" ; je ‡¡slice 0 ?
         jne       deknm2                   ; nen¡ ‡¡slice 0 - dek¢dov n¡
         cmp       byte ptr cs:[citdek],0   ; byla ji‘ nˆjak  ‡¡slice ?
         jne       deknm2                   ; byla ji‘ nˆjak  ‡¡slice
         test      byte ptr cs:[parnum],4   ; je tisku nul ?
         jnz       deknm2                   ; je tisk nul
         mov       byte ptr cs:[citnum]," " ; n hradn¡ znak mezery
         test      byte ptr cs:[parnum],2   ; je tisk mezer ?
         jz        deknm3                   ; nen¡ tisk mezer
         jmp       short deknm21
deknm2:  inc       byte ptr cs:[citdek]     ; zv˜¨en¡ ‡¡ta‡e ‡¡slic
deknm21: push      ax                       ; £schova AX
         mov       al,cs:[citnum]           ; dek¢dovan  ‡¡slice
         stosb                              ; ulo‘en¡ ‡¡slice
         pop       ax                       ; n vrat AX
deknm3:  ret

public   deknm8,deknm81,deknm9
deknm8:
         push      ax
         mov       al,"."                   ; oddˆlovac¡ te‡ka
         test      byte ptr cs:[parnum],9   ; je povolen˜ tisk te‡ky ?
         jnz       deknm9                   ; tisk nen¡ povolen˜
         cmp       byte ptr cs:[citdek],0   ; byla ji‘ platn  ‡¡slice ?
         jne       deknm81                  ; byla ji‘ platn  ‡¡slice
         test      byte ptr cs:[parnum],3   ; je voln˜ form t ?
         jz        deknm9                   ; je voln˜ form t
         test      byte ptr cs:[parnum],4   ; je tisk nul ?
         jnz       deknm81                  ; je tisk nul
         mov       al," "                   ; n hradn¡ oddˆlovac¡ mezera
deknm81: mov       byte ptr es:[di],al      ; oddˆlovac¡ znak
         inc       di                       ; zv˜¨en¡ ukl dac¡ adresy
         cmp       byte ptr cs:[citdek],0   ; je ji‘ nˆjak  ‡¡slice ?
         je        deknm9                   ; nen¡ je¨tˆ ‘ dn  ‡¡slice
         inc       byte ptr cs:[citdek]     ; zv˜¨en¡ ‡¡ta‡e ‡¡slic (znak–)
deknm9:  pop       ax
         ret

; -----------------------------------------------------------------------------
;                        Obsluha segmentace pamˆti
; -----------------------------------------------------------------------------
public   iniedis
iniedis:                                  ;* inicializace segmentu editoru
                                            ; VSTUP: CY=nen¡ voln  pamˆŸ
         push      bx
         push      ax
         mov       word ptr cs:[editnum],0  ; zru¨en¡ obsahu pamˆti
         call      createseg                ; vytvo©en¡ nov‚ho segmentu
         jc        iniedis2                 ; nen¡ voln  polo‘ka
         mov       cs:[editseg],al          ; ulo‘en¡ ‡¡sla segmentu
         call      getmax                   ; poskytnut¡ max. velikosti segmentu
         jc        iniedis2                 ; nen¡ voln  pamˆŸ
         mov       cs:[editmax],bx          ; maxim ln¡ velikost bufferu
         call      modiseg                  ; modifikace segmentu na max. velik.
iniedis2:pop       ax
         pop       bx
         ret
; -----------------------------------------------------------------------------
public   aktedis
aktedis:                                  ;* aktual. segmentu s edit. soub.
                                            ; VSTUP: DS=adresa segmentu
         pushf
         push      bx
         push      ax
         mov       al,cs:[editseg]          ; segment se souborem
;         or        al,al                    ; je segment definovan˜ ?
;         jz        aktedis1                 ; nen¡ definovan˜
         call      getseg                   ; poskytnut¡ adresy segmentu
         mov       cs:[topseged],bx         ; adresa segmentu editoru
aktedis1:pop       ax
         pop       bx
         popf
         ret
; -----------------------------------------------------------------------------
public   deledis
deledis:                                  ;* zru¨en¡ segmentu s edit. soub.

         push      ax
         mov       al,cs:[editseg]          ; segment se souborem
         call      delseg                   ; poskytnut¡ adresy segmentu
         mov       byte ptr cs:[editseg],0  ; zru¨en¡ segmentu
         mov       word ptr cs:[editnum],0  ; zru¨en¡ obsahu pamˆti
         pop       ax
         ret
; -----------------------------------------------------------------------------
public   getmax

getmax:                                   ;* poskytnut¡ maxim.velikosti segmentu
                                            ; VSTUP: BX=maxim. velikost segm.
                                            ;         CY=segment < 16 B

         push      cx
         mov       bx,ds:[segend]           ; segment konce pamˆti
         sub       bx,2                     ; rezerva
         sub       bx,ds:[topseg]           ; voln  pamˆŸ
         jnc       getmax1                  ; nen¡ p©ete‡en¡
         xor       bx,bx                    ; BX <- 0 (nen¡ pamˆŸ)
getmax1: cmp       bx,0fe0h                 ; je vˆt¨¡ ne‘ 64 KB ?
         jb        getmax2                  ; nen¡ vˆt¨¡ ne‘ 64 KB
         mov       bx,0fe0h                 ; omezen¡ na 64 KB
getmax2: mov       cl,4
         shl       bx,cl                    ; voln  kapacita (v bajtech)
         cmp       bx,10h                   ; porovn n¡ s minim ln¡ velikost¡
         pop       cx
         ret
; -----------------------------------------------------------------------------
public   createseg

createseg:                                ;* vytvo©en¡ nov‚ho segmentu dat
                                            ; VSTUP: AL=‡¡slo p©idˆl. segmentu
                                            ;         CY=nen¡ voln  polo‘ka

         push      cx
         push      bx                       ; £schova BX
         xor       al,al                    ; ‡¡ta‡ polo‘ek
         mov       bx,offset seznseg        ; adresa seznamu segment–
         mov       cx,offset seznseg0       ; konec seznamu
         sub       cx,bx                    ; d‚lka seznamu
         shr       cx,1                     ; po‡et polo‘ek v seznamu
creats1: cmp       word ptr cs:[bx],0       ; je voln  polo‘ka ?
         jz        creats2                  ; nalezena voln  polo‘ka
         inc       al                       ; zv˜¨en¡ ‡¡sla polo‘ky
         add       bx,2                     ; zv˜¨en¡ adresy v tabulce
         loop      creats1                  ; test dal¨¡ polo‘ky
         stc                                ; p©¡znak chyby - nen¡ ‘ dn  voln 
         jmp       short creats3            ; n vrat s chybou
creats2: mov       cx,cs:[topseg]           ; segment za‡ tku voln‚ pamˆti
         mov       cs:[bx],cx               ; nastaven¡ nov‚ho segmentu
         mov       bx,1                     ; minim ln¡ d‚lka segmentu
         call      modiseg                  ; modifikace velikosti segmentu
creats3: pop       bx                       ; n vrat BX
         pop       cx                       ; n vrat
         ret

; -----------------------------------------------------------------------------
public   delseg

delseg:                                   ;* zru¨en¡ polo‘ky segmentu
                                            ; VSTUP: AL=‡¡slo ru¨en‚ho segmentu

         push      bx                       ; £schova BX
         xor       bx,bx                    ; nov˜ konec segmentu = 0
         call      modiseg0                 ; modifikace segmentu na 0
         mov       bl,al                    ; ‡¡slo ru¨en‚ho segmentu
         add       bx,bx                    ; ‡¡slo segmentu * 2
         add       bx,offset seznseg        ; adresa polo‘ky segmentu
         mov       word ptr cs:[bx],0       ; vynulov n¡ polo‘ky
         pop       bx                       ; n vrat BX
         ret

; -----------------------------------------------------------------------------
public   getseg

getseg:                                   ;* poskytnut¡ adresy segmentu
                                            ; VSTUP: AL=‡¡slo segmentu
                                            ; VSTUP: BX=adresa segmentu

         xor       bh,bh                    ; BH <- 0
         mov       bl,al                    ; ‡¡slo segmentu v tabulce
         add       bx,bx                    ; ‡¡slo segmentu * 2
         add       bx,offset seznseg        ; adresa polo‘ky v seznamu
         mov       bx,cs:[bx]               ; p©e‡ten¡ adresy segmentu
         ret

; -----------------------------------------------------------------------------
public   getssize

getssize:                                 ;* poskytnut¡ velikosti segmentu
                                            ; VSTUP: AL=‡¡slo segmentu
                                            ; VSTUP: BX=velikost segmentu
         push      ax
         push      dx
         call      getseg                   ; poskytnut¡ adresy segmentu
         mov       dx,bx                    ; £schova adresy segmentu
         call      nextseg                  ; nalezen¡ n sleduj¡c¡ho segmentu
         mov       bx,cs:[topseg]           ; segment za‡ tku voln‚ pamˆti
         jc        getssiz2                 ; nenalezen dal¨¡ segment
         call      getseg                   ; poskytnut¡ adresy segmentu
getssiz2:sub       bx,dx                    ; velikost segmentu (odstavc–)
         add       bx,bx
         add       bx,bx
         add       bx,bx
         add       bx,bx                    ; skute‡n  d‚lka segmentu
         pop       dx
         pop       ax
         ret

; -----------------------------------------------------------------------------
public   nextseg

nextseg:                                  ;* nalezen¡ n sleduj¡c¡ho segmentu
                                            ; VSTUP: AL=‡¡slo segmentu
                                            ; VSTUP: AL=n sled. segment (CN)
                                            ;         CY=nen¡ ji‘ dal¨¡ segment

         push      bx
         push      cx
         push      dx
         push      si
         call      getseg                   ; poskytnut¡ adresy segmentu BX
         lea       si,[seznseg]             ; seznam segment–
         xor       ah,ah                    ; ‡¡ta‡ ‡¡sla polo‘ky
         mov       dx,0ffffh                ; v˜choz¡ minim ln¡ adresa segmentu
         mov       cx,offset seznseg0       ; konec seznamu
         sub       cx,offset seznseg        ; d‚lka seznamu
         shr       cx,1                     ; po‡et polo‘ek v seznamu
nextseg1:cmp       cs:[si],dx               ; je vˆt¨¡ polo‘ka ne‘ nalezen  ?
         jae       nextseg2                 ; nen¡ to men¨¡ polo‘ka
         cmp       cs:[si],bx               ; je z rove¤ vˆt¨¡ ne‘ testovan  ?
         jbe       nextseg2                 ; nen¡ vˆt¨¡
         mov       dx,cs:[si]               ; nov  nejbli‘¨¡ polo‘ka
         mov       al,ah                    ; nov‚ ‡¡slo segmentu
nextseg2:add       si,2                     ; zv˜¨en¡ ukazatele v bufferu
         inc       ah                       ; zv˜¨en¡ ‡¡sla segmentu
         loop      nextseg1                 ; testov n¡ dal¨¡ polo‘ky
         cmp       dx,0ffffh                ; byla nalezena nˆjak  polo‘ka ?
         cmc                                ; CY=polo‘ka nenalezena
         pop       si
         pop       dx
         pop       cx
         pop       bx
         ret

; -----------------------------------------------------------------------------
public   predseg

predseg:                                  ;* nalezen¡ p©edch zej¡c¡ho segmentu
                                            ; VSTUP: AL=‡¡slo segmentu
                                            ; VSTUP: AL=p©edch z. segment (CN)
                                            ;         CY=nen¡ ji‘ dal¨¡ segment

         push      bx
         push      cx
         push      dx
         push      si
         call      getseg                   ; poskytnut¡ adresy segmentu
         lea       si,[seznseg]             ; seznam segment–
         xor       ah,ah                    ; ukazatel ‡¡sla polo‘ky
         xor       dx,dx                    ; v˜choz¡ minim ln¡ adresa segmentu
         mov       cx,offset seznseg0       ; konec seznamu
         sub       cx,offset seznseg        ; d‚lka seznamu
         shr       cx,1                     ; po‡et polo‘ek v seznamu
predseg1:cmp       cs:[si],dx               ; je men¨¡ polo‘ka ne‘ nalezen  ?
         jbe       predseg2                 ; nen¡ to vˆt¨¡ polo‘ka
         cmp       cs:[si],bx               ; je z rove¤ men¨¡ ne‘ testovan  ?
         jae       predseg2                 ; nen¡ men¨¡
         mov       dx,cs:[si]               ; nov  nejbli‘¨¡ polo‘ka
         mov       al,ah
predseg2:add       si,2                     ; zv˜¨en¡ ukazatele v bufferu
         inc       ah
         loop      predseg1                 ; testov n¡ dal¨¡ polo‘ky
         cmp       dx,1                     ; byla nalezena nˆjak  polo‘ka ?
         pop       si
         pop       dx
         pop       cx
         pop       bx
         ret

; -----------------------------------------------------------------------------
public   modiseg,modiseg0

modiseg:                                  ;* modifikace segmentu s ochranou
         push      bx
         or        bx,bx
         jnz       modisg01
         inc       bx                       ; rezerva
modisg01:call      modiseg0
         pop       bx
         ret

modiseg0:                                 ;* modifikace velikosti segmentu
                                          ;* (prov d¡ se po n sobc¡ch 256 B)
                                            ; VSTUP: AL=‡¡slo segmentu
                                            ;        BX=nov  velikost segmentu
                                            ; VSTUP: CY=nedostatek pamˆti
         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di

         add       bx,255                   ; zaokrouhlen¡ na n sobek 256
         jnc       modisegc                 ; nen¡ p©ete‡en¡
         jmp       modisega                 ; p©ete‡en¡
modisegc:xor       bl,bl                    ; BL <- 0; z–stane pouze vy¨¨¡ bajt
         mov       dx,bx                    ; po‘adovan  velikost segmentu
         call      getssize                 ; poskytnut¡ velikosti segmentu
         sub       bx,dx                    ; porovn n¡ vy¨¨¡ch slov velikost¡
         mov       dx,bx                    ; £schova posunu
         ja        modiseg1                 ; po‘adov no sn¡‘en¡ velikosti
         jc        modiseg4                 ; po‘adov no zv˜¨en¡ velikosti
         jmp       modiseg9                 ; zmˆny jsou v © du 256 bajt–
modiseg1:                                 ;* je sn¡‘en¡ velikosti segmentu
         shr       dx,1
         shr       dx,1
         shr       dx,1
         shr       dx,1                     ; posun segment– v odstavc¡ch
modiseg2:call      nextseg                  ; n sleduj¡c¡ segment
         jnc       modiseg8                 ; je je¨tˆ dal¨¡ segment
         sub       word ptr cs:[topseg],dx  ; sn¡‘en¡ za‡ tku voln‚ pamˆti
         jmp       modiseg9                 ; nen¡ ji‘ dal¨¡ segment
modiseg8:call      getseg                   ; poskytnut¡ adresy segmentu
         mov       cx,bx                    ; £schova adresy segmentu
         call      getssize                 ; poskytnut¡ velikosti segmentu
         xchg      cx,bx                    ; BX<-adresa segmentu, CX<-velikost
         push      ds                       ; £schova DS
         push      es                       ; £schova ES
         mov       ds,bx                    ; sou‡asn  adresa segmentu
         sub       bx,dx                    ; nov  adresa segmentu (sn¡‘en )
         mov       es,bx                    ; nov  adresa segmentu (sn¡‘en )
         xor       si,si                    ; offset zdrojov‚ adresy
         xor       di,di                    ; offset c¡lov‚ adresy
         rep       movsb                    ; p©enos obsahu segmentu
         pop       es                       ; n vrat ES
         pop       ds                       ; n vrat DS
         mov       bl,al                    ; ‡¡slo segmentu
         xor       bh,bh                    ; BH <- 0
         add       bx,bx                    ; ‡¡slo segmentu * 2
         add       bx,offset seznseg        ; adresa polo‘ky segmentu
         sub       word ptr cs:[bx],dx      ; sn¡‘en¡ adresy segmentu
         jmp       short modiseg2           ; p©esun dal¨¡ho segmentu

modiseg4:                                 ;* je zv˜¨en¡ velikosti segmentu
         neg       dx                       ; posun segmentu
         shr       dx,1
         shr       dx,1
         shr       dx,1
         shr       dx,1                     ; posun segment– v odstavc¡ch
                                          ;* zjist¡ se, kolik segment– n sleduje
         xor       cx,cx                    ; ‡¡ta‡ zbyl˜ch segment–
modiseg5:inc       cx                       ; zv˜¨en¡ ‡¡ta‡e segment–
         call      nextseg                  ; n sleduj¡c¡ segment
         jnc       modiseg5                 ; nalezen¡ posledn¡ho segmentu
         dec       cx                       ; po‡et n sleduj¡c¡ch segment–
                                          ;* nastaven¡ nov‚ho konce pamˆti
modiseg6:push      ax                       ; £schova ‡¡sla segmentu
         push      cx                       ; £schova ‡¡ta‡e segment–
         call      getseg                   ; poskytnut¡ adresy segmentu
         mov       cx,bx                    ; adresa segmentu
         call      getssize                 ; poskytnut¡ velikosti segmentu
         xchg      cx,bx                    ; BX<-adresa segmentu, CX<-velikost
         mov       ax,cx                    ; velikost segmentu
         add       ax,15                    ; zaokrouhlen¡ na odstavec
         shr       ax,1
         shr       ax,1
         shr       ax,1
         shr       ax,1                     ; velikost segmentu v odstavc¡ch
         add       ax,bx                    ; p–vodn¡ adresa konce segmentu
         add       ax,dx                    ; nov  adresa konce segmentu
         cmp       ax,cs:[segend]           ; kontrola p©ekro‡en¡ konce
         ja        modisg61                 ; p©ekro‡en¡ konce pamˆti
         mov       cs:[topseg],ax           ; nov˜ za‡ tek voln‚ pamˆti
modisg61:pop       cx
         pop       ax
         jbe       modiseg7                 ; nen¡ je¨tˆ p©ekro‡en¡ konce
         stc                                ; p©¡znak chyby
         jmp       short modisega           ; n vrat s chybou
                                          ;* p©esunut¡ v¨ech segment–
modiseg7:jcxz      modiseg9                 ; nen sleduje ‘ dn˜ segment
         push      cx                       ; ‡¡ta‡ segment–
         push      ds                       ; £schova DS
         push      es                       ; £schova ES
         call      getseg                   ; poskytnut¡ adresy segmentu
         mov       cx,bx                    ; adresa segmentu
         call      getssize                 ; poskytnut¡ velikosti segmentu
         xchg      cx,bx                    ; BX<-adresa segmentu, CX<-velikost
         mov       ds,bx                    ; sou‡asn  adresa segmentu
         add       bx,dx                    ; nov  adresa segmentu (zv˜¨en )
         mov       es,bx                    ; nov  adresa segmentu
         mov       si,cx                    ; d‚lka segmentu
         dec       si                       ; offset posledn¡ho bajtu
         mov       di,si                    ; offset posledn¡ho bajtu
         std                                ; smˆr p©enosu dol–
         rep       movsb                    ; posun obsahu segmentu
         cld                                ; smˆr p©enosu nahoru
         pop       es
         pop       ds
         pop       cx
         mov       bl,al                    ; ‡¡slo segmentu
         xor       bh,bh                    ; BH <- 0
         add       bx,bx                    ; ‡¡slo segmentu * 2
         add       bx,offset seznseg        ; adresa polo‘ky segmentu
         add       word ptr cs:[bx],dx      ; zv˜¨en¡ adresy segmentu
         call      predseg                  ; ‡¡slo p©edch zej¡c¡ho segmentu
         loop      modiseg7                 ; p©esun dal¨¡ho segmentu
modiseg9:clc                                ; p©¡znak - operace OK
modisega:pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         call      aktedis                  ; aktualizace segmentu editoru
         ret

; *****************************************************************************
;
;                      Podprogramy pro diskov‚ operace
;
; *****************************************************************************
public   testdev
testdev:                                  ;* test za©¡zen¡
                                            ; VSTUP: BX=identifik tor za©¡zen¡
                                            ; VSTUP: CY=chyba
                                            ;         NZ=je znakov‚ za©¡zen¡
         push      ax
         push      dx
         mov       ax,4400h
         int       21h                      ; poskytnut¡ informac¡ o za©¡zen¡
         jc        testdev2                 ; chyba
         test      dl,80h                   ; je to znakov‚ za©¡zen¡ ?
         jz        testdev2                 ; je diskov‚ za©¡zen¡
         xor       dh,dh                    ; DH <- 0
         or        dl,20h                   ; nastaven¡ bin rn¡ho m¢du
         mov       ax,4401h
         int       21h                      ; nastaven¡ bin rn¡ho m¢du
         or        dx,dx                    ; p©¡znaky NC, NZ
testdev2:pop       dx
         pop       ax
         ret
; -----------------------------------------------------------------------------
public   loadpath
loadpath:                                 ;* na‡ten¡ cesty aktivn¡ho adres ©e
                                            ; VSTUP: ES:DI=ukl dac¡ adresa

         push      ax                       ; £schova AX
         push      dx                       ; £schova DX
         push      si                       ; £schova SI
         push      di                       ; £schova DI
         push      ds                       ; £schova DS
         mov       ah,19h                   ; funkce poskytnut¡ aktivn¡ho disku
         int       21h                      ; poskytnut¡ aktivn¡ho disku
         inc       al                       ; ‡¡slo disku + 1
         mov       dl,al                    ; £schova ‡¡sla disku
         add       al,40h                   ; korekce disku na znak ASCII
         stosb                              ; ulo‘en¡ ozna‡en¡ disku
         mov       ax,"\:"                  ; text ":\"
         stosw                              ; ulo‘en¡ textu ":\"
         mov       si,di                    ; za‡ tek ozna‡en¡ cesty
         xor       al,al
         stosb
         push      es                       ; ES -> stack
         pop       ds                       ; DS <- ES segment s bufferem
         mov       ah,47h                   ; funkce poskytnut¡ aktiv. adres ©e
         int       21h                      ; poskytnut¡ aktivn¡ho adres ©e
         pop       ds                       ; n vrat DS
         pop       di                       ; n vrat DI
         pop       si                       ; n vrat SI
         pop       dx                       ; n vrat DX
         pop       ax                       ; n vrat AX
         ret

; -----------------------------------------------------------------------------
public   aktdir
aktdir:                                   ;* nastaven¡ aktivn¡ho adres ©e
                                            ; VSTUP: DS:SI=p©¡stupov  cesta
                                            ; VSTUP: NZ=diskov  chyba
                                            ;         CY=adres © nenalezen

         push      dx                       ; £schova DX
         push      ax                       ; £schova AX
         cmp       byte ptr ds:[si],0       ; je zad n adres © ?
         je        aktdir2                  ; adres © nen¡ zad n
         cmp       byte ptr ds:[si+1],":"   ; je ud n disk ?
         jne       aktdir1                  ; nen¡ ud n disk
         mov       al,ds:[si]               ; ozna‡en¡ disku
         call      highcase                 ; p©evod na velk‚ p¡smeno
         sub       al,"A"                   ; p©evod na ‡¡slo disku
         mov       dl,al                    ; po‘adovan˜ disk
         mov       ah,0eh                   ; funkce nastaven¡ disku
         int       21h                      ; nastaven¡ aktivn¡ho disku
         mov       ah,19h
         int       21h                      ; poskytnut¡ aktivn¡ho disku
         cmp       al,dl                    ; souhlas¡ nastaven˜ disk ?
         stc
         jne       aktdir2                  ; chyba zad n¡ disku
         cmp       byte ptr ds:[si+2],0     ; je zad n adres © ?
         je        aktdir2                  ; nen¡ zad n adres © - OK
aktdir1: cmp       byte ptr ds:[si],0       ; je zad n adres © ?
         je        aktdir2                  ; nen¡ zad n adres © - OK
         mov       dx,si                    ; p©¡stupov  cesta k adres ©i
         mov       ah,3bh                   ; funkce nastaven¡ aktiv. adres ©e
         int       21h                      ; nastaven¡ aktivn¡ho adres ©e
aktdir2: pop       ax                       ; n vrat AX
         pop       dx                       ; n vrat DX
         ret

; -----------------------------------------------------------------------------
public   dekfile
dekfile:                                    ; zak¢dov n¡ jm‚na souboru
                                            ; VSTUP: DS:SI -> ES:DI
         push      cx
         push      bx
         push      si
         lodsb                              ; atributy polo‘ky
         mov       bl,al                    ; £schova atribut– polo‘ky

                                          ;* p©enos v¨ech znak– jm‚na
         mov       cx,8
dekfil1: lodsb
         test      bl,16h                   ; je adres © nebo skryt˜/syst‚mov˜ ?
         jnz       dekfl11                  ; je adres © nebo skryt˜/syst‚mov˜
         call      lowcase0                 ; p©evod na mal‚ p¡smeno
dekfl11: and       bl,0f9h                  ; zru¨en¡ atribut– SYS/HID
         stosb                              ; ulo‘en¡ znaku
         loop      dekfil1                  ; dal¨¡ znak
         mov       cl,8                     ; po‡et znak– jm‚na = 8

                                          ;* n hrada hvˆzdi‡kou
         cmp       word ptr es:[di-2],"??"  ; kon‡¡ jm‚no otazn¡ky ?
         jne       dekfil7                  ; nekon‡¡ otazn¡ky
dekfil22:dec       di                       ; nastaven¡ na posledn¡ znak
         cmp       byte ptr es:[di],"?"     ; je otazn¡k ?
         loope     dekfil22                 ; vypustˆn¡ otazn¡k–
         je        dekfil23                 ; posledn¡ otazn¡k se nevr t¡
         inc       di                       ; n vrat pozice
         inc       cx                       ; n vrat ‡¡ta‡e
dekfil23:mov       al,"*"
         stosb                              ; ulo‘en¡ koncov‚ "*"
         jmp       short dekfil73

                                          ;* vypustˆn¡ oddˆlovac¡ch mezer
dekfil7: dec       di                       ; nastaven¡ na posledn¡ znak
         cmp       byte ptr es:[di]," "     ; je mezera ?
         loope     dekfil7                  ; vypustˆn¡ koncov˜ch mezer
         je        dekfil73                 ; posledn¡ mezera se nevr t¡
         inc       di                       ; n vrat pozice
         inc       cx                       ; n vrat ‡¡ta‡e

dekfil73:                                 ;* oddˆlovac¡ te‡ka
         cmp       byte ptr ds:[si-8],"."   ; je adres © ?
         je        dekfil4                  ; neplat¡ te‡ka za jm‚nem
         cmp       word ptr ds:[si],"  "    ; je p©¡pona ?
         jne       dekfilb                  ; je p©¡pona
         cmp       byte ptr ds:[si+2]," "   ; je p©¡pona ?
         je        dekfil4                  ; nen¡ p©¡pona
dekfilb: mov       al,"."
         stosb

                                          ;* p©enesen¡ v¨ech znak– p©¡pony
dekfil4: mov       cl,3
dekfil3: lodsb
         test      bl,16h                   ; je adres © nebo skryt˜/syst‚mov˜ ?
         jnz       dekfl31                  ; je adres © nebo skryt˜/syst‚mov˜
         call      lowcase0                 ; p©evod na mal‚ p¡smeno
dekfl31: and       bl,0f9h                  ; zru¨en¡ atribut– SYS/HID
         stosb
         loop      dekfil3
         mov       cl,3                     ; max. po‡et znak– k vypu¨tˆn¡

                                          ;* n hrada hvˆzdi‡kou
         cmp       word ptr es:[di-2],"??"  ; kon‡¡ jm‚no otazn¡ky ?
         jne       dekfil8                  ; nekon‡¡ otazn¡ky
dekfil52:dec       di                       ; nastaven¡ na posledn¡ znak
         cmp       byte ptr es:[di],"?"     ; je otazn¡k ?
         loope     dekfil52                 ; vypustˆn¡ otazn¡k–
         je        dekfil53                 ; posledn¡ otazn¡k se nevr t¡
         inc       di                       ; n vrat pozice
         inc       cx                       ; n vrat ‡¡ta‡e
dekfil53:mov       al,"*"
         stosb                              ; ulo‘en¡ koncov‚ "*"
         jmp       short dekfil83

                                          ;* vypu¨tˆn¡ koncov˜ch mezer
dekfil8: dec       di
         cmp       byte ptr es:[di]," "     ; je mezera ?
         loope     dekfil8                  ; vypu¨tˆn¡ koncov˜ch mezer
         je        dekfil83                 ; byla mezera
         inc       di                       ; n vrat pozice
         inc       cx                       ; n vrat ‡¡ta‡e

dekfil83:
         mov       byte ptr es:[di],0       ; koncov˜ bajt 0
         pop       si
         pop       bx
         pop       cx
         ret

; -----------------------------------------------------------------------------
public   cmppath

cmppath:                                  ;* porovn n¡ cest k okn–m
                                            ; VSTUP: ZY=cesty jsou shodn‚

         push      si
         push      di
         push      bp
         push      ds
         push      es
         push      cs
         pop       ds                       ; DS <- CS
         push      cs
         pop       es                       ; ES <- CS
         call      getnakt                  ; poskytnut¡ adresy neaktivn¡ho okna
         mov       di,ds:[bp+4]             ; p©¡stupov  cesta
         call      getakt                   ; adresa aktivn¡ho okna
         mov       si,ds:[bp+4]             ; p©¡stupov  cesta
         call      comptxt                  ; porovn n¡ ©etˆzc– cest
         pop       es
         pop       ds
         pop       bp
         pop       di
         pop       si
         ret

; -----------------------------------------------------------------------------
public   cmpcpath

cmpcpath:                                 ;* porovn n¡ c¡lov‚ cesty
                                            ; VSTUP: ZY=cesty jsou shodn‚

         push      si
         push      di
         push      bp
         push      ds
         push      es
         push      cs
         pop       ds                       ; DS <- CS
         push      cs
         pop       es                       ; ES <- CS
         lea       di,[selbuf]              ; c¡lov  cesta operace
         mov       si,ds:[bp+4]             ; p©¡stupov  cesta
         call      comptxt                  ; porovn n¡ ©etˆzc– cest
         pop       es
         pop       ds
         pop       bp
         pop       di
         pop       si
         ret

; -----------------------------------------------------------------------------
public   cmpdisk

cmpdisk:                                  ;* porovn n¡ disk– cest k okn–m
                                            ; VSTUP: ZY=disky jsou shodn‚

         push      si
         push      di
         push      bp
         push      ds
         push      es
         push      cs
         pop       ds                       ; DS <- CS
         push      cs
         pop       es                       ; ES <- CS
         call      getnakt                  ; poskytnut¡ adresy neaktivn¡ho okna
         mov       di,ds:[bp+4]             ; p©¡stupov  cesta
         call      getakt                   ; adresa aktivn¡ho okna
         mov       si,ds:[bp+4]             ; p©¡stupov  cesta
         cmpsb                              ; porovn n¡ disk–
         pop       es
         pop       ds
         pop       bp
         pop       di
         pop       si
         ret

; -----------------------------------------------------------------------------
public   cmpcdisk

cmpcdisk:                                 ;* porovn n¡ c¡lov‚ho disku operace
                                            ; VSTUP: ZY=disky jsou shodn‚

         push      si
         push      di
         push      bp
         push      ds
         push      es
         push      cs
         pop       ds                       ; DS <- CS
         push      cs
         pop       es                       ; ES <- CS
         lea       di,[selbuf]              ; c¡lov  cesta operace
         mov       si,ds:[bp+4]             ; p©¡stupov  cesta
         cmpsb                              ; porovn n¡ disk–
         pop       es
         pop       ds
         pop       bp
         pop       di
         pop       si
         ret

; -----------------------------------------------------------------------------
public   aktuald

aktuald:                                  ;* aktualizace disku (nov‚ na‡ten¡)
                                            ; VSTUP: BP=ukazatel okna
                                            ; VSTUP: CY=p©eru¨en¡ operace

         push      ax
         push      bx
         cmp       word ptr cs:[verze],300h ; je verze 3.00 nebo vy¨¨¡ ?
         jb        aktuald1                 ; je ni‘¨¡ verze ne‘ 3.00 - aktual.
         mov       bx,cs:[bp+4]             ; cesta k adres ©i
         mov       bl,cs:[bx]               ; disk
         sub       bl,40h                   ; korekce na ‡¡slo disku
         mov       ax,4408h
         int       21h                      ; dotaz na v˜mˆnnost m‚dia
         cmp       al,1                     ; je disk v˜mˆnn˜ ?
         je        aktuald2                 ; disk nen¡ v˜mˆnn˜
aktuald1:                                 ;* nov‚ na‡ten¡ adres ©e
         call      rerdir                   ; p©¡znak nov‚ho na‡ten¡ adres ©e
         call      windret                  ; na‡ten¡ adres ©e
aktuald2:call      testbreak                ; je p©eru¨en¡ operace ?
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------
public   informc

informc:                                  ;* vymaz n¡ inform. © dku
                                            ; VSTUP: DX=po‡ te‡n¡ pozice textu

         push      ax
         push      bx
         push      cx
         push      si
         push      di
         push      bp
                                          ;* vymaz n¡ informa‡n¡ho © dku
         xor       cl,cl                    ; po‡ te‡n¡ pozice okna
         mov       ch,cs:[displ]            ; posledn¡ © dek
         mov       dl,79                    ; koncov  pozice © dku
         mov       dh,ch                    ; spodn¡ okraj okna
         push      cx                       ; £schova po‡ te‡n¡ pozice
         mov       bh,cs:[col7]             ; barva pro norm ln¡ okno
         mov       ax,600h                  ; funkce vymaz n¡ okna
         int       10h                      ; vymaz n¡ okna
         pop       dx                       ; po‡ te‡n¡ pozice okna
         mov       al,7                     ; barva textu 7
         call      outch1                   ; nastaven¡ barvy 7
         inc       dl                       ; po‡ te‡n¡ pozice = 1
         pop       bp
         pop       di
         pop       si                       ; £vodn¡ text
         pop       cx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------
public   modikap

modikap:                                   ;* modifik. kapacity disku

         call      aktdisk                  ; aktualizace kapacity disku
         call      aktfile                  ; zji¨tˆn¡ kapacity v adres ©i
         call      space                    ; zobrazen¡ informac¡ o disku
         call      directory                ; zobrazen¡ informac¡ o adres ©i
         call      endline                  ; zobrazen¡ koncov‚ho © dku
         ret

; -----------------------------------------------------------------------------
public   inspol

inspol:                                   ;* vlo‘en¡ kop¡r. polo‘ky do seznamu

         push      si
         call      cmpcpath                 ; je adres © stejn˜ jako c¡lov˜ ?
         jne       inspol1                  ; nen¡ c¡lov˜ adres ©
         or        byte ptr ds:[bp+0],4     ; p©¡znak, ‘e se adres. nenuluje
         lea       si,[outbuf+20]           ; vkl dan  polo‘ka
         call      storekur                 ; £schova pozice kurzoru
         call      storfile                 ; vlo‘en¡ nov‚ polo‘ky
         call      settabf                  ; n vrat pozice kurzoru
         call      window                   ; nov‚ zobrazen¡ okna
inspol1: pop       si
         ret

; -----------------------------------------------------------------------------
public   delpol

delpol:                                   ;* zru¨en¡ polo‘ky BX

;         push      bx
;         mov       bx,cs:[bp+kurzl-tabl]    ; ru¨en˜ soubor
         call      delpolf                  ; zru¨en¡ polo‘ky ze seznamu
         call      windret
;         pop       bx
         ret

; -----------------------------------------------------------------------------
public   setfirst

setfirst:                                 ;* nastaven¡ prvn¡ho souboru
                                            ; VSTUP: CY=nen¡ prvn¡ soubor

         push      bx
         push      si
         push      bp
         push      ds
         call      getakt
         call      getkurz                  ; poskytnut¡ kurzoru
         jc        setfst1                  ; nen¡ ‘ dn˜ soubor
         call      test1file                ; je pr ce s kurzorem ?
         jnz       setfst1                  ; je souboru pod kurzorem
         call      srcins                   ; nalezen¡ prvn¡ polo‘ky
         jc        setfst1                  ; polo‘ka nenalezena
         call      setkurz                  ; nastaven¡ kurzoru na BX
setfst1: pop       ds
         pop       bp
         pop       si
         pop       bx
         ret

; -----------------------------------------------------------------------------
public   setkurz,setkurzn

setkurzn:                                 ;* posun o jednu polo‘ku
                                            ; VSTUP: BP=definice okna
                                            ; VSTUP: CY=konec seznamu

         push      bx
         mov       bx,cs:[bp+11]            ; sou‡asn  pozice kurzoru
         inc       bx                       ; zv˜¨en¡ pozice kurzoru
         cmp       bx,cs:[bp+7]             ; je ji‘ konec seznamu ?
         cmc
         jb        setkurz6                 ; je ji‘ konec seznamu
         call      setkurz                  ; nastaven¡ kurzoru na dal¨¡ polo‘ku
setkurz6:pop       bx
         ret


setkurz:                                  ;* nastaven¡ kurzoru pro operaci
                                            ; VSTUP: BX=‡¡slo polo‘ky
                                            ;        BP=definice okna

         push      ax
         call      testaktw                 ; je okno aktivn¡ ?
         jc        setkur4                  ; n povˆda nen¡ aktivn¡
         cmp       word ptr cs:[bp+7],0     ; je nˆjak  polo‘ka ?
         je        setkur4                  ; nen¡ ‘ dn  polo‘ka
         cmp       bx,cs:[bp+7]             ; p©ekro‡ena maxim ln¡ polo‘ka ?
         jb        setkur1                  ; polo‘ka je OK
         mov       bx,cs:[bp+7]             ; po‡et polo‘ek
         dec       bx                       ; maxim ln¡ polo‘ka
setkur1: cmp       bx,cs:[bp+11]            ; je ji‘ dosa‘eno pozice kurzoru ?
         je        setkur4                  ; je ji‘ pozice kurzoru
         mov       ax,4800h                 ; k¢d kurzoru nahoru
         jb        setkur2                  ; m  b˜t posun nahoru
         mov       ax,5000h                 ; k¢d kurzoru dol–
setkur2: call      usesedit                 ; proveden¡ posuvu kurzoru
         jmp       short setkur1            ; dal¨¡ krok posuvu
setkur3:
setkur4: pop       ax
         clc
         ret

; -----------------------------------------------------------------------------
public   setnext

setnext:                                  ;* nastaven¡ dal¨¡ho souboru
                                            ; VSTUP: CY=nen¡ dal¨¡ soubor

         push      bp
         push      bx
         call      getakt
         call      test1file                ; je pr ce s kurzorem ?
         stc
         jnz       setnxt1                  ; je pr ce s kurzorem - konec
         mov       bx,cs:[bp+11]            ; sou‡asn  pozice kurzoru
         call      srcins0                  ; nalezen¡ dal¨¡ polo‘ky (nebo t‚to)
         jc        setnxt1                  ; je ji‘ za koncem seznamu
         call      setkurz                  ; nastaven¡ kurzoru
setnxt1: pop       bx
         pop       bp
         ret

; -----------------------------------------------------------------------------
public   sethome

sethome:                                  ;* sestaven¡ cesty k domovsk‚mu adres.
                                            ; VSTUP: CS:SI=soubor
                                            ;        CS:DI=buffer
                                            ; VSTUP: DS:DX=specifikace souboru

         push      cs
         pop       ds                       ; segment bufferu
         mov       dx,di                    ; adresa bufferu

         push      es
         push      si

         push      cs
         pop       es                       ; ES <- CS

         cld
         mov       si,offset pathhome       ; cesta k dom c¡mu adres ©i
         call      transtxt                 ; p©enesen¡ jm‚na cesty
         mov       al,"\"
         cmp       byte ptr es:[di-1],al    ; byl posledn¡ znak "\" ?
         je        sethome1                 ; posledn¡ znak je ji‘ "\"
         stosb                              ; oddˆlova‡ cesty a jm‚na programu

sethome1:pop       si                       ; jm‚no souboru
         call      transtxt                 ; p©enesen¡ jm‚na
         pop       es
         ret

; -----------------------------------------------------------------------------
public   testbreak

testbreak:                                ;* test p©eru¨en¡ operace
                                            ; VSTUP: CY=je p©eru¨en¡ operace
         push      ax
         cmp       word ptr cs:[parbreak],0 ; je nˆjak  chyba ?
         jne       testbrk3

         test      byte ptr cs:[parrez],80h
         jnz       testbrk3                 ; je p©eru¨en¡ Ctrl-Break

         call      testkey
         clc
         jz        testbrk1                 ; nen¡ p©ipraven znak

         cmp       al,27                    ; je Esc ?
         clc
         jne       testbrk1                 ; nen¡ Esc

testbrk3:and       byte ptr cs:[parrez],not 80h ; zru¨en¡ p©¡znaku p©eru¨en¡
         test      byte ptr cs:[parrez],4   ; je z kaz p©eru¨en¡ ?
         jnz       testbrk1                 ; je z kaz p©eru¨en¡
         mov       word ptr cs:[parbreak],-1 ; p©¡znak p©eru¨en¡
testbrk2:call      flushkey                 ; vypr zdnˆn¡ kl vesnice
         stc                                ; p©¡znak p©eru¨en¡ CY
testbrk1:pop       ax
         ret

; -----------------------------------------------------------------------------
public   retwind

retwind:                                  ;* n vrat parametr– okna po editaci


         push      ax
         push      bx
         push      cx
         push      dx
         call      testaktw                 ; test zapnut¡ okna
         jnc       retwind0
         jmp       retwind9                 ; okno je vypnut‚
retwind0:call      getnumlw                 ; po‡et © dk– soubor–
         mov       dx,cx
;         mov       dx,18
;         call      testakth
;         jnc       retwind1
;         inc       dx
;retwind1:
         mov       ax,ds:[zacrol]           ; prvn¡ zobrazen˜ © dek
         cmp       ax,ds:[bp+9]             ; nastala zmˆna po‡ tku ?
         je        retwind8                 ; nenastala zmˆna po‡ tku
         call      kurzoff                  ; vypnut¡ kurzoru
         mov       bx,ds:[kurrol]           ; pozice kurzoru
         mov       ds:[bp+11],bx            ; nov  pozice kurzoru
         xchg      ax,ds:[bp+9]             ; ulo‘en¡ nov‚ho po‡ tku
         sub       ax,ds:[bp+9]             ; rozd¡l pro posun okna
         ja        retwind5                 ; je rolov n¡ dol–
                                          ;* rolov n¡ nahoru
         neg       ax                       ; po‡et © dk– k rolov n¡
         cmp       ax,8                     ; omezen¡ - nebude se ji‘ rolovat
         ja        retwind4
         call      wrollup                  ; rolov n¡ okna nahoru o AL © dk–
         mov       bx,ds:[bp+9]             ; prvn¡ zobrazen˜ soubor
         add       bl,ds:[delrol]           ; po‡et © dk– okna
         adc       bh,0                     ; posledn¡ © dek
         sub       bx,ax                    ; prvn¡ © dek k doplnˆn¡
retwind6:
         mov       cx,1
         call      testsetw                 ; test nastaven¡ oken
         jz        retwind3                 ; je pln‚ zobrazen¡ oken
         mov       cl,3
retwind3:push      cx
         mov       cx,ax                    ; po‡et © dk–
         push      bx
retwind2:call      zobrfile                 ; zobrazen¡ polo‘ky
         inc       bx                       ; zv˜¨en¡ ukazatele polo‘ek
         loop      retwind2                 ; dal¨¡ polo‘ka
         pop       bx
         pop       cx
         sub       bx,dx
         loop      retwind3
         jmp       short retwind7

retwind5:                                 ;* rolov n¡ dol–
         cmp       ax,8                     ; omezen¡ - nebude se ji‘ rolovat
         ja        retwind4
         call      wrolldown                ; rolov n¡ okna nahoru o AL © dk–
         mov       bx,ds:[bp+9]             ; prvn¡ zobrazen˜ soubor
         neg       dx
         jmp       short retwind6

retwind4:call      files                    ; zobrazen¡ soubor–
         jmp       short retwind9

retwind8:mov       ax,ds:[kurrol]           ; pozice kurzoru
         cmp       ax,ds:[bp+11]            ; byla zmˆna kurzoru ?
         je        retwind9                 ; nen¡ zmˆna kurzoru
         call      kurzoff                  ; vypnut¡ kurzoru
         mov       ds:[bp+11],ax            ; ulo‘en¡ nov‚ pozice kurzoru
retwind7:call      kurzon                   ; zapnut¡ kurzoru
;         call      endline                  ; zobrazen¡ spodn¡ho © dku
retwind9:pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------
public   obrkurz

obrkurz:                                  ;* zobrazen¡ kurzoru p©¡kazov‚ho © dku
         push      bp
         lea       bp,[comlin]             ; definice povelov‚ho © dku
         call      kurzlin                  ; zobrazen¡ kurzoru na © dku
         pop       bp
         ret

; -----------------------------------------------------------------------------
public   obrcomm

obrcomm:                                  ;* zobrazen¡ p©¡kazov‚ho © dku
         push      bp
         push      si
         push      ds
         push      cs
         pop       ds
         call      getakt                   ; nastaven¡ adresy aktivn¡ho okna
         mov       si,cs:[bp+4]             ; cesta k lev‚mu adres ©i
         lea       bp,[comlin]              ; definice povelov‚ho © dku
         call      pathcomm                 ; cesta v povelov‚m © dku
         call      obrlin                   ; zobrazen¡ textu povelov‚ho © dku
         call      kurzlin                  ; zobrazen¡ kurzoru na © dku
         pop       ds
         pop       si
         pop       bp
         ret

; -----------------------------------------------------------------------------
public   pathcomm

pathcomm:                                 ;* zobrazen¡ cesty p©¡kazov‚ho © dku
                                            ; VSTUP: BP=tabulka definice © dku
                                            ;        SI=text p©¡stupov‚ cesty

         push      ax
         push      cx
         push      dx
         push      si
         call      mouseoff                 ; vypnut¡ my¨i
         mov       al,cs:[normat]           ; atributy p©¡kazov‚ho © dku
         mov       cs:[color],al            ; nastaven¡ atribut– barev
         xor       dl,dl                    ; po‡ te‡n¡ pozice kurzoru
         call      getendl                  ; ‡¡slo posledn¡ho © dku displeje
         inc       dh
         test      byte ptr cs:[flags],2    ; jsou okna vypnuta ?
         jz        pathcm1                  ; okna nejsou vypnuta
                                          ;* okna jsou vypnuta
;         push      cx
         mov       cx,80
         call      radeknul                 ; vymaz n¡ © dku
;         pop       cx

         cmp       dh,byte ptr cs:[aktkurz+1] ; je nad kurzorem ?
         jbe       pathcm1
         mov       dh,byte ptr cs:[aktkurz+1] ; © dek kurzoru
                                          ;* zobrazen¡ textu
pathcm1: call      outtxt                   ; zobrazen¡ textu cesty
         mov       al,">"                   ; £vodn¡ prompt
         call      outch1                   ; zobrazen¡ promptu
         mov       al,80                    ; posledn¡ pozice na © dku
         sub       al,dl                    ; po‡et pozic do konce © dku
         jnc       pathcm2
         xor       al,al                    ; n hradn¡ ¨¡©ka textu = 0
pathcm2: mov       cs:[bp+14],al            ; ¨¡©ka © dku textu
         mov       cs:[bp+12],dx            ; pozice za‡ tku textu
         call      mouseon                  ; zapnut¡ my¨i
         pop       si
         pop       dx
         pop       cx
         pop       ax
         ret


; -----------------------------------------------------------------------------
public   setcil

setcil:                                   ;* sestaven¡ jm‚na c¡lov‚ho souboru
                                            ; VSTUP: [outbuf+20]=c¡lov‚ jm‚no
                                            ;         CY=jm‚no je shodn‚

         push      ax
         push      cx
         push      si
         push      di
         push      cs
         pop       es
                                          ;* p©enesen¡ p–vodn¡ho jm‚na souboru
         call      getkurz                  ; poskytnut¡ adresy souboru
         lea       di,[outbuf+20]           ; adresa k ulo‘en¡ rozboru
         push      di                       ; adresa p–vodn¡ho jm‚na souboru
         mov       cx,19                    ; d‚lka identifikace souboru
         lodsb                              ; atributy souboru
         and       al,37h                   ; nulov n¡ atribut–
         stosb
         rep       movsb                    ; p©enesen¡ vzorov‚ho jm‚na
         push      cs
         pop       ds                       ; DS <- CS
         lea       si,[rozbnam]             ; maska pro nastaven¡ jm‚na souboru
         pop       di                       ; adresa p–vodn¡ho jm‚na souboru
         inc       di                       ; p–vodn¡ jm‚no souboru
                                          ;* p©ek¢dov n¡ jm‚na souboru
         mov       cx,11                    ; po‡et znak– jm‚na
setcil2: lodsb                              ; maskovan˜ znak
         cmp       al,"?"                   ; je n hradn¡ znak ?
         je        setcil3                  ; je n hradn¡ znak
         mov       ds:[di],al               ; nastaven¡ nov‚ho znaku
setcil3: inc       di                       ; zv˜¨en¡ ukl dac¡ adresy
         loop      setcil2                  ; dek¢dov n¡ dal¨¡ho znaku
                                          ;* test shody nov‚ho jm‚na se star˜m
         call      cmpcpath                 ; je operace v adres ©i ?
         clc
         jne       setcil4                  ; nen¡ operace v adres ©i
         call      getkurz
         inc       si                       ; p©esko‡en¡ atributu
         lea       di,[outbuf+21]           ; adresa s ulo‘en˜m nov˜m jm‚nem
         mov       cx,11                    ; d‚lka jm‚na
         repe      cmpsb                    ; porovn n¡ jmen
         clc
         jne       setcil4                  ; jm‚na nejsou shodn 
         stc                                ; p©¡znak - jm‚na jsou shodn 
setcil4: push      cs
         pop       ds                       ; DS <- CS
         pop       di
         pop       si
         pop       cx
         pop       ax
         ret

; -----------------------------------------------------------------------------
public   precopy

precopy:                                  ;* nastaven¡ p©edvoleb pro COPY a MOVE

         push      di
         push      ds
         mov       ax,offset bufcop2
         sub       ax,offset bufcop1
         mov       word ptr ds:[bufcopy],ax
         mov       byte ptr ds:[bufcop2],0  ; ‘ dn  p©edvolba
         call      getkurz                  ; poskytnut¡ adresy kurzoru
         jnc       precop33
         jmp       precop3                  ; nen¡ ‘ dn˜ soubor
precop33:                                 ;* nastaven¡ cesty k protˆj¨¡mu oknu
         call      cmppath                  ; porovn n¡ cest k okn–m
         je        precop1                  ; okna jsou shodn 
;         call      test1file                ; je pr ce s kurzorem ?
;         jnz       precop4                  ; je kurzor (volba s ALT)
;         call      soucins                  ; se‡ten¡ ozna‡en˜ch soubor–
;         or        cx,cx                    ; po‡et ozna‡en˜ch soubor–
;         jnz       precop5                  ; je ozna‡en nˆjak˜ soubor
;precop4: push      bp
;         push      ds
;         push      si
;         call      getkurz                  ; poskytnut¡ kurzoru
;         test      byte ptr ds:[si],10h     ; je adres © ?
;         pop       si
;         pop       ds
;         pop       bp
;         jnz       precop1                  ; je adres © - nenab¡z¡ se cesta
                                          ;* je cesta k protˆj¨¡mu oknu
precop5: push      bp
         push      si
         push      ds
         push      es
         push      cs
         pop       ds
         push      cs
         pop       es
         call      getnakt                  ; poskytnut¡ adresy neaktivn¡ho okna
         mov       di,offset outbuf3        ; zde se ulo‘¡ cesta k protˆj¨. oknu
         mov       cs:[bufcop3],di          ; nastaven¡ cesty k protˆj¨¡mu oknu
         mov       si,cs:[bp+4]             ; p©¡stupov  cesta
         call      transtxt
         call      test1file                ; je pr ce s kurzorem ?
         jz        precop54                 ; je v¡ce soubor–
         mov       al,"\"
         cmp       es:[di-1],al             ; ukon‡ena cesta lom¡tkem ?
         je        precop55                 ; cesta je ukon‡ena lom¡tkem
         stosb                              ; ulo‘en¡ uko¤covac¡ho lom¡tka
precop55:call      getakt                   ; poskytnut¡ aktivn¡ho okna
         call      getkurz                  ; poskytnut¡ souboru pod kurzorem
         call      dekfile                  ; ulo‘en¡ jm‚na souboru
precop54:inc       byte ptr cs:[bufcop2]    ; = 1 p©edvolba
         pop       es
         pop       ds
         pop       si
         pop       bp

precop1:                                  ;* nastaven¡ jm‚na souboru
         lea       di,[outbuf]
         test      byte ptr ds:[si],10h     ; je adres © ?
         jnz       precop2                  ; je adres © - povolen‚
         call      test1file                ; je pr ce s kurzorem ?
         jnz       precop2                  ; je kurzor
         call      soucins                  ; sou‡et ozna‡en˜ch soubor–
         cmp       cx,1                     ; je ozna‡en˜ soubor ?
         ja        precop3                  ; je ozna‡en v¡ce ne‘ 1 soubor
precop2: call      dekfile                  ; dek¢dov n¡ jm‚na souboru
         mov       si,cs:[bufcop3]          ; adresa cesty k protˆj¨¡mu adres ©i
         mov       cs:[bufcop4],si          ; adresa cesty jako 2. p©edvolba
         mov       word ptr cs:[bufcop3],offset outbuf ; tiskov˜ buffer
         inc       byte ptr cs:[bufcop2]    ; = 2 p©edvolby
precop3: cmp       byte ptr cs:[bufcop2],2  ; jsou 2 p©edvolby ?
         jne       precop6
         call      getoper                  ; poskytnut¡ k¢du operace
         cmp       al,1                     ; je p©esun ?
         jne       precop8                  ; nen¡ p©esun
         call      test1file                ; je pr ce s kurzorem ?
         jnz       precop9                  ; je pr ce s kurzorem
         call      soucins                  ; sou‡et ozna‡en˜ch soubor–
         cmp       cx,1                     ; je ozna‡en˜ soubor ?
         jae       precop8                  ; je ozna‡en nˆjak˜ soubor
precop9: call      getkurz                  ; poskytnut¡ souboru pod kurzorem
         test      byte ptr ds:[si],10h     ; je to adres © ?
         jnz       precop7                  ; je to adres ©
precop8: call      testaktwn                ; je neaktivn¡ okno zapnut‚ ?
         jnc       precop6                  ; neakt. okno je zapnut‚
precop7: mov       si,cs:[bufcop3]          ; p©edvolba 1
         xchg      si,cs:[bufcop4]          ; z mˆna p©edvoleb
         mov       cs:[bufcop3],si
precop6: pop       ds
         pop       di
         ret

; -----------------------------------------------------------------------------
public   testro
testro:                                   ;* test ru¨en¡ souboru R/O

                                          ;* test, zda je automatick˜ re‘im
         test      byte ptr ds:[flagsc],40h ; prov d¡ se dotaz ?
         jnz       testro3                  ; dotaz se neprov d¡
                                          ;* sestaven¡ textu dotazu
         lea       di,[bufnadp]             ; pracovn¡ buffer
         lea       si,[txtvar]              ; text varov n¡
         call      transtxt                 ; ulo‘en¡ nadpisu
         lea       si,[errdel1]             ; text "Soubor"
         test      byte ptr ds:[95h],10h    ; je to adres © ?
         jz        testro2                  ; nen¡ to adres ©
         lea       si,[errdir1]             ; test "Adres ©"
testro2: call      transtxt                 ; p©enos ozna‡en¡
         mov       si,dx                    ; jm‚no souboru
         call      transtxt                 ; p©enos jm‚na souboru
         lea       si,[errdir2]             ; text "nelze zru¨it, "
         call      transtxt
         lea       si,[errdel2]             ; text "proto‘e je chr nˆn..."
         call      transtxt
                                          ;* provede se dotaz na dal¨¡ postup
         lea       si,[delvolb]             ; volby
         lea       di,[bufnadp]             ; buffer s textem
         xor       al,al
         call      volbyh                   ; proveden¡ voleb
         call      windret                  ; navr cen¡ oken
         jc        testro3                  ; p©eru¨en¡ operace
         mov       cl,6                     ; po‡et rotac¡
         shl       al,cl                    ; ‡¡slo dal¨¡ funkce
         and       byte ptr cs:[flagsc],3fh ; zru¨en¡ star˜ch p©¡znak–
         or        cs:[flagsc],al           ; nastaven¡ nov˜ch p©¡znak–
testro3: ret

; -----------------------------------------------------------------------------
public   informz

informz:                                  ;* zobrazen¡ z kladn¡ho inform. © dku
                                            ; VSTUP: DS:SI=£vodn¡ text
                                            ; VSTUP: DX=pozice konce textu
         call      mouseoff                 ; vypnut¡ my¨i
         push      ax
;         push      bx
;         push      cx
         push      si
         push      di
;         push      bp
                                          ;* £vodn¡ ‡ st textu
         call      informc                  ; vymaz n¡ informa‡n¡ho © dku
         call      outtxt                   ; zobrazen¡ £vodn¡ho textu
         inc       dl                       ; zv˜¨en¡ pozice za textem
                                          ;* zobrazen¡ textu "soubor","adres ©"
         call      getkurz                  ; adresa polo‘ky s kurzorem
         push      si
         push      ds
         test      byte ptr ds:[si],10h     ; je adres © ?
         lea       si,[textvy2+2]           ; text "soubor"
         jz        informz2                 ; nen¡ adres ©
         lea       si,[txtadr]              ; text "adres ©"
informz2:push      cs
         pop       ds                       ; DS <- CS
         call      outtxt                   ; zobrazen¡ textu
         inc       dl                       ; zv˜¨en¡ pozice
         pop       ds
         pop       si
                                          ;* zobrazen¡ jm‚na souboru
         mov       al,10                    ; barva textu 10
         call      outch1                   ; nastaven¡ barvy 10
         lea       di,[outbuf]              ; tiskov˜ buffer
         push      di
         call      dekfile                  ; dek¢dov n¡ jm‚na polo‘ky
         pop       si                       ; adresa textu
         push      cs
         pop       ds                       ; DS <- CS
         call      outtxt                   ; zobrazen¡ jm‚na souboru
         call      kurzout                  ; vypnut¡ kurzoru
;         pop       bp
         pop       di
         pop       si
;         pop       cx
;         pop       bx
         pop       ax
         call      mouseon                  ; zapnut¡ my¨i
         ret


; -----------------------------------------------------------------------------
public   initukaz,initukaz0

initukaz:                                 ;* inicializace ukazatele kop¡rov n¡
                                            ; VSTUP: AL=maxim ln¡ d‚lka ukazat.

         push      ds
         push      si
         call      getkurz
         call      initukaz0
         pop       si
         pop       ds
         ret


initukaz0:
         call      mouseoff                 ; vypnut¡ my¨i
         push      ax
         push      bx
         push      cx
         push      dx
         push      ds

         mov       bl,al                    ; £schova maxim ln¡ d‚lky ukazatele
         xor       ax,ax
         mov       word ptr cs:[blokuk],ax
         mov       word ptr cs:[blokuk+2],ax ; vynulov n¡ velikosti ukazatele
         mov       word ptr cs:[transuk],ax
         mov       word ptr cs:[transuk+2],ax ; vynulov n¡ ukazatele p©enes. dat
                                          ;* v˜po‡et d‚lky ukazatele
         mov       ax,ds:[si+12]            ; velikost souboru - ni‘¨¡ slovo
         mov       dx,ds:[si+14]            ; velikost souboru - vy¨¨¡ slovo
         push      cs
         pop       ds
         push      ax
         push      dx
         xor       bh,bh                    ; ‡¡ta‡ cykl–
inituk1: or        dx,dx                    ; je vy¨¨¡ slovo ji‘ 0 ?
         jnz       inituk2                  ; nen¡ vy¨¨¡ slovo = 0
         cmp       ax,3                     ; je ‡¡slo men¨¡ nebo rovno 3 ?
         jbe       inituk3                  ; je ji‘ ‡¡slo men¨¡ nebo rovno 3
inituk2: shr       dx,1                     ; DX / 2
         rcr       ax,1                     ; velikost / 2
         add       bh,4
         jmp       short inituk1            ; dal¨¡ test
inituk3: add       bh,al                    ; celkov  ¨¡©ka (znak–)
         sub       bh,32                    ; ode‡ten¡ po‡ tku pro 512 bajt–
         jc        inituk32                 ; je men¨¡
         cmp       bh,4
         jae       inituk31                 ; je vˆt¨¡ ne‘ 512 bajt–
inituk32:mov       bh,4                     ; n hradn¡ d‚lka
inituk31:
         cmp       bh,bl                    ; je vˆt¨¡ ne‘ povolen  d‚lka ?
         ja        inituk4                  ; je vˆt¨¡
         mov       bl,bh                    ; omezen¡ na vypo‡tenou velikost
inituk4:                                  ;* v˜po‡et parametr– ukazatele
         pop       ax                       ; velikost souboru - vy¨¨¡ slovo
         pop       cx                       ; velikost souboru - ni‘¨¡ slovo
         or        bl,bl                    ; je povoleno zobrazen¡ ?
         jz        inituk6                  ; nen¡ povoleno zobrazen¡
         xor       dx,dx
         xor       bh,bh
         div       bx                       ; v˜po‡et vy¨¨¡ho slova bloku
         xchg      ax,cx                    ; £schova vy¨¨¡ho slova bloku
         div       bx                       ; v˜po‡et ni‘¨¡ho slova bloku
         mov       word ptr ds:[blokuk],ax
         mov       word ptr ds:[blokuk+2],cx; ulo‘en¡ velikosti ukazatele
         or        ax,cx                    ; je povoleno zobrazen¡ ?
         jz        inituk6                  ; zobrazen¡ nen¡ povoleno
                                          ;* zobrazen¡ ukazatele
         mov       dl,79                    ; konec zobrazen¡
         sub       dl,bl                    ; po‡ te‡n¡ pozice zobrazen¡
         mov       ds:[poziceuk],dl         ; po‡ te‡n¡ pozice ukazatele
         mov       ds:[delkauk],bl          ; £schova d‚lky ukazatele
         call      getdispl                 ; ‡¡slo posledn¡ho © dku
;         mov       dh,24                    ; © dek pro zobrazen¡ ukazatele
         mov       cx,bx                    ; d‚lka zobrazen¡ ukazatele
         mov       al,7                     ; barva
         call      outch1                   ; nastaven¡ barvy
         mov       al,0b0h                  ; podklad ukazatele
         call      outch                    ; zobrazen¡ podkladu ukazatele
inituk6: pop       ds
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         call      mouseon                  ; zapnut¡ my¨i
         ret

; -----------------------------------------------------------------------------
public   modiukaz

modiukaz:                                 ;* modifikace ukazatele
                                            ; VSTUP: AX=po‡et zapsan˜ch bajt–
                                            ;        CY=zobrazen¡ 100%
         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      ds
         push      cs
         pop       ds
         mov       si,word ptr ds:[blokuk]  ; ni‘¨¡ slovo velikosti bloku
         mov       bx,word ptr ds:[blokuk+2]; vy¨¨¡ slovo velikosti bloku
         mov       cl,ds:[delkauk]          ; d‚lka ukazatele
         jc        modiuk5                  ; pln‚ zobrazen¡ ukazatele
         push      bx
         or        bx,si
         pop       bx
         jz        modiuk6                  ; zobrazen¡ nen¡ povoleno
         add       word ptr ds:[transuk],ax
         adc       word ptr ds:[transuk+2],0; zv˜¨en¡ velikosti dat
         mov       ax,word ptr ds:[transuk] ; ni‘¨¡ slovo zapsan˜ch bajt–
         mov       dx,word ptr ds:[transuk+2]; vy¨¨¡ slovo zapsan˜ch bajt–
         xor       cl,cl                    ; ‡¡ta‡ blok–
modiuk2: inc       cl                       ; zv˜¨en¡ ‡¡ta‡e blok–
         sub       ax,si                    ; ni‘¨¡ slovo
         sbb       dx,bx                    ; vy¨¨¡ slovo
         jnc       modiuk2                  ; dal¨¡ blok
         dec       cl
modiuk5: or        bx,si                    ; povoleno zobrazen¡ ?
         jz        modiuk6                  ; zobrazen¡ nen¡ povoleno
         call      mouseoff                 ; vypnut¡ my¨i
         mov       al,7                     ; barva ukazatele
         call      outch1                   ; nastaven¡ barvy ukazatele
         mov       dl,ds:[poziceuk]         ; po‡ te‡n¡ pozice ukazatele
         xor       ch,ch
         mov       al,0b2h                  ; znak ukazatele
         call      getdispl                 ; ‡¡slo posledn¡ho © dku
;         mov       dh,24                    ; © dek pro zobrazen¡
         call      outch                    ; zobrazen¡ ukazatele
         call      mouseon                  ; zapnut¡ my¨i
modiuk6: pop       ds
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------
public   dispcil

dispcil:                                  ;* zobrazen¡ c¡le operace
                                            ; VSTUP: AH=po‡et oddˆlova‡– "\"
                                            ;        DI+1=adresa souboru

         push      ax
         push      cx
         push      si
         push      di
         pushf

         call      mouseoff                 ; vypnut¡ my¨i
         mov       al,10
         call      outch1                   ; nastaven¡ barvy
                                          ;* test, zda je operace v adres ©i
         inc       di
         mov       cl,ds:[di-1]             ; £schova znaku oddˆlova‡e
         mov       byte ptr ds:[di-1],0
         call      cmpcpath                 ; je operace v adres ©i ?
         mov       ds:[di-1],cl             ; n vrat znaku
         pushf
         cmp       cl,"\"                   ; byl uschov n znak "\" ?
         je        dispcil0                 ; byl "\" - OK
         dec       di                       ; nebyl "\" - byl z kladn¡ adres ©
dispcil0:popf
         mov       si,di                    ; za‡ tek jm‚na souboru
         je        dispcil3                 ; je operace v adres ©i

                                          ;* nastaven¡ kurzoru v protˆj¨¡m oknˆ
         lea       si,[selbuf]              ; cesta k c¡lov‚mu adres ©i
         sub       ah,3                     ; je p©ekro‡en po‡et oddˆlova‡– ?
         jbe       dispcil3                 ; je mal˜ po‡et oddˆlova‡–
         mov       cl,ah                    ; £schova ‡¡ta‡e oddˆlova‡–
         xor       ch,ch
         lodsb                              ; ozna‡en¡ disku
         call      outch1
         lodsb                              ; oddˆlova‡ disku ":"
         call      outch1
         lodsb                              ; oddˆlova‡ "\"
         call      outch1
         mov       al,"."                   ; oddˆlovac¡ te‡ka
         call      outch1
         call      outch1
         call      outch1
         inc       cx
dispcil1:lodsb
         cmp       al,"\"
         jnz       dispcil1                 ; nalezen¡ oddˆlova‡e
         loop      dispcil1                 ; dal¨¡ oddˆlova‡
         dec       si                       ; n vrat posledn¡ho odˆlova‡e
dispcil3:
         call      outtxt                   ; zobrazen¡ zbytku textu
         call      mouseon                  ; zapnut¡ my¨i
         call      kurzout                  ; vypnut¡ kurzoru
         popf
         pop       di
         pop       si
         pop       cx
         pop       ax
         ret

public   kurzcil
kurzcil:                                  ;* zobrazen¡ c¡lov‚ho kurzoru BP
         push      si
         push      bx
         call      testaktw                 ; je okno zapnut‚ ?
         jc        kurzcil3                 ; okno nen¡ zapnut‚
         mov       si,offset outbuf+20      ; c¡lov˜ soubor
         call      hledpol0                 ; nalezen¡ shodn‚ho c¡lov‚ho souboru
         jnc       kurzcil2                 ; kurzor nalezen OK
         test      byte ptr ds:[bp+0],10h   ; je net©¡dˆn˜ adres © ?
         jnz       kurzcil3                 ; je net©¡dˆn˜ adres ©
         call      hledpol                  ; nalezen¡ podobn‚ho jm‚na
kurzcil2:                                 ;* soubor nalezen
         call      setkurz                  ; nastaven¡ kurzoru na polo‘ku BX
         call      storekur                 ; £schova pozice kurzoru
kurzcil3:pop       bx
         pop       si
         ret
; -----------------------------------------------------------------------------
public   setpcil

setpcil:                                  ;* sestaven¡ c¡lov‚ cesty k souboru
                                            ; VSTUP: DI=p–vodn¡ konec SELBUF
                                            ;         CY=zdroj shodn˜ s c¡lem
                                            ;         AH=po‡et oddˆlova‡– "\"


         lea       di,[selbuf]              ; c¡lov˜ adres ©
         call      lendi                    ; d‚lka cesty v SELBUF
         xor       ah,ah
         add       di,ax                    ; konec textu
         call      setcil                   ; sestaven¡ jm‚na
         push      di                       ; £schova p–vodn¡ho konce cesty
         pushf
                                          ;* nastaven¡ kurzoru na c¡lov˜ soubor
         xor       ax,ax
         xchg      byte ptr ds:[di],al      ; p©echodn˜ n vrat konce cesty
         call      setpcil8
         xchg      byte ptr ds:[di],al      ; n vrat p–vodn¡ho znaku z cesty

         cmp       byte ptr ds:[di-1],"\"   ; je ji‘ oddˆlova‡ konce cesty ?
         je        setpcil2                 ; je ji‘ oddˆlova‡
         mov       al,"\"
         stosb                              ; ulo‘en¡ oddˆlova‡e
setpcil2:lea       si,[outbuf+20]           ; p©ek¢dovan‚ jm‚no souboru
         call      dekfile                  ; dek¢dov n¡ jm‚na souboru
         lea       si,[selbuf]              ; buffer s cestou
         xor       ah,ah                    ; ‡¡ta‡ oddˆlova‡– "\"
setpcil3:lodsb                              ; na‡ten¡ znaku cesty
         or        al,al                    ; je ji‘ konec cesty ?
         jz        setpcil4                 ; je konec cesty
         cmp       al,"\"                   ; je oddˆlova‡ "\" ?
         jne       setpcil3                 ; nen¡ - dal¨¡ znak
         inc       ah                       ; zv˜¨en¡ ‡¡ta‡e oddˆlova‡–
         jmp       short setpcil3           ; dal¨¡ znak
setpcil4:mov       al,ah                    ; po‡et oddˆlova‡–
         popf
         pop       di                       ; p–vodn¡ konec cesty
         ret


PUBLIC   setpcil8
setpcil8:                                 ;* nastaven¡ kurzoru na c¡lov˜ soubor
         push      bp
         call      getnakt                  ; neaktivn¡ okno
         call      cmppath                  ; jsou obˆ okna stejn  ?
         je        dispcil5                 ; okna jsou stejn 
         call      cmpcpath                 ; je to c¡lov‚ okno ?
         jne       dispcil4                 ; nen¡ to ani c¡lov‚ okno
dispcil5:call      kurzcil                  ; zobrazen¡ c¡lov‚ho souboru
dispcil4:pop       bp
         ret


; -----------------------------------------------------------------------------
;        Kop¡rov n¡ cel‚ho adres ©e
; -----------------------------------------------------------------------------

xcopy9a: mov       si,offset errdr1         ; text - nelze cyklicky kop¡rovat
         call      getoper                  ; poskytnut¡ k¢du operace
         or        al,al                    ; je kop¡rov n¡ ?
         jz        xcopy9c                  ; je kop¡rov n¡
         mov       si,offset errdr2         ; text - nelze cyklicky p©esouvat
xcopy9c: call      zobrchyb                 ; zobrazen¡ chybov‚ho hl ¨en¡
xcopy9:  ret

PUBLIC   XCopy
XCopy:

         push      cs
         pop       ds
         push      cs
         pop       es

         call      storekall                ; £schova pozic kurzor–

                                          ;* nastaven¡ aktu ln¡ho adres ©e
         mov       si,ds:[bp+4]             ; adres © okna
         call      aktdir                   ; nastaven¡ aktivn¡ho adres ©e okna
         call      testbreak                ; je p©eru¨en¡ operace ?
         jc        xcopy9                   ; p©eru¨en¡ operace

                                          ;* nastaven¡ c¡lov‚ho adres ©e
         lea       si,[outbuf]              ; zadan˜ adres ©
         call      aktdir                   ; nastaven¡ c¡lov‚ho adres ©e
         jnc       xcopy1                   ; adres © existuje
         call      testbreak                ; je p©eru¨en¡ operace ?
         jc        xcopy9                   ; p©eru¨en¡ operace
                                          ;* chyba zad n¡ adres ©e
         lea       si,[errcop1]             ; text "Chybn‚ zad n¡ ..."
         jmp       zobrchyb                 ; zobrazen¡ chybov‚ho hl ¨en¡

                                          ;* pokus, zda je zadan‚ jm‚no adres ©e
xcopy1:  lea       di,[outbuf]              ; ulo‘en¡ podadres ©e
         lea       si,[rozbflg]             ; mo‘n‚ jm‚no podadres ©e
         push      di
         call      dekfile                  ; dek¢dov n¡ jm‚na podadres ©e
         pop       si                       ; p©¡padn˜ podadres ©
         call      aktdir                   ; nastaven¡ podadres ©e
         jnc       xcopy2                   ; adres © nastaven OK
         call      testbreak                ; je p©eru¨en¡ operace ?
xcopy99: jc        xcopy9                   ; p©eru¨en¡ operace

                                          ;* vytvo©en¡ zadan‚ho podadres ©e
         mov       dx,si                    ; zadan˜ adres ©
         mov       ah,39h
ifndef   demo
         int       21h                      ; pokus o vytvo©en¡ adres ©e
else
         clc
endif
         jc        xcopy21                  ; adres © nelze vytvo©it - nezad n
         call      aktdir                   ; nov˜ pokus o nastaven¡
         jnc       xcopy2                   ; adres © vytvo©en OK

                                          ;* vytvo©en¡ implicitn¡ho adres ©e
xcopy21: push      ds
         call      getkurz                  ; poskytnut¡ adresy kurzoru
         lea       di,[outbuf]              ; ulo‘en¡ podadres ©e
         push      di
         call      dekfile                  ; dek¢dov n¡ jm‚na podadres ©e
         pop       si                       ; p©¡padn˜ podadres ©
         pop       ds
         call      aktdir                   ; nastaven¡ podadres ©e
         jnc       xcopy2                   ; adres © nastaven OK
         call      testbreak                ; je p©eru¨en¡ operace ?
         jc        xcopy99                  ; p©eru¨en¡ operace

         mov       dx,si                    ; zadan˜ adres ©
         mov       ah,39h
ifndef   demo
         int       21h                      ; vytvo©en¡ implicitn¡ho adres ©e
endif
         call      aktdir                   ; nastaven¡ adres ©e

                                          ;* £schova cesty k c¡lov‚mu adres ©i
xcopy2:  call      testbreak                ; je p©eru¨en¡ operace ?
         jc        xcopy99                  ; p©eru¨en¡ operace
         lea       di,[selbuf]              ; buffer k ulo‘en¡ adres ©e
         call      loadpath                 ; na‡ten¡ c¡lov‚ho adres ©e
         call      testbreak                ; je p©eru¨en¡ operace ?
         jc        xcopy99                  ; p©eru¨en¡ operace
         mov       cx,80
         xor       al,al
         repne     scasb                    ; nalezen¡ konce cesty
         mov       word ptr es:[di-1],"\"   ; ukon‡ovac¡ znak "\"

;                                          ;* aktualizace druh‚ho okna
;         push      bp
;         call      getnakt                  ; neaktivn¡ okno
;         call      cmpcpath                 ; je druh‚ okno c¡lov‚ ?
;         clc
;         jne       xcopy3                   ; nen¡ c¡lov‚
;         call      aktuald                  ; aktualizace adres ©e disku
;xcopy3:  pop       bp
;         jc        xcopy99                  ; p©eru¨en¡ operace

                                          ;* nastaven¡ parametr– bufferu
         mov       ax,ds:[segend]           ; segment konce pamˆti
         sub       ax,ds:[topseg]           ; d‚lka voln‚ pamˆti
         dec       ax
         cmp       ax,0fffh                 ; je vˆt¨¡ ne‘ maximum ?
         jbe       xcopy4                   ; je men¨¡ ne‘ maximum
         mov       ax,0fffh                 ; omezen¡ max. velikosti
xcopy4:  shl       ax,1
         shl       ax,1
         shl       ax,1
         shl       ax,1                     ; velikost bufferu v bajtech
         mov       ds:[maxdbuf],ax          ; maxim ln¡ velikost bufferu
         or        ax,ax
         jz        xcopy9b                  ; chyba - nen¡ voln  pamˆŸ

                                          ;* nastaven¡ aktu ln¡ho adres ©e
         mov       si,cs:[bp+4]             ; adres © okna
         call      aktdir                   ; nastaven¡ aktivn¡ho adres ©e okna
         call      testbreak                ; je p©eru¨en¡ operace ?
         jc        xcopy9b                  ; je p©eru¨en¡ operace
         push      ds
         call      getkurz                  ; poskytnut¡ adresy kurzoru
         lea       di,[outbuf]              ; ulo‘en¡ podadres ©e
         push      di
         call      dekfile                  ; dek¢dov n¡ jm‚na podadres ©e
         pop       si                       ; p©¡padn˜ podadres ©
         pop       ds
         call      aktdir                   ; nastaven¡ podadres ©e
         call      testbreak                ; je p©eru¨en¡ operace ?
         jc        xcopy9b                  ; p©eru¨en¡ operace

                                          ;* nastaven¡ standardn¡ adresy DTA
         mov       dx,80h                   ; standardn¡ adresa DTA
         mov       ah,1ah
         int       21h                      ; nastaven¡ adresy DTA

                                          ;* test, zda je zdroj nadadres ©em
         lea       di,[outbuf]              ; buffer k ulo‘en¡ adres ©e
         call      loadpath                 ; na‡ten¡ zdrojov‚ho adres ©e
xcopy5:  mov       si,offset outbuf         ; zdrojov˜ adres ©
         mov       di,offset selbuf         ; c¡lov˜ adres ©
         cld
xcopy52: lodsb                              ; znak zdrojov‚ho adres ©e
         cmp       al,0                     ; je ji‘ konec (=0) ?
         je        xcopy55                  ; je konec - je cyklick‚ kop¡rov n¡
         scasb                              ; porovn n¡ se znakem c¡lov‚ho adres.
         je        xcopy52                  ; porovn n¡ dal¨¡ho znaku
         jmp       short xcopy56            ; nen¡ podmno‘inou

xcopy55: mov       al,es:[di]               ; dal¨¡ znak
         cmp       al,0                     ; jsou stejn‚ cesty ?
         je        xcopy9aa                 ; cyklick‚ kop¡rov n¡
         cmp       al,"\"                   ; je dal¨¡
         jne       xcopy56                  ; nen¡ cyklick‚ kop¡rov n¡
xcopy9aa:jmp       xcopy9a                  ; je podadres ©

xcopy56:

xcopy7:
         call      xcopx                    ; zkop¡rov n¡ aktu ln¡ho adres ©e
         jc        xcopy9b                  ; p©eru¨en¡ operace

                                          ;* p©i p©esunu zru¨en¡ adres ©e
         call      getoper                  ; poskytnut¡ k¢du operace
         or        al,al                    ; je kop¡rov n¡ ?
         jz        xcopy9b                  ; je kop¡rov n¡

                                          ;* zru¨en¡ adres ©e pod kurzorem
         mov       si,cs:[bp+4]             ; adres © okna
         call      aktdir                   ; nastaven¡ aktivn¡ho adres ©e okna
         push      ds
         call      getkurz                  ; poskytnut¡ adresy kurzoru
         lea       di,[outbuf]              ; ulo‘en¡ podadres ©e
         mov       dx,di                    ; jm‚no adres ©e
         call      dekfile                  ; dek¢dov n¡ jm‚na podadres ©e
         pop       ds
         mov       cx,0                     ; nov‚ atributy - nic nenastaveno
         mov       ax,4301h
         int       21h                      ; nulov n¡ atribut– adres ©e
         mov       ah,3ah
         int       21h                      ; zru¨en¡ adres ©e
         call      testbreak                ; pouze test p©eru¨en¡

xcopy9b: ret

; -----------------------------------------------------------------------------
;        Kop¡rov n¡ obsahu aktivn¡ho adres ©e (SELBUF=c¡lov˜ adres ©)
; -----------------------------------------------------------------------------

PUBLIC   xcopx
xcopx:
         mov       ah,4eh                   ; funkce nalezen¡ souboru
         mov       cx,37h                   ; atributy - v¨echny soubory
         mov       dx,offset all            ; specifikace - v¨echny soubory

                                          ;* nalezen¡ souboru/adres ©e
xcopx1:  int       21h                      ; nalezen¡ prvn¡ho/dal¨¡ho souboru
         jnc       xcopx2                   ; nalezen soubor
         cmp       al,2                     ; soubor nenalezen ?
         je        xcopx9                   ; nen¡ ‘ dn˜ soubor
         cmp       al,12h                   ; nen¡ dal¨¡ soubor ?
         je        xcopx9                   ; nen¡ dal¨¡ soubor
         stc                                ; p©¡znak jin‚ chyby
         jmp       short xcopx9             ; jin  chyba

                                          ;* p©esko‡en¡ adres ©– "." a ".."
xcopx2:  call      testbreak                ; je p©eru¨en¡ operace ?
         jc        xcopx9                   ; je p©eru¨en¡ operace
         cmp       word ptr ds:[9eh],"."
         je        xcopx88                  ; nepovolen‚ jm‚no
         cmp       word ptr ds:[9eh],".."
         jne       xcopx21                  ; povolen‚ jm‚no
xcopx88: jmp       xcopx8

                                          ;* kop¡rov n¡ podadres ©e

                                          ;* sestaven¡ cesty k adres ©i
xcopx21: test      byte ptr ds:[95h],10h    ; je to adres © ?
         jnz       xcopx22                  ; je to adres ©
         jmp       xcopx7                   ; nen¡ adres ©, je to soubor

xcopx22: mov       di,offset selbuf         ; c¡lov˜ adres ©
         mov       cx,80                    ; max. d‚lka cesty
         xor       al,al                    ; hledan˜ bajt
         repne     scasb                    ; nalezen¡ konce cesty
         dec       di                       ; n vrat na posledn¡ znak
         mov       si,9eh                   ; jm‚no nalezen‚ho adres ©e
         call      transtxt                 ; p©enos textu jm‚na souboru

                                          ;* vytvo©en¡ adres ©e
         mov       dx,offset selbuf         ; jm‚no adres ©e
         mov       ah,39h                   ; funkce vytvo©en¡ adres ©e
ifndef   demo
         int       21h                      ; vytvo©en¡ c¡lov‚ho adres ©e
endif
                                          ;* nastaven¡ atribut– adres ©e
         mov       ax,4300h                 ; funkce poskytnut¡ atribut–
         int       21h                      ; poskytnut¡ atribut–
         jc        xcopx3                   ; adres © neexistuje
         test      cl,10h                   ; je to adres © ?
         jz        xcopx3                   ; je to soubor - chyba
         mov       cl,ds:[95h]              ; atributy kop¡rovan‚ho adres ©e
         and       cl,not 10h               ; zru¨en¡ atributu adres ©e
         mov       ax,4301h                 ; funkce nastaven¡ atribut–
         int       21h                      ; test, zda adres © existuje
         jnc       xcopx4                   ; v¨e probˆhlo OK

                                          ;* chyba - adres © nelze vytvo©it
xcopx3:  mov       di,offset outbuf
         mov       si,offset errxc1
         call      transtxt
         mov       si,offset selbuf
         call      transtxt
         mov       si,offset errxc2
         call      transtxt
         mov       si,offset outbuf
xcopx33: jmp       zobrchyb

xcopx9:  jmp       short xcopm

                                          ;* nastaven¡ nov‚ho adres ©e
xcopx4:  call      testbreak
         jc        xcopx9                   ; p©eru¨en¡ operace
         mov       word ptr es:[di],"\"     ; koncov˜ znak adres ©e
         mov       dx,9eh                   ; jm‚no nalezen‚ho adres ©e
         mov       ah,3bh                   ; funkce nastaven¡ adres ©e
         int       21h                      ; nastaven¡ podadres ©e
xcopx44: mov       dx,offset erroper
         jc        xcopx33                  ; chyba - adres © nelze nastavit

                                          ;* kop¡rov n¡ obsahu adres ©e
         call      xpush                    ; £schova bufferu DTA
         call      xcopx                    ; kop¡rov n¡ aktivn¡ho adres ©e
         call      xpop                     ; n vrat bufferu DTA
         jc        xcopx9                   ; chyba operace (p©eru¨en¡)

                                          ;* n vrat do nadadres ©e
         mov       dx,offset nadadr         ; nadadres ©
         mov       ah,3bh                   ; funkce nastaven¡ adres ©e
         int       21h                      ; n vrat do adres ©e
         jc        xcopx44                  ; nˆjak  chyba - nelze nastavit
         call      testbreak
         jc        xcopx9                   ; p©eru¨en¡ operace

                                          ;* zru¨en¡ adres ©e
         call      getoper                  ; poskytnut¡ k¢du operace
         cmp       al,1                     ; je p©esun soubor– ?
         jne       xcopx47                  ; nen¡ p©esun soubor–
ifndef   demo
         mov       dx,offset 9eh            ; jm‚no adres ©e
         mov       cx,0                     ; nov‚ atributy - nic nenastaveno
         mov       ax,4301h
         int       21h                      ; nulov n¡ atribut– adres ©e
         mov       ah,3ah
         int       21h                      ; zru¨en¡ adres ©e
endif
xcopx47:

                                          ;* zru¨en¡ adres ©e z c¡lov‚ cesty
         cld
         mov       di,offset selbuf         ; c¡lov˜ adres ©
         mov       cx,90
         xor       al,al                    ; hledan˜ bajt 0
         repne     scasb                    ; nalezen¡ koncov‚ho bajtu 0
xcopx5:  dec       di
         cmp       byte ptr es:[di-2],"\"
         jne       xcopx5                   ; nalezen¡ dal¨¡ho znaku "\"
         mov       byte ptr es:[di-1],0     ; ozna‡en¡ p–vodn¡ho konce cesty

         jmp       short xcopx8             ; dal¨¡ soubor/adres ©


xcopx7:  call      xpush                    ; £schova tabulky DTA
         call      xcopf                    ; kop¡rov n¡ nalezen‚ho souboru
         call      xpop                     ; n vrat tabulky DTA
         jc        xcopx9                   ; chyba nebo p©eru¨en¡

                                          ;* nalezen¡ dal¨¡ho souboru/adres ©e
xcopx8:  mov       ah,4fh                   ; funkce - nalezen¡ dal¨¡ho souboru
         jmp       xcopx1                   ; nalezen¡ dal¨¡ho souboru


xcopm:
                                          ;* modifikace kapacity disku
;         pushf
;         call      getoper                  ; poskytnut¡ k¢du operace
;         or        al,al                    ; je kop¡rov n¡ ?
;         jnz       xcopm47                  ; je p©esun - nezobraz¡ se kapacita
;         call      cmpcdisk                 ; je c¡lov˜ disk ?
;         jne       xcopm3                   ; nen¡ c¡lov˜ disk
;         call      modikap                  ; modifikace kapacity disku
;xcopm3:  push      bp
;         mov       bp,cs:[bp+1]             ; adresa druh‚ho okna
;         call      cmpcdisk                 ; je c¡lov˜ disk ?
;         jne       xcopm4                   ; nen¡ c¡lov˜ disk
;         call      modikap                  ; modifikace kapacity o soubor
;xcopm4:  pop       bp
;xcopm47: popf
         ret

; -----------------------------------------------------------------------------
;        Kop¡rov n¡ jednoho souboru (SELBUF=c¡lov˜ adres ©)
; -----------------------------------------------------------------------------

PUBLIC   xcopf
xcopf:

         mov       word ptr ds:[blokuk],0
         mov       word ptr ds:[blokuk+2],0 ; z kaz zobrazen¡ ukazatele

         lea       si,[txtcop5]             ; text " Kop¡ruji"
         call      getoper                  ; poskytnut¡ operace
         or        al,al                    ; je kop¡rov n¡ ?
         jz        xcopf1                   ; je kop¡rov n¡
         lea       si,[txtmov5]             ; text "P©esouv m"

                                          ;* zobrazen¡ informa‡n¡ho © dku
xcopf1:  call      mouseoff                 ; vypnut¡ my¨i
         call      informc                  ; vymaz n¡ informa‡n¡ho © dku
         call      outtxt                   ; zobrazen¡ £vodn¡ho textu
         inc       dl                       ; zv˜¨en¡ pozice za textem
         lea       si,[textvy2+2]           ; text "soubor"
         call      outtxt                   ; zobrazen¡ textu
         inc       dl                       ; zv˜¨en¡ pozice
         mov       al,10                    ; barva textu 10
         call      outch1                   ; nastaven¡ barvy 10
         mov       si,9eh                   ; kop¡rovan˜ soubor
         call      outtxt                   ; zobrazen¡ jm‚na souboru
         lea       si,[txtcop6]             ; text "do"
         call      outtxt
         call      kurzout                  ; vypnut¡ kurzoru
         call      mouseon                  ; zapnut¡ my¨i

                                          ;* sestaven¡ c¡le operace
         mov       di,offset selbuf         ; c¡lov˜ adres ©
         xor       al,al
         mov       cx,90
         repne     scasb                    ; nalezen¡ konce adres ©e
         dec       di

         push      di                       ; £schova p–vodn¡ho konce adres ©e

         mov       si,9eh                   ; kop¡rovan˜ soubor
         call      transtxt                 ; p©ipojen¡ k jm‚nu adres ©e
         call      mouseoff                 ; vypnut¡ my¨i
         mov       si,offset selbuf
         call      outtxt                   ; zobrazen¡ c¡le operace
         call      mouseon                  ; zapnut¡ my¨i

                                          ;* £schova parametr– souboru
         mov       si,9eh
         mov       di,offset outbuf         ; buffer k ulo‘en¡ zdroje
         call      transtxt                 ; £schova zdrojov‚ho souboru
         mov       ax,ds:[98h]              ; datum
         mov       word ptr ds:[outbuf+20+16],ax ; datum
         mov       ax,ds:[96h]              ; ‡as
         mov       word ptr ds:[outbuf+20+18],ax ; ‡as
         mov       al,ds:[95h]              ; atributy
         mov       ds:[outbuf+20],al        ; atributy

                                          ;* test, zda soubor ji‘ existuje
         call      testex                   ; pokud existuje, ru¨en¡
         jc        xcopf9                   ; p©eru¨en¡ operace
         jnz       xcopf9                   ; operace se neprov d¡

                                          ;* proveden¡ funkce kop¡rov n¡
         call      msgcek                   ; hl ¨en¡ "‡ekejte"
         call      getoper                  ; poskytnut¡ k¢du operace
         or        al,al                    ; je kop¡rov n¡ ?
         jz        xcopf2                   ; je kop¡rov n¡

         call      xmove                    ; p©esun souboru
         jmp       short xcopf22

xcopf2:  call      copyfk                   ; kop¡rov n¡ 1 souboru

xcopf22: pop       di                       ; p–vodn¡ adresa konce SELBUF
         mov       al,0                     ; ukon‡ovac¡ bajt 0
         stosb                              ; n vrat p–vodn¡ho konce SELBUF
         jc        xcopf7                   ; chyba operace

                                          ;* modifikace kapacity disku
         call      getoper                  ; poskytnut¡ k¢du operace
         or        al,al                    ; je kop¡rov n¡ ?
         jnz       xcopf47                  ; je p©esun - nezobraz¡ se kapacita
         call      cmpcdisk                 ; je c¡lov˜ disk ?
         jne       xcopf3                   ; nen¡ c¡lov˜ disk
         call      modikap                  ; modifikace kapacity disku
xcopf3:  push      bp
         mov       bp,cs:[bp+1]             ; adresa druh‚ho okna
         call      cmpcdisk                 ; je c¡lov˜ disk ?
         jne       xcopf4                   ; nen¡ c¡lov˜ disk
         call      modikap                  ; modifikace kapacity o soubor
xcopf4:  pop       bp
xcopf47: xor       al,al                    ; operace OK
         jmp       short xcopf8

                                          ;* chyba operace
xcopf7:
         stc                                ; p©¡znak chyby
xcopf8:  call      topret                   ; n vrat horn¡ho © dku
         ret

                                          ;* operace se neprovede nebo p©eru¨en¡
xcopf9:  pop       di                       ; p–vodn¡ adresa konce SELBUF
         mov       al,0                     ; ukon‡ovac¡ bajt 0
         stosb                              ; n vrat p–vodn¡ho konce SELBUF
         ret

; -----------------------------------------------------------------------------
;        P©esun souboru
; -----------------------------------------------------------------------------

PUBLIC   xmove
xmove:   call      cmpcdisk                 ; je p©esun mezi disky ?
         jne       xmove5                   ; je p©esun mezi disky

                                          ;* p©esun mezi adres ©i
ifndef   demo
         lea       dx,[selbuf]              ; jm‚no c¡lov‚ho souboru
         mov       ah,41h                   ; funkce ru¨en¡ souboru
         int       21h                      ; zru¨en¡ c¡lov‚ho souboru
endif
         lea       dx,[outbuf]              ; jm‚no p–vodn¡ho souboru
         lea       di,[selbuf]              ; nov‚ jm‚no souboru
ifndef   demo
         mov       ah,56h                   ; funkce p©ejmenov n¡ souboru
         int       21h                      ; p©ejmenov n¡ souboru
else
         call      prodldem
endif
xmove4:  ret

                                          ;* p©esun mezi disky
xmove5:  call      copyfk                   ; zkop¡rov n¡ souboru
         jc        xmove4                   ; chyba operace
         mov       dx,offset outbuf         ; p–vodn¡ soubor
         call      delfk                    ; zru¨en¡ souboru
         ret

code     ends

         end
