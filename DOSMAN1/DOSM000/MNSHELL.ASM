; ##############################################################################
; #                                                                            #
; #                         M I N E S  -  S H E L L                            #
; #                 program pro operace se soubory a s disky                   #
; #                                                                            #
; #                  >>> Autor: ing. Miroslav Nˆme‡ek <<<                      #
; #                                                                 verze 0.10 #
; ##############################################################################

; Chybov‚ n vratov‚ k¢dy:
;
;        0 - odskok do syst‚mu, start programu (z prost©ed¡)
;        1 - odskok do syst‚mu, vyvol n¡ souboru MNEXEC.BAT
;        2 - konec pr ce programu MNSHELL (funkce EXIT)
;        3 - chyba verze opera‡n¡ho syst‚mu
;        4 - chyba grafick‚ karty
;
;

; *****************************************************************************
;
;                             Programov˜ segment
;
; *****************************************************************************
code     segment
         assume    cs:code,ds:data,ss:stack

public   main,init,initwin,uses,deinit,mainx,konecerr,konec0,chkver
public   initmem,initpar,charpar,initdir,readcnf,initmod,testcard
public   testmda,testvmod,setown,initint,pushscr
public   initwin,readdir,dirnul,diskread,dirread,setmem,adrfile,aktdir
public   transfile,kodfile,storfile,cmpfile,normatr,insertf,aktdisk
public   aktfile,settabf,window,topline,space,directory,obrpath,files
public   zobrfile,zobrfilz,zobrfil0,zobrfila,zobrfiln,zobrfilv,zobrfild
public   zobrfilc,endline,kurzon,kurzoff,getkurz,deinit,uses,obrcomm
public   obrcom0,obrcomt,obrcomt0,obrkurz,xchgakt,windnul,moveup
public   movedown,movehome,moveend,pageup,pagedown,insert,invsel
public   enter,testexit,insch,chleft,chright,delchar,delbs,homer,endr
public   ctrlent,xchgpg,xchgw,sethlp,outch1,outch01,outch02,outch,outch0
public   outtxt,outtx0,wrolldown,wrollup,lowcase,highcase,transtxt
public   setkonc,testkonc,readtest,center,tisknm2s,tisknm20,tisknm0
public   setspc,setnul,setfre,deknumx,deknum,deknum0,deknum9,deknum8
public   deknum7,deknum6,deknum5,deknum4,deknum3,deknum2,dekfile
public   loadpath,openex,openw,closew,writte,writex
public   errver,errcard,citdek,citnum,parnum,outbuf,texthlp,textclk
public   textsp1,textsp2,textsp20,textsp21,textsp22,textsp23
public   textsp3,textsp30,textsp4,textvy1,textvy11,textvy12,textvy13
public   textvy14,textvy2,textvy21,textvy22,txtsubd,txtupd,txtdsk,txthelp2
public   txthelp,color,all,filecnf,fileexc,txtcall,normdsk,namepath
public   flags,numchar,poztop,pozcomm,pozbuf,endadr,tabl,tabr,verze
public   segpsp,typdisp,vmod,segvram,segend,aktatr,radek,aktpage,outid
public   inpid,ukazbuf,numbuf,buftxt,diskbuf,normfil,pathhome,pathinit
public   endshell,delenv,srcenv,parenv,maxenv,setenv,selplus,selmin
public   select,selwin,setwsel,testsel,asksel,bufsel,stin,selall,selpol
public   podklad,wram1,wram2,wnadpis,stinw,obrstin,numselb,ukazsel,topsel
public   selbuf,firstsel,kurzsel,setkurzs,editsel,obredit,selwleft
public   selwright,obnov,testchar,delwl,delwr,selleft,selright,selend
public   selhome,selwleft,selwright,srcident,delselect,storesel,cmpname

main:                                     ;* start programu - hlavn¡ ‡ st
         mov       ax,seg data              ; segment dat
         mov       ds,ax                    ; nastaven¡ datov‚ho segmentu
         mov       ds:[segpsp],es           ; £schova segmentu PSP
         mov       es,ax                    ; segment dat
         cld                                ; smˆr p©enosu dat nahoru

         call      init                     ; po‡ te‡n¡ inicializace programu

         cli
         mov       ax,ds:[segend]
         mov       ax,7000h
         mov       ss,ax
;         xor       ax,ax
         mov       ax,1000h
         mov       sp,ax
         sti

         call      initwin                  ; po‡ te‡n¡ zobrazen¡ oken

         call      uses                     ; hlavn¡ program - povely
mainx:  ; call      storecnf                 ; ulo‘en¡ konfigurace do pamˆti
;         call      savecomm                 ; ulo‘en¡ povelov‚ho souboru
         call      deinit
         jmp       short konec0             ; konec programu

konecerr:                                 ;* konec programu s chybou
                                          ;* (AL=n vrat.k¢d, DS:DX=chybov˜ text)
         push      ax                       ; £schova n vratov‚ho k¢du
         call      deinit                   ; deinicializace programu
         mov       ah,9                     ; funkce tisku textu na displej
         int       21h                      ; zobrazen¡ chybov‚ho textu
         pop       ax                       ; n vrat chybov‚ho k¢du

konec0:                                   ;* konec programu (AL=n vratov˜ k¢d)
         mov       ah,4ch                   ; funkce pro n vrat z programu
         int       21h                      ; ukon‡en¡ programu



; *****************************************************************************
;
;                             Inicializace programu
;
; *****************************************************************************

init:                                     ;* po‡ te‡n¡ inicializace programu

         call      chkver                   ; kontrola verze opera‡n¡ho syst‚mu
         call      initmem                  ; inicializace pamˆti (alokace)
         call      initpar                  ; inicializace parametr– (zad n¡)
         call      initdir                  ; nastaven¡ adres ©– - inicializace
         call      readcnf                  ; ‡ten¡ konfigura‡n¡ho souboru
         call      srcenv                   ; nalezen¡ otcovsk‚ho prost©ed¡
;         call      delenv                   ; zru¨en¡ ©etˆzce EXE.. z prost©ed¡
         call      initmod                  ; inicializace videom¢du
         call      initint                  ; inicializace obsluh p©eru¨en¡
         call      pushscr                  ; ulo‘en¡ obsahu obrazovky
         ret


; -----------------------------------------------------------------------------
;                     Kontrola verze opera‡n¡ho syst‚mu
; -----------------------------------------------------------------------------

chkver:                                   ;* kontrola verze opera‡n¡ho syst‚mu
                                            ; zni‡en‚ registry: AX

         mov       ah,30h                   ; funkce poskytnut¡ ‡¡sla verze OS
         int       21h                      ; poskytnut¡ ‡¡sla verze OS
         xchg      al,ah                    ; z mˆna ‡¡sla verze a podverze
         mov       [verze],ax               ; £schova ‡¡sla verze OS
         cmp       ax,200h                  ; je verze alespo¤ 2.00 ?
         jb        chkver1                  ; je chyba ‡¡sla verze syst‚mu
         ret
                                          ;* chyba verze opera‡n¡ho syst‚mu
chkver1: pop       ax                       ; zru¨en¡ n vratu do INIT
         pop       ax                       ; zru¨en¡ n vratu do hlavn¡ho prog.
         mov       al,3                     ; k¢d chyby verze opera‡n¡ho syst‚mu
         mov       dx,offset errver         ; text chyby verze opera‡n¡ho syst.
         jmp       konecerr                 ; chybov˜ n vrat z programu

; -----------------------------------------------------------------------------
;
; -----------------------------------------------------------------------------

initmem:                                  ;* inicializace pamˆti (alokace)

                                          ;* £schova voln‚ kapacity pamˆti
         push      ds                       ; £schova DS
         mov       ds,ds:[segpsp]           ; segment PSP
         mov       ax,ds:[2]                ; segment konce p©idˆlen‚ho bloku
         pop       ds                       ; n vrat DS
         mov       ds:[segend],ax           ; segment konce p©idˆlen‚ho bloku
         ret

; -----------------------------------------------------------------------------
;                  Inicializace parametr– z p©¡kazov‚ho © dku
; -----------------------------------------------------------------------------

initpar:                                  ;* inicializace parametr– z p©¡k. © d.
                                            ; zni‡en‚ registry: AX,CX,SI,DI

         cld
         push      ds                       ; £schova DS
         mov       ds,ds:[segpsp]           ; segment PSP
         mov       si,81h                   ; p©¡kazov˜ © dek
         xor       ch,ch                    ; CH <- 0
         mov       cl,ds:[si-1]             ; d‚lka ©etˆzce p©¡kazov‚ho © dku
         jcxz      initpr3                  ; nen¡ zad n ‘ dn˜ parametr
         mov       di,offset pathl
;         mov       di,offset filecnf        ; jm‚no konfigura‡n¡ho souboru
                                          ;* nalezen¡ za‡ tku textu
initpr1: call      charpar                  ; na‡ten¡ znaku z p©¡kaz. © dku
         jc        initpr3                  ; je konec © dku
         jz        initpr1                  ; test dal¨¡ho znaku
                                          ;* p©enos jm‚na souboru
initpr2: call      highcase                 ; p©evod na velk‚ p¡smeno
         stosb                              ; ulo‘en¡ znaku
         call      charpar                  ; na‡ten¡ dal¨¡ho znaku z p©¡k. © d.
         ja        initpr2                  ; nen¡ oddˆlova‡ ani konec - ulo‘en¡
         xor       al,al                    ; ukon‡ovac¡ bajt 0
         stosb                              ; ulo‘en¡ ukon‡ovac¡ho bajtu 0
initpr3: pop       ds                       ; n vrat DS
         ret

; -----------------------------------------------------------------------------

charpar:                                  ;* na‡ten¡ znaku z p©¡kaz. © dku
                                            ; VSTUP: CX=‡¡ta‡ znak–
                                            ;        DS:SI=ukazatel textu
                                            ; VSTUP: CY=je konec textu
                                            ;         ZY=je oddˆlovac¡ znak

         stc                                ; p©¡znak konce textu
         jcxz      charpr1                  ; je konec textu p©¡kaz. © dku
         lodsb                              ; na‡ten¡ znaku
         dec       cx                       ; sn¡‘en¡ ‡¡ta‡e znak–
         cmp       al," "                   ; je oddˆlovac¡ mezera ?
         je        charpr1                  ; je oddˆlovac¡ mezera
         cmp       al,9                     ; je tabel tor ?
         je        charpr1                  ; je oddˆlovac¡ tabel tor
         cmp       al,"/"                   ; je ozna‡en¡ parametr– ?
         je        charpr1                  ; je ozna‡en¡ parametr–
         clc                                ; maz n¡ p©¡znaku konce textu
charpr1: ret

; -----------------------------------------------------------------------------
;                  Inicializace adres ©– (p©¡stupov˜ch cest)
; -----------------------------------------------------------------------------

initdir:                                  ;* nastaven¡ adres ©– - inicializace
                                            ; zni‡en‚ registry: AX,CX,SI,DI

         cld
         push      ds                       ; £schova DS
         mov       di,offset pathinit       ; inicializa‡n¡ aktivn¡ cesta
         call      loadpath                 ; na‡ten¡ aktivn¡ cesty
         mov       di,offset pathl          ; cesta k lev‚mu adres ©i
         cmp       byte ptr ds:[di],0
         jne       initdr01
         call      loadpath                 ; na‡ten¡ aktivn¡ cesty
initdr01:mov       di,offset pathr          ; cesta k prav‚mu adres ©i
         call      loadpath                 ; na‡ten¡ aktivn¡ cesty
         cmp       word ptr ds:[verze],300h ; je verze 3.00 nebo vy¨¨¡ ?
         jb        initdr4                  ; je ni‘¨¡ verze ne‘ 3.00
         mov       ds,ds:[segpsp]           ; segment PSP
         mov       ds,ds:[2ch]              ; segment prost©ed¡
         mov       cx,7fffh                 ; maxim ln¡ d‚lka prost©ed¡
         xor       si,si                    ; po‡ te‡n¡ offset prost©ed¡
initdr1: cmp       word ptr ds:[si],0       ; je konec prost©ed¡ ?
         je        initdr2                  ; je konec prost©ed¡
         inc       si                       ; zv˜¨en¡ adresy textu
         loop      initdr1                  ; test dal¨¡ho znaku
         jmp       short initdr4            ; chyba - nenalezen konec
initdr2: inc       si
         inc       si                       ; po‡et ©etˆzc–
         cmp       word ptr ds:[si],8       ; je velk˜ po‡et ©etˆzc– ?
         ja        initdr4                  ; je velk˜ po‡et ©etˆzc–
         inc       si
         inc       si                       ; za‡ tek ©etˆzce
         mov       di,offset pathhome       ; cesta k domovsk‚mu adres ©i
         mov       cx,127                   ; maxim ln¡ d‚lka cesty
         push      di                       ; £schova adresy za‡ tku textu
initdr3: lodsb                              ; na‡ten¡ znaku ©etˆzce
         stosb                              ; ulo‘en¡ znaku
         cmp       al,"\"                   ; je ozna‡en¡ adres ©e ?
         jne       initdr31                 ; nen¡ znak adres ©e
         add       sp,2                     ; zru¨en¡ adresy textu
         push      di                       ; ulo‘en¡ nov‚ adresy konce textu
initdr31:or        al,al                    ; je ji‘ konec ©etˆzce ?
         jnz       initdr32                 ; nen¡ je¨tˆ konec ©etˆzce
         pop       di                       ; n vrat adresy konce ©etˆzce
         dec       di
         stosb                              ; ulo‘en¡ koncov‚ho bajtu 0
         jmp       short initdr5            ; konec ©etˆzce
initdr32:loop      initdr3                  ; p©enos dal¨¡ho znaku
         pop       di
initdr4:                                  ;* nestandardn¡ na‡ten¡ dom. cesty
         mov       di,offset pathhome       ; cesta k domovsk‚mu adres ©i
         call      loadpath                 ; na‡ten¡ cesty k domovsk‚mu adr.
initdr5: pop       ds                       ; n vrat DS
         ret

; -----------------------------------------------------------------------------
;                     €ten¡ souboru konfigurace
; -----------------------------------------------------------------------------

readcnf:                                  ;* ‡ten¡ konfigura‡n¡ho souboru
         ret

; -----------------------------------------------------------------------------
;                  Nastaven¡ videom¢du, test displeje
; -----------------------------------------------------------------------------

initmod:                                  ;* inicializace videom¢du

         call      testvmod                 ; test nastaven‚ho videom¢du
         jnc       initmd1                  ; videom¢d je nastaven spr vnˆ
         mov       ax,3                     ; po‘adovan˜ videom¢d 3
         int       10h                      ; nastaven¡ videom¢du 3
         call      testvmod                 ; test nastaven‚ho videom¢du
         jnc       initmd1                  ; videom¢d je nastaven spr vnˆ
         mov       ax,2                     ; po‘adovan˜ videom¢d 2
         int       10h                      ; nastaven¡ videom¢du 2
         call      testvmod                 ; test nastaven‚ho videom¢du
         jnc       initmd1                  ; videom¢d je nastaven spr vnˆ
         mov       ax,7                     ; po‘adovan˜ videom¢d 7
         int       10h                      ; nastaven¡ videom¢du 7
         call      testvmod                 ; test nastaven‚ho videom¢du
         jnc       initmd1                  ; videom¢d je nastaven spr vnˆ
         pop       ax                       ; zru¨en¡ n vratu do INIT
         pop       ax                       ; zru¨en¡ n vratu do hlavn¡ho prog.
         mov       al,4                     ; k¢d chyby grafick‚ karty
         mov       dx,offset errcard        ; text chyby grafick‚ karty
         jmp       konecerr                 ; chybov˜ n vrat z programu
initmd1:
         mov       ds:[vmod],al             ; ulo‘en¡ aktivn¡ho videom¢du
         mov       ds:[segvram],dx          ; adresa segmentu VRAM
         mov       ds:[aktpage],3           ; aktivn¡ str nka = 1
         mov       ah,3                     ; dotaz na pozici kurzoru
         xor       bh,bh                    ; str nka 0
         int       10h                      ; poskytnut¡ pozice kurzoru
         mov       ds:[radek],dh            ; £schova © dku s kurzorem
         mov       ah,5                     ; funkce nastaven¡ str nky displeje
         mov       al,ds:[aktpage]          ; po‘adovan  str nka
         and       al,1
         int       10h                      ; nastaven¡ str nky displeje 1
initmd11:call      testcard                 ; test videokarty
         cmp       byte ptr ds:[vmod],2     ; je videom¢d 2 ?
         je        initmd2                  ; je videom¢d 2
         cmp       byte ptr ds:[vmod],3     ; je videom¢d 3 ?
         jne       initmd4                  ; nen¡ ani videom¢d 3
initmd2: cmp       byte ptr ds:[typdisp],1  ; je displej CGA ?
         je        initmd3                  ; je displej CGA
         cmp       byte ptr ds:[typdisp],3  ; je displej EGA ?
         jne       initmd4                  ; nen¡ ani displej EGA
initmd3: call      setown                   ; nastaven¡ vlastn¡ obsluhy displeje
initmd4: mov       ah,8
         xor       bh,bh
         int       10h
         mov       ds:[aktatr],ah           ; atribut znaku
         call      obrcomm
         call      obrkurz
         ret


; -----------------------------------------------------------------------------

testcard:                                 ;* test (rozpozn n¡) videokarty
                                            ; zni‡en‚ registry: AX,BX

         mov       ah,12h                   ; funkce alternativ. nastaven¡ EGA
         mov       bl,10h                   ; podslu‘ba - informace o EGA
         int       10h                      ; poskytnut¡ informac¡ o EGA
         cmp       bl,10h                   ; funkce nen¡ podporov na ?
         je        testcr1                  ; funkce nen¡ podporov na (nen¡ EGA)
         mov       byte ptr ds:[typdisp],3  ; je grafick  karta EGA
         ret
testcr1: mov       al,ds:[vmod]             ; aktivn¡ videom¢d
         cmp       al,7                     ; je textov˜ re‘im MDA ?
         jne       testcr2                  ; nen¡ textov˜ re‘im MDA - je CGA
         call      testmda                  ; test grafick‚ karty MDA
         jz        testcr2                  ; nen¡ grafick  karta MDA
         mov       byte ptr ds:[typdisp],2  ; je grafick  karta MDA
         ret
testcr2: mov       byte ptr ds:[typdisp],1  ; je grafick  karta CGA
         ret

; -----------------------------------------------------------------------------

testmda:                                  ;* test grafick‚ karty MDA
                                          ;* VSTUP: ZY=nen¡ karta MDA

         push      ds                       ; £schova registru DS
         push      dx                       ; £schova registru DX
         push      cx                       ; £schova registru CX
         xor       ax,ax                    ; AX <- 0000
         mov       ds,ax                    ; DS <- 0000
         mov       dx,ds:[463h]             ; b zov  ©¡dic¡ adresa displeje
         and       dl,0f0h                  ; z klad adresy
         or        dl,0ah                   ; adresa stavov‚ho registru
         mov       cx,2000h                 ; maxim ln¡ doba pro test impulsu
testmda1:in        al,dx                    ; vstup ze stavov‚ho registru
         test      al,80h                   ; je synchroniza‡n¡ impuls MDA ?
         jz        testmda2                 ; p©i¨el impuls - je karta MDA
         loop      testmda1                 ; dal¨¡ ‡ek n¡ na impuls MDA
testmda2:or        cx,cx                    ; bylo p©ete‡en¡ ‡ekac¡ smy‡ky ?
         pop       cx                       ; n vrat registru CX
         pop       dx                       ; n vrat registru DX
         pop       ds                       ; n vrat registru DS
         ret

; -----------------------------------------------------------------------------

testvmod:                                 ;* test videom¢du
                                            ; VSTUP: CY=nespr vn˜ videom¢d
                                            ;         AL=videom¢d
                                            ;         DX=segment videopamˆti

         mov       ah,0fh                   ; funkce dotazu na videom¢d
         int       10h                      ; dotaz na nastaven˜ videom¢d
         mov       dx,0b800h                ; segment videopamˆti EGA,CGA
         cmp       al,2                     ; je videom¢d 2 ?
         je        testvm1                  ; je videom¢d 2 - OK
         cmp       al,3                     ; je videom¢d 3 ?
         je        testvm1                  ; je videom¢d 3 - OK
         cmp       al,7                     ; je videom¢d 7 ?
         jne       testvm2                  ; nen¡ videom¢d 7
         mov       dh,0b0h                  ; segment videopamˆti MDA
testvm1: or        bh,bh                    ; je str nka 0 ?
         jnz       testvm2                  ; nen¡ str nka 0
         cmp       ah,80                    ; je po‡et pozic na © dek 80 ?
         jne       testvm2                  ; nen¡ 80 znak– na © dek
         ret
testvm2: stc                                ; p©¡znak - nespr vn˜ videom¢d
         ret

; -----------------------------------------------------------------------------

setown:                                   ;* nastaven¡ vlastn¡ obsluhy displeje

         mov       ax,offset(outch01-setout)-3
         mov       word ptr cs:[setout+1],ax ; instrukce skoku
         ret


; -----------------------------------------------------------------------------
;                   Instalace vlastn¡ch obsluh p©eru¨en¡
; -----------------------------------------------------------------------------

initint:                                    ; inicializace obsluh p©eru¨en¡
         ret

; -----------------------------------------------------------------------------
;                       £schova obsahu obrazovky
; -----------------------------------------------------------------------------

pushscr:                                    ; inicializace obsluh p©eru¨en¡
         ret


; *****************************************************************************
;
;                          Po‡ te‡n¡ zobrazen¡ oken
;
; *****************************************************************************

initwin:                                  ;* po‡ te‡n¡ zobrazen¡ oken
                                            ; zni‡en‚ registry: BP

         mov       bp,offset tabr           ; tabulka definice okna
         call      readdir                  ; aktualizace - na‡ten¡ adres ©–
         call      window                   ; zobrazen¡ okna
         mov       bp,offset tabl           ; tabulka definice okna
         call      readdir                  ; aktualizace - na‡ten¡ adres ©–
         call      window                   ; zobrazen¡ okna
         ret


; -----------------------------------------------------------------------------
;                            Na‡ten¡ adres ©e
; -----------------------------------------------------------------------------

readdir:                                  ;* aktualizace - na‡ten¡ adres ©e
                                            ; VSTUP: BP=ukazatel okna

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         call      dirnul                   ; vynulov n¡ adres ©e
         call      diskread                 ; na‡ten¡ platn˜ch disk–
         call      dirread                  ; na‡ten¡ nov‚ho adres ©e
         call      aktdisk                  ; aktualizace parametr– disku
         call      aktfile                  ; aktualizace kapacity soubor–
         call      settabf                  ; nastaven¡ ukazatel– tabulky
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------

dirnul:                                   ;* vynulov n¡ adres ©e
                                            ; VSTUP: BP=adresa okna
                                            ; zni‡en‚ reg.: CX,SI,DI

         cld                                ; smˆr posunu ukazatele nahoru
         mov       di,ds:[adrfr]            ; offset prav‚ho okna
         mov       si,ds:[adrfl]            ; offset lev‚ho okna
         test      byte ptr ds:[bp+flagsl-tabl],4 ; je lev‚ okno ?
         jz        dirout1                  ; je prav‚ okno
         xchg      si,di
                                          ;* DI-vypou¨.adres ©, SI-druh˜ adres ©
dirout1: cmp       di,si                    ; je nov˜ adres © DI n¡‘ ne‘ SI ?
         ja        dirout2                  ; nov˜ adres © je v˜¨ - OK
         push      di                       ; £schova adresy adres.
         mov       cx,ds:[endadr]           ; adresa konce pamˆti
         sub       cx,si                    ; d‚lka dat druh‚ho adres ©e SI
         rep       movsb                    ; p©enos druh‚ho adres ©e SI->DI
         pop       si                       ; adresa druh‚ho adres ©e
dirout2: mov       ds:[endadr],di           ; nov  adresa konce pamˆti
         test      byte ptr ds:[bp+flagsl-tabl],4 ; je lev‚ okno ?
         jz        dirout3                  ; je prav‚ okno
         xchg      si,di
dirout3: mov       ds:[adrfr],di            ; nov  adresa prav‚ho adres ©e
         mov       ds:[adrfl],si            ; nov  adresa lev‚ho adres ©e
         mov       word ptr ds:[bp+numfl-tabl],0 ; po‡et soubor–
         ret

; -----------------------------------------------------------------------------

diskread:                                 ;* na‡ten¡ disk– v syst‚mu
                                            ; VSTUP: BP=adresa okna
                                            ; zni‡en‚ reg.: AX,BX,CX,DX,SI,DI

         xor       dl,dl                    ; ‡¡slo disku
         xor       bx,bx                    ; ‡¡ta‡ disk–
         mov       cx,30                    ; maxim ln¡ po‡et disk–
diskrd1: mov       ah,0eh                   ; funkce v˜bˆru aktivn¡ho disku
         int       21h                      ; v˜bˆr aktivn¡ho disku DL
         mov       ah,19h                   ; funkce poskytnut¡ aktivn¡ho disku
         int       21h                      ; poskytnut¡ aktivn¡ho disku
         cmp       al,dl                    ; kontrola ‡¡sla disku
         jne       diskrd4                  ; nen¡ platn˜ disk
                                          ;* ulo‘en¡ ozna‡en¡ disku
         push      dx
         push      cx
         mov       si,offset normdsk        ; ozna‡en¡ disku
         add       al,"A"                   ; korekce ‡¡sla disku na znak ASCII
         mov       ds:[si+6],al             ; ulo‘en¡ ozna‡en¡ disku
         mov       di,offset normfil        ; normalizovan˜ soubor
         mov       cx,12                    ; d‚lka polo‘ky
         rep       movsb                    ; p©enos polo‘ky
diskrd2: call      setmem                   ; nastaven¡ konce pamˆti
         jc        diskrd3                  ; p©ekro‡en¡ konce pamˆti
         push      bx                       ; £schova ‡¡ta‡e polo‘ek
         call      storfile                 ; ulo‘en¡ polo‘ky do seznamu adres.
         pop       bx                       ; n vrat ‡¡ta‡e polo‘ek
         inc       bx                       ; zv˜¨en¡ ‡¡ta‡e polo‘ek
diskrd3: pop       cx
         pop       dx

diskrd4: inc       dl                       ; zv˜¨en¡ ‡¡sla disku
         loop      diskrd1                  ; nastaven¡ dal¨¡ho disku
         mov       ds:[bp+numfl-tabl],bx    ; ulo‘en¡ po‡tu disk–
         ret


; -----------------------------------------------------------------------------

dirread:                                  ;* na‡ten¡ nov‚ho adres ©e
                                            ; VSTUP: BP=adresa okna
                                            ; zni‡en‚ reg.: AX,BX,CX,DX,SI,DI

         lea       si,ds:[bp+pathl-tabl]    ; cesta k adres ©i
         call      aktdir                   ; nastaven¡ aktivn¡ho adres ©e okna
         mov       bx,ds:[bp+numfl-tabl]    ; po‡et ulo‘en˜ch soubor–
         mov       cx,16h                   ; atribut pro hled n¡ polo‘ek
         mov       dx,offset all            ; specifikace - v¨echny soubory
         mov       ah,4eh                   ; funkce nalezen¡ prvn¡ polo‘ky
                                          ;* za‡ tek cyklu na‡¡t n¡ polo‘ek
dirrd2:  int       21h                      ; na‡ten¡ polo‘ky adres ©e
         jc        dirrd5                   ; nen¡ dal¨¡ polo‘ka
                                          ;* kontrola, zda to nen¡ adres. "."
         push      ds                       ; £schova DS
         mov       ds,ds:[segpsp]           ; adresa segmentu PSP
         mov       ax,ds:[95h]              ; atributy nalezen‚ polo‘ky
         test      al,10h                   ; byl nalezen adres © ?
         mov       ax,word ptr ds:[9eh]     ; prvn¡ 2 znaky jm‚na souboru
         pop       ds                       ; n vrat DS
         jz        dirrd3                   ; nen¡ to adres ©
         cmp       ax,"."                   ; je to ozna‡en¡ aktiv. adres ©e ?
         je        dirrd4                   ; je aktivn¡ adres © - neulo‘¡ se
                                          ;* ulo‘en¡ souboru do seznamu
dirrd3:  call      setmem                   ; nastaven¡ konce pamˆti
         jc        dirrd5                   ; p©ekro‡en¡ konce pamˆti
         push      bx
         call      transfile                ; p©enos parametr– souboru
         call      storfile                 ; ulo‘en¡ polo‘ky do seznamu adres.
         pop       bx
         inc       bx                       ; zv˜¨en¡ ‡¡ta‡e polo‘ek
dirrd4:  mov       ah,4fh                   ; funkce nalezen¡ dal¨¡ polo‘ky
         jmp       short dirrd2             ; nalezen¡ dal¨¡ polo‘ky
dirrd5:                                   ;* konec na‡¡t n¡ soubor–
         mov       ds:[bp+numfl-tabl],bx    ; po‡et soubor–
         xor       ax,ax                    ; AX <- 0
         mov       ds:[bp+insfil-tabl],ax   ; nastaven¡ po‡tu ozna‡en˜ch soubor–
         mov       ds:[bp+kapfil-tabl],ax   ; nastaven¡ zabran‚ kapacity soubory
         mov       ds:[bp+2+(kapfil-tabl)],ax ; - " - (vy¨¨¡ slovo)
         ret

; -----------------------------------------------------------------------------

setmem:                                   ;* nastaven¡ konce pamˆti
                                            ; VSTUP: BX=‡¡slo nov‚ polo‘ky
                                            ;        BP=ukazatel tabulky okna
                                            ; VSTUP: CY=p©ekro‡en konec pamˆti
                                            ; zni‡en‚ registry: AX,SI

         mov       ax,20                    ; d‚lka 1 polo‘ky
         mul       bx                       ; offset polo‘ky v seznamu
         mov       si,ds:[bp+adrfl-tabl]    ; adresa za‡ tku tabulky
         add       si,20                    ; konec polo‘ky
         add       si,ax                    ; adresa polo‘ky (CY=p©ete‡en¡)
         jc        setmem1                  ; p©ete‡en¡
         mov       ds:[endadr],si           ; nov  adresa konce pamˆti
setmem1: ret


; -----------------------------------------------------------------------------

adrfile:                                  ;* adresa polo‘ky v seznamu
                                            ; VSTUP: BX=‡¡slo polo‘ky
                                            ;        BP=ukazatel tabulky okna
                                            ; VSTUP: SI=adresa polo‘ky
                                            ;         CY=neplatn‚ ‡¡slo polo‘ky

         push      ax
         push      bx
         push      dx
         mov       ax,bx
         inc       ax
         cmp       ds:[bp+numfl-tabl],ax    ; kontrola ‡¡sla polo‘ky
         jc        adrfil1                  ; nespr vn‚ ‡¡slo polo‘ky
         dec       ax
         mov       bx,20
         mul       bx
         mov       si,ds:[bp+adrfl-tabl]    ; adresa za‡ tku tabulky
         add       si,ax                    ; adresa polo‘ky (CY=p©ete‡en¡)
adrfil1: pop       dx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------

aktdir:                                   ;* nastaven¡ aktivn¡ho adres ©e
                                            ; VSTUP: DS:SI=p©¡stupov  cesta
                                            ; VSTUP: CY=chyba

         push      dx                       ; £schova DX
         push      ax                       ; £schova AX
         cmp       byte ptr ds:[si+1],":"   ; je ud n disk ?
         jne       aktdir1                  ; nen¡ ud n disk
         mov       al,ds:[si]               ; ozna‡en¡ disku
         call      highcase                 ; p©evod na velk‚ p¡smeno
         sub       al,"A"                   ; p©evod na ‡¡slo disku
         mov       dl,al                    ; po‘adovan˜ disk
         mov       ah,0eh                   ; funkce nastaven¡ disku
         int       21h                      ; nastaven¡ aktivn¡ho disku
         cmp       byte ptr ds:[si+2],0     ; je zad n adres © ?
         je        aktdir2                  ; nen¡ zad n adres © - OK
aktdir1: cmp       byte ptr ds:[si],0       ; je zad n adres © ?
         je        aktdir2                  ; nen¡ zad n adres © - OK
         mov       dx,si                    ; p©¡stupov  cesta k adres ©i
         mov       ah,3bh                   ; funkce nastaven¡ aktiv. adres ©e
         int       21h                      ; nastaven¡ aktivn¡ho adres ©e
aktdir2: pop       ax                       ; n vrat AX
         pop       dx                       ; n vrat DX
         ret


; -----------------------------------------------------------------------------

transfile:                                  ; p©enos jm‚na souboru
                                            ; zni‡en‚ reg.: AX,BX,CX,DX,SI,DI

         push      ds                       ; £schova DS
         mov       ds,ds:[segpsp]           ; adresa segmentu PSP
         mov       di,offset normfil        ; ukl dac¡ adresa
         mov       si,95h                   ; atribut nalezen‚ho souboru
         lodsb                              ; atribut souboru
         stosb                              ; p©enos atributu polo‘ky
         mov       si,9eh                   ; jm‚no nalezen‚ho souboru
         call      kodfile                  ; zak¢dov n¡ jm‚na souboru
         add       di,11                    ; zv˜¨en¡ ukl dac¡ adresy
         mov       si,9ah                   ; velikost souboru
         movsw                              ; ni‘¨¡ slovo velikosti souboru
         movsw                              ; p©enos velikosti souboru
         mov       si,98h                   ; datum souboru
         movsw                              ; p©enos data souboru
         mov       si,96h                   ; ‡as souboru
         movsw                              ; p©enos ‡asu souboru
         pop       ds                       ; n vrat DS
         ret

; -----------------------------------------------------------------------------

kodfile:                                  ;* zak¢dov n¡ jm‚na souboru
                                            ; VSTUP: DS:SI=jm‚no souboru ASCIIZ
                                            ;        ES:DI=jm‚no v pevn‚m k¢du

         push      ax
         push      cx
         push      si
         push      di
         mov       cx,8                     ; po‡et znak– jm‚na souboru
         mov       ax,ds:[si]               ; prvn¡ dva znaky jm‚na
         cmp       ax,".."                  ; je nad©azen˜ adres © ".." ?
         jne       kodfi1                   ; nen¡ nad©azen˜ adres © ".."
         stosw                              ; ulo‘en¡ textu ".."
         mov       al," "                   ; mezera
         mov       cl,9                     ; po‡et mezer k ulo‘en¡
         rep       stosb                    ; doplnˆn¡ mezerami
         jmp       short kodfi6             ; n vrat z podprogramu
kodfi1:  lodsb                              ; na‡ten¡ znaku jm‚na
         or        al,al                    ; je konec jm‚na ?
         jz        kodfi2                   ; je konec jm‚na
         cmp       al,"."                   ; je ozna‡en¡ extentu ?
         jnz       kodfi3                   ; nen¡ ozna‡en¡ extentu
kodfi2:  mov       al," "                   ; n hradn¡ znak mezery
         dec       si                       ; n vrat ‡tec¡ adresy
kodfi3:  stosb                              ; ulo‘en¡ jm‚na souboru
         loop      kodfi1                   ; p©enos dal¨¡ho znaku jm‚na
         mov       cl,3                     ; po‡et znak– extentu
kodfi4:  lodsb                              ; na‡ten¡ znaku jm‚na
         cmp       al,"."                   ; je ozna‡en¡ extentu ?
         jz        kodfi4                   ; ignorov n¡ ozna‡en¡ extentu
         or        al,al                    ; je konec jm‚na ?
         jnz       kodfi5                   ; nen¡ konec jm‚na
         mov       al," "                   ; n hradn¡ znak mezery
         dec       si                       ; n vrat ‡tec¡ adresy
kodfi5:  stosb                              ; ulo‘en¡ jm‚na souboru
         loop      kodfi4                   ; p©enos dal¨¡ho znaku jm‚na
kodfi6:  pop       di
         pop       si
         pop       cx
         pop       ax
         ret


; -----------------------------------------------------------------------------

storfile:                                 ;* ulo‘en¡ souboru do seznamu
                                            ; VSTUP: BP=ukazatel tabulky okna
                                            ;        BX=po‡et ulo‘en˜ch soubor–
                                            ; zni‘en‚ registry: AX,CX,SI,DI

         mov       cx,bx                    ; po‡et ulo‘en˜ch soubor–
         mov       di,ds:[bp+adrfl-tabl]    ; adresa seznamu adres ©e
         mov       si,offset normfil        ; ukl dan˜ soubor
         jcxz      storf2                   ; nen¡ ‘ dn˜ soubor
storf1:  call      cmpfile                  ; porovn n¡ soubor– SI,DI
         jb        storf2                   ; soubor je men¨¡ - ulo‘en¡
         add       di,20                    ; zv˜¨en¡ adresy v bufferu
         loop      storf1                   ; test dal¨¡ho souboru
storf2:  call      insertf                  ; vlo‘en¡ souboru do seznamu
         ret

; -----------------------------------------------------------------------------

cmpfile:                                    ; porovn n¡ soubor– DS:SI a ES:DI
                                            ; VSTUP: ES:DI,DS:SI=soubory
                                            ; zni‘en‚ registry: AX

         push      cx
         push      si
         push      di
         mov       al,[di]                  ; atribut souboru 2
         call      normatr                  ; normalizace atributu 2
         xchg      ah,al                    ; AH <- atribut souboru 2
         mov       al,[si]                  ; atribut souboru 1
         call      normatr                  ; normalizace atributu 1
         cmp       ah,al                    ; porovn n¡ atribut– 2 a 1
         jne       cmpfil2                  ; atributy nejsou shodn‚
         mov       cx,11                    ; po‡et znak– jm‚na a extentu
         inc       si                       ; adresa jm‚na souboru 1
         inc       di                       ; adresa jm‚na souboru 2
         repe      cmpsb                    ; porovn n¡ jmen
cmpfil2: pop       di
         pop       si
         pop       cx
         ret

; -----------------------------------------------------------------------------

normatr:                                    ; normalizace atributu
                                            ; VSTUP: AL=atribut souboru

         push      bx
         mov       bl,al                    ; £schova atributu
         shl       al,1                     ; AL * 2
         and       al,4                     ; ponech  bit 2 (=bit 1)
         or        al,bl                    ; atribut souboru
         and       al,54h                   ; ponech  bity 6, 4 a 2
         test      al,50h                   ; je disk nebo adres © ?
         jz        normatr1                 ; nen¡ disk ani adres ©
         and       al,50h                   ; zru¨en¡ p©¡znaku HID a SYS
normatr1:pop       bx
         ret

; -----------------------------------------------------------------------------

insertf:                                    ; vlo‘en¡ souboru SI
                                            ; VSTUP: ES:DI=ukl dac¡ adresa
                                            ;        DS:SI=soubor
                                            ; zni‡en‚ reg.: CX,SI,DI

         push      si                       ; adresa souboru
         push      di                       ; ukl dac¡ adresa
         mov       si,ds:[endadr]           ; nov˜ konec pamˆti
         sub       si,20                    ; nov˜ konec pamˆti - 20
         mov       cx,si                    ; nov˜ konec pamˆti - 20
         sub       cx,di                    ; po‡et bajt– k p©esunu
         mov       di,ds:[endadr]           ; nov˜ konec pamˆti
         std                                ; smˆr posunu ukazatele dol–
         dec       si                       ; korekce na konec dat
         dec       di                       ; korekce na konec dat
         rep       movsb                    ; p©enos zbytku pamˆti (INSERT)
         cld                                ; smˆr posunu ukazatele nahoru
         pop       di                       ; n vrat ukl dac¡ adresy
         pop       si                       ; n vrat adresy souboru
         mov       cl,20                    ; d‚lka polo‘ky souboru
         rep       movsb                    ; p©enos - ulo‘en¡ polo‘ky souboru
         ret

; -----------------------------------------------------------------------------
;                        Aktualizace parametr– disku
; -----------------------------------------------------------------------------

aktdisk:                                  ;* aktualizace kapacity aktiv. disku
                                            ; VSTUP: BP=adresa tabulky parametr–
                                            ; zni‡en‚ reg.: AX,BX,CX,DX,SI,DI

         lea       di,ds:[bp+alokl-tabl]    ; za‡ tek parametr– disku
         mov       ah,36h                   ; funkce poskytnut¡ kapacity disku
         mov       dl,0                     ; aktivn¡ disk
         int       21h                      ; poskytnut¡ kapacity disku
         push      bx                       ; po‡et voln˜ch aloka‡n¡ch blok–
         push      dx                       ; celkov˜ po‡et aloka‡n¡ch blok–
         mul       cx                       ; v˜po‡et kapacity aloka‡n¡ho bloku
         stosw                              ; ulo‘en¡ kapacity aloka‡n¡ho bloku
         mov       cx,ax                    ; £schova kapacity aloka‡n¡ho bloku
         pop       ax                       ; celkov˜ po‡et aloka‡n¡ch blok–
         mul       cx                       ; v˜po‡et celkov‚ kapacity disku
         stosw                              ; ni‘¨¡ slovo kapacity disku
         mov       ax,dx                    ; vy¨¨¡ slovo kapacity disku
         stosw                              ; vy¨¨¡ slovo kapacity disku
         pop       ax                       ; po‡et voln˜ch aloka‡n¡ch blok–
         mul       cx                       ; v˜po‡et voln‚ kapacity disku
         stosw                              ; ni‘¨¡ slovo voln‚ kapacity disku
         mov       ax,dx                    ; vy¨¨¡ slovo voln‚ kapacity disku
         stosw                              ; vy¨¨¡ slovo voln‚ kapacity disku
         ret

; -----------------------------------------------------------------------------
;                  Aktualizace obsazen‚ kapacity v adres ©i
; -----------------------------------------------------------------------------

aktfile:                                  ;* zji¨tˆn¡ kapacity v adres ©i
                                            ; VSTUP: BP=adresa tabulky parametr–
                                            ; zni‡en‚ reg.: AX,BX,CX,DX,SI,DI

         lea       di,ds:[bp+soubl-tabl]    ; za‡ tek parametr– soubor–
         mov       bx,ds:[bp+alokl-tabl]    ; velikost aloka‡n¡ho bloku
         xor       ax,ax                    ; AX <- 0
         push      di                       ; £schova ukl dac¡ adresy
         stosw                              ; po‡et soubor– = 0
         stosw                              ; kapacita soubor– (LOW) = 0
         stosw                              ; kapacita soubor– (HIGH) = 0
         pop       di                       ; n vrat ukl dac¡ adresy
         mov       cx,ds:[bp+numfl-tabl]    ; celkov˜ po‡et soubor– v adres ©i
         jcxz      aktfil3                  ; nen¡ ‘ dn˜ soubor
         mov       si,ds:[bp+adrfl-tabl]    ; adresa seznamu adres ©e
                                          ;* za‡ tek smy‡ky s‡¡t n¡ kapacity
aktfil1: test      byte ptr ds:[si],50h     ; je adres © nebo disk ?
         jnz       aktfil2                  ; je adres © nebo disk - dal¨¡ soub.
         inc       word ptr ds:[di]         ; zv˜¨en¡ ‡¡ta‡e soubor–
         mov       ax,ds:[si+12]            ; ni‘¨¡ slovo velikosti
         mov       dx,ds:[si+14]            ; vy¨¨¡ slovo velikosti
aktfil4: or        dx,dx                    ; je DX=0 ?
         jnz       aktfil5                  ; DX nen¡ 0
         or        ax,ax                    ; je AX=0 ?
         jz        aktfil2                  ; AX i DX je = 0
aktfil5: add       word ptr es:[di+2],bx    ; p©i‡ten¡ aloka‡n¡ho bloku k sou‡tu
         adc       word ptr es:[di+4],0     ; p©enos do vy¨¨¡ho slova
         sub       ax,bx                    ; ode‡ten¡ od ni‘¨¡ho slova
         sbb       dx,0                     ; ode‡ten¡ p©enosu
         jnc       aktfil4                  ; nen¡ je¨tˆ p©ete‡en¡
aktfil2: add       si,20                    ; zv˜¨en¡ adresy souboru
         loop      aktfil1                  ; dal¨¡ soubor
aktfil3: ret

; -----------------------------------------------------------------------------
;                    Nastaven¡ ukazatel– tabulky soubor–
; -----------------------------------------------------------------------------

settabf:                                  ;* nastaven¡ ukazatel– tabulky soubor–
                                            ; VSTUP: BP=adresa tabulky parametr–
                                            ; zni‡en‚ registry:AX,BX,CX,DX,SI,DI

         mov       di,offset outbuf         ; tiskov˜ buffer
         mov       si,offset namepath       ; jm‚no adres ©e
         push      di                       ; tiskov˜ buffer
         call      kodfile                  ; zak¢dov n¡ jm‚na souboru
         pop       si                       ; tiskov˜ buffer
         mov       cx,ds:[bp+numfl-tabl]    ; po‡et soubor–
         mov       di,ds:[bp+adrfl-tabl]    ; adresa seznamu soubor–
         xor       bx,bx                    ; ukazatel jm‚na souboru
         xor       dx,dx                    ; ukazatel za‡ tku zobrazen¡
         jcxz      settabf4                 ; nen¡ ‘ dn˜ soubor
settabf1:test      byte ptr ds:[di],40h     ; je disk ?
         jz        settabf2                 ; nen¡ disk
         inc       dx                       ; zv˜¨en¡ ukazatele za‡ tku zobraz.
         inc       bx                       ; zv˜¨en¡ ukazatele kurzoru
         add       di,20                    ; zv˜¨en¡ adresy v seznamu
         loop      settabf1                 ; test dal¨¡ho souboru
settabf2:mov       ds:[bp+numdskl-tabl],dl  ; ulo‘en¡ po‡tu disk– v seznamu
         inc       di                       ; prvn¡ soubor + 1
         jcxz      settabf4                 ; nen¡ ‘ dn˜ soubor
settabf3:push      cx                       ; £schova ‡¡ta‡e polo‘ke
         push      si                       ; hledan‚ jm‚no
         push      di                       ; ukazatel v seznamu
         mov       cx,8                     ; d‚lka kontrolovan‚ho jm‚na
         repe      cmpsb                    ; porovn n¡ jmen
         pop       di                       ; n vrat ukazatele seznamu
         pop       si                       ; n vrat hledan‚ho jm‚na
         pop       cx                       ; n vrat ‡¡ta‡e polo‘ek
         je        settabf4                 ; jm‚no bylo nalezeno
         add       di,20                    ; zv˜¨en¡ adresy v seznamu
         inc       bx                       ; zv˜¨en¡ ukazatele polo‘ek
         loop      settabf3                 ; test dal¨¡ho souboru
         mov       bx,dx                    ; nastaven¡ kurzoru na za‡ tek
settabf4:mov       word ptr ds:[bp+kurzl-tabl],bx ; nastaven¡ pozice kurzoru
         sub       bx,17                    ; pozice pro prvn¡ zobrazen˜ soub.
         cmp       bx,dx                    ; kontrola po‡ tku zobrazen¡
         jnl       settabf5
         mov       bx,dx
settabf5:mov       word ptr ds:[bp+firstl-tabl],bx
         ret

; -----------------------------------------------------------------------------
;                            Zobrazen¡ okna
; -----------------------------------------------------------------------------

window:                                     ; zobrazen¡ okna
                                            ; VSTUP: BP=ukazatel parametr– okna

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         test      byte ptr ds:[bp+flagsl-tabl],2 ; p©¡znak okna
         jnz       window1                  ; zapnut¡ okna
         call      windnul                  ; vypnut¡ okna
         jmp       short window2
window1: call      topline                  ; zobrazen¡ 1. © dku
         call      space                    ; zobrazen¡ informac¡ o disku
         call      directory                ; zobrazen¡ informac¡ o adres ©i
         call      obrpath                  ; zobrazen¡ cesty
         call      files                    ; zobrazen¡ soubor– z adres ©e
         call      endline                  ; zobrazen¡ koncov‚ linky
window2: pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------
;                      vypnut¡ okna (zobrazen¡ podkladu)
; -----------------------------------------------------------------------------

windnul:                                  ;* vypnut¡ okna (zobrazen¡ podkladu)

         push      cx
         push      dx
         push      si
         push      di
         push      bp
                                          ;* p©enos okna
         cmp       ds:[typdisp],1           ; je displej CGA ?
         je        windnul1                 ; je displej CGA
         cmp       ds:[typdisp],3           ; je displej EGA ?
         jne       windnul3                 ; nen¡ displej EGA
windnul1:mov       dl,ds:[bp+pozl-tabl]     ; pozice na © dku
         xor       dh,dh
         mov       si,dx
         add       si,si                    ; po‡ te‡n¡ adresa - str nka 0
         mov       cx,24                    ; po‡et © dk–
         test      byte ptr ds:[flags],8    ; je n povˆda ?
         jz        windnul4
         dec       cx
windnul4:push      ds
         push      es
         mov       es,ds:[segvram]
         mov       ds,ds:[segvram]
         cld
windnul2:push      cx
         push      si
         mov       di,si
         add       di,1000h                 ; adresa str nky 1
         mov       cx,40
         rep       movsw
         pop       si
         pop       cx
         add       si,160
         loop      windnul2
         pop       es
         pop       ds
         jmp       short windnul7

windnul3:
         mov       dl,ds:[bp+pozl-tabl]     ; pozice na © dku
         xor       dh,dh
         mov       si,dx
         add       si,si                    ; po‡ te‡n¡ adresa - str nka 0
         mov       cx,24                    ; po‡et © dk–
         test      byte ptr ds:[flags],8    ; je n povˆda ?
         jz        windnul5
         dec       cx
windnul5:push      cx
         push      dx
         mov       cx,40
windnul6:push      cx
         push      dx
         xor       bh,bh
         mov       ah,2
         int       10h
         pop       dx
         push      dx
         mov       bh,1
         mov       ah,2
         int       10h
         mov       ah,8
         xor       bh,bh
         int       10h
         mov       bl,ah
         mov       ah,9
         mov       bh,1
         mov       cx,1
         int       10h
         pop       dx
         pop       cx
         inc       dl
         loop      windnul6
         pop       dx
         pop       cx
         inc       dh
         loop      windnul5
windnul7:
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         ret

; -----------------------------------------------------------------------------
;                   Zobrazen¡ horn¡ho © dku okna (1. © dek)
; -----------------------------------------------------------------------------

topline:                                  ;* zobrazen¡ 1. © dku okna
                                            ; VSTUP: BP=tabulka okna

         push      ax
         push      cx
         push      dx
         push      si
                                          ;* zobrazen¡ podkladov‚ linky
         mov       dl,ds:[bp+pozl-tabl]     ; po‡ te‡n¡ pozice okna
         xor       dh,dh                    ; © dek pro kreslen¡ linky
         mov       al,1                     ; atribut 1
         call      outch1                   ; nastaven¡ atributu 1
         mov       al,201                   ; lev˜ horn¡ roh
         call      outch1                   ; zobrazen¡ znaku rohu
         mov       al,205                   ; rovn  dvojit  ‡ ra
         mov       cx,38                    ; d‚lka linky
         call      outch                    ; zobrazen¡ horn¡ linky
         mov       al,187                   ; prav˜ horn¡ roh
         call      outch1                   ; zobrazen¡ prav‚ho horn¡ho rohu
                                          ;* zobrazen¡ textu v roz¡ch
         mov       dl,1                     ; pozice textu "<F1>=?"
         mov       si,offset texthlp        ; text "<F1>=?"
         cmp       byte ptr ds:[bp+pozl-tabl],40 ; je lev‚ okno ?
         jb        toplin1                 ; je lev‚ okno
         mov       dl,72                    ; je prav‚ okno
         mov       si,offset textclk        ; text hodin
toplin1: call      outtxt                   ; zobrazen¡ textu
         pop       si
         pop       dx
         pop       cx
         pop       ax
         ret

; -----------------------------------------------------------------------------
;                  Zobrazen¡ informac¡ o disku (2. © dek)
; -----------------------------------------------------------------------------

space:                                    ;* zobrazen¡ informac¡ o disku
                                            ; VSTUP: BP=tabulka okna

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
                                          ;* vykreslen¡ podkladu © dku
         mov       dl,ds:[bp+pozl-tabl]     ; po‡ te‡n¡ pozice okna
         mov       dh,1                     ; © dek k tisku
         mov       al,1                     ; nastaven¡ z kladn¡ barvy textu
         call      outch1                   ; nastaven¡ z kladn¡ barvy textu
         push      dx                       ; £schova pozice k tisku
         mov       al,186                   ; dvojit  svisl  ‡ ra
         call      outch1                   ; zobrazen¡ lev‚ ‡ ry
         mov       al,32                    ; znak mezery
         mov       cx,38                    ; po‡et znak– k tisku mezery
         call      outch                    ; vymaz n¡ © dku
         mov       al,186                   ; dvojit  svisl  ‡ ra
         call      outch1                   ; zobrazen¡ lev‚ ‡ ry
                                          ;* vyti¨tˆn¡ informac¡ o disku

         mov       di,offset outbuf         ; tiskov˜ buffer
         mov       byte ptr ds:[di],2       ; p©¡znak nastaven¡ zv˜raznˆn‚ barvy
         inc       di                       ; zv˜¨en¡ ukl dac¡ pozice v bufferu
         push      di                       ; adresa ‡¡sla voln‚ kapacity
         mov       ax,ds:[bp+volnl-tabl]    ; voln  kapacita disku - LOW
         mov       dx,ds:[bp+volnl-tabl+2]  ; voln  kapacita disku - HIGH
         call      deknumx                  ; dek¢dov n¡ ‡¡sla DX:AX
         mov       si,offset textsp1        ; text "bajt"
         call      transtxt                 ; ulo‘en¡ textu "bajt"
         pop       si                       ; adresa ‡¡sla voln‚ kapacity
         push      si                       ; £schova adresy ‡¡sla
         call      setkonc                  ; nastaven¡ koncovky "y", "–"
         mov       si,offset textsp2        ; text "voln"
         call      transtxt                 ; ulo‘en¡ textu "voln"
         pop       si                       ; adresa ‡¡sla
         call      testkonc                 ; test koncovky ‡¡sla
         mov       si,offset textsp20       ; koncovka "˜"
         or        al,al
         jz        space2                   ; je ‡¡slo "1"
         mov       si,offset textsp21       ; koncovka "‚"
         dec       al                       ; je ‡¡slo 2,3,4 ?
         jz        space2                   ; je koncovka "‚"
         mov       si,offset textsp22       ; koncovka "˜ch"
space2:  call      transtxt                 ; ulo‘en¡ koncovky
         mov       si,offset textsp23       ; text "z"
         call      transtxt                 ; ulo‘en¡ textu "z"
         mov       ax,ds:[bp+celkl-tabl]    ; celkov  kapacita disku - LOW
         mov       dx,ds:[bp+celkl-tabl+2]  ; celkov  kapacita disku - HIGH
         call      deknumx                  ; dek¢dov n¡ ‡¡sla DX:AX

         pop       dx                       ; n vrat pozice kurzoru
         mov       si,offset outbuf         ; tiskov˜ buffer
         inc       dl                       ; za‡ tek textu na druh‚ pozici
         mov       cl,38                    ; ¨¡©ka voln‚ho okna
         call      center                   ; centrov n¡ textu
         call      outtxt                   ; tisk textu DS:SI
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------
;                 Zobrazen¡ informac¡ o adres ©i (3. © dek)
; -----------------------------------------------------------------------------

directory:                                ;* zobrazen¡ informac¡ o adres ©i
                                            ; VSTUP: BP=tabulka okna

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
                                          ;* vykreslen¡ podkladu © dku
         mov       dl,ds:[bp+pozl-tabl]     ; po‡ te‡n¡ pozice okna
         mov       dh,2                     ; © dek k tisku
         mov       al,1                     ; nastaven¡ z kladn¡ barvy textu
         call      outch1                   ; nastaven¡ z kladn¡ barvy textu
         push      dx                       ; £schova pozice k tisku
         mov       al,186                   ; dvojit  svisl  ‡ ra
         call      outch1
         mov       al,32                    ; znak mezery
         mov       cx,38                    ; po‡et znak– k tisku mezery
         call      outch                    ; vymaz n¡ © dku
         mov       al,186                   ; dvojit  svisl  ‡ ra
         call      outch1                   ; zobrazen¡ lev‚ ‡ ry
                                          ;* vyti¨tˆn¡ informac¡ o adres ©i
         mov       di,offset outbuf         ; tiskov˜ buffer
         mov       byte ptr ds:[di],2       ; p©¡znak nastaven¡ zv˜raznˆn‚ barvy
         inc       di                       ; zv˜¨en¡ ukl dac¡ pozice v bufferu
         mov       ax,ds:[bp+soubl-tabl]    ; ni‘¨¡ slovo po‡tu soubor–
         mov       dx,0                     ; vy¨¨¡ slovo po‡tu soubor–
         push      di                       ; ukazatel textu ‡¡sla
         or        byte ptr ds:[parnum],1   ; z kaz tisku te‡ek
         call      deknum                   ; dek¢dov n¡ po‡tu soubor–
         and       byte ptr ds:[parnum],0feh ; povolen¡ tisku te‡ek
         mov       si,offset textsp3        ; text "soubor"
         call      transtxt                 ; ulo‘en¡ textu "soubor"
         pop       si                       ; ukazatel textu ‡¡sla
         call      setkonc                  ; nastaven¡ koncovky textu "y", "–"
         mov       si,offset textsp30       ; test "zab¡r "
         call      transtxt                 ; ulo‘en¡ textu "zab¡r "
         mov       ax,ds:[bp+zabl-tabl]     ; ni‘¨¡ slovo zabran‚ kapacity
         mov       dx,ds:[bp+zabl-tabl+2]   ; vy¨¨¡ slovo zabran‚ kapacity
         push      di                       ; ukazatel ‡¡sla
         call      deknum                   ; dek¢dov n¡ zabran‚ kapacity
         mov       si,offset textsp4        ; text "bajt"
         call      transtxt                 ; ulo‘en¡ textu "bajt"
         pop       si                       ; ukazatel ‡¡sla
         call      setkonc                  ; nastaven¡ koncovky "y", "–"
         pop       dx                       ; n vrat pozice kurzoru
         mov       si,offset outbuf         ; tiskov˜ buffer
         inc       dl                       ; druh  pozice kurzoru
         mov       cl,38                    ; d‚lka prostoru pro tisk
         call      center                   ; centrov n¡ textu
         call      outtxt                   ; vyti¨tˆn¡ textu
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------
;                         Zobrazen¡ cesty adres ©e
; -----------------------------------------------------------------------------

obrpath:                                  ;* zobrazen¡ cesty adres ©e
                                            ; VSTUP: BP=tabulka okna

         push      ax
         push      cx
         push      dx
         push      si
         push      di
                                          ;* vykreslen¡ podkladov‚ ‡ ry
         mov       dl,ds:[bp+pozl-tabl]     ; po‡ te‡n¡ pozice okna
         mov       dh,3                     ; © dek k tisku
         mov       al,1                     ; nastaven¡ z kladn¡ barvy textu
         call      outch1                   ; nastaven¡ z kladn¡ barvy textu
         push      dx                       ; £schova po‡ te‡n¡ pozice kurzoru
         mov       al,204                   ; lev  ‡ ra
         call      outch1                   ; zobrazen¡ lev‚ ‡ ry
         mov       al,205                   ; vodorovn  dvojit  ‡ ra
         mov       cx,38                    ; d‚lka linky
         call      outch                    ; zobrazen¡ linky
         mov       al,185                   ; prav  ‡ ra
         call      outch1                   ; zobrazen¡ prav‚ ‡ ry
         pop       dx                       ; n vrat pozice kurzoru

         mov       al,ds:[bp+flagsl-tabl]   ; p©¡znaky adres ©e
         shr       al,1
         shr       al,1                     ; bit 0 = p©¡znak okna
         xor       al,ds:[flags]            ; porovn n¡ s aktivn¡m oknem
         and       al,1                     ; v˜sledn˜ bit
         mov       al,1                     ; nastaven¡ norm ln¡ barvy
         jnz       obrpth2                  ; nen¡ to aktivn¡ okno
         mov       al,3                     ; barva kurzoru proaktivn¡ okno
obrpth2: call      outch1                   ; nastaven¡ barvy kurzoru
         lea       si,ds:[bp+pathl-tabl]    ; cesta adres ©e
         inc       dl                       ; za‡ tek k tisku textu
         mov       cl,36                    ; max. ¨¡©ka © dku
         call      center                   ; centrov n¡ textu
         mov       al," "                   ; znak oddˆlovac¡ mezery
         call      outch1                   ; zobrazen¡ mezery
         lodsb                              ; znak jm‚na disku
         call      outch1                   ; zobrazen¡ jm‚na disku
         lodsb                              ; znak ":"
         call      outch1                   ; zobrazen¡ znaku ":"
         lodsb                              ; znak "\"
         call      outch1                   ; zobrazen¡ znaku "\"

         mov       di,si                    ; za‡ tek cesty (za ozna‡en¡m disku)
         mov       cx,255                   ; ‡¡ta‡ d‚lky ©etˆzce
         xor       al,al                    ; hledan˜ bajt 0
         repnz     scasb                    ; nalezen¡ konce textu
         not       cl                       ; d‚lka ©etˆzce
         cmp       cl,36                    ; je text del¨¡ ne‘ 36 znak– ?
         jna       outpth3                  ; text nen¡ del¨¡ ne‘ 36 znak–
         sub       cl,31                    ; p©ebytek d‚lky ©etˆzce
         add       si,cx                    ; za‡ tek ©etˆzce
         mov       al,"."                   ; oddˆlovac¡ te‡ka
         mov       cx,3                     ; po‡et znak– "."
         call      outch                    ; zobrazen¡ textu "..."
outpth3: call      outtxt                   ; zobrazen¡ (zbytku) cesty adres ©e
         mov       al," "                   ; znak oddˆlovac¡ mezery
         call      outch1                   ; zobrazen¡ mezery
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       ax
         ret

; -----------------------------------------------------------------------------
;                        Zobrazen¡ soubor– v oknˆ
; -----------------------------------------------------------------------------

files:                                      ; zobrazen¡ soubor– z adres ©e
                                            ; VSTUP: BP=tabulka okna

         push      bx
         push      cx
         push      dx
         mov       dl,ds:[bp+pozl-tabl]     ; po‡ te‡n¡ pozice okna
         mov       dh,4                     ; po‡ te‡n¡ © dek k tisku
         mov       bx,ds:[bp+firstl-tabl]   ; ‡¡slo prvn¡ho souboru
         mov       cx,19                    ; po‡et © dk– displeje
         test      byte ptr ds:[flags],8    ; je n povˆda ?
         jz        files1
         dec       cx                       ; je aktivn¡
files1:  call      zobrfile                 ; zobrazen¡ souboru
         inc       bx                       ; zv˜¨en¡ ‡¡sla souboru
         inc       dh                       ; zv˜¨en¡ © dku
         loop      files1                   ; zobrazen¡ dal¨¡ho souboru
         pop       dx
         pop       cx
         pop       bx
         ret


; -----------------------------------------------------------------------------

zobrfile:                                 ;* zobrazen¡ polo‘ky adres ©e
                                            ; VSTUP: DX=pozice na displeji
                                            ;        BX=‡¡slo polo‘ky
                                            ;        BP=adresa parametr– okna

         push      ax
         push      bx                       ; ‡¡slo polo‘ky
         push      cx
         push      dx                       ; pozice na displeji
         push      si
         push      di
         call      zobrfilz                 ; zobrazen¡ zna‡ky kurzoru
         call      adrfile                  ; zji¨tˆn¡ adresy souboru v seznamu
         jnc       zobrf01                  ; je platn˜ soubor
                                          ;* nen¡ platn˜ soubor
         call      zobrfil0                 ; zobrazen¡ pr zdn‚ © dky
         jmp       zobrf02                  ; zobrazen¡ konce souboru
                                          ;* zobrazen¡ platn‚ho souboru
zobrf01: call      zobrfila                 ; nastaven¡ barvy © dku souboru
         call      zobrfiln                 ; zobrazen¡ jm‚na souboru

         mov       al,179                   ; svisl  ‡ ra
         test      byte ptr ds:[si],80h     ; je soubor vybran˜
         jz        zobrf04                  ; soubor nen¡ vybran˜
         mov       al,0b2h                  ; ozna‡en¡ vybran‚ho souboru
zobrf04: call      outch1                   ; zobrazen¡ oddˆlovac¡ ‡ ry

         call      zobrfilv                 ; zobrazen¡ velikosti souboru

         mov       al,179                   ; svisl  ‡ ra
         call      outch1                   ; zobrazen¡ oddˆlovac¡ ‡ ry

         call      zobrfild                 ; zobrazen¡ data souboru

         mov       al,179                   ; svisl  ‡ ra
         call      outch1                   ; zobrazen¡ oddˆlovac¡ ‡ ry

         call      zobrfilc                 ; zobrazen¡ ‡asu souboru
zobrf02: call      zobrfilz                 ; zobrazen¡ zna‡ky kurzoru
zobrf03: pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------

zobrfilz:                                 ;* zobrazen¡ zna‡ky kurzoru
                                            ; VSTUP: DX=pozice na displeji
                                            ;        BP=adresa parametr– okna
                                            ;        BX=‡¡slo souboru
                                            ; VSTUP: DX=nov  pozice na displeji

         push      ax
         mov       al,1                     ; p©¡znak norm ln¡ barvy
         call      outch1                   ; nastaven¡ norm ln¡ barvy
         cmp       bx,ds:[bp+kurzl-tabl]    ; je soubor s kurzorem ?
         mov       al,186                   ; norm ln¡ dvojit  ‡ ra
         jnz       zobrfz1                  ; nen¡ soubor s kurzorem
         mov       al,0b0h                  ; zna‡ka kurzoru
zobrfz1: call      outch1                   ; zobrazen¡ ozna‡en¡ kurzoru
         pop       ax
         ret

; -----------------------------------------------------------------------------

zobrfil0:                                 ;* zobrazen¡ pr zdn‚ © dky souboru
                                            ; VSTUP: DX=pozice na displeji
                                            ;        BP=adresa parametr– okna
                                            ; VSTUP: DX=nov  pozice na displeji

         push      ax
         push      cx
         mov       ax,179*256+32            ; jednoduch  svisl  ‡ ra a mezera
         mov       cx,12                    ; mezera pro jm‚no souboru
         call      outch                    ; zobrazen¡ mezery pro jm‚no souboru
         xchg      ah,al                    ; AL <- znak ‡ ry
         call      outch1                   ; zobrazen¡ oddˆlovac¡ ‡ ry
         xchg      ah,al                    ; AL <- mezera
         mov       cl,10                    ; mezera pro velikost souboru
         call      outch                    ; zobrazen¡ mezery pro velik. soub.
         xchg      ah,al                    ; AL <- znak ‡ ry
         call      outch1                   ; zobrazen¡ oddˆlovac¡ ‡ ry
         xchg      ah,al                    ; AL <- mezera
         mov       cl,8                     ; mezera pro datum
         call      outch                    ; zobrazen¡ mezery pro datum
         xchg      ah,al                    ; AL <- znak ‡ ry
         call      outch1                   ; zobrazen¡ oddˆlovac¡ ‡ ry
         xchg      ah,al                    ; AL <- mezera
         mov       cl,5                     ; mezera pro ‡as
         call      outch                    ; zobrazen¡ mezery pro ‡as
         pop       cx
         pop       ax
         ret

; -----------------------------------------------------------------------------

zobrfila:                                 ;* nastaven¡ barvy © dku souboru
                                            ; VSTUP: DX=pozice na displeji
                                            ;        BX=‡¡slo souboru
                                            ;        BP=adresa parametr– okna
                                            ;        DS:SI=polo‘ka souboru
                                            ; VSTUP: DX=nov  pozice na displeji

         push      ax
         push      si
         mov       ah,ds:[si]               ; atribut souboru
         mov       al,1                     ; norm ln¡ barva
         test      ah,80h                   ; je vybran˜ soubor ?
         jz        zobrfa1                  ; nen¡ vybran˜ soubor
         inc       al                       ; barva vybran‚ho souboru
zobrfa1: cmp       bx,ds:[bp+kurzl-tabl]    ; je soubor s kurzorem ?
         jnz       zobrfa2                  ; nen¡ soubor s kurzorem
         mov       ah,ds:[bp+flagsl-tabl]   ; p©¡znaky adres ©e
         shr       ah,1
         shr       ah,1                     ; bit 0 = p©¡znak okna
         xor       ah,ds:[flags]            ; porovn n¡ s aktivn¡m oknem
         and       ah,1                     ; v˜sledn˜ bit
         jnz       zobrfa2                  ; nen¡ to aktivn¡ okno
         add       al,2                     ; barva kurzoru
zobrfa2: call      outch1                   ; nastaven¡ barvy kurzoru
         pop       si
         pop       ax
         ret

; -----------------------------------------------------------------------------

zobrfiln:                                 ;* zobrazen¡ jm‚na souboru v adres.
                                            ; VSTUP: DX=pozice na displeji
                                            ;        DS:SI=polo‘ka souboru
                                            ;        BP=adresa parametr– okna
                                            ; VSTUP: DX=nov  pozice na displeji

         push      ax
         push      bx                       ; £schova ‡¡sla souboru
         push      cx
         push      si
         lodsb                              ; atributy souboru
         mov       bl,al                    ; atributy (£schova)
         mov       bh,al                    ; atributy (£schova)
                                          ;* zobrazen¡ jm‚na souboru
         mov       cx,8                     ; d‚lka jm‚na
zobrfn1: lodsb                              ; na‡ten¡ znaku jm‚na
         test      bl,50h                   ; je adres © nebo disk ?
         jnz       zobrfn3                  ; je adres ©
         test      bl,6                     ; je skryt˜ nebo syst‚mov˜ ?
         jnz       zobrfn2                  ; je skryt˜ nebo syst‚mov˜
         call      lowcase                  ; p©evod na mal‚ p¡smeno
zobrfn2: and       bl,0f9h                  ; nulov n¡ atrib. skryt. a syst. s.
zobrfn3: call      outch1                   ; zobrazen¡ znaku jm‚na
         loop      zobrfn1                  ; zobrazen¡ dal¨¡ho znaku jm‚na
                                          ;* zobrazen¡ oddˆlovac¡ho znaku
         mov       al," "                   ; oddˆlovac¡ mezera
         test      bh,1                     ; je z kaz z pisu ?
         jz        zobrfn4                  ; nen¡ z kaz z pisu
         mov       al,177                   ; ozna‡en¡ souboru R/O
zobrfn4: test      bh,6                     ; je skryt˜ nebo syst‚mov˜ soubor ?
         jz        zobrfn5                  ; nen¡ skryt˜ ani syst‚mov˜ soubor
         mov       al,176                   ; ozna‡en¡ souboru SYS nebo HID
zobrfn5: test      bh,80h                   ; je vybran˜ soubor ?
         jz        zobrfn6                  ; nen¡ vybran˜ soubor
         test      byte ptr ds:[flags],40h  ; je ozna‡en¡ vybran‚ho souboru ?
         jz        zobrfn6                  ; nen¡ ozna‡en¡ vybran‚ho souboru
         mov       al,0bah                  ; ozna‡en¡ vybran‚ho souboru
         test      bh,7                     ; je atribut SYS, HID nebo R/O ?
         jz        zobrfn6                  ; nen¡ atribut SYS, HID nebo R/O
         mov       al,0dbh                  ; ozna‡en¡ souboru SYS, HID, R/O
zobrfn6: call      outch1                   ; zobrazen¡ oddˆlovac¡ho znaku
                                          ;* zobrazen¡ p©¡pony souboru
         mov       cx,3                     ; d‚lka p©¡pony soubour
zobrfn7: lodsb                              ; na‡ten¡ znaku p©¡pony souboru
         test      bh,50h                   ; je adres © nebo disk ?
         jnz       zobrfn8                  ; je adres © - jsou velk  p¡smena
         call      lowcase                  ; p©evod na mal‚ p¡smeno
zobrfn8: call      outch1                   ; zobrazen¡ znaku p©¡pony
         loop      zobrfn7                  ; zobrazen¡ dal¨¡ho znaku jm‚na
         pop       si
         pop       cx
         pop       bx                       ; n vrat ‡¡sla souboru
         pop       ax
         ret

; -----------------------------------------------------------------------------

zobrfilv:                                 ;* zobrazen¡ velikosti souboru
                                            ; VSTUP: DX=pozice na displeji
                                            ;        DS:SI=polo‘ka souboru
                                            ;        BP=adresa parametr– okna
                                            ; VSTUP: DX=nov  pozice na displeji

         push      ax
         push      bx                       ; £schova ‡¡sla souboru
         push      cx
         push      si
         add       si,12                    ; ukazatel na velikost souboru
         test      byte ptr ds:[si-12],50h  ; je podadres © nebo disk ?
         jz        zobrfv2                  ; nen¡ podadres © ani disk
                                          ;* zobrazen¡ textu disku nebo adres ©e
         push      si                       ; £schova ukazatele souboru
         test      byte ptr ds:[si-12],40h  ; je disk ?
         mov       si,offset txtdsk         ; text ozna‡en¡ disku "<--DISK-->"
         jnz       zobrfv1                  ; je disk
         pop       si                       ; ukazatel souboru
         push      si                       ; ukazatel souboru
         cmp       word ptr ds:[si-11],".." ; je ozna‡en¡ nad©azen‚ho podadres.?
         mov       si,offset txtsubd        ; text podadres ©e "<POD--ADR>"
         jne       zobrfv1                  ; nen¡ adres © ".."
         mov       si,offset txtupd         ; text podadres ©e "<NAD--ADR>"
zobrfv1: call      outtxt                   ; zobrazen¡ ozna‡en¡ adres ©e, disku
         pop       si                       ; n vrat ukazatele dat souboru
         add       si,4                     ; adresa data
         jmp       short zobrfv3
                                          ;* zobrazen¡ velikosti souboru
zobrfv2: lodsw                              ; ni‘¨¡ slovo velikosti souboru
         mov       bx,ax                    ; BX <- ni‘¨¡ slovo velikosti
         lodsw                              ; vy¨¨¡ slovo velikosti souboru
         xchg      ax,bx                    ; AX <- ni‘¨¡, BX <- vy¨¨¡ slovo
         call      tisknm0                  ; tisk ‡¡sla velikosti (10 ‡¡slic)
zobrfv3: pop       si
         pop       cx
         pop       bx                       ; n vrat ‡¡sla souboru
         pop       ax
         ret

; -----------------------------------------------------------------------------

zobrfild:                                 ;* zobrazen¡ data souboru
                                            ; VSTUP: DX=pozice na displeji
                                            ;        DS:SI=polo‘ka souboru
                                            ;        BP=adresa parametr– okna
                                            ; VSTUP: DX=nov  pozice na displeji

         push      ax
         push      cx
         push      si
         add       si,16
         test      byte ptr ds:[si-16],40h  ; je disk ?
         jz        zobrfd0                  ; nen¡ disk
                                          ;* n hradn¡ mezera
         mov       al," "                   ; mazac¡ mezera
         mov       cx,8                     ; d‚lka mezery
         call      outch                    ; zobrazen¡ mezery
         jmp       short zobrfd3
                                          ;* zobrazen¡ data souboru
zobrfd0: lodsw                              ; datum souboru
         push      ax                       ; £schova data souboru
         and       al,1fh                   ; maska pro den
         call      tisknm2s                 ; zobrazen¡ dne
         mov       al,"."                   ; oddˆlovac¡ te‡ka
         call      outch1                   ; zobrazen¡ oddˆlovac¡ te‡ky
         pop       ax                       ; n vrat data souboru
         push      ax                       ; £schova data souboru
         mov       cl,5                     ; po‡et rotac¡
         shr       ax,cl                    ; AX >> mˆs¡c
         and       al,0fh                   ; maska pro mˆs¡c
         call      tisknm20                 ; zobrazen¡ mˆs¡ce
         mov       al,"."                   ; oddˆlovac¡ te‡ka
         call      outch1                   ; zobrazen¡ oddˆlovac¡ te‡ky
         pop       ax                       ; n vrat data souboru
         mov       cl,9                     ; po‡et rotac¡
         shr       ax,cl                    ; AX >> rok
         and       ax,7fh                   ; maska pro rok
         add       ax,80                    ; korekce - p©i‡ten¡ roku 1980
zobrfd1: cmp       ax,100                   ; je ‡¡slo men¨¡ ne‘ 99 ?
         jb        zobrfd2                  ; je ‡¡slo men¨¡ ne‘ 99
         sub       ax,100                   ; korekce - ode‡ten¡ 100
         jmp       short zobrfd1            ; dal¨¡ kontrola ‡¡sla
zobrfd2: call      tisknm20                 ; zobrazen¡ roku
zobrfd3: pop       si
         pop       cx
         pop       ax
         ret

; -----------------------------------------------------------------------------

zobrfilc:                                 ;* zobrazen¡ ‡asu souboru
                                            ; VSTUP: DX=pozice na displeji
                                            ;        DS:SI=polo‘ka souboru
                                            ;        BP=adresa parametr– okna
                                            ; VSTUP: DX=nov  pozice na displeji

         push      ax
         push      cx
         push      si
         add       si,18
         test      byte ptr ds:[si-18],40h  ; je disk ?
         jz        zobrfc0                  ; nen¡ disk
                                          ;* n hradn¡ mezera
         mov       al," "                   ; mazac¡ mezera
         mov       cx,5                     ; d‚lka mezery
         call      outch                    ; zobrazen¡ mezery
         jmp       short zobrfc1
                                          ;* zobrazen¡ data souboru
zobrfc0: lodsw                              ; ‡as souboru
         push      ax                       ; £schova ‡asu souboru
         mov       cl,11                    ; po‡et rotac¡
         shr       ax,cl                    ; AX >> hodina
         and       al,1fh                   ; maska pro hodinu
         call      tisknm2s                 ; zobrazen¡ hodiny
         mov       al,"."                   ; oddˆlovac¡ te‡ka
         call      outch1                   ; zobrazen¡ oddˆlovac¡ te‡ky
         pop       ax                       ; n vrat ‡asu souboru
         mov       cl,5                     ; po‡et rotac¡
         shr       ax,cl                    ; AX >> minuta
         and       al,3fh                   ; maska pro minutu
         call      tisknm20                 ; zobrazen¡ minuty
zobrfc1: pop       si
         pop       cx
         pop       ax
         ret

; -----------------------------------------------------------------------------
;                  Zobrazen¡ posledn¡ho © dku okna
; -----------------------------------------------------------------------------

endline:                                  ;* zobrazen¡ koncov‚ linky
                                            ; VSTUP: BP=adresa parametr– okna

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         mov       dl,ds:[bp+pozl-tabl]     ; po‡ te‡n¡ pozice okna
         mov       dh,23                    ; © dek k tisku
         test      byte ptr ds:[flags],8    ; je n povˆda ?
         jz        endlin5                  ; nen¡ aktivn¡
         dec       dh
endlin5:
                                          ;* zobrazen¡ podkladov‚ linky
         mov       al,200                   ; lev˜ doln¡ roh
         call      outch1                   ; zobrazen¡ lev‚ho doln¡ho rohu
         push      dx                       ; £schova po‡ te‡n¡ pozice
         mov       al,205                   ; rovn  dvojit  ‡ ra
         mov       cx,38                    ; d‚lka linky
         call      outch                    ; zobrazen¡ horn¡ linky
         mov       al,188                   ; prav˜ doln¡ roh
         call      outch1                   ; zobrazen¡ prav‚ho doln¡ho rohu

         mov       ax,ds:[bp+insfil-tabl]   ; po‡et ozna‡en˜ch soubor–
         or        ax,ax                    ; je ozna‡en nˆjak˜ soubor ?
         jnz       endlin0                  ; je soubor
         jmp       endlin1                  ; nen¡ ozna‡en ‘ dn˜ soubor
                                          ;* dek¢dov n¡ obsazen‚ kapacity
endlin0: push      ax                       ; £schova po‡tu soubor–
         mov       di,offset outbuf         ; tiskov˜ buffer
         mov       byte ptr ds:[di],2       ; nastaven¡ zv˜raznˆn‚ho textu
         inc       di                       ; zv˜¨en¡ ukl dac¡ adresy
         mov       byte ptr ds:[di],32      ; ulo‘en¡ £vodn¡ mezery
         inc       di                       ; zv˜¨en¡ ukl dac¡ adresy
         push      di
         mov       ax,ds:[bp+kapfil-tabl]   ; zabran  kapacita - LOW
         mov       dx,ds:[bp+kapfil-tabl+2] ; zabran  kapacita - HIGH
         call      deknum                   ; dek¢dov n¡ ‡¡sla
         mov       si,offset textvy1        ; text "bajt"
         call      transtxt                 ; ulo‘en¡ textu "bajt"
         pop       si
         call      setkonc                  ; nastaven¡ koncovky "y", "–"
         pop       ax                       ; po‡et soubor–

         push      ax                       ; po‡et soubor–
         mov       si,offset textvy13       ; ukon‡en¡ textu ' v '
         cmp       ax,2
         jb        endlin4
         cmp       ax,4                     ; je vˆt¨¡ ne‘ 4 ?
         jbe       endlin3                  ; je men¨¡ ne‘ 5
         cmp       ax,12
         jb        endlin4
         cmp       ax,14
         jbe       endlin3
         cmp       ax,20
         jb        endlin4
         cmp       ax,49
         jbe       endlin3
         cmp       ax,100
         jb        endlin4
         cmp       ax,499
         ja        endlin4
endlin3: mov       si,offset textvy14       ; ukon‡en¡ textu ' ve '
endlin4: call      transtxt                 ; ulo‘en¡ textu ' v ', 've'
         pop       ax                       ; n vrat po‡tu soubor–
                                          ;* dek¢dov n¡ po‡tu soubor–
         mov       dx,0                     ; vy¨¨¡ slovo ‡¡sla
         push      di                       ; ukazatel na ‡¡slo
         call      deknum5                  ; dek¢dov n¡ po‡tu soubor–
         mov       si,offset textvy2        ; text "soubor"
         call      transtxt                 ; ulo‘en¡ textu "soubor"
         pop       si                       ; ukazatel na ‡¡slo
         call      testkonc                 ; test koncovky ‡¡sla
         mov       si,offset textvy21       ; koncovka pro ‡¡slo "1"
         or        al,al                    ; je koncovka pro "1" ?
         jz        endlin2                  ; je koncovka pro "1"
         mov       si,offset textvy22       ; koncovka pro ostatn¡ ‡¡sla
endlin2: call      transtxt                 ; ulo‘en¡ koncovky

                                          ;* tisk informac¡ o v˜bˆru soubor–
         pop       dx                       ; n vrat po‡ te‡n¡ pozice
         push      dx                       ; £schova po‡ te‡n¡ pozice
         mov       si,offset outbuf         ; tiskov˜ buffer
         mov       cl,38                    ; ¨¡©ka prostoru k tisku
         call      center                   ; centrov n¡ textu
         call      outtxt                   ; tisk textu
endlin1: pop       dx                       ; n vrat pozice na displeji
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------
;                         Zapnut¡ kurzoru
; -----------------------------------------------------------------------------

kurzon:                                   ;* zapnut¡ kurzoru

         push      ax
         push      bx
         push      dx
         push      bp
         mov       bp,offset tabl           ; tabulka lev‚ho okna
         test      byte ptr ds:[flags],1    ; je lev‚ okno ?
         jnz       kurzon1                  ; je lev‚ okno
         mov       bp,offset tabr           ; tabulka prav‚ho okna
kurzon1:
         mov       dl,ds:[bp+pozl-tabl]     ; pozice na displeji
         mov       bx,ds:[bp+kurzl-tabl]    ; soubor s kurzorem
         jmp       short kurzof2

; -----------------------------------------------------------------------------
;                           Vypnut¡ kurzoru
; -----------------------------------------------------------------------------

kurzoff:                                  ;* vypnut¡ kurzoru

         push      ax
         push      bx
         push      dx
         push      bp
         mov       bp,offset tabl           ; tabulka lev‚ho okna
         test      byte ptr ds:[flags],1    ; je lev‚ okno ?
         jnz       kurzof1                  ; je lev‚ okno
         mov       bp,offset tabr           ; tabulka prav‚ho okna
kurzof1:
         mov       dl,ds:[bp+pozl-tabl]     ; pozice na displeji
         mov       bx,-1                    ; nov  pozice pro zru¨en¡ kurzoru
         xchg      bx,ds:[bp+kurzl-tabl]    ; soubor s kurzorem
kurzof2: mov       ax,4                     ; po‡ te‡n¡ © dek
         add       ax,bx                    ; p©i‡ten¡ ‡¡sla soub. s kurzorem
         sub       ax,ds:[bp+firstl-tabl]   ; ode‡ten¡ prvn¡ho souboru
         mov       dh,al                    ; © dek s kurzorem
         call      zobrfile                 ; zobrazen¡ souboru
         mov       ds:[bp+kurzl-tabl],bx    ; n vrat ‡¡sla kurzoru
         pop       bp
         pop       dx
         pop       bx
         pop       ax
         ret


; -----------------------------------------------------------------------------
;                 Poskytnut¡ adresy souboru s kurzorem
; -----------------------------------------------------------------------------

getkurz:                                    ; poskytnut¡ adresy s kurzorem
                                            ; VSTUP: DS:SI

         push      ax
         push      bx
         push      bp
         mov       bp,offset tabl           ; lev‚ okno
         test      byte ptr ds:[flags],1    ; je lev‚ okno ?
         jnz       getkurz1                 ; je lev‚ okno
         mov       bp,offset tabr           ; prav‚ okno
getkurz1:mov       bx,ds:[bp+kurzl-tabl]    ; ‡¡slo souboru s kurzorem
         call      adrfile                  ; poskytnut¡ adresy souboru
         pop       bp
         pop       bx
         pop       ax
         ret


; -----------------------------------------------------------------------------

deinit:
         push      ax
         push      dx
         xor       al,al
         mov       ah,5
         int       10h
         mov       al,13
         int       29h
         mov       al,10
         int       29h
         pop       dx
         pop       ax
         ret


; *****************************************************************************
;
;                          Ovl d n¡ programu
;
; *****************************************************************************

uses:
         call      obrcomm                  ; zobrazen¡ povelov‚ho © dku

uses0:
         call      obrkurz                  ; zobrazen¡ kurzoru na © dku

uses04:  mov       ah,1
         int       16h
         pushf
         push      ds
         xor       ax,ax
         mov       ds,ax
         test      byte ptr ds:[417h],8
         pop       ds
         jz        uses05                   ; nen¡ kl vesa ALT stisknuta

         test      byte ptr ds:[flags],10h
         jnz       uses07
         or        byte ptr ds:[flags],10h
         mov       dx,25*256+80
         xor       al,al
         call      outch
         mov       dx,24*256
         mov       si,offset txthelp2
         call      outtxt
         jmp       short uses07

uses05:  test      byte ptr ds:[flags],10h
         jz        uses07
         and       byte ptr ds:[flags],0efh
         test      byte ptr ds:[flags],8
         jz        uses08
         call      obrkurz
         mov       dx,24*256
         mov       si,offset txthelp
         call      outtxt
         jmp       short uses07
uses08:
         call      obrkurz
         call      obrcomm

uses07:  popf
         jz        uses04
uses06:  xor       ah,ah
         int       16h

         mov       bp,offset tabl           ; tabulka lev‚ho okna
         test      byte ptr ds:[flags],1    ; je lev‚ okno ?
         jnz       uses01                   ; je lev‚ okno
         mov       bp,offset tabr           ; tabulka prav‚ho okna
uses01:


uses02:  cmp       al,19h                   ; je Ctrl-Y ?
         je        uses03                   ; je vymaz n¡ © dku
         cmp       ax,011bh                 ; je Esc ?
         jne       uses1

uses03:  xor       al,al
         mov       ds:[pozcomm],al
         mov       ds:[pozbuf],al
         mov       ds:[buftxt],al
         mov       ds:[poztop],al
         mov       ds:[numchar],al
         jmp       uses

uses1:   cmp       ax,0f09h                 ; Tab
         jnz       uses2

         call      xchgakt                  ; zmˆna aktivn¡ho okna
         jmp       uses


uses2:   cmp       ax,4800h                 ; kurzor nahoru
         jne       uses3

         call      moveup                   ; posun kurzoru nahoru
         jmp       uses0


uses3:   cmp       ax,5000h                 ; kurzor dol–
         jne       uses4                    ; nen¡ kurzor dol–

         call      movedown                 ; posun kurzoru dol–
         jmp       uses0



uses4:   cmp       ax,4700h                 ; Home
         jne       uses41
         cmp       byte ptr ds:[numchar],0
         je        uses42
uses41:  cmp       ax,8400h                 ; ^Page Up
         jne       uses5

uses42:
         call      movehome                 ; p©esun na za‡ tek adres ©e
         jmp       uses0


uses44:  call      kurzon                   ; zapnut¡ kurzoru
uses45:  jmp       uses0


uses5:   cmp       ax,4f00h                 ; End
         jne       uses51
         cmp       byte ptr ds:[numchar],0
         je        uses52
uses51:  cmp       ax,7600h                 ; ^Page Down
         jne       uses6

uses52:
         call      moveend                  ; p©esun na konec adres ©e
uses50:  jmp       uses0


uses6:   cmp       ax,4900h                 ; Page Up
         jne       uses7

         call      pageup                   ; p©esun o str nku nahoru
         jmp       uses0


uses7:   cmp       ax,5100h                 ; Page Down
         jne       uses8

         call      pagedown                 ; p©esun o str nku dol–
         jmp       uses0


uses8:   cmp       ax,4e2bh                 ; [+]
         jne       uses81

         call      selplus
         jmp       uses0

uses81:  cmp       ax,4a2dh                 ; [-]
         jne       uses82

         call      selmin
         jmp       uses0

uses82:  cmp       ax,5200h                 ; Insert
         jne       uses9

         call      insert                   ; ozna‡en¡ soubor–
         jmp       uses0


uses9:   cmp       ax,1c0dh                 ; Enter
         je        uses90
         jmp       usesa
uses90:

         call      enter                    ; funkce Enter
         jmp       uses

usesa:   or        ah,ah
         jz        usesa0
         cmp       ax,0300h
         jz        usesa0
         cmp       al,32
         jb        usesb
usesa0:
         call      insch                    ; vlo‘en¡ znaku
         jmp       uses0


usesb:   cmp       ax,0e08h
         jne       usesc

         call      delbs                    ; zru¨en¡ znaku p©ed kurzorem
         jmp       uses0

usesc:   cmp       ax,4b00h                 ; kurzor vlevo
         jne       usesd

         call      chleft                   ; posun o znak vlevo
         jmp       uses0

usesd:   cmp       ax,4d00h                 ; kurzor vpravo
         jne       usese

         call      chright                  ; posun o znak vpravo
         jmp       uses0

usese:   cmp       ax,5300h                 ; Delete
         jne       usesf

         call      delchar                  ; vymaz n¡ znaku nad kurzorem
         jmp       uses0

usesf:   cmp       ax,4700h                 ; Home © dku
         jne       usesg

         call      homer                    ; funkce Home na © dku
         jmp       uses0

usesg:   cmp       ax,4f00h                 ; End © dku
         jne       usesh

         call      endr                     ; funkce End na © dku
         jmp       uses0

usesh:   cmp       ax,0f00h                 ; Shift - Tab
         jne       usesi

         call      invsel                   ; inverze v˜bˆru soubor–
         jmp       uses0

usesi:   cmp       al,0ah                   ; Ctrl - Enter, Ctrl-J
         jne       usesj

         call      ctrlent                  ; p©enesen¡ jm‚na souboru
         jmp       uses0

usesj:   cmp       al,0fh                   ; Ctrl-O
         jne       usesk

         call      xchgpg                   ; zmˆna str nky displeje
         jmp       uses0

usesk:   cmp       al,10h                   ; Ctrl-P
         jne       usesl

         call      xchgw                    ; zmˆna okna
         jmp       uses0

usesl:   cmp       al,2                     ; Ctrl-B
         jne       usesm

         call      sethlp
         jmp       uses0

usesm:   cmp       ax,3f00h                 ; F5 (kop¡rov n¡)
         jne       usesn

         call      copyfile
         jmp       uses0

usesn:



jmp       uses0

; -----------------------------------------------------------------------------

obrcomm:                                    ; zobrazen¡ p©¡kazov‚ho © dku
         push      dx
         and       byte ptr ds:[aktpage],0feh
         mov       dh,ds:[radek]
         call      obrcom0
         or        byte ptr ds:[aktpage],1
         mov       dh,24
         test      byte ptr ds:[flags],8
         jz        obrcomm4                 ; n povˆda nen¡ aktivn¡
         dec       dh
obrcomm4:call      obrcom0
         pop       dx
         ret

obrcom0: push      ax
         push      dx
         push      si
         mov       al,ds:[aktatr]           ; aktivn¡ atribut
         mov       ds:[color],al
         xor       dl,dl                    ; po‡ te‡n¡ pozice kurzoru
         mov       si,offset pathl          ; cesta k lev‚mu adres ©i
         test      byte ptr ds:[flags],1    ; je aktivn¡ lev‚ okno ?
         jnz       obrcom1                  ; je aktivn¡ lev‚ okno
         mov       si,offset pathr          ; cesta k prav‚mu adres ©i
obrcom1: call      outtx0                   ; zobrazen¡ textu cesty
         mov       al,">"                   ; £vodn¡ prompt
         call      outch1                   ; zobrazen¡ promptu
         mov       ds:[pozcomm],dl          ; pozice za‡ tku p©¡kazu

         mov       dl,ds:[pozbuf]           ; pozice v bufferu
         sub       dl,ds:[poztop]           ; offset od promptu
         add       dl,ds:[pozcomm]          ; offset od za‡ tku © dku
         sub       dl,78                    ; kontrola p©ete‡en¡ okraje
         jc        obrcom2                  ; nen¡ p©ekro‡en¡ okraje
         add       ds:[poztop],dl           ; zv˜¨en¡ offsetu za‡ tku
obrcom2: call      obrcomt0                 ; zobrazen¡ textu p©¡kazu
         pop       si
         pop       dx
         pop       ax
         ret

; -----------------------------------------------------------------------------

obrcomt:                                  ;* zobrazen¡ p©¡kazov‚ho © dku
         push      dx
         and       byte ptr ds:[aktpage],0feh
         mov       dh,ds:[radek]
         call      obrcomt0
         or        byte ptr ds:[aktpage],1
         mov       dh,24
         test      byte ptr ds:[flags],8
         jz        obrcomt4                 ; n povˆda nen¡ aktivn¡
         dec       dh
obrcomt4:call      obrcomt0
         pop       dx
         ret

obrcomt0:
         push      ax
         push      cx
         push      dx
         push      si
         mov       al,ds:[aktatr]
         mov       ds:[color],al
         mov       dl,ds:[pozcomm]          ; pozice za‡ tku p©¡kazu
         mov       cl,ds:[poztop]           ; pozice za‡ tku zobrazen‚ho textu
         xor       ch,ch                    ; CH <- 0
         mov       si,offset buftxt         ; buffer p©¡kazov‚ho © dku
         add       si,cx                    ; za‡ tek zobrazen‚ho textu
         mov       al,ds:[numchar]          ; po‡et znak– v bufferu
         sub       al,cl                    ; zbytek © dku
         mov       cl,al                    ; po‡et znak– k tisku
         jbe       obrcomt2                 ; nen¡ ‘ dn˜ znak
         mov       ah,80                    ; ¨¡©ka © dku
         sub       ah,dl                    ; po‡et znak– do zbytku © dku
         cmp       cl,ah                    ; je p©ekro‡en po‡et znak– ?
         jbe       obrcomt1                 ; nen¡ p©ekro‡en po‡et znak–
         mov       cl,ah                    ; omezen¡ po‡tu znak–
obrcomt1:lodsb                              ; na‡ten¡ znaku k v˜stupu
         push      cx
         mov       cl,1
         call      outch0                   ; v˜stup znaku
         pop       cx
         loop      obrcomt1                 ; v˜stup dal¨¡ho znaku
obrcomt2:mov       cl,80
         sub       cl,dl                    ; sou‡asn  pozice na © dku
         mov       al," "                   ; mazac¡ mezera
         call      outch                    ; vymaz n¡ zbytku © dku
         pop       si
         pop       dx
         pop       cx
         pop       ax
         ret

; -----------------------------------------------------------------------------

obrkurz:                                    ; zobrazen¡ kurzoru na © dku

         push      ax
         push      dx
         mov       dl,ds:[pozbuf]           ; pozice v bufferu
         sub       dl,ds:[poztop]           ; ode‡ten¡ po‡ te‡n¡ pozice
         add       dl,ds:[pozcomm]          ; po‡ te‡n¡ pozice na displeji
         xor       al,al                    ; povel k nastaven¡ kurzoru
         and       byte ptr ds:[aktpage],0feh
         mov       dh,ds:[radek]
         call      outch1                   ; nastaven¡ pozice kurzoru
         or        byte ptr ds:[aktpage],1
         mov       dh,24                    ; © dek p©¡kazov‚ho © dku
         test      byte ptr ds:[flags],8
         jz        obrkur2                  ; n povˆda nen¡ aktivn¡
         dec       dh
obrkur2: call      outch1                   ; nastaven¡ pozice kurzoru
         pop       dx
         pop       ax
         ret

; -----------------------------------------------------------------------------

xchgakt:                                  ;* zmˆna aktivn¡ho okna

                                          ;* vypnut¡ zobrazen¡ kurzoru
         push      bp
         test      byte ptr ds:[flags],1    ; je lev‚ okno ?
         mov       bp,offset tabr           ; p©¡znak prav‚ho okna
         jnz       xchakt1                  ; je lev‚ okno
         mov       bp,offset tabl           ; p©¡znak lev‚ho okna
xchakt1: test      byte ptr ds:[bp+flagsl-tabl],2 ; p©¡znak okna
         pop       bp
         jz        xchakt2                  ; okno nen¡ zapnut‚ - p©epnut¡ oken

         push      word ptr ds:[col3]       ; £schova zv˜raznˆn˜ch barev
         mov       ax,word ptr ds:[col1]    ; norm ln¡ barvy
         mov       word ptr ds:[col3],ax    ; nastaven¡ barev pro kurzor
         call      kurzon                   ; zobrazen¡ pr zdn‚ho kurzoru
         pop       word ptr ds:[col3]       ; n vrat barev pro kurzor
xchakt3:                                  ;* zmˆna zobrazen¡ kurzor–
         xor       byte ptr ds:[flags],1    ; zmˆna aktivn¡ho okna
         mov       bp,offset tabl           ; adresa lev‚ho okna
         call      obrpath                  ; zobrazen¡ cesty lev‚ho okna
         mov       bp,offset tabr           ; adresa prav‚ho okna
         call      obrpath                  ; zobrazen¡ cesty prav‚ho okna
         call      kurzon                   ; zapnut¡ nov‚ho kurzoru
         ret

xchakt2:                                  ;* zmˆna pozice okna
         mov       al,ds:[pozr]
         xchg      al,ds:[pozl]
         mov       ds:[pozr],al
         mov       bp,offset tabl
         call      window
         mov       bp,offset tabr
         call      window
         ret

; -----------------------------------------------------------------------------

moveup:                                   ;* posun kurzoru nahoru

         push      ax
         push      dx
         mov       ax,ds:[bp+kurzl-tabl]    ; pozice kurzoru
         or        ax,ax                    ; je ji‘ prvn¡ pozice kurzoru ?
         jz        moveup2                  ; kurzor je ji‘ na prvn¡ pozici
         call      kurzoff                  ; vypnut¡ kurzoru
         dec       ax                       ; sn¡‘en¡ pozice kurzoru
         mov       ds:[bp+kurzl-tabl],ax    ; nastaven¡ nov‚ pozice kurzoru
         mov       dl,ds:[bp+numdskl-tabl]  ; po‡et disk–
         xor       dh,dh                    ; DH <- 0
         cmp       ds:[bp+firstl-tabl],dx   ; prvn¡ zobrazen˜ soubor
         jle       moveup3                  ; je ji‘ p©ed ozna‡en¡m disk–

         cmp       ax,ds:[bp+firstl-tabl]
         jnb       moveup4
         call      wrolldown
moveup4: dec       ax
         cmp       ax,ds:[bp+firstl-tabl]
         jnb       moveup1
         mov       ds:[bp+firstl-tabl],ax
         call      wrolldown

         mov       bx,ax
         mov       dh,4
         mov       dl,ds:[bp+pozl-tabl]
         call      zobrfile
         jmp       short moveup1

moveup3: cmp       ax,ds:[bp+firstl-tabl]   ; je kurzor p©ed prvn¡m souborem ?
         jnb       moveup1                  ; kurzor nen¡ p©ed prvn¡m souborem
         mov       ds:[bp+firstl-tabl],ax   ; nastaven¡ prvn¡ho souboru
         call      wrolldown                ; rolov n¡ okna dol–
moveup1: call      kurzon                   ; zapnut¡ kurzoru
moveup2: pop       dx
         pop       ax
         ret

; -----------------------------------------------------------------------------

movedown:                                 ;* posun kurzoru dol–

         push      ax
         push      bx
         push      dx
         mov       ax,ds:[bp+kurzl-tabl]    ; pozice kurzoru
         inc       ax                       ; pozice kurzoru + 1
         cmp       ax,ds:[bp+numfl-tabl]    ; je to ji‘ posledn¡ soubor ?
         jae       moved3                   ; je to ji‘ posledn¡ soubor
         call      kurzoff                  ; vypnut¡ kurzoru
         mov       ds:[bp+kurzl-tabl],ax    ; nastaven¡ nov‚ pozice kurzoru

         mov       bx,ds:[bp+firstl-tabl]   ; prvn¡ zobrazen˜ soubor
         add       bx,18                    ; p©edpokl dan˜ konec + 1
         test      byte ptr ds:[flags],8
         jnz       moved6
         inc       bx
moved6:  cmp       bx,ds:[bp+numfl-tabl]    ; je p©ed koncem ?
         jae       moved1                   ; je posledn¡ nebo p©edposledn¡

         dec       bx
         cmp       ax,bx
         jb        moved2                   ; je p©ed p©edposledn¡m souborem
         je        moved4
         inc       bx
         call      wrollup
moved4:  sub       bx,16
         test      byte ptr ds:[flags],8
         jnz       moved8
         dec       bx
moved8:  mov       ds:[bp+firstl-tabl],bx   ; zv˜¨en¡ prvn¡ho souboru
         call      wrollup                  ; rolov n¡ okna nahoru
         inc       ax
         mov       bx,ax
         mov       dh,22
         test      byte ptr ds:[flags],8
         jz        moved7
         dec       dh
moved7:  mov       dl,ds:[bp+pozl-tabl]
         call      zobrfile
         jmp       short moved2

moved1:  dec       bx
         cmp       ax,bx                    ; je kurzor za koncem ?
         jna       moved2                   ; kurzor nen¡ za koncem
         inc       word ptr ds:[bp+firstl-tabl] ; zv˜¨en¡ prvn¡ho souboru
         call      wrollup                  ; rolov n¡ okna nahoru
moved2:  call      kurzon                   ; zapnut¡ kurzoru
moved3:  pop       dx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------

movehome:                                 ;* p©esun na za‡ tek adres ©e

         push      ax
         push      dx
         mov       ax,ds:[bp+kurzl-tabl]    ; pozice kurzoru
         mov       dl,ds:[bp+numdskl-tabl]  ; po‡et disk– v seznamu
         xor       dh,dh                    ; DH <- 0
         cmp       ax,dx                    ; je ji‘ na prvn¡ pozici adres ©e ?
         je        moveh1                   ; kurzor je ji‘ na prvn¡ pozici
         call      kurzoff                  ; vypnut¡ kurzoru
         mov       word ptr ds:[bp+kurzl-tabl],dx ; nastaven¡ pozice kurzoru
         mov       word ptr ds:[bp+firstl-tabl],dx ; prvn¡ zobrazen˜ soubor
         call      files                    ; zobrazen¡ adres ©e
moveh1:  pop       dx
         pop       ax
         ret

; -----------------------------------------------------------------------------

moveend:                                    ; p©esun na konec adres ©e

         push      ax
         push      dx
         mov       dl,ds:[bp+numdskl-tabl]  ; po‡et disk– v seznamu
         xor       dh,dh                    ; DH <- 0
         mov       ax,ds:[bp+numfl-tabl]    ; po‡et soubor–
         sub       ax,18                    ; p©edpokl dan˜ prvn¡ soubor
         test      byte ptr ds:[flags],8
         jnz       movee5
         dec       ax
movee5:  cmp       ax,dx                    ; je p©ed prvn¡m souborem ?
         jl        movee1                   ; je p©ed prvn¡m souborem
         mov       dx,ax                    ; prvn¡ soubor
movee1:  mov       ax,ds:[bp+numfl-tabl]    ; po‡et soubor–
         dec       ax                       ; posledn¡ soubor
         or        ah,ah                    ; je vy¨¨¡ slovo = 0 ?
         jnz       movee2                   ; nen¡ po‡et < 256
         cmp       al,ds:[bp+numdskl-tabl]  ; je nˆjak˜ soubor ?
         jbe       movee4                   ; nen¡ ‘ dn˜ soubor
movee2:  cmp       dx,ds:[bp+firstl-tabl]   ; je ji‘ nastaven prvn¡ soubor ?
         jne       movee3                   ; prvn¡ soubor nebyl nastaven
         cmp       ax,ds:[bp+kurzl-tabl]    ; kontrola nastaven¡ kurzoru
         je        movee4                   ; je ji‘ na posledn¡ pozici
movee3:  call      kurzoff                  ; vypnut¡ kurzoru
         mov       ds:[bp+kurzl-tabl],ax    ; nov  pozice kurzoru
         mov       ds:[bp+firstl-tabl],dx   ; nov˜ za‡ tek zobrazen¡
         call      files                    ; zobrazen¡ adres ©e
movee4:  pop       dx
         pop       ax
         ret

; -----------------------------------------------------------------------------

pageup:                                   ;* p©esun o str nku nahoru

         push      ax
         push      dx
         mov       ax,ds:[bp+kurzl-tabl]    ; pozice kurzoru
         mov       dl,ds:[bp+numdskl-tabl]  ; po‡et disk– v seznamu
         xor       dh,dh                    ; DH <- 0
         cmp       ax,dx                    ; je ji‘ na prvn¡ pozici adres ©e ?
         jb        pageup1                  ; je men¨¡ - skok na prvn¡ pozici
         jne       pageup2                  ; kurzor nen¡ na prvn¡ pozici
         cmp       dx,ds:[bp+firstl-tabl]   ; je zobrazen prvn¡ soubor ?
         je        pageup5                  ; ‘ dn  funkce
pageup1: call      movehome                 ; nen¡ - funkce Home
         jmp       short pageup5

pageup2: call      kurzoff                  ; vypnut¡ kurzoru
         mov       ax,ds:[bp+firstl-tabl]   ; prvn¡ zobrazen˜ soubor
         cmp       ax,dx                    ; je prvn¡ polo‘ka ?
         je        pageup1                  ; je ji‘ zobrazen  prvn¡ polo‘ka
                                          ;* posun prvn¡ zobrazen‚ polo‘ky
         sub       ax,17                    ; sn¡‘en¡ ukazatele prvn¡ polo‘ky
         test      byte ptr ds:[flags],8
         jnz       pageup6
         dec       ax
pageup6: cmp       ax,dx                    ; je ni‘¨¡ ne‘ prvn¡ polo‘ka ?
         jnl       pageup3                  ; nen¡ men¨¡ ne‘ prvn¡ polo‘ka
         add       ax,17                    ; n vrat ukazatele prvn¡ polo‘ky
         test      byte ptr ds:[flags],8
         jnz       pageup7
         inc       ax
pageup7: sub       ax,dx                    ; po‡et polo‘ek k posuvu
         sub       ds:[bp+kurzl-tabl],ax    ; nov  pozice kurzoru
         mov       ax,dx                    ; po‡et disk–
         jmp       short pageup4
pageup3: sub       word ptr ds:[bp+kurzl-tabl],18 ; sn¡‘en¡ ukazatele kurzoru
         test      byte ptr ds:[flags],8
         jz        pageup4
         inc       word ptr ds:[bp+kurzl-tabl] ; sn¡‘en¡ ukazatele kurzoru
pageup4: mov       ds:[bp+firstl-tabl],ax   ; nastaven¡ prvn¡ zobrazen‚ polo‘ky
         call      files
pageup5: pop       dx
         pop       ax
         ret

; -----------------------------------------------------------------------------

pagedown:                                 ;* p©esun o str nku dol–

         push      ax
         push      bx
         push      cx
         push      dx
         mov       bx,word ptr ds:[bp+numfl-tabl] ; po‡et soubor–
         dec       bx                       ; posledn¡ soubor
         cmp       bx,word ptr ds:[bp+kurzl-tabl] ; je dosa‘eno konce ?
         jg        pagedo0                  ; nen¡ je¨tˆ konec
         jmp       pagedo6                  ; je konec adres ©e
pagedo0: call      kurzoff
         mov       cx,bx                    ; posledn¡ soubor
         mov       dl,ds:[bp+numdskl-tabl]  ; po‡et disk–
         xor       dh,dh                    ; DH <- 0
         sub       bx,17                    ; max. ‡¡slo za‡ tku
         test      byte ptr ds:[flags],8
         jnz       pagedo7
         dec       bx
pagedo7: cmp       bx,dx
         jnl       pagedo1                  ; po‡et soubor– >= 18
         mov       bx,dx                    ; max. ‡¡slo za‡ tku
pagedo1: mov       ax,ds:[bp+firstl-tabl]
         add       ax,17
         test      byte ptr ds:[flags],8
         jnz       pagedo8
         inc       ax
pagedo8: cmp       ax,bx
         jbe       pagedo2
         cmp       word ptr ds:[bp+firstl-tabl],bx ; kontrola po‡ tku
         jnz       pagedo4                  ; nen¡ je¨tˆ dosa‘eno maxima
pagedo2: mov       ax,ds:[bp+kurzl-tabl]    ; pozice kurzoru
         add       ax,17                    ; zv˜¨en¡ pozice kurzoru
         test      byte ptr ds:[flags],8
         jnz       pagedo9
         inc       ax
pagedo9: cmp       ax,cx
         jna       pagedo3                  ; nen¡ p©ekro‡en¡
;         cmp       word ptr ds:[bp+firstl-tabl],0
;         jne       pagedo4
         mov       ax,cx
pagedo3: mov       ds:[bp+kurzl-tabl],ax
pagedo4: mov       ax,ds:[bp+firstl-tabl]
         add       ax,17
         test      byte ptr ds:[flags],8
         jnz       pagedoa
         inc       ax
pagedoa: cmp       ax,bx
         jna       pagedo5
         sub       ax,17
         test      byte ptr ds:[flags],8
         jnz       pagedob
         dec       ax
pagedob: sub       ax,bx
         neg       ax
         add       ds:[bp+kurzl-tabl],ax
         mov       ax,bx
pagedo5: mov       ds:[bp+firstl-tabl],ax
         call      files
pagedo6: pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------

insert:                                     ; ozna‡en¡ soubor–

         push      ax
         push      bx
         push      cx
         push      dx
         push      di
         call      kurzoff
         mov       ax,ds:[bp+kurzl-tabl]
         mov       bx,20
         mul       bx
         add       ax,ds:[bp+adrfl-tabl]
         mov       di,ax
         test      byte ptr ds:[di],50h
         jz        insert1

         jnz       insert4
          ;
         cmp       word ptr ds:[di+1],".."
         je        insert4
insert1: xor       byte ptr ds:[di],80h
         mov       ax,word ptr ds:[di+12]
         mov       dx,word ptr ds:[di+14]
         test      byte ptr ds:[di],80h
         jz        insert2                  ; je zru¨en¡
         inc       word ptr ds:[bp+insfil-tabl]
         add       word ptr ds:[bp+kapfil-tabl],ax
         adc       word ptr ds:[bp+kapfil-tabl+2],dx
         jmp       short insert3
insert2: dec       word ptr ds:[bp+insfil-tabl]
         sub       word ptr ds:[bp+kapfil-tabl],ax
         sbb       word ptr ds:[bp+kapfil-tabl+2],dx
insert3: call      endline                  ; vykreslen¡ koncov‚ linky
insert4: call      kurzon
         call      movedown                 ; posun kurzoru dol–
         pop       di
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------

invsel:                                     ; inverze v˜bˆru soubor–

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         mov       cx,ds:[bp+numfl-tabl]
         jcxz      invsel3
         mov       si,ds:[bp+adrfl-tabl]
invsel1: test      byte ptr ds:[si],50h
         jnz       invsel2                  ; je disk nebo adres ©
         xor       byte ptr ds:[si],80h     ; zmˆna v˜bˆru

         mov       ax,word ptr ds:[si+12]
         mov       dx,word ptr ds:[si+14]
         test      byte ptr ds:[si],80h
         jz        invsel4                  ; je zru¨en¡
         inc       word ptr ds:[bp+insfil-tabl]
         add       word ptr ds:[bp+kapfil-tabl],ax
         adc       word ptr ds:[bp+kapfil-tabl+2],dx
         jmp       short invsel2
invsel4: dec       word ptr ds:[bp+insfil-tabl]
         sub       word ptr ds:[bp+kapfil-tabl],ax
         sbb       word ptr ds:[bp+kapfil-tabl+2],dx
invsel2: add       si,20
         loop      invsel1
         call      files                    ; zobrazen¡ adres ©e
         call      endline                  ; koncov˜ © dek
invsel3: pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------

enter:                                    ;* funkce ENTER

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         mov       al,ds:[numchar]
         cbw
         or        al,al                    ; je nˆjak˜ znak v bufferu ?
         je        enter0                   ; v bufferu nen¡ ‘ dn˜ znak
         call      testexit                 ; test EXIT
         jc        enter08

         mov       al,2
         jmp       short enter09

enter08:
         mov       si,offset buftxt         ; buffer textu
         push      si
         add       si,ax                    ; adresa konce textu v bufferu
         mov       byte ptr ds:[si],0       ; nastaven¡ koncov‚ho bajtu 0
         pop       si
enter02:
         call      setenv                   ; z pis do prost©ed¡
         mov       al,0
         jnc       enter09                  ; z pis OK - proveden¡ povelu

         call      openex                   ; otev©en¡ souboru MNEXEC.BAT
         jc        enter04                  ; chyba otev©en¡ souboru
         call      writte                   ; z pis textu do bufferu
         jc        enter04
         call      closew                   ; uzav©en¡ souboru
         mov       al,1                     ; n vratov˜ k¢d

enter09: add       sp,16                    ; zru¨en¡ dat v z sobn¡ku
         push      ax
         lea       si,ds:[bp+pathl-tabl]    ; adresa cesty
         call      aktdir                   ; nastaven¡ aktivn¡ cesty
         pop       ax
         jmp       mainx                    ; n vrat do syst‚mu

enter04: jmp       enter7

enter0:  call      getkurz                  ; poskytnut¡ adres souboru s kurz.
         test      byte ptr ds:[si],10h     ; je adres © ?
         jnz       enter2                   ; je adres ©
         test      byte ptr ds:[si],40h     ; je disk ?
         jnz       enter1                   ; je disk
                                          ;* proveden¡ p©¡kazu s kurzorem
         mov       di,offset outbuf         ; tiskov˜ buffer
         cmp       word ptr ds:[si+9],"OC"  ; je soubor COM ?
         jne       enter05                  ; nen¡ souboru COM
         cmp       byte ptr ds:[si+11],"M"  ; je soubor COM ?
         je        enter07                  ; je soubor COM
enter05: cmp       word ptr ds:[si+9],"XE"  ; je soubor EXE ?
         jne       enter06                  ; nen¡ soubor EXE
         cmp       byte ptr ds:[si+11],"E"  ; je soubor EXE ?
         je        enter07                  ; je soubor EXE
enter06: cmp       word ptr ds:[si+9],"AB"  ; je soubor BAT ?
         jne       enter04                  ; nen¡ soubor BAT
         cmp       byte ptr ds:[si+11],"T"  ; je soubor BAT ?
         jne       enter04                  ; nen¡ soubor BAT
         push      si
         mov       si,offset txtcall        ; text CALL
         call      transtxt                 ; p©enos textu
         pop       si
enter07:
         push      di
         call      dekfile                  ; dek¢dov n¡ jm‚na souboru
         pop       si

         mov       si,offset outbuf         ; buffer s textem
         jmp       short enter02            ; z pis a proveden¡ p©¡kazu

enter1:                                   ;* je zmˆna disku
         mov       byte ptr ds:[namepath],0 ; zru¨en¡ jm‚na cesty
         mov       al,ds:[si+6]             ; ozna‡en¡ disku
         sub       al,"A"                   ; korekce na ‡¡slo disku
         mov       dl,al                    ; po‘adovan˜ nov˜ disk
         mov       ah,0eh                   ; funkce nastaven¡ aktivn¡ho disku
         int       21h                      ; nastaven¡ nov‚ho aktivn¡ho disku
         lea       di,ds:[bp+pathl-tabl]    ; p©¡stupov  cesta k adres ©i
         call      loadpath                 ; sestaven¡ nov‚ p©¡stupov‚ cesty
         call      readdir                  ; aktualizace - na‡ten¡ adres ©–
         call      window                   ; zobrazen¡ okna
         jmp       enter7
                                          ;* je zmˆna adres ©e
enter2:                                   ;* £schova cesty pro n vrat kurzoru
         push      si
         mov       di,offset namepath       ; £schova jm‚na cesty
         mov       ax,".."
         cmp       ax,ds:[si+1]             ; je cesta dol– ?
         jne       enter5                   ; nen¡ cesta dol– - ulo‘en¡ ".."
         cmp       byte ptr ds:[si+3]," "
         jne       enter5
                                          ;* ulo‘en¡ jm‚na adres ©e
         lea       si,ds:[bp+pathl-tabl]
         push      si
enter3:  lodsb
         or        al,al
         jz        enter4
         cmp       al,"\"
         jnz       enter3
         pop       ax
         push      si
         jmp       short enter3
enter4:  pop       si
         call      transtxt                 ; p©enos textu
         jmp       short enter6

enter5:  stosw                              ; bude cesta ".."
         xor       al,al
         stosb
enter6:  pop       si                       ; polo‘ka adres ©e v seznamu
                                          ;* p©epnut¡ adres ©e
         mov       di,offset outbuf         ; tiskov˜ buffer
         call      dekfile                  ; zak¢dov n¡ jm‚na souboru
         lea       si,ds:[bp+pathl-tabl]    ; cesta k adres ©i
         call      aktdir                   ; nastaven¡ aktivn¡ho adres ©e
         mov       si,di                    ; jm‚no nov‚ cesty
         call      aktdir                   ; zapnut¡ nov‚ho adres ©e
         lea       di,ds:[bp+pathl-tabl]    ; p©¡stupov  cesta k adres ©i
         call      loadpath                 ; sestaven¡ nov‚ p©¡stupov‚ cesty
         call      readdir                  ; aktualizace - na‡ten¡ adres ©–
         call      window                   ; zobrazen¡ okna
enter7:  pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------

testexit:
                                          ;* test p©¡kazu EXIT
         mov       si,offset buftxt
         mov       cx,ax                    ; po‡et znak–
enter01: lodsb
         cmp       al,32
         jnb       enter03
         loop      enter01
         jmp       short exit1
enter03: call      highcase                 ; p©eveden¡ na velk‚ p¡smeno
         cmp       al,"E"
         jne       exit1
         dec       cx
         jcxz      exit1
         lodsb
         call      highcase                 ; p©eveden¡ na velk‚ p¡smeno
         cmp       al,"X"
         jne       exit1
         dec       cx
         jcxz      exit1
         lodsb
         call      highcase                 ; p©eveden¡ na velk‚ p¡smeno
         cmp       al,"I"
         jne       exit1
         dec       cx
         jcxz      exit1
         lodsb
         call      highcase                 ; p©eveden¡ na velk‚ p¡smeno
         cmp       al,"T"
         je        exit0
exit1:   stc
         ret

exit0:   clc
         ret

; -----------------------------------------------------------------------------

insch:                                      ; vlo‘en¡ znaku

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di

         cmp       byte ptr ds:[numchar],128 ; po‡et znak– v bufferu
         jnb       insch3                   ; je ji‘ maxim ln¡ po‡et znak–
                                          ;* nejd©¡ve se vytvo©¡ m¡sto pro znak
         mov       cx,127
         sub       cl,ds:[pozbuf]           ; po‡et znak– do zbytku © dku
         mov       di,offset buftxt + 127
         mov       si,offset buftxt + 126
         std
         rep       movsb
         cld
                                          ;* ulo‘en¡ nov‚ho znaku
         mov       dl,ds:[pozbuf]           ; pozice v bufferu
         xor       dh,dh
         mov       di,offset buftxt
         add       di,dx                    ; adresa znaku v bufferu
         stosb
                                          ;* zv˜¨en¡ ukazatel–
         inc       byte ptr ds:[pozbuf]     ; zv˜¨en¡ pozice v bufferu
         inc       byte ptr ds:[numchar]    ; zv˜¨en¡ po‡tu znak– v bufferu
         inc       dl
         sub       dl,ds:[poztop]           ; offset od promptu
         add       dl,ds:[pozcomm]          ; offset od za‡ tku © dku
         cmp       dl,79
         jb        insch2
         inc       byte ptr ds:[poztop]
insch2:  call      obrcomt
insch3:  pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------

chleft:                                     ; posun o znak vlevo

         push      dx
         mov       dl,ds:[pozbuf]           ; pozice znaku v bufferu
         or        dl,dl                    ; je ji‘ za‡ tek ?
         jz        chleft4                  ; je ji‘ na za‡ tku
         dec       byte ptr ds:[pozbuf]     ; sn¡‘en¡ pozice v bufferu
         dec       dl
         cmp       dl,ds:[poztop]           ; pozice za‡ tku textu
         ja        chleft4                  ; nen¡ je¨tˆ na za‡ tku
         cmp       byte ptr ds:[poztop],0   ; je ji‘ za‡ tek textu ?
         je        chleft4                  ; je ji‘ za‡ tek textu
         dec       byte ptr ds:[poztop]     ; sn¡‘en¡ za‡ tku textu
         call      obrcomt                  ; zobrazen¡ p©¡kazov‚ho © dku
chleft4: pop       dx
         ret

; -----------------------------------------------------------------------------

chright:                                    ; posun o znak vpravo

         push      dx
         push      di
         mov       dl,ds:[pozbuf]           ; pozice znaku v bufferu
         cmp       dl,ds:[numchar]          ; je ji‘ konec textu ?
         jae       chrig2                   ; je ji‘ konec textu
         inc       byte ptr ds:[pozbuf]     ; ulo‘en¡ nov‚ pozice
         sub       dl,ds:[poztop]           ; offset od za‡ tku zobraz. textu
         add       dl,ds:[pozcomm]          ; offset od za‡ tku © dku
         cmp       dl,78                    ; je p©ekro‡en konec © dku ?
         jb        chrig2                   ; nen¡ p©ekro‡en konec © dku
         inc       byte ptr ds:[poztop]     ; zv˜¨en¡ pozice za‡ tku textu
         call      obrcomt                  ; zobrazen¡ textu
chrig2:  pop       di
         pop       dx
         ret

; -----------------------------------------------------------------------------

delchar:                                    ; vymaz n¡ znaku nad kurzorem

         push      ax
         push      cx
         push      dx
         push      si
         push      di
         mov       dl,ds:[pozbuf]           ; pozice v bufferu
         xor       dh,dh
         mov       di,offset buftxt
         add       di,dx                    ; adresa znaku v bufferu
         mov       si,di
         inc       si
         mov       cx,128
         sub       cx,dx                    ; po‡et znak– za kurzorem
         rep       movsb                    ; p©enos textu
         mov       al,ds:[pozbuf]           ; ukazatel kurzoru
         cmp       al,ds:[numchar]          ; je nˆjak˜ znak za kurzorem ?
         jae       delchar1                 ; za kurzorem nen¡ ‘ dn˜ znak
         dec       byte ptr ds:[numchar]    ; sn¡‘en¡ po‡tu znak– v bufferu
         call      obrcomt                  ; zobrazen¡ textu
delchar1:pop       di
         pop       si
         pop       dx
         pop       cx
         pop       ax
         ret

; -----------------------------------------------------------------------------

delbs:                                      ; zru¨en¡ znaku p©ed kurzorem

         push      dx
         cmp       byte ptr ds:[pozbuf],0   ; je ji‘ za‡ tek bufferu ?
         je        delbs1                   ; je ji‘ za‡ tek bufferu
         call      chleft                   ; posun kurzoru vlevo
         call      delchar                  ; vymaz n¡ znaku nad kurzorem
delbs1:  pop       dx
         ret

; -----------------------------------------------------------------------------

homer:                                      ; nastaven¡ prvn¡ pozice na © dku

         push      ax
         xor       al,al
         cmp       al,ds:[pozbuf]           ; je ji‘ na prvn¡ pozici ?
         je        homer1                   ; je ji‘ na prvn¡ pozici
         mov       ds:[pozbuf],al           ; pozice kurzoru v bufferu
         mov       ds:[poztop],al           ; prvn¡ zobrazen  pozice textu
         call      obrcomt                  ; zobrazen¡ textu
homer1:  pop       ax
         ret

; -----------------------------------------------------------------------------

endr:                                       ; nastaven¡ posledn¡ pozice na © dku

         push      ax
         push      dx
         mov       al,ds:[numchar]          ; po‡et znak– v bufferu
         or        al,al                    ; posledn¡ pozice textu
         jz        endr1                    ; nen¡ ‘ dn˜ znak textu
         cmp       al,ds:[pozbuf]           ; pozice v bufferu
         je        endr1                    ; je ji‘ na posledn¡ pozici bufferu
         mov       ds:[pozbuf],al           ; nastaven¡ posledn¡ pozice bufferu
         mov       dl,78                    ; posledn¡ zobrazen  pozice
         sub       dl,ds:[pozcomm]          ; po‡et znak– do konce © dku
         sub       al,dl                    ; pozice po‡ tku © dku
         jns       endr2                    ; je p©ekro‡en¡ konce © dku
         xor       al,al                    ; po‡ te‡n¡ pozice = 0
endr2:   mov       ds:[poztop],al           ; po‡ te‡n¡ zobrazen¡ textu
         call      obrcomt                  ; zobrazen¡ textu
endr1:   pop       dx
         pop       ax
         ret

; -----------------------------------------------------------------------------

ctrlent:                                    ; p©enesen¡ jm‚na souboru

         push      ax
         push      bx
         push      si
         push      di
         push      ds
         xor       ax,ax
         mov       ds,ax
         test      byte ptr ds:[417h],3     ; je Shift ?
         pop       ds
         jz        ctrlen4

         mov       bx,ds:[bp+kurzl-tabl]
         call      adrfile
         jc        ctrlen51
         test      byte ptr ds:[si],40h
         jz        ctrlen51

         mov       al,ds:[si+6]             ; ozna‡en¡ disku
         cmp       al,ds:[bp+pathl-tabl]    ; je aktivn¡ disk ?
         je        ctrlen51                 ; je aktivn¡ adres ©
         sub       al,"A"
         mov       dl,al
         mov       ah,0eh
         int       21h
         mov       di,offset outbuf
         push      di
         call      loadpath
         pop       si
         jmp       short ctrlen5

ctrlen51:lea       si,ds:[bp+pathl-tabl]
ctrlen5: lodsb
         or        al,al
         jz        ctrlen6
         call      insch                    ; vlo‘en¡ znaku
         jmp       short ctrlen5
ctrlen6: cmp       byte ptr ds:[si-2],"\"
         je        ctrlen3
         mov       al,"\"
         jmp       short ctrlen7

ctrlen4:
         mov       bx,ds:[bp+kurzl-tabl]
         call      adrfile
         jc        ctrlen3
         test      byte ptr ds:[si],40h     ; je disk ?
         jz        ctrlen8                  ; nen¡ disk
         mov       al,ds:[si+6]             ; ozna‡en¡ disku
         call      insch
         mov       al,":"
         jmp       short ctrlen7

ctrlen8: mov       di,offset outbuf         ; tiskov˜ buffer
         push      di
         call      dekfile                  ; dek¢dov n¡ jm‚na souboru
         pop       si
ctrlen1: lodsb
         or        al,al
         jz        ctrlen2
         call      insch                    ; vlo‘en¡ znaku
         jmp       short ctrlen1
ctrlen2: mov       al," "
ctrlen7: call      insch
ctrlen3: pop       di
         pop       si
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------

xchgpg:                                   ;* zmˆna str nky displeje
         push      ax
         xor       byte ptr ds:[aktpage],2  ; zmˆna aktivn¡ str nky displeje
         mov       al,ds:[aktpage]          ; aktivn¡ str nka displeje
         shr       al,1
         and       al,1
         mov       ah,5
         int       10h                      ; nastaven¡ nov‚ aktivn¡ str nky
         pop       ax
         ret

; -----------------------------------------------------------------------------

xchgw:                                    ;* zmˆna okna

         push      ax
         push      bp
         test      byte ptr ds:[bp+flagsl-tabl],4 ; je lev‚ okno ?
         mov       bp,offset tabr           ; p©¡znak prav‚ho okna
         jnz       xchgw1                   ; je lev‚ okno
         mov       bp,offset tabl           ; p©¡znak lev‚ho okna
xchgw1:  test      byte ptr ds:[aktpage],1
         jz        xchgw2                   ; nen¡ str nka 1
         xor       byte ptr ds:[bp+flagsl-tabl],2 ; zmˆna p©¡znaku okna
         call      window                   ; zapnut¡ okna
xchgw2:  pop       bp
         pop       ax
         ret

; -----------------------------------------------------------------------------

sethlp:                                   ;* nastaven¡ n povˆdy

         xor       byte ptr ds:[flags],8    ; zmˆna p©¡znaku n povˆdy
         test      byte ptr ds:[flags],8
         jz        sethlp2                  ; n povˆda nen¡ aktivn¡
         mov       si,offset txthelp
         mov       dx,24*256
         call      outtxt
sethlp2: push      bp
         call      obrcomm
         mov       ax,ds:[kurzl]
         sub       ax,ds:[firstl]
         cmp       ax,18
         jc        sethlp3
         inc       word ptr ds:[firstl]
sethlp3:
         mov       ax,ds:[kurzr]
         sub       ax,ds:[firstr]
         cmp       ax,18
         jc        sethlp4
         inc       word ptr ds:[firstr]
sethlp4:
         mov       bp,offset tabr           ; tabulka definice okna
         call      window                   ; zobrazen¡ okna
         mov       bp,offset tabl           ; tabulka definice okna
         call      window                   ; zobrazen¡ okna
         pop       bp
         ret

; -----------------------------------------------------------------------------

selmin:                                   ;* v˜bˆr soubor– pomoc¡ [-]

         mov       di,offset txtunsel       ; nadpis "zru¨it v˜bˆr"
         xor       bl,bl                    ; p©¡znak zru¨en¡ ozna‡en¡
         jmp       short selfile

; -----------------------------------------------------------------------------

selplus:                                  ;* v˜bˆr soubor– pomoc¡ [+]

         mov       di,offset txtsel         ; nadpis "vybrat soubory"
         mov       bl,80h                   ; p©¡znak nastaven¡ ozna‡en¡

selfile: mov       al,ds:[aktpage]          ; aktivn¡ str nka
         shr       al,1                     ; AL / 2
         xor       al,ds:[aktpage]          ; porovn n¡ str nek
         and       al,1                     ; souhlas¡ str nky ?
         jz        selplus0                 ; str nky souhlas¡
         ret
selplus0:mov       si,offset bufsel         ; buffer masek pro v˜bˆr
         mov       dl,8                     ; po‡ tek pro prav‚ okno
         cmp       byte ptr ds:[bp+pozl-tabl],40 ; je lev‚ nebo prav‚ okno ?
         jnb       selplus1                 ; je prav‚ okno
         mov       dl,46                    ; po‡ tek pro lev‚ okno
selplus1:mov       cl,25                    ; ¨¡©ka okna
         xor       dh,dh                    ; © dek okna automatick˜
         mov       ch,12                    ; maxim ln¡ v˜¨ka okna
         call      select                   ; zad n¡ masky
         mov       di,ds:[bp+adrfl-tabl]    ; seznam soubor–
         mov       cx,ds:[bp+numfl-tabl]    ; po‡et soubor– v seznamu
         jcxz      selplus6                 ; nen¡ ‘ dn˜ soubor
selplusb:lodsb
         cmp       al," "                   ; je mezera p©ed textem ?
         jz        selplusb                 ; vypu¨tˆn¡ mezer p©ed textem
         dec       si                       ; n vrat adresy za‡ tku ©etˆzce
                                          ;* smy‡ka ozna‡en¡ soubor–
selplus3:test      byte ptr ds:[di],50h     ; je disk nebo adres © ?
         jnz       selplus5                 ; je disk nebo adres ©
         push      di
         push      si
         push      cx
         inc       di                       ; za‡ tek jm‚na souboru
                                          ;* test znak– jm‚na
         mov       cx,8                     ; maxim ln¡ po‡et znak– jm‚na
         call      cmpname                  ; porovn n¡ jm‚na
         jnz       selplusm                 ; nen¡ shoda jm‚na
                                          ;* vypu¨tˆn¡ znak– * a .
         lodsb                              ; na‡ten¡ dal¨¡ho znaku
         or        al,al                    ; byl konec ?
         jz        selpluse                 ; byl konec textu
         cmp       al,"."                   ; je oddˆlovac¡ te‡ka ?
         jz        selplusi                 ; p©esko‡en¡ te‡ky
         cmp       al,"*"                   ; je n hradn¡ znak "*" ?
         jnz       selpluse                 ; nen¡ znak "*"
         lodsb                              ; na‡ten¡ dal¨¡ho znaku
         cmp       al,"."                   ; je te‡ka ?
         je        selplusi                 ; p©esko‡en¡ te‡ky
selpluse:dec       si                       ; n vrat pozice znaku
                                          ;* test znak– p©¡pony
selplusi:mov       cx,3                     ; maxim ln¡ po‡et znak– p©¡pony
         call      cmpname                  ; porovn n¡ p©¡pony
         jnz       selplusm                 ; nen¡ shoda p©¡pony

         mov       al,ds:[di-12]            ; atributy
         xor       al,bl                    ; zmˆna atribut–
         and       al,80h                   ; je zmˆna atribut– ?
         jz        selplusm                 ; nen¡ zmˆna atribut–
         and       byte ptr ds:[di-12],7fh  ; zru¨en¡ ozna‡en¡
         or        ds:[di-12],bl            ; nastaven¡ v˜bˆru souboru

         push      dx
         mov       ax,word ptr ds:[di]
         mov       dx,word ptr ds:[di+2]
         test      byte ptr ds:[di-12],80h
         jz        selplusj                 ; je zru¨en¡
         inc       word ptr ds:[bp+insfil-tabl]
         add       word ptr ds:[bp+kapfil-tabl],ax
         adc       word ptr ds:[bp+kapfil-tabl+2],dx
         jmp       short selplusk
selplusj:dec       word ptr ds:[bp+insfil-tabl]
         sub       word ptr ds:[bp+kapfil-tabl],ax
         sbb       word ptr ds:[bp+kapfil-tabl+2],dx
selplusk:pop       dx

selplusm:pop       cx
         pop       si
         pop       di
selplus5:add       di,20                    ; zv˜¨en¡ adresy polo‘ky v seznamu
         loop      selplus3                 ; test dal¨¡ho souboru

selplus6:push      bp
         mov       bp,offset tabl
         call      window
         mov       bp,offset tabr
         call      window
         pop       bp

selplus9:ret

; -----------------------------------------------------------------------------

cmpname:                                  ;* porovn n¡ jm‚na/p©¡pony souboru
                                            ; VSTUP: DS:SI=jm‚no ASCII
                                            ;        DS:DI=jm‚no ve vnit©. tvaru
                                            ;        CX=po‡et znak–
                                            ; VSTUP: SI,DI=nov‚ pozice

cmpname1:lodsb                              ; znak zad n¡
         call      highcase                 ; p©evod znaku na velk‚ p¡smeno
         or        al,al                    ; je konec zad n¡ ?
         jz        cmpname2                 ; je konec textu zad n¡
         cmp       al,"."                   ; je oddˆlovac¡ te‡ka ?
         jne       cmpname3                 ; nen¡ oddˆlovac¡ te‡ka
cmpname2:dec       si                       ; n vrat pozice
         mov       al," "                   ; n hradn¡ znak - mezera
cmpname3:cmp       al,"*"                   ; je n hradn¡ znak "*" ?
         jne       cmpname4
         dec       si                       ; n vrat ukazatele na znak "*"
         mov       al,"?"                   ; n hradn¡ znak "?"
cmpname4:cmp       al,"?"                   ; je n hradn¡ znak "?" ?
         je        cmpname5                 ; znak se nekontroluje
         cmp       al,ds:[di]               ; je shoda znak– ?
         jnz       cmpname6                 ; nen¡ shoda znak–
cmpname5:inc       di                       ; zv˜¨en¡ pozice v seznamu
         loop      cmpname1                 ; test dal¨¡ho znaku jm‚na
         xor       al,al                    ; p©¡znak ZF=shoda
cmpname6:ret

; -----------------------------------------------------------------------------

copyfile:                                 ;* kop¡rov n¡ souboru

         mov       si,offset bufcopy        ; buffer polo‘ek
         mov       di,offset txtcopy        ; text nadpis
         mov       ch,16
         mov       cl,76
         mov       dl,2
         call      select
         push      bp
         mov       bp,offset tabl
         call      window
         mov       bp,offset tabr
         call      window
         pop       bp
         ret

; -----------------------------------------------------------------------------

;numchar  db        0                        ; po‡et znak– v bufferu
;poztop   db        0                        ; pozice za‡ tku zobrazen‚ho textu
;pozcomm  db        0                        ; pozice za‡ tku textu na displeji
;pozbuf   db        0                        ; pozice kurzoru v textov‚m bufferu
;buftxt   db        129 dup(0)               ; textov˜ buffer
; -----------------------------------------------------------------------------

select:                                   ;* zad n¡ volby
                                            ; VSTUP: DS:SI=buffer pro zad n¡
                                            ;        DS:DI=text nadpisu
                                            ;        DL=po‡ tek okna (0=aut.)
                                            ;        DH=© dek okna (0=aut.)
                                            ;        CL=¨¡©ka okna (0=aut.)
                                            ;        CH=max. v˜¨ka okna
                                            ; VSTUP: DS:SI=ukazatel na ©etˆzec
                                            ;         AX=ukon‡ovac¡ kl vesa

         push      es
         push      ds
         pop       es
         push      bx
         push      dx
         push      cx
         call      testsel                  ; test bufferu zad n¡
         call      setwsel                  ; nastaven¡ parametr– okna voleb
         call      selwin                   ; vykreslen¡ okna pro volby
select1: call      selall                   ; zobrazen¡ polo‘ek voleb

                                            ;        BH=maxim. d‚lka ©etˆzce
                                            ;        BL=po‡et polo‘ek
                                            ;        DL=po‡ tek okna (0=aut.)
                                            ;        DH=© dek okna (0=aut.)
                                            ;        CL=¨¡©ka okna (0=aut.)
                                            ;        CH=max. v˜¨ka okna (0=max.)

select2: call      setkurzs                 ; nastaven¡ kurzoru pro select
         xor       ah,ah
         int       16h                      ; ‡ek n¡ na znak z kl vesnice
                                          ;* <Esc>
select21:
                                          ;* kurzor nahoru
select3: cmp       ax,4800h                 ; kurzor nahoru
         jne       select4
         mov       al,ds:[kurzsel]
         or        al,al                    ; je ji‘ prvn¡ pozice ?
         jz        select2                  ; je ji‘ prvn¡ pozice
select32:dec       al                       ; sn¡‘en¡ pozice kurzoru
         mov       ds:[kurzsel],al          ; nov  pozice kurzoru
         cmp       al,ds:[firstsel]         ; je ji‘ nad prvn¡ polo‘kou ?
         jnb       select31                 ; nen¡ nad prvn¡ polo‘kou
         dec       byte ptr ds:[firstsel]   ; sn¡‘en¡ prvn¡ polo‘ky
         jmp       short select1            ; nov‚ zobrazen¡ okna

select31:mov       al,ds:[kurzsel]          ; pozice s kurzorem
         call      selpol                   ; zobrazen¡ nov‚ polo‘ky
         inc       al                       ; nov  pozice
         call      selpol                   ; zobrazen¡ p–vodn¡ polo‘ky
         jmp       short select2
                                          ;* kurzor dol–
select4: cmp       ax,5000h                 ; kurzor dol–
         jne       select5
         mov       al,ds:[kurzsel]          ; pozice kurzoru
         inc       al                       ; nov  pozice kurzoru
         cmp       al,bl                    ; je ji‘ konec polo‘ek ?
         jnb       select2                  ; je ji‘ konec
         inc       byte ptr ds:[kurzsel]    ; zv˜¨en¡ pozice kurzoru
         sub       al,ds:[firstsel]         ; <- po‡et polo‘ek na displeji
         add       al,4                     ; + okraje
         cmp       al,ch                    ; nep©esahuje okraj okna ?
         jb        select41                 ; nen¡ je¨tˆ pod okrajem okna
         inc       byte ptr ds:[firstsel]   ; zv˜¨en¡ prvn¡ polo‘ky
         jmp       short select1
select41:mov       al,ds:[kurzsel]          ; pozice s kurzorem
         call      selpol                   ; zobrazen¡ nov‚ polo‘ky
         dec       al                       ; nov  pozice
         call      selpol                   ; zobrazen¡ p–vodn¡ polo‘ky
select45:jmp       select2
                                          ;* Page Up, Ctrl-Page Up
select5: cmp       ax,8400h                 ; je Ctrl-Page Up ?
         je        select51
         cmp       ax,4900h                 ; je str nka nahoru ? (=prvn¡ pol.)
         jne       select6
select51:cmp       byte ptr ds:[kurzsel],0  ; je ji‘ prvn¡ polo‘ka ?
         je        select45                 ; je ji‘ prvn¡ polo‘ka
select52:xor       al,al
         mov       ds:[firstsel],al         ; prvn¡ zobrazen˜ polo‘ka = 0
         mov       ds:[kurzsel],al          ; nastaven¡ kurzoru
select55:jmp       select1
                                          ;* Page Down, Ctrl-Page Down
select6: cmp       ax,7600h                 ; je Ctrl-Page Down
         je        select61
         cmp       ax,5100h                 ; je str nka dol– ?
         jne       select7
select61:mov       al,bl                    ; po‡et polo‘ek
         dec       al                       ; ‡¡slo posledn¡ polo‘ky
         cmp       al,ds:[kurzsel]          ; je ji‘ posledn¡ polo‘ka ?
         je        select45                 ; je ji‘ posledn¡ polo‘ka
         mov       ds:[kurzsel],al          ; nastaven¡ nov‚ho kurzoru
         sub       al,ch
         add       al,5                     ; p©i‡ten¡ okraj–
         mov       ds:[firstsel],al         ; nastaven¡ prvn¡ zobrazen‚ polo‘ky
         jmp       short select55

select7:                                  ;* je jin  kl vesa - editace
         cmp       al,27                    ; je <Esc> ?
         je        select22

         call      editsel                  ; editace © dku volby
         cmp       al,0dh                   ; je Enter ?
         je        select22                 ; je Enter
         jmp       select21                 ; obsluha ©¡dic¡ kl vesy

                                          ;* <Enter>
select22:
         pop       cx
         pop       dx
         pop       bx
         pop       es
         ret


; -----------------------------------------------------------------------------

editsel:                                  ;* editace polo‘ky volby
                                            ; VSTUP: AX=prvn¡ kl vesa
                                            ;        DS:SI=buffer polo‘ek
                                            ;        DL=po‡ tek okna
                                            ;        DH=© dek okna
                                            ;        CL=¨¡©ka okna
                                            ;        CH=v˜¨ka okna

         push      bx
         push      cx
         push      dx
         push      di

                                          ;* inicializace parametr– pro editaci
edits01: mov       byte ptr ds:[ukazsel],0  ; nulov n¡ ukazatele kurzoru
         mov       byte ptr ds:[topsel],0   ; nulov n¡ po‡ tku zobrazen¡
         mov       byte ptr ds:[numselb],0  ; nulov n¡ po‡tu znak– v bufferu
         cmp       al,32                    ; je platn˜ znak ?
         jnb       edits22                  ; je platn˜ znak ASCII - nulov n¡
         call      obnov                    ; obnoven¡ © dku
         jmp       short edits22            ; proveden¡ obsluhy kl vesy

edits2:  call      kurzedsel                ; zobrazen¡ kurzoru na © dku
edits21: xor       ah,ah
         int       16h                      ; ‡ek n¡ na vstup z kl vesnice
                                          ; <Esc>, <Enter>
edits22: cmp       al,27                    ; je <Esc> ?
         jz        edits27
         cmp       al,13                    ; je <Enter> ?
         jnz       edits28
         call      storesel                 ; ulo‘en¡ polo‘ky do bufferu
                                            ; DS:SI=adresa textu
edits27:
         pop       di
         pop       dx
         pop       cx
         pop       bx
         ret

edits28:                                  ;* platn˜ znak ASCII
         cmp       al,7fh
         je        edits3                   ; je znak Ctrl-<BS>
         cmp       al,32                    ; je platn˜ znak ?
         jb        edits3                   ; nen¡ platn˜ znak ASCII
         call      inschsel                 ; vlo‘en¡ znaku do bufferu SELECT
         jmp       short edits2
                                          ;* kurzor vlevo
edits3:  cmp       ax,4b00h                 ; je kurzor vlevo ?
         jne       edits4

         call      selleft                  ; posun kurzoru vlevo
         jmp       short edits2
                                          ;* kurzor vpravo
edits4:  cmp       ax,4d00h                 ; je kurzor vpravo
         jne       edits5

         call      selright                 ; posun kurzoru vpravo
         jmp       short edits2
                                          ;* za‡ tek © dku
edits5:  cmp       ax,4700h                 ; je kl vesa <Home> ?
         jne       edits6

         call      selhome                  ; posun na za‡ tek © dku
         jmp       short edits2
                                          ;* konec © dku
edits6:  cmp       ax,4f00h                 ; je kl vesa <End> ?
         jne       edits7

         call      selend                   ; posun na konec © dku
         jmp       short edits2
                                          ;* vymaz n¡ znaku za kurzorem Del
edits7:  cmp       ax,5300h                 ; je kl vesa <Delete> ?
         jne       edits8

         call      seldel                   ; vymaz n¡ znaku nad kurzorem
         jmp       short edits2

edits8:  cmp       al,8                     ; je kl vesa <BS> ?
         jne       edits9

         call      seldelbs                 ; vymaz n¡ znaku nad kurzorem
         jmp       short edits2

edits9:  cmp       al,19h                   ; je Ctrl-Y ?
         jne       editsa

         mov       byte ptr ds:[ukazsel],0  ; nulov n¡ ukazatele kurzoru
         mov       byte ptr ds:[topsel],0   ; nulov n¡ po‡ tku zobrazen¡
         mov       byte ptr ds:[numselb],0  ; nulov n¡ po‡tu znak– v bufferu
         call      obredit                  ; zobrazen¡ © dku
         jmp       short edits2

editsa:  cmp       al,0ch                   ; je Ctrl-L ?
         jne       editsb

         call      obnov                    ; obnoven¡ © dku textu
         jmp       edits2

editsb:  cmp       ax,7300h                 ; slovo vlevo
         jne       editsc

         call      selwleft                 ; skok o slovo vlevo
         jmp       edits2

editsc:  cmp       ax,7400h                 ; slovo vpravo
         jne       editsd

         call      selwright                ; skok o slovo vpravo
         jmp       edits2

editsd:  cmp       al,14h                   ; Ctrl-T
         jne       editse                   ; je vymaz n¡ slova vpravo

         call      delwr                    ; vymaz n¡ slova vpravo
         jmp       edits2

editse:  cmp       al,7fh                   ; Ctrl-<BS>
         jne       editsf

         call      delwl                    ; vymaz n¡ slova vlevo
         jmp       edits2

editsf:

editsx:
         cmp       ax,4800h                 ; kurzor nahoru ?
         je        editsx1
         cmp       ax,5000h                 ; kurzor dol– ?
         je        editsx1
         cmp       ax,4900h                 ; Pge Up ?
         je        editsx1
         cmp       ax,8400h                 ; Ctrl-Page Up
         je        editsx1
         cmp       ax,5100h                 ; Page Down ?
         je        editsx1
         cmp       ax,7600h                 ; Ctrl-Page Down ?
         je        editsx1
         jmp       edits21

editsx1: call      obnov                    ; obnoven¡ © dku
         jmp       edits27



;numselb  db        0                        ; po‡et znak– v bufferu pro v˜bˆr
;ukazsel  db        0                        ; ukazatel kurzoru pro buffer v˜bˆru
;topsel   db        0                        ; za‡ tek zobrazen‚ho textu volby
;firstsel db        0                        ; prvn¡ zobrazen  polo‘ka voleb
;kurzsel  db        0                        ; kurzor pro volby
; selbuf  db 128 dup(?)                     ; buffer

; -----------------------------------------------------------------------------

storesel:                                 ;* ulo‘en¡ polo‘ky do bufferu
                                            ; VSTUP:DS:SI=buffer polo‘ek
                                            ; VSTUP: DS:SI=adresa textu ASCIIZ

         push      ax
         push      bx
         push      cx
         push      di
         call      srcident                 ; nalezen¡ stejn‚ polo‘ky v seznamu
         jc        stores1                  ; stejn  polo‘ka nenalezena
         call      delselect                ; vypu¨tˆn¡ polo‘ky ze seznamu
stores1: mov       di,si                    ; za‡ tek bufferu
         mov       cx,ds:[si]               ; d‚lka bufferu
         inc       di
stores3: inc       di                       ; n sleduj¡c¡ adresa
         cmp       word ptr ds:[di],0       ; je konec bufferu ?
         loopnz    stores3                  ; nalezen¡ konce bufferu
         push      di
         sub       di,2
         cmp       si,di
         pop       di
         je        stores2
         inc       di
stores2: mov       bl,ds:[numselb]          ; d‚lka dat v bufferu
         xor       bh,bh
         push      bx
         add       bx,4                     ; rezerva pro koncov‚ nuly
         cmp       bx,cx                    ; posta‡uje kapacita ?
         pop       bx
         jb        stores4                  ; kapacita posta‡uje
         xor       al,al                    ; ru¨¡ se polo‘ka 0
         call      delselect                ; zru¨en¡ polo‘ky 0
         jmp       short stores1
stores4: mov       cx,bx                    ; d‚lka dat k p©enosu
         mov       si,offset selbuf         ; buffer s textem
         push      di                       ; adresa za‡ tku textu
         rep       movsb                    ; ulo‘en¡ textu
         xor       ax,ax
         stosw                              ; ulo‘en¡ koncov‚ho slova 0
         pop       si                       ; text zad n¡
         pop       di
         pop       cx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------

srcident:                                 ;* nalezen¡ stejn‚ polo‘ky ze seznamu
                                            ; VSTUP:DS:SI=buffer polo‘ek
                                            ; VSTUP: AL=‡¡slo polo‘ky
                                            ;         CY=polo‘ka nenalezena

         push      bx
         push      cx
         push      dx
         push      si
         push      di
                                          ;* nastaven¡ parametr–
         lodsw                              ; d‚lka bufferu
         mov       cx,ax                    ; d‚lka bufferu
         xor       bh,bh                    ; ‡¡ta‡ ©etˆzc– text–
         mov       bl,ds:[numselb]          ; d‚lka textu v bufferu
         or        bl,bl                    ; je nˆjak˜ znak v bufferu ?
         jz        srcid7                   ; nen¡ ‘ dn˜ znak v bufferu
                                          ;* za‡ tek porovn n¡ jedn‚ polo‘ky
srcid1:  jcxz      srcid7                   ; je konec bufferu - text nenalezen
         cmp       byte ptr ds:[si],0       ; je ji‘ konec bufferu ?
         je        srcid7                   ; je ji‘ konec bufferu - nenalezen
         mov       di,offset selbuf         ; buffer se zadan˜m textem
         push      bx                       ; £schova ‡¡ta‡e polo‘ek a d‚lky
                                          ;* cyklus porovn n¡ jednoho znaku
srcid2:  lodsb                              ; znak z bufferu polo‘ek
         or        al,al                    ; je ji‘ konec ©etˆzce ?
         jz        srcid3                   ; je konec ©etˆzce
         or        bl,bl                    ; je konec textu ?
         jz        srcid4                   ; je konec textu
         cmp       al,ds:[di]               ; porovn n¡ se zadan˜m textem
         jne       srcid4                   ; nen¡ shoda ©etˆzc–
         inc       di                       ; zv˜¨en¡ adresy zadan‚ho textu
         dec       bl                       ; sn¡‘en¡ ‡¡ta‡e d‚lky textu
         loop      srcid2                   ; test dal¨¡ho znaku
srcid3:  or        bl,bl                    ; je i konec textu ?
         pop       bx                       ; n vrat BX
         jz        srcid9                   ; jsou konce obou ©etˆzc–
         inc       bh                       ; zv˜¨en¡ ‡¡ta‡e polo‘ek
         jmp       short srcid1             ; nen¡ shoda ©etˆzc– - dal¨¡
                                          ;* nalezen rozd¡l ©etˆzc–
srcid4:  dec       cx
         jz        srcid6                   ; konec bufferu
srcid5:  lodsb
         or        al,al
         loopnz    srcid5                   ; nalezen¡ konce ©etˆzce
srcid6:  pop       bx
         inc       bh                       ; zv˜¨en¡ ‡¡ta‡e ©etˆzc–
         jmp       short srcid1             ; test dal¨¡ho ©etˆzce
                                          ;* je konec bufferu - text nenalezen
srcid7:  xor       bh,bh
         stc                                ; p©¡znak - polo‘ka nenalezena
srcid9:  mov       al,bh                    ; ‡¡slo nalezen‚ polo‘ky
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         ret

; -----------------------------------------------------------------------------

delselect:                                ;* vypu¨tˆn¡ polo‘ky ze seznamu
                                            ; VSTUP:DS:SI=buffer polo‘ek
                                            ;       AL=‡¡slo polo‘ky
                                            ; VSTUP: CY=polo‘ka nenalezena

         push      ax
         push      cx
         push      si
         push      di
                                          ;* nalezen¡ adresy ru¨en‚ polo‘ky
         mov       cx,ds:[si]               ; d‚lka bufferu
         add       cx,si                    ; adresa konce bufferu
         mov       ah,al                    ; ‡¡ta‡ ©etˆzc–
         add       si,2                     ; za‡ tek text–
delsel2: cmp       byte ptr ds:[si],0       ; je ji‘ konec bufferu ?
         stc                                ; p©¡znak chyby
         jz        delsel4                  ; je konec bufferu - nenalezen
         or        ah,ah                    ; je po‘adovan˜ ©etˆzec ?
         jz        delsel3                  ; nalezen po‘adovan˜ ©etˆzec
delsel5: lodsb                              ; na‡ten¡ znaku
         or        al,al                    ; je konec ©etˆzce ?
         jnz       delsel5                  ; nen¡ je¨tˆ konec ©etˆzce
         dec       ah                       ; sn¡‘en¡ ‡¡ta‡e ©etˆzc–
         jmp       short delsel2            ; dal¨¡ ©etˆzec

;         call      asksel                   ; nalezen¡ polo‘ky v seznamu
;         jc        delsel4                  ; polo‘ka nenalezena
delsel3: sub       cx,si                    ; d‚lka zbytku bufferu
         mov       di,si                    ; £schova za‡ tku ru¨en‚ polo‘ky
                                          ;* nalezen¡ adresy n sleduj¡c¡ polo‘ky
         cmp       byte ptr ds:[si],0       ; n sleduje nˆjak  polo‘ka ?
         jne       delsel1                  ; je nˆjak  polo‘ka
         mov       byte ptr ds:[di],0       ; zru¨en¡ polo‘ky
         jmp       short delsel4
delsel1: lodsb                              ; na‡ten¡ znaku polo‘ky
         or        al,al                    ; je konec polo‘ky ?
         loopnz    delsel1                  ; nalezen¡ konce polo‘ky
         jcxz      delsel4
         rep       movsb                    ; p©enos textu - vypu¨tˆn¡ polo‘ky
delsel4: pop       di
         pop       si
         pop       cx
         pop       ax
         ret

; -----------------------------------------------------------------------------

delwr:                                    ;* vymaz n¡ slova vpravo

         push      ax
         push      bx
         push      cx
         push      dx
                                          ;* vymaz n¡ platn˜ch znak–
delwr1:  mov       al,ds:[ukazsel]          ; pozice znaku v bufferu
         cmp       al,ds:[numselb]          ; je ji‘ na konci © dku ?
         je        delwr4                   ; je ji‘ posledn¡ pozice
         xor       ah,ah
         push      si
         mov       si,offset selbuf
         add       si,ax                    ; pozice znaku nad kurzorem
         lodsb                              ; na‡ten¡ znaku nad kurzorem
         pop       si
         call      testchar                 ; test, zda je oddˆlovac¡ znak
         jc        delwr2                   ; je oddˆlovac¡ znak
         call      seldel                   ; vymaz n¡ znaku nad kurzorem
         jmp       delwr1
                                          ;* vymaz n¡ oddˆlovac¡ch znak–
delwr2:  mov       al,ds:[ukazsel]          ; pozice znaku v bufferu
         cmp       al,ds:[numselb]          ; je ji‘ na konci © dku ?
         je        delwr4                   ; je ji‘ posledn¡ pozice
         xor       ah,ah
         push      si
         mov       si,offset selbuf
         add       si,ax                    ; pozice n sleduj¡c¡ho znaku
         lodsb                              ; na‡ten¡ n sleduj¡c¡ho znaku
         pop       si
         call      testchar                 ; test, zda je oddˆlovac¡ znak
         jnc       delwr4                   ; nen¡ oddˆlovac¡ znak - konec
         call      seldel                   ; vymaz n¡ znaku nad kurzorem
         jmp       short delwr2             ; dal¨¡ znak
delwr4:  pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------

delwl:                                    ;* vymaz n¡ slova vlevo

         push      ax
         push      bx
         push      cx
         push      dx
                                          ;* vymaz n¡ oddˆlovac¡ch znak–
delwl1:  mov       al,ds:[ukazsel]          ; pozice znaku v bufferu
         or        al,al
         jz        delwl4                   ; je ji‘ prvn¡ pozice
         xor       ah,ah
         push      si
         mov       si,offset selbuf
         dec       al
         add       si,ax                    ; pozice znaku p©ed kurzorem
         lodsb                              ; na‡ten¡ znaku p©ed kurzorem
         pop       si
         call      testchar                 ; test, zda je oddˆlovac¡ znak
         jnc       delwl2                   ; nen¡ oddˆlovac¡ znak
         call      seldelbs                 ; vymaz n¡ znaku p©ed kurzorem
         jmp       short delwl1
                                          ;* vymaz n¡ platn˜ch znak–
delwl2:  mov       al,ds:[ukazsel]          ; pozice znaku v bufferu
         or        al,al
         jz        delwl4                   ; je ji‘ prvn¡ pozice
         xor       ah,ah
         push      si
         mov       si,offset selbuf
         dec       al
         add       si,ax                    ; pozice znaku p©ed kurzorem
         lodsb                              ; na‡ten¡ znaku p©ed kurzorem
         pop       si
         call      testchar                 ; test, zda je oddˆlovac¡ znak
         jc        delwl3                   ; je oddˆlovac¡ znak - konec
         call      seldelbs                 ; vymaz n¡ znaku p©ed kurzorem
         jmp       short delwl2             ; dal¨¡ znak
delwl3:  mov       al,ds:[ukazsel]          ; pozice znaku v bufferu
         push      ax                       ; £schova pozice
         call      selleft                  ; posun o znak vlevo
         call      selleft                  ; posun o znak vlevo
         call      selleft                  ; posun o znak vlevo
         call      selleft                  ; posun o znak vlevo
         call      selleft                  ; posun o znak vlevo
         call      selleft                  ; posun o znak vlevo
         pop       ax
delwl31: cmp       al,ds:[ukazsel]          ; je ji‘ navr cena pozice ?
         jbe       delwl32                  ; pozice je navr cena
         push      ax
         call      selright                 ; posun o znak vpravo
         pop       ax
         jmp       short delwl31            ; posun o dal¨¡ znak
delwl32: call      obredit                  ; zobrazen¡ © dku
delwl4:  pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------

selwleft:                                 ;* posun o slovo vlevo

         push      ax
         push      bx
         push      cx
         push      dx
                                          ;* vypu¨tˆn¡ oddˆlovac¡ch znak–
selwl1:  call      selleft                  ; posun o znak vlevo
         mov       al,ds:[ukazsel]          ; pozice znaku v bufferu
         or        al,al                    ; je ji‘ na prvn¡ pozici ?
         jz        selwl4                   ; je ji‘ prvn¡ pozice
         xor       ah,ah
         push      si
         mov       si,offset selbuf
         add       si,ax                    ; pozice nov‚ho znaku
         lodsb                              ; na‡ten¡ znaku
         pop       si
         call      testchar                 ; test, zda je oddˆlovac¡ znak
         jc        selwl1                   ; je oddˆlovac¡ znak - dal¨¡ znak
                                          ;* vypu¨tˆn¡ platn˜ch znak–
selwl2:  mov       al,ds:[ukazsel]          ; pozice znaku v bufferu
         or        al,al                    ; je ji‘ na prvn¡ pozici ?
         jz        selwl4                   ; je ji‘ prvn¡ pozice
         xor       ah,ah
         push      si
         mov       si,offset selbuf
         dec       al                       ; n sleduj¡c¡ znak
         add       si,ax                    ; pozice n sleduj¡c¡ho znaku
         lodsb                              ; na‡ten¡ n sleduj¡c¡ho znaku
         pop       si
         call      testchar                 ; test, zda je oddˆlovac¡ znak
         jc        selwl4                   ; je oddˆlovac¡ znak - konec
         call      selleft                  ; posun na dal¨¡ pozici
         jmp       short selwl2             ; dal¨¡ znak
selwl4:  pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------

selwright:                                ;* posun o slovo vpravo

         push      ax
         push      bx
         push      cx
         push      dx
                                          ;* vypu¨tˆn¡ platn˜ch znak–
selwr1:  mov       al,ds:[ukazsel]          ; pozice znaku v bufferu
         cmp       al,ds:[numselb]          ; je ji‘ na konci © dku ?
         je        selwr4                   ; je ji‘ posledn¡ pozice
         push      ax                       ; £schova pozice
         call      selright                 ; posun o znak vpravo
         pop       ax
         xor       ah,ah
         push      si
         mov       si,offset selbuf
         add       si,ax                    ; pozice posledn¡ho znaku
         lodsb                              ; na‡ten¡ posledn¡ho znaku
         pop       si
         call      testchar                 ; test, zda je oddˆlovac¡ znak
         jnc       selwr1                   ; nebyl oddˆlovac¡ znak - dal¨¡ znak
                                          ;* vypu¨tˆn¡ oddˆlovac¡ch znak–
selwr2:  mov       al,ds:[ukazsel]          ; pozice znaku v bufferu
         cmp       al,ds:[numselb]          ; je ji‘ na konci © dku ?
         jz        selwr4                   ; je ji‘ posledn¡ pozice
         xor       ah,ah
         push      si
         mov       si,offset selbuf
         add       si,ax                    ; pozice n sleduj¡c¡ho znaku
         lodsb                              ; na‡ten¡ n sleduj¡c¡ho znaku
         pop       si
         call      testchar                 ; test, zda je oddˆlovac¡ znak
         jnc       selwr4                   ; nen¡ oddˆlovac¡ znak - konec
         call      selright                 ; posun na dal¨¡ pozici
         jmp       short selwr2             ; dal¨¡ znak
selwr4:  pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------

testchar:                                 ;* test znaku, zda je oddˆlovac¡
                                            ; VSTUP: AL=znak
                                            ; VSTUP: CY=je oddˆlovac¡ znak

         cmp       al,"!"
         je        testch5
         cmp       al,"#"
         je        testch5
         cmp       al,"$"
         je        testch5
         cmp       al,"%"
         je        testch5
         cmp       al,"*"
         je        testch5
         cmp       al,"0"
         jb        testch6
         cmp       al,"9"+1
         jb        testch5
         cmp       al,"@"
         jb        testch6
         cmp       al,"Z"+1
         jb        testch5
         cmp       al,"a"
         jb        testch6
         cmp       al,"z"+1
         jb        testch5
         cmp       al,128
         jb        testch6
testch5: clc                                ; p©¡znak - nen¡ oddˆlovac¡ znak
         ret
testch6: stc                                ; p©¡znak - je oddˆlovac¡ znak
         ret

; -----------------------------------------------------------------------------

obnov:                                    ;* obnoven¡ © dku textu
                                            ; VSTUP:DS:SI=buffer polo‘ek
                                            ;       DL=po‡ tek okna
                                            ;       DH=© dek okna
                                            ;       CL=¨¡©ka okna
                                            ;       CH=v˜¨ka okna

         push      ax
         push      cx
         push      si
         mov       byte ptr ds:[ukazsel],0  ; nulov n¡ ukazatele kurzoru
         mov       byte ptr ds:[topsel],0   ; nulov n¡ po‡ tku zobrazen¡
         mov       al,ds:[kurzsel]          ; ‡¡slo editovan‚ polo‘ky
         call      asksel                   ; poskytnut¡ adresy polo‘ky
         cmp       al,128                   ; je d‚lka vˆt¨¡ ne‘ 128 ?
         jna       obnov1                   ; d‚lka nen¡ vˆt¨¡ ne‘ 128
         mov       al,128                   ; omezen¡ na d‚lku 128
obnov1:  mov       ds:[numselb],al          ; po‡et znak– textu
         mov       cl,al                    ; d‚lka polo‘ky
         xor       ch,ch                    ; CH <- 0
         jcxz      obnov2                   ; nen¡ ‘ dn˜ znak k p©enosu
         mov       di,offset selbuf         ; buffer pro editaci textu
         rep       movsb                    ; p©enos textu do bufferu
obnov2:  pop       si
         pop       cx
         pop       ax
         call      obredit                  ; zobrazen¡ © dku
         call      kurzedsel                ; zobrazen¡ kurzoru na © dku
         ret

; -----------------------------------------------------------------------------

seldel:                                   ;* vymaz n¡ znaku nad kurzorem

         push      ax
         push      cx
         push      dx
         push      si
         push      di
         mov       dl,ds:[ukazsel]          ; pozice v bufferu
         xor       dh,dh
         mov       di,offset selbuf
         add       di,dx                    ; adresa znaku v bufferu
         mov       si,di
         inc       si
         mov       cx,128
         sub       cx,dx                    ; po‡et znak– za kurzorem
         rep       movsb                    ; p©enos textu
         pop       di
         pop       si
         pop       dx
         pop       cx
         mov       al,ds:[ukazsel]          ; ukazatel kurzoru
         cmp       al,ds:[numselb]          ; je nˆjak˜ znak za kurzorem ?
         jae       seldel1                  ; za kurzorem nen¡ ‘ dn˜ znak
         dec       byte ptr ds:[numselb]    ; sn¡‘en¡ po‡tu znak– v bufferu
         call      obredit                  ; zobrazen¡ textu
seldel1: pop       ax
         ret

; -----------------------------------------------------------------------------

seldelbs:                                 ;* zru¨en¡ znaku p©ed kurzorem

         push      dx
         cmp       byte ptr ds:[ukazsel],0  ; je ji‘ za‡ tek bufferu ?
         je        seldbs1                  ; je ji‘ za‡ tek bufferu
         call      selleft                  ; posun kurzoru vlevo
         call      seldel                   ; vymaz n¡ znaku nad kurzorem
seldbs1: pop       dx
         ret

; -----------------------------------------------------------------------------

selend:                                   ;* posun na konec © dku

         push      ax
         mov       al,ds:[numselb]          ; po‡et znak– v bufferu
         or        al,al                    ; posledn¡ pozice textu
         jz        selend2                  ; nen¡ ‘ dn˜ znak textu
         cmp       al,ds:[ukazsel]          ; pozice v bufferu
         je        selend2                  ; je ji‘ na posledn¡ pozici bufferu
         mov       ds:[ukazsel],al          ; nastaven¡ posledn¡ pozice bufferu
         push      cx
         sub       cl,11                    ; skute‡n  ¨¡©ka © dku
         sub       al,cl                    ; pozice po‡ tku © dku
         jns       selend1                  ; je p©ekro‡en¡ konce © dku
         xor       al,al                    ; po‡ te‡n¡ pozice = 0
selend1: mov       ds:[topsel],al           ; po‡ te‡n¡ zobrazen¡ textu
         pop       cx
         call      obredit                  ; zobrazen¡ textu
selend2: pop       ax
         ret

; -----------------------------------------------------------------------------

selhome:                                  ;* posun na za‡ tek © dku

         push      ax
         xor       al,al
         cmp       al,ds:[ukazsel]          ; je ji‘ na prvn¡ pozici ?
         je        selhom1                  ; je ji‘ na prvn¡ pozici
         mov       ds:[ukazsel],al          ; pozice kurzoru v bufferu
         mov       ds:[topsel],al           ; prvn¡ zobrazen  pozice textu
         call      obredit                  ; zobrazen¡ textu
selhom1: pop       ax
         ret

; -----------------------------------------------------------------------------

selright:                                   ; posun o znak vpravo

         push      di
         push      cx
         push      dx
         sub       cl,12                    ; ¨¡©ka textu
         mov       dl,ds:[ukazsel]          ; pozice znaku v bufferu
         cmp       dl,ds:[numselb]          ; je ji‘ konec textu ?
         jae       selrg2                   ; je ji‘ konec textu
         inc       byte ptr ds:[ukazsel]    ; ulo‘en¡ nov‚ pozice
         push      dx
         add       dl,3
         cmp       dl,ds:[numselb]          ; je p©edposledn¡ znak ?
         pop       dx
         je        selrg1                   ; je posledn¡ znak
         sub       dl,ds:[topsel]           ; offset od za‡ tku zobraz. textu
         cmp       dl,cl                    ; je p©ekro‡en konec © dku ?
         jb        selrg2                   ; nen¡ p©ekro‡en konec © dku
         inc       byte ptr ds:[topsel]     ; zv˜¨en¡ pozice za‡ tku textu
selrg1:  pop       dx
         pop       cx
         call      obredit                  ; zobrazen¡ textu
         push      cx
         push      dx
selrg2:  pop       dx
         pop       cx
         pop       di
         ret

; -----------------------------------------------------------------------------

selleft:                                    ; posun o znak vlevo

         push      dx
         mov       dl,ds:[ukazsel]          ; pozice znaku v bufferu
         or        dl,dl                    ; je ji‘ za‡ tek ?
         jz        selleft1                 ; je ji‘ na za‡ tku
         dec       byte ptr ds:[ukazsel]    ; sn¡‘en¡ pozice v bufferu
         dec       dl
         cmp       dl,ds:[topsel]           ; pozice za‡ tku textu
         ja        selleft1                 ; nen¡ je¨tˆ na za‡ tku
         cmp       byte ptr ds:[topsel],0   ; je ji‘ za‡ tek textu ?
         je        selleft1                 ; je ji‘ za‡ tek textu
         dec       byte ptr ds:[topsel]     ; sn¡‘en¡ za‡ tku textu
         pop       dx
         call      obredit                  ; zobrazen¡ p©¡kazov‚ho © dku
         push      dx
selleft1:pop       dx
         ret

; -----------------------------------------------------------------------------

inschsel:                                 ;* vlo‘en¡ znaku do bufferu SELECT
                                            ; VSTUP: AL=znak k ulo‘en¡

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di

         cmp       byte ptr ds:[numselb],128 ; po‡et znak– v bufferu
         jnb       inschs3                   ; je ji‘ maxim ln¡ po‡et znak–
                                          ;* nejd©¡ve se vytvo©¡ m¡sto pro znak
         mov       bl,cl                    ; ¨¡©ka okna
         sub       bl,10                    ; skute‡n  ¨¡©ka textu
         mov       cx,127
         sub       cl,ds:[ukazsel]           ; po‡et znak– do zbytku © dku
         mov       di,offset selbuf + 127
         mov       si,offset selbuf + 126
         std                                ; smˆr p©enosu dol–
         rep       movsb                    ; p©enos textu - Insert
         cld                                ; smˆr p©enosu nahoru
                                          ;* ulo‘en¡ nov‚ho znaku
         mov       dl,ds:[ukazsel]           ; pozice v bufferu
         xor       dh,dh
         mov       di,offset selbuf         ; buffer textu
         add       di,dx                    ; adresa znaku v bufferu
         stosb                              ; ulo‘en¡ znaku
                                          ;* zv˜¨en¡ ukazatel–
         inc       byte ptr ds:[ukazsel]    ; zv˜¨en¡ pozice v bufferu
         inc       byte ptr ds:[numselb]    ; zv˜¨en¡ po‡tu znak– v bufferu
         inc       dl                       ; nov  pozice v bufferu
         sub       dl,ds:[topsel]           ; offset od za‡ tku © dku
         cmp       dl,bl                    ; p©esahuje konec © dku ?
         jb        inschs3
         inc       byte ptr ds:[topsel]     ; zv˜¨en¡ po‡ te‡n¡ho offsetu
         stc
inschs3: pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         jnc       inschs2                  ; neprovede se zobrazen¡ © dku
         call      obredit                  ; zobrazen¡ editovan‚ho © dku
inschs2: ret

; -----------------------------------------------------------------------------

kurzedsel:                                ;* zobrazen¡ kurzoru p©i editaci SEL.
                                            ; VSTUP: DL=po‡ tek okna
                                            ;        DH=© dek okna
                                            ;        CL=¨¡©ka okna
                                            ;        CH=v˜¨ka okna

         push      ax
         push      dx
         add       dh,2                     ; p©esko‡en¡ horn¡ho okraje
         add       dh,ds:[kurzsel]          ; p©i‡ten¡ ‡¡sla polo‘ky s kurzorem
         sub       dh,ds:[firstsel]         ; ode‡ten¡ ‡¡sla prvn¡ polo‘ky
         add       dl,5                     ; p©esko‡en¡ lev‚ho okraje
         add       dl,ds:[ukazsel]          ; p©i‡ten¡ ukazatele v bufferu
         sub       dl,ds:[topsel]           ; ode‡ten¡ po‡ tku zobrazen¡
         xor       al,al                    ; p©¡znak nastaven¡ kurzoru
         call      outch1                   ; nastaven¡ pozice kurzoru
         pop       dx
         pop       ax
         ret

; -----------------------------------------------------------------------------

obredit:                                  ;* zobrazen¡ editovan‚ho © dku
                                            ; VSTUP: DL=po‡ tek okna
                                            ;        DH=© dek okna
                                            ;        CL=¨¡©ka okna
                                            ;        CH=v˜¨ka okna
         push      si
         push      ax
         push      bx
         push      cx
         push      dx
         mov       al,3                     ; barva pro zobrazen¡ textu
         call      outch1                   ; nastaven¡ barvy 3
         mov       al,ds:[kurzsel]          ; © dek s kurzorem
         sub       al,ds:[firstsel]         ; offset © dku s kurzorem
         add       dh,2
         add       dh,al                    ; © dek s kurzorem
         add       dl,5                     ; po‡ tek pro zobrazen¡ textu
         xor       ch,ch                    ; CH <- 0
         sub       cl,10                    ; skute‡n  d‚lka textu na © dku
         mov       si,offset selbuf         ; buffer textu
         mov       bl,ds:[topsel]           ; po‡ te‡n¡ offset textu
         xor       bh,bh
         add       si,bx                    ; adresa zobrazen‚ ‡ sti textu
         mov       bl,ds:[numselb]          ; po‡et znak– v bufferu
         sub       bl,ds:[topsel]           ; ode‡ten¡ po‡ te‡n¡ho offsetu
         or        bl,bl                    ; je nˆjak˜ znak k zobrazen¡ ?
         jz        obredit2                 ; nen¡ ‘ dn˜ znak k zobrazen¡
obredit1:lodsb                              ; na‡ten¡ znaku k zobrazen¡
         call      outch1                   ; zobrazen¡ znaku
         dec       bl                       ; je ji‘ konec textu ?
         jz        obredit4                 ; je ji‘ konec textu
         loop      obredit1                 ; zobrazen¡ dal¨¡ho znaku
         jmp       obredit3                 ; nezobraz¡ se dal¨¡ znak
obredit4:dec       cx                       ; sn¡‘en¡ ‡¡ta‡e znak–
obredit2:mov       al," "                   ; mazac¡ znak mezery
         call      outch                    ; vymaz n¡ zbytku © dku
obredit3:pop       dx
         pop       cx
         pop       bx
         pop       ax
         pop       si
         ret

; -----------------------------------------------------------------------------

setkurzs:                                 ;* nastaven¡ kurzoru pro select
                                            ; VSTUP:DS:SI=buffer polo‘ek
                                            ;       DL=po‡ tek okna
                                            ;       DH=© dek okna
                                            ;       CL=¨¡©ka okna
                                            ;       CH=v˜¨ka okna
         push      ax
         push      dx
         add       dh,2                     ; p©i‡ten¡ horn¡ho okraje
         mov       al,ds:[kurzsel]          ; kurzor
         sub       al,ds:[firstsel]         ; prvn¡ zobrazen  polo‘ka
         add       dh,al                    ; offset © dku s kurzorem
         add       dl,5                     ; offset od lev‚ho okraje
         xor       al,al                    ; p©¡znak - nastaven¡ kurzoru
         call      outch1                   ; nastaven¡ pozice kurzoru
         pop       dx
         pop       ax
         ret

; -----------------------------------------------------------------------------

selall:                                   ;* zobrazen¡ v¨ech polo‘ek voleb
                                            ; VSTUP:DS:SI=buffer polo‘ek
                                            ;       DL=po‡ tek okna
                                            ;       DH=© dek okna
                                            ;       CL=¨¡©ka okna
                                            ;       CH=v˜¨ka okna

         push      ax
         push      cx
         push      dx
         mov       al,ds:[firstsel]         ; prvn¡ zobrazen  polo‘ka voleb
         mov       ah,ch                    ; v˜¨ka okna
         sub       ah,4                     ; po‡et polo‘ek k zobrazen¡
         jbe       selall2                  ; je mal˜ po‡et polo‘ek
selall1: call      selpol                   ; zobrazen¡ jedn‚ polo‘ky volby
         inc       al                       ; zv˜¨en¡ ‡¡sla polo‘ky
         dec       ah                       ; sn¡‘en¡ ‡¡ta‡e © dk–
         jnz       selall1                  ; zobrazen¡ dal¨¡ polo‘ky
selall2: pop       dx
         pop       cx
         pop       ax
         ret

; -----------------------------------------------------------------------------

selpol:                                   ;* zobrazen¡ jedn‚ polo‘ky volby
                                            ; VSTUP: DS:SI=buffer polo‘ek
                                            ;        AL=‡¡slo polo‘ky k zobraz.
                                            ;        DH=po‡ te‡n¡ © dek okna
                                            ;        DL=po‡ te‡n¡ pozice okna
                                            ;        CL=¨¡©ka okna
                                            ;        CH=v˜¨ka okna

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         cmp       al,ds:[firstsel]         ; je pod prvn¡ polo‘kou ?
         jb        selpol6                  ; je pod prvn¡ polo‘kou
         push      ax
         mov       ah,7                     ; barva pro norm ln¡ polo‘ku
         cmp       al,ds:[kurzsel]          ; je polo‘ka s kurzorem ?
         jne       selpol0                  ; nen¡ polo‘ka s kurzorem
         mov       ah,3                     ; barva polo‘ky s kurzorem
selpol0: mov       al,ah                    ; barva pro zobrazen¡ polo‘ky
         call      outch1                   ; nastaven¡ barvy textu
         pop       ax
         mov       ah,dh                    ; £schova po‡ te‡n¡ho © dku
         add       ah,ch                    ; koncov˜ © dek
         sub       ah,2                     ; ode‡ten¡ spodn¡ho okraje
         add       dh,2                     ; p©i‡ten¡ horn¡ho okraje
         add       dh,al                    ; p©i‡ten¡ ‡¡sla polo‘ky
         sub       dh,ds:[firstsel]         ; ode‡ten¡ po‡ te‡n¡ polo‘ky
         cmp       dh,ah                    ; p©esahuje polo‘ka spodn¡ okraj ?
         ja        selpol6                  ; polo‘ka je mimo rozsah
         add       dl,5                     ; po‡ tek pro zobrazen¡ textu
         xor       ch,ch                    ; CH <- 0
         sub       cl,10                    ; ode‡ten¡ okraj– okna
         jnc       selpol1                  ; nen¡ podte‡en¡ ¨¡©ky okna
         xor       cl,cl                    ; n hradn¡ ¨¡©ka textu
selpol1: call      asksel                   ; poskytnut¡ adresy polo‘ky
         cmp       al,cl                    ; je polo‘ka vˆt¨¡ ne‘ maxim ln¡ ?
         jbe       selpol2                  ; je men¨¡ nebo shodn 
         mov       al,cl                    ; maxim ln¡ d‚lka polo‘ky k tisku
selpol2: mov       bl,al                    ; d‚lka zobrazen‚ polo‘ky
         sub       cl,bl                    ; zbytek © dku
         or        bl,bl                    ; je nˆjak˜ znak ?
         jz        selpol4                  ; nen¡ ji‘ dal¨¡ znak
selpol3: lodsb                              ; znak k tisku
         call      outch1                   ; zobrazen¡ znaku
         dec       bl                       ; sn¡‘en¡ ‡¡ta‡e znak–
         jnz       selpol3                  ; dal¨¡ znak
selpol4: jcxz      selpol6                  ; nen¡ ‘ dn  mezera k tisku
         mov       al," "                   ; znak mezery k tisku
selpol5: call      outch1                   ; zobrazen¡ mezery
         loop      selpol5                  ; zobrazen¡ dal¨¡ mezery
selpol6: pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------

selwin:                                   ;* vykreslen¡ okna pro volby
                                            ; VSTUP:DS:DI=text nadpisu
                                            ;       DL=po‡ tek okna
                                            ;       DH=© dek okna
                                            ;       CL=¨¡©ka okna
                                            ;       CH=v˜¨ka okna

         push      ax
         mov       al,7                     ; barva pro okno volby
         call      outch1                   ; nastaven¡ barvy ‡¡slo 7
         call      podklad                  ; vykreslen¡ podkladu okna
         call      wram1                    ; zobrazen¡ vnit©n¡ho r me‡ku
         call      wram2                    ; zobrazen¡ vnˆj¨¡ho r me‡ku
         call      wnadpis                  ; zobrazen¡ nadpisu okna
         call      stinw                    ; zobrazen¡ st¡nu okna
         pop       ax
         ret

; -----------------------------------------------------------------------------

podklad:                                  ;* vykreslen¡ podkladu pod oknem
                                            ; VSTUP: DL=po‡ tek okna
                                            ;        DH=© dek okna
                                            ;        CL=¨¡©ka okna
                                            ;        CH=v˜¨ka okna

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         mov       ax,600h                  ; funkce vymaz n¡ okna
         mov       bh,ds:[color]            ; barva pro podklad okna
         xchg      cx,dx                    ; CX <- pozice lev‚ho horn¡ho rohu
         add       dl,cl                    ; koncov  pozice + 1
         dec       dl                       ; koncov  pozice
         add       dh,ch                    ; koncov˜ © dek + 1
         dec       dh                       ; koncov˜ © dek
         int       10h                      ; vymaz n¡ okna
         pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------

wram1:                                    ;* zobrazen¡ vnit©n¡ho r mu okna
                                            ; VSTUP: DL=po‡ tek okna
                                            ;        DH=© dek okna
                                            ;        CL=¨¡©ka okna
                                            ;        CH=v˜¨ka okna

         push      ax
         push      cx
         push      dx
                                          ;* inicializace parametr–
         inc       dh                       ; druh˜ © dek
         add       dl,3                     ; po‡ te‡n¡ pozice
         sub       cl,8                     ; ¨¡©ka okna - 8
         sub       ch,4                     ; v˜¨ka okna - 4
                                          ;* vykreslen¡ horn¡ linky
         push      cx                       ; rozmˆry okna
         push      dx                       ; sou©adnice okna
         mov       al,218                   ; lev˜ horn¡ roh
         call      outch1                   ; vykreslen¡ lev‚ho horn¡ho rohu
         xor       ch,ch                    ; CH <- 0
         mov       al,196                   ; vodorovn  linka
         call      outch                    ; vykreslen¡ horn¡ linky
         mov       al,191                   ; prav˜ horn¡ roh
         call      outch1                   ; vykreslen¡ prav‚ho horn¡ho rohu
         pop       dx
         pop       cx
         inc       dh                       ; © dek + 1
                                          ;* vykreslen¡ vnit©n¡ch © dk–
wram11:  push      cx
         push      dx
         mov       al,179                   ; lev  linka
         call      outch1                   ; vykreslen¡ lev‚ linky
         add       dl,cl                    ; zv˜¨en¡ pozice
         call      outch1                   ; vykreslen¡ prav‚ linky
         pop       dx
         pop       cx
         inc       dh                       ; zv˜¨en¡ © dku
         dec       ch                       ; sn¡‘en¡ ‡¡ta‡e © dk–
         jnz       wram11                   ; dal¨¡ © dek
                                          ;* zobrazen¡ spodn¡ linky
         mov       al,192                   ; lev˜ doln¡ roh
         call      outch1                   ; vykreslen¡ lev‚ho doln¡ho rohu
         mov       al,196                   ; vodorovn  linka
         call      outch                    ; vykreslen¡ doln¡ linky
         mov       al,217                   ; prav˜ doln¡ roh
         call      outch1                   ; vykreslen¡ prav‚ho doln¡ho rohu
         pop       dx
         pop       cx
         pop       ax
         ret

; -----------------------------------------------------------------------------

wram2:                                    ;* zobrazen¡ vnˆj¨¡ho r mu okna
                                            ; VSTUP: DL=po‡ tek okna
                                            ;        DH=© dek okna
                                            ;        CL=¨¡©ka okna
                                            ;        CH=v˜¨ka okna

         push      ax
         push      cx
         push      dx
         mov       al,176                   ; zobrazovan˜ znak
                                          ;* zobrazen¡ horn¡ linky
         push      cx
         push      dx
         xor       ch,ch
         call      outch                    ; horn¡ linka
         pop       dx
         pop       cx
         inc       dh                       ; zv˜¨en¡ ‡¡sla © dku
         sub       ch,2                     ; ode‡ten¡ horn¡ho a doln¡ho okraje
wram21:                                   ;* zobrazen¡ vnit©n¡ch © dk–
         push      cx
         push      dx
         call      outch1                   ; lev˜ okraj
         call      outch1
         add       dl,cl                    ; zv˜¨en¡ pozice pro prav˜ okraj
         sub       dl,4                     ; ode‡ten¡ okraj–
         call      outch1
         call      outch1                   ; prav˜ okraj
         pop       dx
         pop       cx
         inc       dh                       ; zv˜¨en¡ ‡¡sla © dku
         dec       ch                       ; sn¡‘en¡ ‡¡ta‡e © dk–
         jnz       wram21                   ; zobrazen¡ dal¨¡ho © dku
                                          ;* zobrazen¡ spodn¡ho © dku
         xor       ch,ch
         call      outch                    ; spodn¡ linka

         pop       dx
         pop       cx
         pop       ax
         ret

; -----------------------------------------------------------------------------

wnadpis:                                  ;* zobrazen¡ nadpisu okna
                                            ; VSTUP:DS:DI=text nadpisu
                                            ;       DL=po‡ tek okna
                                            ;       DH=© dek okna
                                            ;       CL=¨¡©ka okna
                                            ;       CH=v˜¨ka okna
         push      ax
         push      bx
         push      cx
         push      dx                       ; £schova sou©adnice
         push      si
         push      di
         mov       si,di                    ; text
         xor       bx,bx                    ; ‡¡ta‡ d‚lky ©etˆzce
         xor       al,al
wnadp1:  cmp       al,ds:[di]               ; je ji‘ konec ©etˆzce ?
         je        wnadp2                   ; je konec ©etˆzce
         inc       bx                       ; zv˜¨en¡ ‡¡ta‡e znak– ©etˆzce
         inc       di                       ; zv˜¨en¡ ukazatele textu
         jmp       short wnadp1             ; test dal¨¡ho znaku
wnadp2:  or        bh,bh                    ; je d‚lka vˆt¨¡ ne‘ 255 ?
         jz        wnadp3                   ; d‚lka nen¡ vˆt¨¡ ne‘ 255
         mov       bl,255                   ; n hradn¡ d‚lka 255
wnadp3:  shr       bl,1                     ; d‚lka ©etˆzce / 2
         shr       cl,1                     ; ¨¡©ka okna / 2
         add       dl,cl                    ; prost©edek okna
         sub       dl,bl                    ; po‡ te‡n¡ pozice pro tisk textu
         inc       dh                       ; prvn¡ © dek
         call      outtxt                   ; zobrazen¡ textu nadpisu
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

; -----------------------------------------------------------------------------

stinw:                                    ;* zobrazen¡ st¡nu pod oknem
                                            ; VSTUP: DL=po‡ tek okna
                                            ;        DH=© dek okna
                                            ;        CL=¨¡©ka okna
                                            ;        CH=v˜¨ka okna

         push      ax
         push      bx
         push      si
                                          ;* nastaven¡ offsetu pro st¡n
         push      cx
         push      dx
         mov       ah,2ch                   ; funkce poskytnut¡ syst. ‡asu
         int       21h                      ; poskytnut¡ syst‚mov‚ho ‡asu
         mov       al,ch                    ; hodina
         mov       si,offset tabstin        ; tabulka hodin pro st¡n
         mov       cx,6                     ; po‡et hodnot v tabulce
         mov       bl,3                     ; v˜choz¡ hodnota offsetu st¡nu
stinw1:  cmp       al,ds:[si]               ; je men¨¡ ne‘ £daj hodin ?
         jb        stinw2                   ; nalezen ‡asov˜ interval
         inc       si                       ; zv˜¨en¡ ukazatele v tabulce
         dec       bl                       ; posun st¡nu o znak vlevo
         loop      stinw1                   ; test dal¨¡ho ‡asu
stinw2:  pop       dx
         pop       cx
         cmp       bl,-3                    ; je pozdˆ ve‡er ?
         je        stinw5                   ; je noc
         cmp       bl,3                     ; je brzo r no ?
         je        stinw5                   ; je noc
                                          ;* zobrazen¡ st¡nu
         push      cx
         push      dx
         inc       dh                       ; zv˜¨en¡ © dku
stinw3:  call      obrstin                  ; zobrazen¡ st¡nu na © dku
         inc       dh                       ; zv˜¨en¡ ‡¡sla © dku
         dec       ch                       ; sn¡‘en¡ ‡¡ta‡e © dk–
         jnz       stinw3                   ; zobrazen¡ dal¨¡ho © dku
         dec       dh                       ; n vrat posledn¡ho © dku
         add       dl,bl                    ; po‡ tek st¡nu
stinw4:  call      stin                     ; zobrazen¡ st¡nu v posledn¡m © dku
         loop      stinw4                   ; zobrazen¡ dal¨¡ho znaku v © dku
         pop       dx
         pop       cx

stinw5:  pop       si
         pop       bx
         pop       ax
         ret


; -----------------------------------------------------------------------------

obrstin:                                  ;* vykreslen¡ st¡nu na © dku
                                            ; VSTUP:DL=pozice za‡ tku okna
                                            ;       DH=© dek pro zobrazen¡
                                            ;       CL=¨¡©ka okna
                                            ;       BL=offset st¡nu
         push      ax
         push      cx
         push      dx
         or        bl,bl                    ; je offset z porn˜ ?
         js        obrst0                   ; offset je z porn˜
         add       dl,cl                    ; p©i‡ten¡ ¨¡©ky okna
obrst0:  mov       cl,bl                    ; offset st¡nu
         or        bl,bl
         jns       obrst1                   ; je nez porn˜ offset
         neg       cl                       ; absolutn¡ hodnota offsetu
         add       dl,bl                    ; p©i‡ten¡ offsetu st¡nu
obrst1:  xor       ch,ch
         jcxz      obrst4
obrst3:  call      stin                     ; vykreslen¡ st¡nu
         loop      obrst3                   ; dal¨¡ st¡n
obrst4:  pop       dx
         pop       cx
         pop       ax
         ret

; -----------------------------------------------------------------------------

stin:                                     ;* vykreslen¡ st¡nu (1 znak)
                                            ; VSTUP:DL=pozice
                                            ;       DH=© dek
                                            ; VSTUP: DX=nov  pozice

         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         push      bp
         mov       bh,ds:[aktpage]          ; aktivn¡ str nka
         and       bh,1                     ; aktivn¡ str nka pro v˜stup
         push      bx                       ; £schova str nky
         mov       ah,2                     ; funkce nastaven¡ pozice kurzoru
         int       10h                      ; nastaven¡ pozice kurzoru
         pop       bx                       ; str nka
         mov       ah,8                     ; funkce ‡ten¡ znaku
         int       10h                      ; poskytnut¡ znaku a atributu
         mov       bl,ah
         and       ah,7fh
         mov       cl,4
         shr       ah,cl
         and       bl,0fh
         cmp       ah,bl
         mov       ah,7
         jna       stin1
         mov       ah,70h
stin1:   pop       bp
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         push      word ptr ds:[color]      ; £schova atributu barev
         mov       ds:[color],ah            ; barva b¡l‚ p¡smo na ‡ern‚m pozad¡
         call      outch1                   ; v˜stup znaku s nov˜m atributem
         pop       word ptr ds:[color]      ; navr cen¡ p–vodn¡ barvy
         pop       ax
         ret

; -----------------------------------------------------------------------------

setwsel:                                  ;* nastaven¡ okna pro volby
                                            ; VSTUP: DS:DI=text nadpisu
                                            ;        BH=maxim. d‚lka ©etˆzce
                                            ;        BL=po‡et polo‘ek
                                            ;        DL=po‡ tek okna (0=aut.)
                                            ;        DH=© dek okna (0=aut.)
                                            ;        CL=¨¡©ka okna (0=aut.)
                                            ;        CH=max. v˜¨ka okna (0=max.)
                                            ; VSTUP:DL=po‡ tek okna
                                            ;        DH=© dek okna
                                            ;        BH=maxim. d‚lka ©etˆzce
                                            ;        BL=po‡et polo‘ek
                                            ;        CL=¨¡©ka okna
                                            ;        CH=v˜¨ka okna

         push      ax
                                          ;* nastaven¡ ¨¡©ky okna
         or        cl,cl                    ; je ¨¡©ka okna zadan  ?
         jnz       setwsl2                  ; ¨¡©ka okna je zadan 
         mov       cl,bh                    ; maxim ln¡ d‚lka ©etˆzce
         call      lendi                    ; zji¨tˆn¡ d‚lky ©etˆzce DS:DI
         cmp       al,cl                    ; je d‚lka vˆt¨¡ ne‘ nalezen  ?
         jna       setwsl04                 ; d‚lka nen¡ vˆt¨¡
         mov       cl,al                    ; nastaven¡ nov‚ d‚lky textu
setwsl04:cmp       cl,66                    ; je d‚lka ©etˆzce vˆt¨¡ ne‘ 68 ?
         jna       setwsl11                 ; ¨¡©ka nen¡ vˆt¨¡ ne‘ 68
         mov       cl,66                    ; omezen¡ na ¨¡©ku 68
setwsl11:cmp       cl,12                    ; minim ln¡ ¨¡©ka polo‘ky
         jnb       setwsl12                 ; nen¡ men¨¡ ne‘ 12
         mov       cl,12                    ; omezen¡ na 12
setwsl12:add       cl,10                    ; p©i‡ten¡ okraj–
                                          ;* nastaven¡ v˜¨ky okna
setwsl2: mov       al,bl                    ; po‡et polo‘ek
         cmp       al,19                    ; je vˆt¨¡ po‡et polo‘ek ne‘ 19 ?
         jna       setwsl21                 ; nen¡ vˆt¨¡ po‡et polo‘ek ne‘ 19
         mov       al,19                    ; omezen¡ na 21 polo‘ek
setwsl21:or        al,al                    ; je nˆjak  polo‘ka ?
         jnz       setwsl22                 ; je nˆjak  polo‘ka
         inc       al                       ; 1 polo‘ka
setwsl22:add       al,4                     ; p©i‡ten¡ okraj–
         or        ch,ch                    ; je zad na v˜¨ka okna ?
         jz        setwsl23                 ; v˜¨ka okna nen¡ zadan 
         cmp       al,ch                    ; je v˜¨ka okna vˆt¨¡ ne‘ po‘adov. ?
         jae       setwsl3                  ; v˜¨ka okna je vˆt¨¡ nebo shodn 
setwsl23:mov       ch,al                    ; zmen¨en¡ v˜¨ky okna
                                         ;* nastaven¡ po‡ tku okna
setwsl3: or        dl,dl                    ; je nastaven po‡ tek okna ?
         jnz       setwsl4                  ; po‡ tek okna je nastaven
         mov       dl,80                    ; ¨¡©ka obrazovky
         sub       dl,cl                    ; ode‡ten¡ ¨¡©ky okna
         shr       dl,1                     ; offset od za‡ tku © dku
                                          ;* nastaven¡ © dku okna
setwsl4: or        dh,dh                    ; je nastaven © dek okna ?
         jnz       setwsl5                  ; © dek okna je nastaven
         mov       dh,25                    ; v˜¨ka obrazovky
         sub       dh,ch                    ; ode‡ten¡ v˜¨ky okna
         shr       dh,1                     ; po‡et © dk– nad oknem
         mov       al,ch                    ; po‡et © dk– celkem
         shr       al,1                     ; AL / 2
         shr       al,1                     ; AL / 4
         shr       al,1                     ; AL / 8 (po‡et © dk– 0 a‘ 3)
         sub       al,3
         neg       al                       ; 3 a‘ 0
         sub       dh,al
setwsl5:                                  ;* nastaven¡ prvn¡ zobrazen‚ polo‘ky
         mov       al,bl                    ; po‡et polo‘ek
         dec       al
         mov       ds:[kurzsel],al          ; nastaven¡ kurzoru na posledn¡ pol.
         sub       al,ch                    ; ode‡ten¡ v˜¨ky okna
         add       al,4                     ; korekce okraj–
         inc       al
         mov       ds:[firstsel],al         ; prvn¡ zobrazen  polo‘ka
         pop       ax
         ret

; -----------------------------------------------------------------------------

asksel:                                   ;* poskytnut¡ polo‘ky zad n¡
                                            ; VSTUP: DS:SI=buffer pro zad n¡
                                            ;        AL=‡¡slo polo‘ky 0...
                                            ; VSTUP: DS:SI=adresa polo‘ky
                                            ;         AL=d‚lka polo‘ky
         push      bx
         push      cx
         push      dx
         push      di
         push      es
         push      ds
         pop       es
         mov       dl,al                    ; ‡¡ta‡ polo‘ek
         lodsw                              ; na‡ten¡ d‚lky
         mov       di,si                    ; ukazatel ©etˆzce
         mov       cx,ax                    ; ‡¡ta‡ znak– bufferu
         xor       al,al                    ; AL <- 0
                                          ;* nalezen¡ ©etˆzce v bufferu
asksel1: cmp       al,[di]                  ; je ji‘ konec dat ?
         je        asksel2                  ; je konec dat v bufferu
         or        dl,dl                    ; je po‘adovan˜ ©etˆzec ?
         jz        asksel4                  ; ©etˆzec nalezen
         dec       dl                       ; sn¡‘en¡ ‡¡ta‡e ©etˆzc–
         repnz     scasb                    ; nalezen¡ konce ©etˆzce
         jz        asksel1                  ; nen¡ je¨tˆ konec - dal¨¡ ©etˆzec
asksel2:                                  ;* konec bufferu - budou p©edvolby
         add       di,cx                    ; konec bufferu
         mov       bx,di                    ; £schova adresy konce dat
         mov       si,di
         lodsb                              ; po‡et polo‘ek p©edvoleb
         mov       dh,al                    ; ‡¡ta‡ polo‘ek p©edvoleb
asksel3: xor       ax,ax
         or        dh,dh                    ; je je¨tˆ nˆjak  polo‘ka ?
         jz        asksel8                  ; polo‘ka nenalezena
         lodsw                              ; na‡ten¡ adresy polo‘ky
         dec       dh                       ; sn¡‘en¡ ‡¡ta‡e polo‘ek
         mov       di,ax                    ; adresa polo‘ky
         dec       dl                       ; sn¡‘en¡ ‡¡ta‡e polo‘ek
         jns       asksel3                  ; nen¡ je¨tˆ po‘adovan˜ ©etˆzec
asksel4:                                  ;* nalezen ©etˆzec
         xor       al,al                    ; AL <- 0
         mov       bx,di                    ; £schova za‡ tku ©etˆzce
         mov       cx,255                   ; maxim ln¡ d‚lka ©etˆzce
         repnz     scasb                    ; nalezen¡ d‚lky ©etˆzce
         sub       di,bx                    ; d‚lka ©etˆzce
         mov       ax,di                    ; d‚lka ©etˆzce
         dec       ax
asksel8: mov       si,bx                    ; adresa za‡ tku ©etˆzce
         or        ah,ah                    ; je ©etˆzec del¨¡ ne‘ 255 ?
         jz        asksel9                  ; nen¡ vˆt¨¡ ne‘ 255
         mov       al,255                   ; n hradn¡ d‚lka ©etˆzce
asksel9: pop       es
         pop       di
         pop       dx
         pop       cx
         pop       bx
         ret

; -----------------------------------------------------------------------------

testsel:                                  ;* test bufferu zad n¡
                                            ; VSTUP: DS:SI=buffer pro zad n¡
                                            ; VSTUP: BH=maxim. d‚lka ©etˆzce
                                            ;         BL=po‡et polo‘ek

         push      ax
         push      cx
         push      dx
         push      si
         push      di
         xor       bx,bx                    ; BH=max. d‚lka, BL=po‡et polo‘ek
testsl1: mov       al,bl                    ; po‘adovan˜ ©etˆzec
         push      si                       ; £schova adresy bufferu
         call      asksel                   ; test polo‘ky
         pop       si                       ; n vrat adresy bufferu
         or        al,al                    ; je ji‘ posledn¡ ©etˆzec ?
         jz        testsl3                  ; byl to posledn¡ ©etˆzec
         inc       bl                       ; zv˜¨en¡ ‡¡ta‡e polo‘ek
         jz        testsl2                  ; p©ete‡en¡ po‡tu ©etˆzc–
         cmp       al,bh                    ; maxim ln¡ d‚lka ©etˆzc–
         jb        testsl1                  ; nen¡ vˆt¨¡ d‚lka
         mov       bh,al                    ; nov  maxim ln¡ d‚lka
         jmp       short testsl1
testsl2: dec       bl                       ; BL <- 255 max. po‡et ©etˆzc–
testsl3: pop       di
         pop       si
         pop       dx
         pop       cx
         pop       ax
         ret

; -----------------------------------------------------------------------------
; -----------------------------------------------------------------------------

; *****************************************************************************
;
;                    Znakov‚ vstupnˆ/v˜stupn¡ podprogramy
;
; *****************************************************************************

outch1:                                     ; v˜stup 1 znaku na displej
                                            ; VSTUP: DX=pozice kurzoru
                                            ;        AL=ASCII znak

         push      cx                       ; £schova CX
         mov       cx,1                     ; po‡et - 1 znak
         call      outch                    ; zobrazen¡ 1 znaku
         pop       cx                       ; n vrat CX
         ret

; -----------------------------------------------------------------------------

outch01:                                    ; v˜stup znaku na displej
                                            ; VSTUP: DX=pozice kurzoru
                                            ;        CX=po‡et znak–
                                            ;        AL=ASCII znak
                                            ;        BL=atribut barvy


         push      cx
         push      dx
         push      es
         cmp       dl,79                    ; je p©ekro‡ena pozice ?
         ja        outch011                 ; pozice p©ekro‡ena
         cmp       dh,24                    ; je p©ekro‡en © dek ?
         ja        outch011                 ; © dek p©ekro‡en
         push      ax
         mov       al,80
         mul       dh
         xor       dh,dh
         add       ax,dx
         add       ax,ax
         mov       di,ax
         test      byte ptr ds:[aktpage],1  ; je aktivn¡ str nka 0 ?
         jz        outch012                 ; je aktivn¡ str nka 0
         add       di,1000h                 ; d‚lka 1 str nky
outch012:mov       ax,0b800h
         mov       es,ax
         pop       ax
         mov       ah,bl
         rep       stosw
outch011:pop       es
         pop       dx
         pop       cx
         ret



outch02: push      cx
         push      dx
         push      ax
         mov       bh,ds:[aktpage]          ; aktivn¡ str nka
         and       bh,1
         mov       ah,2                     ; funkce nastaven¡ pozice kurzoru
         int       10h                      ; nastaven¡ pozice kurzoru
         pop       ax
         mov       ah,9                     ; funkce z pisu znaku s atributem
         int       10h                      ; z pis znaku
         pop       dx
         pop       cx
         ret

outch:                                      ; v˜stup znaku na displej (- ©¡dic¡)
                                            ; VSTUP: DX=pozice kurzoru
                                            ;        CX=po‡et znak–
                                            ;        AL=ASCII znak
                                            ;        DS=datov˜ segment

         cmp       al,9                     ; je nastaven¡ atributu ?
         ja        outch0                   ; nen¡ nastaven¡ atributu - tisk
         push      ax                       ; £schova AX (znak k tisku)
         push      bx                       ; £schova BX
         push      cx
         push      dx
         push      si                       ; £schova SI
         push      di                       ; £schova DI
         push      bp                       ; £schova BP
         or        al,al                    ; je pouze nastaven¡ kurzoru ?
         jz        outch12                  ; je nastaven¡ kurzoru
         xor       ah,ah                    ; AH <- 0
         mov       si,offset col1-1         ; tabulka barev
         add       si,ax                    ; adresa barvy
         lodsb                              ; na‡ten¡ barvy
         mov       ds:[color],al            ; nastaven¡ atributu barvy
         jmp       short outch13            ; n vrat z obsluhy
                                          ;* nastaven¡ kurzoru
outch12: mov       bh,ds:[aktpage]          ; aktivn¡ videostr nka
         and       bh,1
         mov       ah,2                     ; funkce nastaven¡ pozice kurzoru
         int       10h                      ; nastaven¡ pozice kurzoru
outch13: pop       bp                       ; n vrat BP
         pop       di                       ; n vrat DI
         pop       si                       ; n vrat SI
         pop       dx
         pop       cx
         pop       bx                       ; n vrat BX
         pop       ax                       ; n vrat AX (znak k tisku)
         ret

outch0:                                     ; v˜stup znaku na displej
                                            ; VSTUP: DX=pozice kurzoru
                                            ;        CX=po‡et znak–
                                            ;        AL=ASCII znak
                                            ;        DS=datov˜ segment

         push      es
         push      bx                       ; £schova BX
         push      si                       ; £schova SI
         push      di                       ; £schova DI
         push      bp                       ; £schova BP
         push      ax                       ; £schova AX (znak k tisku)
         mov       bl,ds:[color]            ; aktivn¡ barva (atribut)
setout:  call      outch02                  ; v˜stup znaku (nastaven˜ ovlada‡)
         add       dl,cl                    ; zv˜¨en¡ pozice kurzoru
         pop       ax                       ; n vrat AX (znak k tisku)
         pop       bp                       ; n vrat BP
         pop       di                       ; n vrat DI
         pop       si                       ; n vrat SI
         pop       bx                       ; n vrat BX
         pop       es
         ret


; -----------------------------------------------------------------------------

outtxt:                                     ; v˜stup textu na displej
                                            ; VSTUP: DX=pozice kurzoru
                                            ;        DS:SI=text k tisku
                                            ;        (DS=datov˜ segment)

         lodsb                              ; na‡ten¡ znaku k tisku
         or        al,al                    ; je koncov˜ bajt 0 ?
         jz        outtx1                   ; je koncov˜ bajt 0 - konec
         call      outch1                   ; tisk 1 znaku
         jmp       short outtxt             ; tisk dal¨¡ho znaku
outtx1:  ret

; -----------------------------------------------------------------------------

outtx0:                                     ; v˜stup textu na displej
                                            ; VSTUP: DX=pozice kurzoru
                                            ;        DS:SI=text k tisku
                                            ;        (DS=datov˜ segment)

         push      ax
         push      cx
         mov       cx,1
outtx2:  lodsb                              ; na‡ten¡ znaku k tisku
         or        al,al                    ; je koncov˜ bajt 0 ?
         jz        outtx3                   ; je koncov˜ bajt 0 - konec
         call      outch0                   ; tisk 1 znaku
         jmp       short outtx2             ; tisk dal¨¡ho znaku
outtx3:  pop       cx
         pop       ax
         ret

; -----------------------------------------------------------------------------

wrolldown:                                  ; rolov n¡ okna dol–
                                            ; VSTUP: BP=ukazatel okna
         push      bp
         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         mov       cl,ds:[bp+pozl-tabl]
         mov       dl,cl
         add       dl,39
         mov       ax,701h
         mov       bh,ds:[col1]
         mov       ch,4
         mov       dh,22
         test      byte ptr ds:[flags],8
         jz        wrolldn1
         dec       dh
wrolldn1:int       10h
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         pop       bp
         ret

; -----------------------------------------------------------------------------

wrollup:                                    ; rolov n¡ okna nahoru
                                            ; VSTUP: BP=ukazatel okna
         push      bp
         push      ax
         push      bx
         push      cx
         push      dx
         push      si
         push      di
         mov       cl,ds:[bp+pozl-tabl]
         mov       dl,cl
         add       dl,39
         mov       ax,601h
         mov       bh,ds:[col1]
         mov       ch,4
         mov       dh,22
         test      byte ptr ds:[flags],8
         jz        wrollup1
         dec       dh
wrollup1:int       10h
         pop       di
         pop       si
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         pop       bp
         ret

; *****************************************************************************
;
;                       V¨eobecn‚ pomocn‚ podprogramy
;
; *****************************************************************************

lowcase:                                  ;* p©evod znaku na mal‚ p¡smeno
                                            ; VSTUP: AL=znak k p©evodu

         cmp       al,"A"                   ; je znak men¨¡ ne‘ "A" ?
         jb        highc1                   ; nen¡ velk‚ p¡smeno
         cmp       al,"Z"                   ; je znak vˆt¨¡ ne‘ "Z" ?
         ja        lowc1                    ; nen¡ velk‚ p¡smeno
         add       al,32                    ; korekce na mal‚ p¡smeno
lowc1:   ret

; -----------------------------------------------------------------------------

highcase:                                 ;* korekce AL na velk‚ p¡smeno
                                            ; VSTUP: AL=znak k p©evodu

         cmp       al,"a"                   ; je znak men¨¡ ne‘ "a" ?
         jb        highc1                   ; nen¡ mal‚ p¡smeno
         cmp       al,"z"                   ; je znak vˆt¨¡ ne‘ "z" ?
         ja        highc1                   ; nen¡ mal‚ p¡smeno
         sub       al,32                    ; korekce na velk‚ p¡smeno
highc1:  ret

; -----------------------------------------------------------------------------

transtxt:                                   ; p©enos textu ASCIIZ
                                            ; VSTUP: DS:SI -> ES:DI
                                            ; VSTUP: ES:DI - koncov  0

         push      ax                       ; £schova AX
transt1: lodsb                              ; na‡ten¡ znaku k p©enosu
         stosb                              ; ulo‘en¡ znaku na v˜stup
         or        al,al                    ; byl to ji‘ koncov˜ bajt 0 ?
         jnz       transt1                  ; nebyla je¨tˆ koncov  0 - dal¨¡
         dec       di                       ; n vrat ukazatele na posledn¡ 0
         pop       ax                       ; n vrat AX
         ret

; -----------------------------------------------------------------------------

setkonc:                                    ; z pis koncovky "y", "–"
                                            ; VSTUP: DS:SI=text ‡¡sla
                                            ;        ES:DI=ukl dac¡ adresa
         push      ax
         call      testkonc                 ; test koncovky ‡¡sla
         or        al,al
         jz        endli3                   ; je ‡¡slo 1
         mov       si,offset textvy11       ; koncovka ‡¡sla 2,3,4
         dec       al
         jz        endli2                   ; je ‡¡slo 2,3,4
         mov       si,offset textvy12       ; kocovka ostatn¡ch ‡¡sel
endli2:  call      transtxt                 ; ulo‘en¡ koncovky
endli3:  pop       ax
         ret


testkonc:                                   ; test koncovky ‡¡sla
                                            ; VSTUP: DS:SI=text ‡¡sla
                                            ; VSTUP: AL=0 je koncovka pro "1"
                                            ;         AL=1 koncovka pro 2,3,4
                                            ;         AL=2 ostatn¡ ‡¡sla

         push      dx
         push      si
         mov       dx,"00"                  ; posledn¡ 2 ‡¡slice
testkon1:call      readtest                 ; na‡ten¡ dal¨¡ ‡¡slice
         jc        testkon2                 ; nen¡ ji‘ dal¨¡ ‡¡slice
         mov       dh,dl                    ; p©edposledn¡ ‡¡slice
         mov       dl,al                    ; posledn¡ ‡¡slice
         jmp       short testkon1           ; na‡ten¡ dal¨¡ ‡¡slice
testkon2:mov       al,2                     ; p©¡znak jin‚ho ‡¡sla
         cmp       dh,"1"                   ; byly des¡tky ?
         je        testkon3                 ; jsou mno‘n  ‡¡sla
         cmp       dl,"4"                   ; je ‡¡slice vˆt¨¡ ne‘ "4" ?
         ja        testkon3                 ; je ‡¡slice vˆt¨¡ ne‘ "4"
         cmp       dl,"0"                   ; je ‡¡slice "0" ?
         je        testkon3                 ; je ‡¡slice "0"
         dec       al                       ; p©¡znak ‡¡sla 2,3,4
         cmp       dl,"1"                   ; je ‡¡slice "1" ?
         jne       testkon3                 ; nen¡ "1" - je 2,3,4
         dec       al                       ; p©¡znak ‡¡slice "1"
testkon3:pop       si
         pop       dx
         ret


readtest:                                   ; ‡ten¡ ‡¡slice p©i testu
                                            ; VSTUP: CY=nen¡ platn  ‡¡slice

         lodsb                              ; na‡ten¡ dal¨¡ ‡¡slice
         cmp       al,"."                   ; je oddˆlovac¡ te‡ka ?
         je        readtest
         or        al,al                    ; je ji‘ konec textu ?
         stc
         jz        readtst1
         cmp       al,"0"                   ; je ‡¡slice ?
         jb        readtst1                 ; nen¡ ‡¡slice
         cmp       al,"9"+1                 ; je ‡¡slice ?
         cmc                                ; CY=nen¡ ‡¡slice
readtst1:ret

; -----------------------------------------------------------------------------

center:                                     ; centrov n¡ textu ASCIIZ
                                            ; VSTUP: DS:SI=adresa textu
                                            ;        CL=max. ¨¡©ka © dku
                                            ;        DL=pozice kurzoru na © dku
                                            ; VSTUP: CL=offset za‡ tku textu
                                            ;         DL=nov  pozice kurzoru

         push      ax                       ; £schova AX
         push      si                       ; £schova SI
center1: lodsb                              ; na‡ten¡ znaku z bufferu
         or        al,al                    ; je konec textu ?
         jz        center2                  ; je ji‘ konec textu
         cmp       al,8                     ; je znak men¨¡ ne‘ 8 (©¡dic¡) ?
         jbe       center1                  ; je bajt 8 nebo men¨¡ - dal¨¡ znak
         dec       cl                       ; sn¡‘en¡ ‡¡ta‡e znak–
         jnz       center1                  ; nen¡ je¨tˆ 0 - dal¨¡ znak
center2: shr       cl,1                     ; CL/2 - offset od za‡ tku
         add       dl,cl                    ; zv˜¨en¡ pozice kurzoru
         pop       si                       ; n vrat SI
         pop       ax                       ; n vrat AX
         ret

; -----------------------------------------------------------------------------

lensi:                                    ;* zji¨tˆn¡ d‚lky ©etˆzce ASCIIZ DS:SI
                                            ; VSTUP: DS:SI=adresa ©etˆzce
                                            ; VSTUP: AL=d‚lka ©etˆzce
                                            ;         CY=p©ete‡en¡ (AL=255)

         push      si
         xor       al,al                    ; ‡¡ta‡ d‚lky ©etˆzce
lens1:   cmp       byte ptr ds:[si],0       ; je ji‘ konec ©etˆzce ?
         je        lens2                    ; je konec ©etˆzce
         inc       si                       ; zv˜¨en¡ ukazatele textu
         inc       al                       ; zv˜¨en¡ ‡¡ta‡e znak– ©etˆzce
         jnz       lens1                    ; test dal¨¡ho znaku
         dec       al                       ; AL <- 255 p©ete‡en¡ textu
         stc                                ; p©¡znak p©ete‡en¡ textu
lens2:   pop       si
         ret

; -----------------------------------------------------------------------------

lendi:                                    ;* zji¨tˆn¡ d‚lky ©etˆzce ASCIIZ DS:DI
                                            ; VSTUP: DS:DI=adresa ©etˆzce
                                            ; VSTUP: AL=d‚lka ©etˆzce
                                            ;         CY=p©ete‡en¡ (AL=255)

         push      di
         xor       al,al                    ; ‡¡ta‡ d‚lky ©etˆzce
lend1:   cmp       byte ptr ds:[di],0       ; je ji‘ konec ©etˆzce ?
         je        lend2                    ; je konec ©etˆzce
         inc       di                       ; zv˜¨en¡ ukazatele textu
         inc       al                       ; zv˜¨en¡ ‡¡ta‡e znak– ©etˆzce
         jnz       lend1                    ; test dal¨¡ho znaku
         dec       al                       ; AL <- 255 p©ete‡en¡ textu
         stc                                ; p©¡znak p©ete‡en¡ textu
lend2:   pop       di
         ret

; -----------------------------------------------------------------------------

tisknm2s:                                   ; tisk ‡¡sla 2 ‡¡slice s mezerami
                                            ; VSTUP: AL=‡¡slo
                                            ;        DX=pozice

         call      setspc                   ; nastaven¡ m¢du tisku mezer
         jmp       short tisknm21

tisknm20:                                   ; tisk ‡¡sla 2 ‡¡slice s nulami
                                            ; VSTUP: AL=‡¡slo
                                            ;        DX=pozice

         call      setnul                   ; nastaven¡ m¢du tisku mezer
tisknm21:push      di
         push      si
         push      bx
         push      cx
         push      dx                       ; pozice
         mov       di,offset outbuf         ; tiskov˜ buffer
         xor       ah,ah
         call      deknum2                  ; dek¢dov n¡ ‡¡sla 2 ‡¡slice
         pop       dx                       ; pozice
         mov       si,offset outbuf         ; tiskov˜ buffer
         call      outtxt                   ; zobrazen¡ textu
         call      setfre                   ; voln˜ form t tisku ‡¡sla
         pop       cx
         pop       bx
         pop       si
         pop       di
         ret

; -----------------------------------------------------------------------------

tisknm0:                                    ; tisk ‡¡sla 10 ‡¡slic zarov. vpravo
                                            ; VSTUP: BX:AX=‡¡slo
                                            ;        DX=pozice

         push      di
         push      si
         push      dx                       ; pozice
         call      setspc                   ; nastaven¡ m¢du tisku mezer
         mov       dx,bx                    ; vy¨¨¡ slovo ‡¡sla
         mov       di,offset outbuf         ; tiskov˜ buffer
         call      deknumx                  ; p©ek¢dov n¡ ‡¡sla s omezen¡m
         pop       dx                       ; pozice
         mov       si,offset outbuf         ; tiskov˜ buffer
         call      outtxt                   ; zobrazen¡ textu
         call      setfre                   ; voln˜ form t tisku ‡¡sla
         pop       si
         pop       di
         ret

; -----------------------------------------------------------------------------

setspc:                                   ;* zapnut¡ tisku mezer p©ed ‡¡slem

         and       byte ptr ds:[parnum],0fdh ; vypnut¡ tisku nul
         or        byte ptr ds:[parnum],2   ; zapnut¡ tisku mezer
         ret


setnul:                                   ;* zapnut¡ tisku nul p©ed ‡¡slem

         and       byte ptr ds:[parnum],0fbh ; vypnut¡ tisku mezer
         or        byte ptr ds:[parnum],4   ; zapnut¡ tisku nul
         ret


setfre:                                   ;* zapnut¡ voln‚ho form tu ‡¡sla

         and       byte ptr ds:[parnum],0f9h ; vypnut¡ tisku mezer a nul
         ret

; -----------------------------------------------------------------------------

deknumx:                                    ; p©ek¢dov n¡ ‡¡sla s omezen¡m
                                            ; VSTUP: (DX:)AX=‡¡slo k p©ek¢dov n¡
                                            ;        ES:DI=adr. k ulo‘en¡ textu
                                            ; VSTUP: AL=po‡et znak– ©etˆzce
                                            ; zni‡en‚ registry: AX,BX,CX,DX

         cmp       dx,5f5h                  ; vy¨¨¡ slovo ‡¡sla 100 000 000
         jne       deknmx1                  ; nen¡ shoda
         cmp       ax,0e100h                ; ni‘¨¡ slovo ‡¡sla 100 000 000
deknmx1: jnb       deknmx2                  ; je ‡¡slo 100 000 000 nebo vy¨¨¡
         jmp       deknum8                  ; dek¢dov n¡ ‡¡sla 8 ‡¡slic

deknmx2: cmp       dx,3b9ah                 ; vy¨¨¡ slovo ‡¡sla 1000 000 000
         jne       deknmx3                  ; nen¡ shoda
         cmp       ax,0ca00h                ; ni‘¨¡ slovo ‡¡sla 1000 000 000
deknmx3: jnb       deknmx4                  ; je ‡¡slo 1000 000 000 nebo vy¨¨¡
         or        byte ptr ds:[parnum],10h ; z kaz tisku te‡ku tis¡c–
         call      deknum9                  ; dek¢dov n¡ ‡¡sla 9 ‡¡slic
         and       byte ptr ds:[parnum],0efh; povolen¡ tisku te‡ky tis¡c–
         ret

deknmx4: or        byte ptr ds:[parnum],1   ; z kaz tisku te‡ek
         call      deknum0                  ; dek¢dov n¡ ‡¡sla 10 ‡¡slic
         and       byte ptr ds:[parnum],0feh ; povolen¡ tisku te‡ek
         ret

; -----------------------------------------------------------------------------

deknum:                                   ;* p©evod ‡¡sla na dekadick‚ ‡¡slo
                                            ; VSTUP: (DX:)AX=‡¡slo k p©ek¢dov n¡
                                            ;        ES:DI=adr. k ulo‘en¡ textu
                                            ; VSTUP: AL=po‡et znak– ©etˆzce
                                            ; zni‡en‚ registry: AX,BX,CX,DX

deknum0:                                  ;* dek¢dov n¡ 10 ‡¡slic
         mov       bx,3b9ah                 ; vy¨¨¡ slovo ‡¡sla 1000 000 000
         mov       cx,0ca00h                ; ni‘¨¡ slovo ‡¡sla 1000 000 000
         call      deknm0                   ; dek¢dov n¡ 1 ‡¡slice
deknum9:                                  ;* dek¢dov n¡ 9 ‡¡slic
         mov       bx,5f5h                  ; vy¨¨¡ slovo ‡¡sla 100 000 000
         mov       cx,0e100h                ; ni‘¨¡ slovo ‡¡sla 100 000 000
         call      deknm0                   ; dek¢dov n¡ 1 ‡¡slice
deknum8:                                  ;* dek¢dov n¡ 8 ‡¡slic
         mov       bl,98h                   ; vy¨¨¡ slovo ‡¡sla 10 000 000
         mov       cx,9680h                 ; ni‘¨¡ slovo ‡¡sla 10 000 000
         call      deknm02                  ; dek¢dov n¡ 1 ‡¡slice
deknum7:                                  ;* dek¢dov n¡ 7 ‡¡slic
         mov       bl,0fh                   ; vy¨¨¡ slovo ‡¡sla 1 000 000
         mov       cx,4240h                 ; ni‘¨¡ slovo ‡¡sla 1 000 000
         call      deknm02                  ; dek¢dov n¡ 1 ‡¡slice
         call      deknm8                   ; oddˆlovac¡ te‡ka
deknum6:                                  ;* dek¢dov n¡ 6 ‡¡slic
         mov       bl,1                     ; vy¨¨¡ slovo ‡¡sla 100 000
         mov       cx,86a0h                 ; ni‘¨¡ slovo ‡¡sla 100 000
         call      deknm02                  ; dek¢dov n¡ 1 ‡¡slice
deknum5:                                  ;* dek¢dov n¡ 5 ‡¡slic
         mov       cx,10000                 ; ni‘¨¡ slovo ‡¡sla 10 000
         call      deknm01                  ; dek¢dov n¡ 1 ‡¡slice
deknum4:                                  ;* dek¢dov n¡ 4 ‡¡slic
         mov       cx,1000                  ; ni‘¨¡ slovo ‡¡sla 1 000
         call      deknm00                  ; dek¢dov n¡ 1 ‡¡slice
         test      byte ptr ds:[parnum],10h ; je z kaz tisku tis¡c– ?
         jz        deknum41                 ; nen¡ z kaz tisku tis¡c–
         or        byte ptr ds:[parnum],8   ; p©echodn˜ z kaz tisku te‡ky
deknum41:call      deknm8                   ; oddˆlovac¡ te‡ka
deknum3:                                  ;* dek¢dov n¡ 3 ‡¡slic
         mov       cx,100                   ; ni‘¨¡ slovo ‡¡sla 100
         call      deknm00                  ; dek¢dov n¡ 1 ‡¡slice
deknum2:                                  ;* dek¢dov n¡ 2 ‡¡slic
         mov       cx,10                    ; ni‘¨¡ slovo ‡¡sla 10
         call      deknm00                  ; dek¢dov n¡ 1 ‡¡slice
                                          ;* dek¢dov n¡ 1 ‡¡slice
         add       al,"0"                   ; korekce na ‡¡slici
         stosb                              ; ulo‘en¡ posledn¡ ‡¡slice
         xor       al,al
         stosb                              ; ulo‘en¡ ukon‡ovac¡ho bajtu 0
         dec       di                       ; ukazatel na posledn¡ bajt 0
         and       byte ptr ds:[parnum],0f7h; zru¨en¡ p©echodn‚ho z kazu te‡ky
         xchg      al,ds:[citdek]           ; vynulov n¡ ‡¡ta‡e ‡¡slic
         inc       al                       ; po‡et znak– dek¢dovan‚ho textu
         ret

                                          ;* dek¢dov n¡ ‡¡slice 32-bit. ‡¡sla
                                            ; VSTUP: DX:AX=‡¡slo
                                            ;        BX:CX=dˆlitel ‡¡sla
                                            ;        ES:DI=adr. k ulo‘en¡ textu

deknm00: xor       dx,dx                    ; DX <- 0
deknm01: xor       bl,bl                    ; BL <- 0
deknm02: xor       bh,bh                    ; BH <- 0
deknm0:  mov       byte ptr ds:[citnum],2fh ; inicializace ‡¡ta‡e ‡¡slice
deknm1:  inc       byte ptr ds:[citnum]     ; zv˜¨en¡ ‡¡ta‡e ‡¡slice
         sub       ax,cx                    ; ode‡ten¡ ni‘¨¡ho slova ‡¡sla
         sbb       dx,bx                    ; ode‡ten¡ vy¨¨¡ho slova ‡¡sla
         jnc       deknm1                   ; nen¡ je¨tˆ p©ete‡en¡
         add       ax,cx                    ; n vrat ni‘¨¡ho slova ‡¡sla
         adc       dx,bx                    ; n vrat vy¨¨¡ho slova ‡¡sla
         cmp       byte ptr ds:[citnum],"0" ; je ‡¡slice 0 ?
         jne       deknm2                   ; nen¡ ‡¡slice 0 - dek¢dov n¡
         cmp       byte ptr ds:[citdek],0   ; byla ji‘ nˆjak  ‡¡slice ?
         jne       deknm2                   ; byla ji‘ nˆjak  ‡¡slice
         test      byte ptr ds:[parnum],4   ; je tisku nul ?
         jnz       deknm2                   ; je tisk nul
         mov       byte ptr ds:[citnum]," " ; n hradn¡ znak mezery
         test      byte ptr ds:[parnum],2   ; je tisk mezer ?
         jz        deknm3                   ; nen¡ tisk mezer
         jmp       short deknm21
deknm2:  inc       byte ptr ds:[citdek]     ; zv˜¨en¡ ‡¡ta‡e ‡¡slic
deknm21: push      ax                       ; £schova AX
         mov       al,ds:[citnum]           ; dek¢dovan  ‡¡slice
         stosb                              ; ulo‘en¡ ‡¡slice
         pop       ax                       ; n vrat AX
deknm3:  ret

deknm8:
         push      ax
         mov       al,"."                   ; oddˆlovac¡ te‡ka
         test      byte ptr ds:[parnum],9   ; je povolen˜ tisk te‡ky ?
         jnz       deknm9                   ; tisk nen¡ povolen˜
         cmp       byte ptr ds:[citdek],0   ; byla ji‘ platn  ‡¡slice ?
         jne       deknm81                  ; byla ji‘ platn  ‡¡slice
         test      byte ptr ds:[parnum],3   ; je voln˜ form t ?
         jz        deknm9                   ; je voln˜ form t
         test      byte ptr ds:[parnum],4   ; je tisk nul ?
         jnz       deknm81                  ; je tisk nul
         mov       al," "                   ; n hradn¡ oddˆlovac¡ mezera
deknm81: mov       byte ptr es:[di],al      ; oddˆlovac¡ znak
         inc       di                       ; zv˜¨en¡ ukl dac¡ adresy
         cmp       byte ptr ds:[citdek],0   ; je ji‘ nˆjak  ‡¡slice ?
         je        deknm9                   ; nen¡ je¨tˆ ‘ dn  ‡¡slice
         inc       byte ptr ds:[citdek]     ; zv˜¨en¡ ‡¡ta‡e ‡¡slic (znak–)
deknm9:  pop       ax
         ret


; -----------------------------------------------------------------------------

dekfile:                                    ; zak¢dov n¡ jm‚na souboru
                                            ; VSTUP: DS:SI -> ES:DI
         push      cx
         push      si
         push      di
         inc       si
         mov       cx,8
dekfil1: lodsb
         cmp       al," "
         je        dekfil2
         stosb
dekfil2: loop      dekfil1
         test      byte ptr ds:[si-9],10h
         jnz       dekfil21                 ; je adres ©
         mov       al,"."
         stosb
dekfil21:mov       cl,3
dekfil3: lodsb
         cmp       al," "
         je        dekfil4
         stosb
dekfil4: loop      dekfil3
         xor       al,al
         stosb
         pop       di
         pop       si
         pop       cx
         ret


; *****************************************************************************
;
;                      Podprogramy pro diskov‚ operace
;
; *****************************************************************************

loadpath:                                 ;* na‡ten¡ cesty aktivn¡ho adres ©e
                                            ; VSTUP: ES:DI=ukl dac¡ adresa

         push      ax                       ; £schova AX
         push      dx                       ; £schova DX
         push      si                       ; £schova SI
         push      di                       ; £schova DI
         push      ds                       ; £schova DS
         mov       ah,19h                   ; funkce poskytnut¡ aktivn¡ho disku
         int       21h                      ; poskytnut¡ aktivn¡ho disku
         inc       al                       ; ‡¡slo disku + 1
         mov       dl,al                    ; £schova ‡¡sla disku
         add       al,40h                   ; korekce disku na znak ASCII
         stosb                              ; ulo‘en¡ ozna‡en¡ disku
         mov       ax,"\:"                  ; text ":\"
         stosw                              ; ulo‘en¡ textu ":\"
         mov       si,di                    ; za‡ tek ozna‡en¡ cesty
         push      es                       ; ES -> stack
         pop       ds                       ; DS <- ES segment s bufferem
         mov       ah,47h                   ; funkce poskytnut¡ aktiv. adres ©e
         int       21h                      ; poskytnut¡ aktivn¡ho adres ©e
         pop       ds                       ; n vrat DS
         pop       di                       ; n vrat DI
         pop       si                       ; n vrat SI
         pop       dx                       ; n vrat DX
         pop       ax                       ; n vrat AX
         ret

; -----------------------------------------------------------------------------

openex:                                   ;* otev©en¡ souboru MNEXEC.BAT
                                            ; VSTUP: CY=chyba otev©en¡
                                            ;         AL=chybov˜ k¢d

         push      dx
         push      si
         mov       si,offset pathhome       ; p©¡stupov  cesta k domovsk‚mu adr.
         call      aktdir                   ; nastaven¡ aktivn¡ho adres ©e
         jc        openex2                  ; chyba otev©en¡ adres ©e
         mov       dx,offset fileexc        ; jm‚no souboru "MNEXEC.BAT"
         call      openw                    ; otev©en¡ souboru pro z pis
openex2: pop       si
         pop       dx
         ret

; -----------------------------------------------------------------------------

openw:                                    ;* otev©en¡ souboru pro z pis
                                            ; VSTUP: DX=jm‚no souboru
                                            ; VSTUP: CY=chyba otev©en¡
                                            ;         AL=chybov˜ k¢d

         push      ax                       ; £schova AX
         push      cx
         xor       cx,cx                    ; atributy souboru
         mov       ah,3ch                   ; funkce vytvo©en¡ souboru
         int       21h                      ; otev©en¡ souboru pro z pis
         jc        openw1                   ; chyba otev©en¡ souboru
         mov       ds:[outid],ax            ; identidik tor souboru
         xor       ax,ax                    ; AX <- 0
         mov       ds:[ukazbuf],ax          ; nulov n¡ ukazatele bufferu
         mov       ds:[numbuf],ax           ; nulov n¡ po‡tu dat v bufferu
openw1:  pop       cx
         pop       ax                       ; n vrat AX
         ret

; -----------------------------------------------------------------------------

closew:                                   ;* uzav©en¡ v˜stupn¡ho souboru
                                            ; VSTUP: CY=chyba uzav©en¡
                                            ;         AL=chybov˜ k¢d

         push      bx
         mov       bx,ds:[outid]            ; identifik tor v˜stupn¡ho souboru
         mov       ah,3eh                   ; funkce uzav©en¡ souboru
         int       21h                      ; uzav©en¡ v˜stupn¡ho souboru
         pop       bx
         ret

; -----------------------------------------------------------------------------

srcenv:                                   ;* nalezen¡ otcovsk‚ho prost©ed¡

         push      es
         push      ds
         mov       es,ds:[segpsp]
         mov       es,es:[2ch]              ; ES <- adresa prost©ed¡
         mov       word ptr ds:[parenv+2],es
         mov       ax,es                    ; dekrementace ES
         dec       ax
         mov       es,ax
         mov       dx,es:[3]                ; d‚lka aloka‡n¡ho bloku (odstavc–)
         inc       ax
         mov       es,ax
         xor       di,di                    ; DI <- 0
                                          ;* nalezen¡ konce prost©ed¡
         xor       ax,ax                    ; AX <- 0 hledan‚ slovo
         mov       cx,8000h                 ; maxim ln¡ d‚lka prost©ed¡
hled1:   cmp       es:[di],ax               ; je konec prost©ed¡ ?
         je        hled0                    ; je konec prost©ed¡
         inc       di                       ; dal¨¡ znak
         loop      hled1                    ; test dal¨¡ho bajtu
hled0:                                    ;* nalezen¡ otcovsk‚ho prost©ed¡
         mov       bx,di                    ; d‚lka prost©ed¡
         mov       word ptr ds:[parenv],di  ; konec prost©ed¡
         mov       cx,es                    ; maxim ln¡ po‡et segment–
         push      es
         pop       ds                       ; DS <- adresa prost©ed¡
hled2:   xor       di,di                    ; DI <- 0
         xor       si,si                    ; SI <- 0
         mov       ax,es                    ; dekrementace ES
         dec       ax
         mov       es,ax
         push      cx
         mov       cx,bx                    ; d‚lka prost©ed¡
         repz      cmpsb                    ; porovn n¡ prost©ed¡
         pop       cx
         jne       hled3                    ; prost©ed¡ nen¡ nalezeno

         mov       ax,es                    ; dekrementace ES
         dec       ax
         mov       es,ax
         cmp       byte ptr es:[0],"M"      ; je aloka‡n¡ blok ?
         jne       hled3                    ; nen¡ aloka‡n¡ blok
         mov       dx,es:[3]                ; d‚lka aloka‡n¡ho bloku (odstavc–)
         mov       ax,es                    ; inkrementace ES
         inc       ax
         mov       es,ax
         jmp       short hled4

hled3:   loop      hled2                    ; test dal¨¡ho odstavce
hled4:   pop       ds
         shl       dx,1
         shl       dx,1
         shl       dx,1
         shl       dx,1
         mov       ds:[maxenv],dx           ; max. d‚lka prost©ed¡
         mov       word ptr ds:[parenv+2],es
         xor       ax,ax                    ; AX <- 0 hledan‚ slovo
         mov       cx,8000h                 ; maxim ln¡ d‚lka prost©ed¡
hled5:   cmp       es:[di],ax               ; je konec prost©ed¡ ?
         je        hled6                    ; je konec prost©ed¡
         inc       di                       ; dal¨¡ znak
         loop      hled5                    ; test dal¨¡ho bajtu
hled6:   mov       word ptr ds:[parenv],di  ; konec prost©ed¡
         pop       es
         ret

; -----------------------------------------------------------------------------

delenv:                                   ;* zru¨en¡ prost©ed¡ EXECUTE

         push      es
         push      ds
         mov       si,offset envexec        ; nastaven¡ prost©ed¡ pro EXECUTE
         xor       di,di                    ; adresa za‡ tku prost©ed¡
         mov       es,word ptr ds:[parenv+2] ; segment prost©ed¡
delenv1: xor       al,al
         cmp       es:[di],al               ; je posledn¡ ©etˆzec prost©ed¡ ?
         je        delenv3                  ; je konec prost©ed¡
         push      si
         push      di
         mov       cx,8                     ; d‚lka ©etˆzce
         repe      cmpsb                    ; porovn n¡ ©etˆzc–
         mov       cx,-1
         je        delenv2                  ; ©etˆzce jsou shodn‚
         pop       di
         pop       si
         repne     scasb                    ; nalezen¡ konce ©etˆzce
         jmp       short delenv1
delenv2:                                  ;* ©etˆzec nalezen
         pop       di
         push      di                       ; za‡ tek ©etˆzce EXECUTE
         repne     scasb
         mov       si,di                    ; za‡ tek n sleduj¡c¡ho ©etˆzce
delenv5: cmp       al,es:[di]
         je        delenv4
         repne     scasb
         jmp       short delenv5            ; nalezen¡ konce prost©ed¡

delenv4: mov       cx,di                    ; konec prost©ed¡
         pop       di                       ; za‡ tek ©etˆzce EXECUTE
         sub       cx,si                    ; d‚lka prost©ed¡
         mov       ax,es
         mov       ds,ax
         inc       cx
         rep       movsb                    ; p©enos prost©ed¡
         pop       si
delenv3: pop       ds
         pop       es
         dec       di
         mov       word ptr ds:[parenv],di  ; konec prost©ed¡
         add       di,90                    ; nov˜ po‘adovan˜ konec prost©ed¡
         cmp       di,ds:[maxenv]           ; kontrola maxim ln¡ d‚lky prost©ed¡
         jb        delenv6                  ; nebylo by p©ete‡en¡ prost©ed¡
                                          ;* zobrazen¡ hl ¨en¡ o p©ete‡en¡
         push      di                       ; po‘adovan  velikost prost©ed¡
         mov       si,offset errenv         ; text chyby prost©ed¡ syst‚mu
         mov       di,offset outbuf
         call      transtxt                 ; p©enos textu
         pop       ax                       ; po‘adovan  velikost prost©ed¡
         add       ax,15                    ; zaokrouhlen¡ na odstavec
         and       ax,NOT 0fh               ; zaokrouhlen¡ na odstavec
         xor       dx,dx                    ; DX <- 0
         call      deknum5                  ; dek¢dov n¡ ‡¡sla
         mov       si,offset errenv0
         call      transtxt                 ; p©enos zbytku textu
         call      setkonc                  ; nastaven¡ koncovky
         mov       si,offset errenv1
         call      transtxt
         mov       dx,offset outbuf         ; chybov˜ text
         mov       ah,9
         int       21h

delenv6: ret
; -----------------------------------------------------------------------------

setenv:                                   ;* z pis textu do prost©ed¡ (EXECUTE)
                                            ; VSTUP: DS:SI=text k z pisu
                                            ; VSTUP: CY=nedostate‡n  kapacita

         push      es
         push      si
;         mov       ax,ds
;         mov       es,ax
;         mov       cx,-1
;         xor       al,al
;         mov       di,si
;         repnz     scasb                    ; nalezen¡ konce textu
;         jcxz      setenv4
;         neg       cx                       ; d‚lka textu
;         add       cx,11                    ; p©i‡ten¡ textu "EXECUTE"
;         add       cx,word ptr ds:[parenv]  ; p©i‡ten¡ konce prost©ed¡
;         cmp       cx,ds:[maxenv]           ; kontrola konce prost©ed¡
;         jae       setenv4                  ; prost©ed¡ nedosta‡uje
;
;         les       di,ds:[parenv]           ; adresa konce prost©ed¡
;         mov       si,offset envexec        ; nastaven¡ prost©ed¡ pro EXECUTE
;         call      transtxt                 ; p©enos textu "EXECUTE"
;
;         push      es
;         mov       ax,0b800h
;         mov       es,ax
;         mov       ax,ds:[maxenv]
;         mov       es:[1000h],al
;         mov       es:[1002h],ah
;         mov       ax,word ptr ds:[parenv]
;         mov       es:[1004h],al
;         mov       es:[1006h],ah
;         mov       ax,cx
;         mov       es:[1008h],al
;         mov       es:[100ah],ah
;         mov       ax,di
;         mov       es:[100ch],al
;         mov       es:[100eh],ah
;         pop       es
;
;         pop       si
;         call      transtxt
;         xor       ax,ax
;         mov       es:[di],ax
;         pop       es
;         clc
;         ret

setenv4: pop       si
         pop       es
         stc                                ; p©¡znak chyby prost©ed¡
         ret

; -----------------------------------------------------------------------------

writte:                                   ;* z pis © dku textu do MNEXEC.BAT
                                            ; VSTUP: DS:SI=text k z pisu
         push      si
writt1:  lodsb
         or        al,al
         jz        writt2                   ; konec textu
         call      writex                   ; z pis znaku do souboru MNEXEC.BAT
         jc        writt3                   ; chyba z pisu
         cmp       word ptr ds:[si-2],0a0dh ; byl text od© dkov n¡ ?
         jne       writt1                   ; nebyl konec © dku - dal¨¡ znak
writt2:  mov       al,0dh
         call      writex
         jc        writt3
         mov       al,0ah
         call      writex
         jc        writt3
writt4:  xor       al,al
         stc
         call      writex                   ; vypr zdnˆn¡ bufferu
writt3:  pop       si
         ret

; -----------------------------------------------------------------------------

writex:                                   ;* z pis bajtu do souboru MNEXEC.BAT
                                            ; VSTUP: AL=bajt k z pisu
                                            ;        CY=vypr zdnˆn¡ bufferu

         push      bx
         push      cx
         push      dx
         mov       bx,ds:[numbuf]           ; po‡et bajt– v bufferu
         jc        writex1                  ; je vypr zdnˆn¡ bufferu
         mov       ds:[bx+diskbuf],al       ; ulo‘en¡ dal¨¡ho znaku do bufferu
         inc       bx                       ; zv˜¨en¡ po‡tu bajt– v bufferu
         cmp       bx,512                   ; je ji‘ buffer zaplnˆn ?
         cmc                                ; negace bitu CF
         jnc       writex2                  ; buffer je¨tˆ nen¡ zaplnˆn
writex1: mov       cx,bx                    ; po‡et bajt– k z pisu
         clc
         jcxz      writex2                  ; nejsou ‘ dn  data k z pisu
         mov       ah,40h                   ; funkce z pisu do souboru
         mov       bx,ds:[outid]            ; identifik tor v˜stupn¡ho souboru
         mov       dx,offset diskbuf        ; diskov˜ buffer
         int       21h                      ; z pis dat do souboru
         jc        writex3                  ; chyva diskov‚ operace
         xor       bx,bx                    ; po‡et bajt v bufferu = 0
writex2: mov       ds:[numbuf],bx           ; nastaven¡ nov‚ho po‡tu bajt–
writex3: pop       dx
         pop       cx
         pop       bx
         ret

; -----------------------------------------------------------------------------
; -----------------------------------------------------------------------------
; -----------------------------------------------------------------------------
; -----------------------------------------------------------------------------
; -----------------------------------------------------------------------------
; -----------------------------------------------------------------------------


code     ends

; *****************************************************************************
;
;                             Datov˜ segment
;
; *****************************************************************************
data     segment

errver   db        'Chybn  verze opera‡n¡ho syst‚mu !',13,10,'$'
errcard  db        'Chybn  grafick  karta (nen¡ m¢d 80x25 znak–) !',13,10,'$'
errenv   db        'Nedostate‡n  velikost prost©ed¡ syst‚mu - zmˆ¤te alespo¤'
         db        ' na ',0
errenv0  db        ' bajt',0
errenv1  db        ' !',13,10,'$',0

                                          ;* registry pro dek¢dov n¡ ‡¡sla
citdek   db        0                        ; ‡¡ta‡ platn˜ch ‡¡slic pro p©evod
citnum   db        0                        ; ‡¡ta‡ 1 ‡¡slice
parnum   db        0                        ; parametry pro dek¢dov n¡ ‡¡sla
                                            ;   bit 0=z kaz oddˆlovac¡ te‡ky
                                            ;   bit 1=tisk mezer
                                            ;   bit 2=tisk nul
                                            ;   bit 3=p©echodn˜ z kaz te‡ky
                                            ;   bit 4=z kaz tisku te‡ky tis¡c–

tabstin  db        5,8,11,13,16,19          ; tabulka hodin pro st¡n

outbuf   db        100 dup(0)               ; tiskov˜ buffer

texthlp  db        3,' <F1>=? ',0
textclk  db        3,' 00:00 ',0

textsp1  db        1,' bajt',0
textsp2  db        ' voln',0
textsp20 db        98h,0                    ; koncovka "˜"
textsp21 db        82h,0                    ; koncovka "‚"
textsp22 db        '˜ch',0
textsp23 db        ' z ',2,0

textsp3  db        1,' soubor',0
textsp30 db        ' zab¡r  ',2,0
textsp4  db        1,' bajt',0

textvy1  db        1,' bajt',0
textvy11 db        'y',0                    ; koncovka "y"
textvy12 db        96h,0                    ; koncovka "–"
textvy13 db        ' v ',2,0
textvy14 db        ' ve ',2,0

textvy2  db        1,' soubor',0
textvy21 db        'u ',0
textvy22 db        'ech ',0

txtsubd  db        '<POD--ADR>',0
txtupd   db        '<NAD--ADR>',0
txtdsk   db        '  <DISK>  ',0

txthelp2 db        9,'1',3,'pomoc'
         db        9,' 2',3,'povel'
         db        9,' 3',3,'zobraz'
         db        9,' 4',3,'edituj'
         db        9,' 5',3,'kop¡ruj'
         db        9,' 6',3,'p©esu¤'
         db        9,' 7',3,'hledej'
         db        9,' 8',3,'obnov'
         db        9,' 9',3,'porovnej'
         db        9,' 10',3,'!konec'
         db        0

txthelp  db        9,'1',3,'pomoc'
         db        9,' 2',3,'volby'
         db        9,' 3',3,'zobraz'
         db        9,' 4',3,'edituj'
         db        9,' 5',3,'kop¡ruj'
         db        9,' 6',3,'p©esu¤'
         db        9,' 7',3,'vytvo©'
         db        9,' 8',3,'zru¨'
         db        9,' 9',3,'atributy'
         db        9,' 10',3,'kalkul.'
         db        0

txtsel   db        ' v˜bˆr soubor– ',0
txtunsel db        ' zru¨it v˜bˆr ',0
txtcopy  db        ' budou se kop¡rovat soubory do ',0

col1     db        1bh                      ; atribut norm ln¡ho textu okna
col2     db        1eh                      ; atribut vysv¡cen‚ho textu
col3     db        30h                      ; atribut negovan‚ho textu
col4     db        3eh                      ; atribut negovan‚ho vysv¡cen‚ho tx.
col5     db        6fh                      ; ukazatel norm ln¡
col6     db        6ch                      ; ukazatel vysv¡cen¡
col7     db        70h                      ; norm ln¡ okno
col8     db        47h                      ; chybov‚ hl ¨en¡
col9     db        07h                      ; text ‡¡sla funk‡n¡ kl v. n povˆdy

color    db        1bh                      ; aktivn¡ barva textu
                                            ; (n sled. bajt slou‘¡ t‚‘ k PUSH)

all      db        '*.*',0                  ; ozna‡en¡ - v¨echny soubory

filecnf  db        'MNSHELL.CNF',0          ; konfigura‡n¡ soubor programu
fileexc  db        'MNEXEC.BAT',0           ; p©¡kazov˜ soubor
txtcall  db        'CALL ',0                ; vol n¡ souboru BAT
envexec  db        'EXECUTE=',0,0,0,0       ; nastaven¡ prost©ed¡ pro EXECUTE

normdsk  db        40h                      ; polo‘ka disku
         db        '   - A -   '            ; jm‚no disku

namepath db        '..',0,0,0,0,0,0,0,0,0,0 ; jm‚no adres ©e

flags    db        00000000b                ; indik tor
                                            ; bit 0: 1=aktivn¡ je lev‚ okno
                                            ; bit 1: 1=operace prob¡h  s L oknem
                                            ; bit 2: 1=obˆ okna jsou vypnuta
                                            ; bit 3: 1=n povˆda je zapnuta
                                            ; bit 4: 1=je n povˆda s ALT
                                            ; bit 6: 1=je zna‡ka v˜bˆru souboru
                                          ;* ukazatele pro editaci p©¡kaz. © dku
numchar  db        0                        ; po‡et znak– v bufferu
poztop   db        0                        ; pozice za‡ tku zobrazen‚ho textu
pozcomm  db        0                        ; pozice za‡ tku textu na displeji
pozbuf   db        0                        ; pozice kurzoru v textov‚m bufferu
                                          ;* ukazatele pro editaci © dku volby
numselb  db        0                        ; po‡et znak– v bufferu pro v˜bˆr
ukazsel  db        0                        ; ukazatel kurzoru pro buffer v˜bˆru
topsel   db        0                        ; za‡ tek zobrazen‚ho textu volby
firstsel db        0                        ; prvn¡ zobrazen  polo‘ka voleb
kurzsel  db        0                        ; kurzor pro volby

endadr   dw        offset endshell          ; adresa konce pamˆti (offset)

bufsel   label     byte                   ;* buffer masek pro selekci soubor–
         dw        bufsel2-bufsel1          ; d‚lka bufferu
bufsel1  db        128 dup(?)               ; buffer
bufsel2  db        1                        ; po‡et p©edvoleb
         dw        offset all               ; adresa p©edvolby "*.*"

bufcopy  label     byte                   ;* buffer pro kop¡rov n¡ soubor–
         dw        bufcop2-bufcop1
bufcop1  db        256 dup(?)
bufcop2  db        2
         dw        offset pathl
         dw        offset pathr

; -----------------------------------------------------------------------------

TABL     label     byte                     ; tabulka definic lev‚ho okna

flagsl   db        6                        ; indik tor stavu okna
                                            ; bit 0: 1=okno m  b˜t zapnuto
                                            ; bit 1: 1=okno je zapnuto
                                            ; bit 2: 1=p©¡znak - je lev‚ okno

pozl     db        0                        ; po‡ te‡n¡ pozice okna
radkul   db        19                       ; po‡et © dk– soubor–
insfil   dw        ?                        ; po‡et ozna‡en˜ch soubor–
kapfil   dd        ?                        ; kapacita ozna‡en˜ch soubor–

numdskl  db        ?                        ; po‡et disk– v seznamu
numfl    dw        ?                        ; po‡et soubor– v adres ©i
kurzl    dw        ?                        ; ‡¡slo souboru s kurzorem
firstl   dw        ?                        ; ‡¡slo prvn¡ho zobrazen‚ho souboru
adrfl    dw        offset endshell          ; offset adresy bufferu soubor–
                                          ;* parametry disku (dodr‘et po©ad¡ !)
alokl    dw        ?                        ; kapacita aloka‡n¡ho bloku lev‚ho
celkl    dd        ?                        ; celkov  kapacita lev‚ho disku
volnl    dd        ?                        ; voln  kapacita lev‚ho disku
soubl    dw        ?                        ; soubor– v lev‚m adres ©i
zabl     dd        ?                        ; zabran  kapacita soubor– vlevo

pathl    db        128 dup(?)               ; cesta k lev‚mu adres ©i

; -----------------------------------------------------------------------------

TABR     label     byte                     ; tabulka definic prav‚ho okna

flagsr   db        2                        ; indik tor stavu okna
                                            ; bit 0: 1=okno m  b˜t zapnuto
                                            ; bit 1: 1=okno je zapnuto
                                            ; bit 2: 0=p©¡znak - je prav‚ okno

pozr     db        40                       ; po‡ te‡n¡ pozice okna
radkur   db        19                       ; po‡et © dk– soubor–
insfir   dw        ?                        ; po‡et ozna‡en˜ch soubor–
kapfir   dd        ?                        ; kapacita ozna‡en˜ch soubor–

numdskr  db        ?                        ; po‡et disk– v seznamu
numfr    dw        ?                        ; po‡et soubor– v adres ©i
kurzr    dw        ?                        ; ‡¡slo souboru s kurzorem
firstr   dw        ?                        ; ‡¡slo prvn¡ho zobrazen‚ho souboru
adrfr    dw        offset endshell          ; offset adresy bufferu soubor–
                                          ;* parametry disku (dodr‘et po©ad¡ !)
alokr    dw        ?                        ; kapacita aloka‡n¡ho bloku prav‚ho
celkr    dd        ?                        ; celkov  kapacita prav‚ho disku
volnr    dd        ?                        ; voln  kapacita prav‚ho disku
soubr    dw        ?                        ; soubor– v prav‚m adres ©i
zabr     dd        ?                        ; zabran  kapacita soubor– vpravo

pathr    db        128 dup(?)               ; cesta k prav‚mu adres ©i

; -----------------------------------------------------------------------------

verze    dw        ?                        ; ‡¡slo verze opera‡n¡ho syst‚mu
segpsp   dw        ?                        ; segment PSP
typdisp  db        ?                        ; typ displeje:
                                            ;    0=nestandardn¡
                                            ;    1=CGA
                                            ;    2=MDA
                                            ;    3=EGA
vmod     db        ?                        ; aktivn¡ videom¢d
segvram  dw        ?                        ; segment videopamˆti
segend   dw        ?                        ; segment konce aloka‡n¡ho bloku
aktatr   db        ?                        ; aktivn¡ atribut displeje
radek    db        ?                        ; © dek kurzoru
aktpage  db        ?                        ; aktivn¡ str nka displeje
                                            ;  bit 1=str nka pro v˜stup
                                            ;  bit 2=str nka pro displej
parenv   dd        ?                        ; otcovsk‚ prost©ed¡
maxenv   dw        ?                        ; maxim ln¡ d‚lka prost©ed¡

outid    dw        ?                        ; identifik tor v˜stupn¡ho souboru
inpid    dw        ?                        ; identifik tor vstupn¡ho souboru
ukazbuf  dw        ?                        ; ukazatel dat v diskov‚m buffer
numbuf   dw        ?                        ; po‡et bajt– v diskov‚m bufferu

buftxt   db        129 dup(?)               ; textov˜ buffer

diskbuf  db        512 dup(?)               ; diskov˜ buffer

normfil  db        20 dup(?)                ; normalizovan˜ soubor
                                            ; 1 bajt - atributy
                                            ; 8 bajt– - jm‚no
                                            ; 3 bajty - p©¡pona
                                            ; 4 bajty - velikost
                                            ; 2 bajty - datum
                                            ; 2 bajty - ‡as

pathhome db        128 dup(?)               ; p©¡stupov  cesta k domovsk‚mu adr.
pathinit db        128 dup(?)               ; inicializa‡n¡ aktivn¡ cesta

selbuf   db        128 dup(?)               ; buffer pro editaci p©i v˜bˆru


endshell label   byte                       ; adresa konce programu

data     ends

; *****************************************************************************
;
;                  Segment z sobn¡ku (p©echodnˆ do inicializace)
;
; *****************************************************************************

stack    segment   stack
         dw        512 dup(?)               ; z sobn¡k
stack    ends

         END       main                     ; startovac¡ adresa
