



; -----------------------------------------------------------------------------
;        konstanty
; -----------------------------------------------------------------------------

HI       EQU       256

bit0     EQU       1
bit1     EQU       2
bit2     EQU       4
bit3     EQU       8
bit4     EQU       10h
bit5     EQU       20h
bit6     EQU       40h
bit7     EQU       80h
bit8     EQU       100h
bit9     EQU       200h
bit10    EQU       400h
bit11    EQU       800h
bit12    EQU       1000h
bit13    EQU       2000h
bit14    EQU       4000h
bit15    EQU       8000h

; -----------------------------------------------------------------------------
;                        vstupn konektor LPT
; -----------------------------------------------------------------------------
; pota CANON25                             tiskrna CANON25
;  pin 1  )o( pin 1     STROBE
;                   
;  pin 15 )Ŀ              (peruit) X( pin 15    ERROR
;                  
;  pin 18 )oo( pin 18    GND
;                     
;  pin 16 )o( pin 16    INIT
;                  R1  R2     R3
;                       
;                     2K2   68K
;                       
;                 5K6        
;                                      SHARP MZ800 - NF konektor
;                 o>(         READ
;                                   C1
;                                   68n
;                oĴ <(         WRITE
;                          R4  
;                                22K
;                              
;               o(         zem (stnn)
; Odpory R3 a R4 je vhodn ze zatku nahradit odporovm trimrem 100K a nastavit
; stedn polohu signlu pi penosu do potae (testovacm programem)
; -----------------------------------------------------------------------------

Code     segment
         assume    cs:code,ds:code
         org       100h

; ------ zmenen souasnho konce bloku programu

start:   mov       bx,offset(Konec-Start+10fh)/16 ; velikost programu v odstav.
         mov       ah,4ah
         int       21h                      ; zmenen bloku pamti
         mov       dx,offset MemTxt
         jc        Chyba2                   ; nedostatek pamti

; ------ inicializace zsobnku

         mov       sp,offset Zasob

; ------ vytvoen datovho bloku

         mov       bx,1000h                 ; velikost datovho bloku
         mov       ah,48h
         int       21h                      ; vytvoen datovho bloku
         jc        Chyba2                   ; nedostatek pamti
         mov       ds:[DatSegm],ax          ; adresa datovho bloku

; ------ zatek jmna souboru

         mov       si,81h
         mov       cl,ds:[si-1]
         mov       ch,0
         cld
Start1:  jcxz      chyba
         mov       dx,si
         lodsb
         dec       cx
         cmp       al," "
         je        Start1
         cmp       al,9
         je        Start1
         cmp       al,13
         jne       Start2

; ------ chyba - nen nic zadno

Chyba:   mov       dx,offset HlpTxt
Chyba2:  mov       ah,9
         int       21h
         int       20h

; ------ oznaen konce jmna souboru

Start2:  jcxz      Start3
         lodsb
         dec       cx
         cmp       al," "
         ja        Start2
         dec       si
Start3:  mov       byte ptr ds:[si],0
         mov       ds:[SoubName],dx         ; schova adresy jmna souboru

; ------ test, zda soubor ji existuje

         mov       cx,7
         mov       ah,4eh
         int       21h                      ; existuje ji soubor ?
         mov       dx,offset ExTxt
         jnc       Chyba2                   ; chyba - soubor neexistuje

; ------ hlen o zahjen pjmu

         mov       dx,offset Text1
         mov       ah,9
         int       21h

; ------ pjem hlaviky souboru

         mov       di,100                   ; dlka vodnho tnu
         mov       bh,40                    ; dlka synchronizace 1
         mov       bl,40                    ; dlka synchronizace 2
         mov       cx,offset(HlavaEnd-Hlava) ; dlka hlaviky
         push      ds
         pop       es                       ; segment adresy hlaviky
         mov       si,offset Hlava          ; hlavika
         call      LoadHD                   ; pjem hlaviky
         jc        StartX                   ; peruen programu

; ------ zobrazen jmna souboru

         mov       cx,17                    ; max. dlka textu
         mov       si,offset HlavaNam
Start6:  mov       dl,ds:[si]
         inc       si
         cmp       dl,13
         je        Start62
         mov       ah,2
         int       21h
         loop      Start6
Start62:

; ------ pjem dat souboru

         mov       di,100                   ; dlka vodnho tnu
         mov       bh,20                    ; synchronizace 1
         mov       bl,20                    ; synchronizace 2
         mov       cx,ds:[HlavaDel]         ; dlka souboru
         xor       si,si                    ; poten adresa
         mov       es,ds:[DatSegm]          ; datov segment
         call      LoadHD                   ; pjem dat souboru
         jc        StartX                   ; peruen programu

; ------ oddkovn

         mov       dx,offset CRTxt
         mov       ah,9
         int       21h

; ------ test, zda byla chyba CRC

         test      byte ptr ds:[Param],bit1
         jz        Start8

         mov       dx,offset CRCTxt
StartCh1:mov       ah,9
         int       21h
         jmp       short StartX

; ------ vytvoen souboru

Start8:  xor       cx,cx
         mov       dx,ds:[SoubName]         ; adresa jmna souboru
         mov       ah,3ch
         int       21h                      ; vytvoen souboru
         mov       dx,offset CreaTxt
         jc        StartCh1                 ; chyba - nelze vytvoit
         mov       bx,ax                    ; identifiktor souboru

; ------ uloen hlaviky souboru

         mov       cx,offset(HlavaEnd-Hlava) ; dlka hlaviky
         mov       dx,offset Hlava          ; zatek hlaviky
         mov       ah,40h
         int       21h                      ; zpis hlaviky souboru

; ------ zpis dat souboru

         push      ds
         mov       cx,ds:[HlavaDel]         ; velikost dat
         mov       ds,ds:[DatSegm]          ; datov segment
         xor       dx,dx                    ; adresa k zpisu dat
         mov       ah,40h
         int       21h                      ; zpis souboru do souboru
         pop       ds
         mov       dx,offset WrTxt
         jc        StartCh1
         cmp       ax,cx
         jne       StartCh1

; ------ uzaven souboru

         mov       ah,3eh
         int       21h                      ; uzaven souboru
         jc        StartCh1

; ------ konec programu

StartX:
         int       20h

; -----------------------------------------------------------------------------
;        pjem hlaviky nebo dat
; -----------------------------------------------------------------------------
; VSTUP: BH=dlka synchronizace 1
;        BL=dlka synchronizace 2
;        CX=dlka bloku dat
;        DI=dlka vodnho tnu
;        ES:SI=poten adresa dat
;        DS=datov segment
; VSTUP:CY=peruen programu
; -----------------------------------------------------------------------------

LoadHD   PROC      NEAR

; ------ schova registr

         push      ax
         push      dx
         call      InitPort                 ; instalace a inicializace port

; ------ pjem vodu ped daty

         call      LoadUvod                 ; pjem vodnho tnu a synchron.
         jc        LoadHD9                  ; peruen programu

; ------ pjem dat

         call      LoadBlok                 ; vysln obsahu dat
         jc        LoadHD9                  ; peruen programu

; ------ pjem kontrolnho soutu

         call      LoadByte                 ; pjem prvnho bajtu CRC
         jc        LoadHD9                  ; peruen programu
         mov       ah,al                    ; schova kontrolnho soutu HIGH
         call      LoadByte                 ; pjem druhho bajtu CRC
         jc        LoadHD9                  ; peruen programu

; ------ kontrola kontrolnho souet dat

         call      KontrS                   ; kontroln souet dat
         cmp       ax,dx                    ; souhlas kontroln souet ?
         je        LoadHD9                  ; kontroln souet je OK
         or        byte ptr ds:[Param],bit1 ; pznak chyby CRC

; ------ nvrat registr

LoadHD9: pushf
         call      DeInit                   ; nvrat port a peruen
         popf
         pop       dx
         pop       ax
         ret

LoadHD   ENDP

; -----------------------------------------------------------------------------
;        kontroln souet bloku dat
; -----------------------------------------------------------------------------
; VSTUP: ES:SI=adresa bloku dat
;        CX=poet bajt dat
;        DS=datov segment
; VSTUP:DX=kontroln souet dat
; -----------------------------------------------------------------------------

KontrS   PROC      NEAR

; ------ schova registr

         push      ax
         push      cx
         push      si

; ------ kontrola, zda jsou njak data

         xor       dx,dx                    ; stada kontrolnho soutu
         jcxz      KontrS9                  ; nejsou dn data

; ------ piten jednoho bajtu dat

KontrS1: mov       al,es:[si]               ; datov bajt k piten
         inc       si                       ; zven ukazatele dat
         mov       ah,8                     ; poet bit bajtu
KontrS2: shl       al,1                     ; jeden bit k piten
         adc       dx,0                     ; piten bitu ke stadai
         dec       ah                       ; ta bit
         jnz       KontrS2                  ; piten dalho bitu bajtu
         loop      KontrS1                  ; piten dalho bajtu

; ------ nvrat registr

KontrS9: pop       si
         pop       cx
         pop       ax
         ret

KontrS   ENDP

; -----------------------------------------------------------------------------
;        synchronizace na vod dat
; -----------------------------------------------------------------------------
; VSTUP: BH=poet bit synchronizace 1 ("1") (40=hlavika, 20=data)
;        BL=poet bit synchronizace 2 ("0") (40=hlavika, 20=data)
;        DS=datov segment
; VSTUP:CY=peruen programu
; -----------------------------------------------------------------------------

LoadUvod PROC      NEAR

; ------ schova registr

         push      ax
         push      cx
         push      bx

; ------ nvrat registr pi chyb

LoadUv1: pop       bx
         push      bx

; ------ ekn na vodn hranu signlu "1"

         call      LoadImp                  ; ekn na vodn hranu signlu
         jc        LoadUv9                  ; peruen programu

; ------ men dlky impulsu "1"

         mov       ah,bit3
         mov       cx,-1
         call      Cekej                    ; ekn na zatek impulsu "0"
         jc        LoadUv9                  ; peruen programu
         jcxz      LoadUv1                  ; peteen asu

; ------ vpoet konstanty

         neg       cx                       ; poet cykl
         mov       ax,cx
         shr       ax,1
         add       cx,ax                    ; doba * 1.5
         jc        LoadUv1
         jcxz      LoadUv1                  ; peteen
         mov       ds:[Delka0],cx           ; dlka impulsu "0"
         shl       cx,1
         jc        LoadUv1                  ; peteen
         mov       ds:[Delka1],cx           ; dlka impulsu "1"

; ------ pjem vodnho tnu

LoadUv2: call      LoadBit                  ; pjem jednoho bitu
         jc        LoadUv9                  ; peruen programu
         jz        LoadUv2                  ; je bit "0" - OK
         dec       bh                       ; prvn bit se ztratil

; ------ pjem synchronizace 1

         cmp       bh,0                     ; jsou njak bity ?
         je        LoadUv5                  ; nejsou dn bity
LoadUv4: call      LoadBit                  ; pjem dalho bitu
         jc        LoadUv9                  ; peruen programu
         jz        LoadUv1                  ; nen bit "1"
         dec       bh                       ; ta poadovanch bit
         jnz       LoadUv4                  ; pjem dalho bitu

; ------ pjem synchronizace 2

LoadUv5: cmp       bl,0                     ; jsou njak bity ?
         je        LoadUv7                  ; nejsou dn bity
LoadUv6: call      LoadBit                  ; pjem dalho bitu
         jc        LoadUv9                  ; peruen programu
         jnz       LoadUv1                  ; nen bit "0"
         dec       bl                       ; ta poadovanch bit
         jnz       LoadUv6                  ; pjem dalho bitu

; ------ pjem startovacho bitu

LoadUv7: call      LoadBit                  ; pjem startovacho bitu
         jc        LoadUv9                  ; peruen programu
         jz        LoadUv1                  ; nen startovac bit "1"

; ------ nvrat registr

LoadUv9: pop       bx
         pop       cx
         pop       ax
         ret

LoadUvod ENDP

; -----------------------------------------------------------------------------
;        pjem bloku dat
; -----------------------------------------------------------------------------
; VSTUP: ES:SI=adresa k naten bloku dat
;        CX=poet bajt k naten bloku dat
;        DS=datov segment
; VSTUP:CY=peruen programu
; -----------------------------------------------------------------------------

LoadBlok PROC      NEAR

; ------ schova registr

         push      ax
         push      cx
         push      si

; ------ kontrola, zda jsou njak data k vysln

         clc
         jcxz      LoadBlk9                 ; nejsou dn data

; ------ pjem bloku dat

LoadBlk1:call      LoadByte                 ; pjem jednoho datovho bajtu
         jc        LoadBlk9                 ; peruen programu
         mov       es:[si],al               ; uloen bajtu
         inc       si                       ; zven adresy v bufferu
         loop      LoadBlk1                 ; pjem dalho bajtu

; ------ nvrat registr

LoadBlk9:pop       si
         pop       cx
         pop       ax
         ret

LoadBlok ENDP

; -----------------------------------------------------------------------------
;        pjem jednoho bajtu
; -----------------------------------------------------------------------------
; VSTUP: DS=datov segment
; VSTUP:CY=peruen
;        AL=pijat bajt
; -----------------------------------------------------------------------------

LoadByte PROC      NEAR

         call      LoadBit
         mov       al,1

LoadByt1:call      LoadBit
         jc        LoadByt9                 ; peruen programu
         jz        LoadByt2                 ; je bit "0"
         stc                                ; je bit "1"
LoadByt2:rcl       al,1                     ; rotace bitu do AL
         jnc       LoadByt1                 ; pjem dalho bitu
         clc

LoadByt9:ret

LoadByte ENDP

; -----------------------------------------------------------------------------
;        pjem jednoho bitu
; -----------------------------------------------------------------------------
; VSTUP: DS=datov segment
; VSTUP:NZ=je bit 1
;        CY=je peruen programu
; -----------------------------------------------------------------------------

LoadBit  PROC      NEAR

         push      ax
         push      cx
         push      dx

; ------ ekn na vodn hranu signlu

         xor       dx,dx                    ; stada doby
LoadBit1:call      LoadImp                  ; ekn na vodn hranu signlu
         jc        LoadBit9                 ; peruen programu

; ------ zmen dlky impulsu

         mov       ah,bit3
         mov       cx,-1
         call      Cekej                    ; ekn na zatek impulsu "0"
         jc        LoadBit9                 ; peruen programu
         neg       cx                       ; poet cykl
         add       cx,dx                    ; piten stadae
         mov       dx,cx                    ; schova do stadae

; ------ filtrace krtkch impuls

         mov       ax,ds:[Delka0]           ; krat impuls
         shr       ax,1                     ; minimln doba
         cmp       cx,ax                    ; je minimln doba ?
         jb        LoadBit1                 ; je pli krtk impuls

; ------ vpoet namen doby

         mov       ax,cx
         shr       ax,1
         add       cx,ax                    ; doba * 1.5

; ------ vpoet stedn doby

         mov       ax,ds:[Delka0]
         add       ax,ds:[Delka1]
         shr       ax,1                     ; stedn hodnota

; ------ rozlien, zda je bit 0 nebo 1

         cmp       cx,ax                    ; je bit 0 nebo 1 ?
         jb        LoadBit3                 ; je bit 0

; ------ je bit 1

         shr       cx,1                     ; korekce na zkladn dlku "0"
         add       cx,ds:[Delka0]
         add       cx,ds:[Delka1]
         shr       cx,1
         mov       ds:[Delka1],cx
         shr       cx,1
         mov       ds:[Delka0],cx
         or        cx,cx
         jmp       short LoadBit9

; ------ je bit 0

LoadBit3:add       cx,ds:[Delka0]
         add       cx,ds:[Delka1]
         shr       cx,1
         mov       ds:[Delka1],cx
         shr       cx,1
         mov       ds:[Delka0],cx
         cmp       cx,cx

; ------ nvrat registr

LoadBit9:pop       dx
         pop       cx
         pop       ax
         ret

LoadBit  ENDP

; -----------------------------------------------------------------------------
;        ekn na vodn hranu signlu (CY=peruen)
; -----------------------------------------------------------------------------
; VSTUP: DS=datov segment
; VSTUP:CY=peruen
; -----------------------------------------------------------------------------

LoadImp  PROC      NEAR

; ------ schova registr

         push      ax
         push      dx
PortLPT: mov       dx,379h                  ; port LPT

; ------ test, zda je peruen porgramu

         sti                                ; peruen mus bt povoleno
LoadImp1:test      byte ptr ds:[Param],bit0 ; je peruen programu ?
         stc                                ; pznak peruen
         jnz       LoadImp9                 ; je peruen programu

; ------ ekn na konec stavu 1

         in        al,dx                    ; ten stavu portu
         test      al,bit3                  ; test stavovho bitu
         jz        LoadImp1                 ; je stav 1 - ekn na konec

; ------ test, zda je peruen porgramu

LoadImp2:test      byte ptr ds:[Param],bit0 ; je peruen programu ?
         stc                                ; pznak peruen
         jnz       LoadImp9                 ; je peruen programu

; ------ ekn na zatek novho stavu 1

         in        al,dx                    ; ten stavu portu
         test      al,bit3                  ; test stavovho bitu
         jnz       LoadImp2                 ; je stav 0 - ekn na konec

; ------ nvrat registr

LoadImp9:pop       dx
         pop       ax
         ret

LoadImp  ENDP

; -----------------------------------------------------------------------------
;        ekn po dobu CX na stav portu LPT AH (vrac CX=zbytek konstanty)
; -----------------------------------------------------------------------------
; VSTUP: CX=max. doba pro ekn
;        AH=poadovan stav portu LPT (bit 3)
; VSTUP:CX=zbyl poet takt
;        CY=peruen programu
; -----------------------------------------------------------------------------

Cekej    PROC      NEAR

; ------ schova registr

         push      ax
         push      dx
         sti
PortLPT3:mov       dx,379h                  ; port LPT

; ------ test, zda je peruen programu

Cekej1:  test      byte ptr ds:[Param],bit0 ; je peruen programu ?
         stc                                ; pznak peruen
         jnz       Cekej9                   ; je peruen programu

; ------ test stavu portu LPT

         in        al,dx
         and       al,bit3
         cmp       al,ah
         loopne    Cekej1
         clc

; ------ nvrat registr

Cekej9:  pop       dx
         pop       ax
         ret

Cekej    ENDP

; -----------------------------------------------------------------------------
;        obsluha INT 08h
; -----------------------------------------------------------------------------

INT08    PROC      FAR

; ------ schova registr

         push      ax

; ------ uvolnn adie peruen

         mov       al,20h
         out       [20h],al                 ; uvolnn adie peruen

; ------ nvrat registr

         pop       ax
         iret

INT08    ENDP

; -----------------------------------------------------------------------------
;        obsluha INT 09h
; -----------------------------------------------------------------------------

INT09    PROC      FAR

; ------ schova registr

         push      ax
         push      cx

; ------ uvolnn adie peruen

         mov       al,20h
         out       [20h],al                 ; uvolnn adie peruen
         sti

; ------ uzamen klvesnice

         call      Cekej09                  ; ekn na pipravenost klvesnice
         mov       al,0adh
         out       [64h],al                 ; vysln povelu-uzamen klvesnice

; ------ ten kdu klvesy

         call      Cekej09                  ; ekn na pipravenost klvesnice
         in        al,[60h]

; ------ peruen programu

         cmp       al,1
         jne       Int095
         or        byte ptr cs:[Param],bit0 ; pznak peruen programu ESC

; ------ uvolnn klvesnice

Int095:  call      Cekej09                  ; ekn na pipravenost klvesnice
         mov       al,0aeh
         out       [64h],al                 ; vysln povelu-uvolnn klvesnice

; ------ nvrat registr

         pop       cx
         pop       ax

         jmp       dword ptr cs:[Old09]
;         iret

Int09    ENDP

; -----------------------------------------------------------------------------
;        ekn na pipravenost klvesnice (ni registry CX a AL !)
; -----------------------------------------------------------------------------

Cekej09  PROC      NEAR

         cli
         mov       cx,5000
Cekej091:in        al,[64h]
         test      al,bit1
         loopnz    Cekej091
         ret

Cekej09  ENDP

; *****************************************************************************
;                             InitPort
;               inicializace port, start hry melodie
; -----------------------------------------------------------------------------
; VSTUP: DS=datov segment
; *****************************************************************************

InitPort PROC      NEAR

; ------ schova registr

         push      ax
         push      bx
         push      cx
         push      dx
         push      es

; ------ vypnut motor disketovch mechanik

         mov       dx,3f2h
         mov       al,0ch
         out       dx,al                    ; vypnut motor mechanik
         push      ds
         xor       ax,ax
         mov       ds,ax
         and       byte ptr ds:[43fh],not bit0+bit1+bit2+bit3+bit4+bit5 ; motory

; ------ inicializace adres port

         mov       ax,ds:[408h]
         or        ax,ax
         jnz       InitPrt1
         mov       ax,ds:[40ah]
         or        ax,ax
         jnz       InitPrt1
         mov       ax,ds:[40ch]
         or        ax,ax
         jnz       InitPrt1
         mov       ax,ds:[40eh]
         or        ax,ax
         jnz       InitPrt1
         mov       ax,378h
InitPrt1:inc       ax
         mov       word ptr cs:[PortLPT+1],ax    ; adresa portu LPT
;         mov       word ptr cs:[PortLPT2+1],ax   ; adresa portu LPT
         mov       word ptr cs:[PortLPT3+1],ax   ; adresa portu LPT
         pop       ds

; ------ schova vektoru INT 08h

         mov       ax,3508h
         int       21h                      ; poskytnut adresy INT 08h
         mov       word ptr ds:[Old08],bx
         mov       word ptr ds:[Old08+2],es ; schova adresy INT 08h

; ------ schova vektoru INT 09h

         mov       ax,3509h
         int       21h                      ; poskytnut adresy INT 09h
         mov       word ptr ds:[Old09],bx
         mov       word ptr ds:[Old09+2],es ; schova adresy INT 09h

; ------ schova registru masky peruen

         in        al,[21h]                 ; registr masky peruen
         mov       ds:[OldMask],al          ; schova masky peruen

; ------ pechodn zkaz vech peruen

         cli                                ; zkaz peruen
         mov       al,0ffh
         out       [21h],al                 ; zkaz vech peruen

; ------ instalace INT 08h

         mov       dx,offset Int08          ; obsluha INT 08h
         mov       ax,2508h
         int       21h                      ; instalace obsluhy INT 08h

; ------ instalace INT 09h

         mov       dx,offset Int09          ; obsluha INT 09h
         mov       ax,2509h
         int       21h                      ; instalace obsluhy INT 09h

; ------ uvolnn peruen od klvesnice a hodin

         mov       al,20h
         out       [20h],al
         mov       al,0fdh
         out       [21h],al                 ; uvolnn peruen
         sti                                ; povolen peruen

; ------ nvrat registr

         pop       es
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

InitPort ENDP

; *****************************************************************************
;                                DeInit
;                     odinstalovn port po he
; -----------------------------------------------------------------------------
; VSTUP: DS=datov segment
; *****************************************************************************

DeInit   PROC      NEAR

; ------ schova registr

         push      ax
         push      bx
         push      cx
         push      dx
         push      es

; ------ pechodn zkaz vech peruen

         mov       al,0ffh
         out       [21h],al                 ; zkaz vech peruen
         cli                                ; zkaz peruen

; ------ vypnut vstupu na reproduktor

         in        al,[61h]
         and       al,not 3
         out       [61h],al                 ; vypnut vstupu na reproduktor

; ------ nvrat adresy INT 08h

         push      ds
         lds       dx,ds:[Old08]            ; pvodn adresa INT 08h
         mov       ax,2508h
         int       21h                      ; nvrat adresy INT 08h
         pop       ds

; ------ nvrat adresy INT 09h

         push      ds
         lds       dx,ds:[Old09]            ; pvodn adresa INT 09h
         mov       ax,2509h
         int       21h                      ; nvrat adresy INT 09h
         pop       ds

; ------ nvrat registru peruen

         mov       al,ds:[OldMask]          ; pvodn registr masky peruen
         out       [21h],al                 ; nvrat registru masky peruen
         sti                                ; povolen peruen

; ------ ten hodin systmovho asu (AT)

         mov       dl,8ah                   ; pednastaven na nesmysl
         mov       ah,2
         int       1ah                      ; ten hodin
         cmp       dl,1                     ; peteen pes plnoc ?
         ja        Deinit3                  ; hodiny neplat

; ------ nvrat pznaku peteen pes plnoc

         jne       DeInit2                  ; nen peteen pes plnoc
         xor       ax,ax
         mov       es,ax
         mov       byte ptr es:[470h],dl    ; nvrat pznaku peteen plnoci

; ------ konverze z BCD formtu na binrn

DeInit2: mov       al,ch                    ; hodina
         call      BcdBin                   ; konverze na binrn tvar
         mov       ch,al
         mov       al,cl                    ; minuta
         call      BcdBin                   ; konverze na binrn tvar
         mov       cl,al
         mov       al,dh                    ; sekunda
         call      BcdBin                   ; konverze na binrn tvar
         mov       dh,al

; ------ nastaven asu

         xor       dl,dl                    ; setina sekundy
         mov       ah,2dh
         int       21h                      ; nastaven systmovho asu

DeInit3:

; ------ nvrat registr

         pop       es
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

DeInit   ENDP

; -----------------------------------------------------------------------------
;        Konverze sla z BCD tvaru na binrn
; -----------------------------------------------------------------------------

BcdBin   PROC      NEAR

         push      cx
         mov       ch,al
         and       ch,0fh
         and       al,0f0h
         mov       cl,4
         shr       al,cl
         mov       cl,10
         mul       cl
         add       al,ch
         pop       cx
         ret

BcdBin   ENDP

; -----------------------------------------------------------------------------
;        data
; -----------------------------------------------------------------------------

MemTxt   db        7,'Nedostatek pameti !',13,10,'$'
ExTxt    db        7,'Soubor jiz existuje !',13,10,'$'

CreaTxt  db        7,'Soubor nelze vytvorit !',13,10,'$'

WrTxt    db        7,'Chyba zapisu souboru !',13,10,'$'

hlptxt   db        7,'Zadejte jmeno souboru k prijmu !'
CRTxt    db        13,10,'$'

CRCTxt   db        7,'Chyba pri prenosu souboru !',13,10,'$'

Text1    db        'Prijimam $'

Param    db        0                        ; parametry
                                            ;  bit 0: 1=peruen ESC
                                            ;  bit 1: 1=chyba kontrolnho soutu

CitImp   db        0                        ; ta impulsu k rozhodovn

Delka0   dw        100                      ; dlka impulsu "0"
Delka1   dw        100                      ; dlka impulsu "1"

SegmVRAM dw        0b000h                   ; segment videopamti

Konst08  db        72                       ; asov konstanta hodin
                                            ;     (16.57 kHz / 60.34 us)
                                            ;      hodiny 1 193 182 Hz
DelImp   db        6                        ; dlka impulsu pro rozhodovn

; ------ zhlav souboru (80h bajt)

Hlava    label     byte
HlavaTyp db        1                        ; typ souboru (1=stroj., 5=BASIC)
HlavaNam db        17 dup(" ")              ; jmno souboru
HlavaDel dw        0                        ; dlka souboru
HlavaBeg dw        1200h                    ; poten adresa souboru
HlavaExe dw        1200h                    ; startovac adresa programu
HlavaCom db        80h - 24 dup(0)          ; poznmka souboru
HlavaEnd label     byte

Old08    dd        ?                        ; pvodn adresa INT 08h
Old09    dd        ?                        ; pvodn adresa INT 09h
OldMask  db        ?                        ; pvodn maska peruen

DatSegm  dw        ?                        ; adresa datovho bloku
DatNum   dw        ?                        ; poet bajt v bufferu

SoubName dw        ?                        ; adresa jmna souboru

         dw        200h dup(?)              ; zsobnk
zasob    label     byte                     ; konec zsobnku

konec    label     byte                     ; konec programu

code     ends
         end       start
