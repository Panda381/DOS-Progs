; -----------------------------------------------------------------------------
;        konstanty
; -----------------------------------------------------------------------------

HI       EQU       256

bit0     EQU       1
bit1     EQU       2
bit2     EQU       4
bit3     EQU       8
bit4     EQU       10h
bit5     EQU       20h
bit6     EQU       40h
bit7     EQU       80h
bit8     EQU       100h
bit9     EQU       200h
bit10    EQU       400h
bit11    EQU       800h
bit12    EQU       1000h
bit13    EQU       2000h
bit14    EQU       4000h
bit15    EQU       8000h

; -----------------------------------------------------------------------------
;                        vstupn konektor LPT
; -----------------------------------------------------------------------------
; pota CANON25                             tiskrna CANON25
;  pin 1  )o( pin 1     STROBE
;                   
;  pin 15 )Ŀ              (peruit) X( pin 15    ERROR
;                  
;  pin 18 )oo( pin 18    GND
;                     
;  pin 16 )o( pin 16    INIT
;                  R1  R2     R3
;                       
;                     2K2   68K
;                       
;                 5K6        
;                                      SHARP MZ800 - NF konektor
;                 o>(         READ
;                                   C1
;                                   68n
;                oĴ <(         WRITE
;                          R4  
;                                22K
;                              
;               o(         zem (stnn)
; Odpory R3 a R4 je vhodn ze zatku nahradit odporovm trimrem 100K a nastavit
; stedn polohu signlu pi penosu do potae (testovacm programem)
; -----------------------------------------------------------------------------

code     segment
         assume    cs:code,ds:code
         org       100h

; ------ zmenen souasnho konce bloku programu

start:   mov       bx,offset(Konec-Start+10fh)/16 ; velikost programu v odstav.
         mov       ah,4ah
         int       21h                      ; zmenen bloku pamti
         mov       dx,offset MemTxt
         jc        Chyba2                   ; nedostatek pamti

; ------ inicializace zsobnku

         mov       sp,offset Zasob

; ------ vytvoen datovho bloku

         mov       bx,1000h                 ; velikost datovho bloku
         mov       ah,48h
         int       21h                      ; vytvoen datovho bloku
         jc        Chyba2                   ; nedostatek pamti
         mov       ds:[DatSegm],ax          ; adresa datovho bloku

; ------ zatek jmna souboru

         mov       si,81h
         mov       cl,ds:[si-1]
         mov       ch,0
         cld
Start1:  jcxz      chyba
         mov       dx,si
         lodsb
         dec       cx
         cmp       al," "
         je        Start1
         cmp       al,9
         je        Start1
         cmp       al,13
         jne       Start2

; ------ chyba - nen nic zadno

Chyba:   mov       dx,offset HlpTxt
Chyba2:  mov       ah,9
         int       21h
         int       20h

; ------ oznaen konce jmna souboru

Start2:  jcxz      Start3
         lodsb
         dec       cx
         cmp       al," "
         ja        Start2
         dec       si
Start3:  mov       byte ptr ds:[si],0

; ------ oteven souboru

         mov       ax,3d00h
         int       21h                      ; oteven souboru pro ten
         jc        Chyba                    ; chybn zadn souboru
         mov       bx,ax                    ; identifiktor souboru

; ------ naten hlaviky souboru

         mov       cx,offset(HlavaEnd-Hlava) ; dlka hlaviky
         mov       dx,offset Hlava          ; zatek hlaviky
         mov       ah,3fh
         int       21h                      ; naten hlaviky souboru

; ------ naten souboru do pamti

         push      ds
         mov       ds,ds:[DatSegm]          ; datov segment
         mov       cx,0ffffh                ; velikost dat
         xor       dx,dx                    ; adresa k naten dat
         mov       ah,3fh
         int       21h                      ; naten 1. sti souboru do pamti
         pop       ds
         mov       ds:[DatNum],ax           ; poet bajt

; ------ uzaven souboru

         pushf
         mov       ah,3eh
         int       21h                      ; uzaven souboru
         popf
         jc        Chyba                    ; chyba ten

; ------ hlen o vysln souboru

         mov       dx,offset TextVys
         mov       ah,9
         int       21h

; ------ zobrazen jmna souboru

         mov       cx,17                    ; max. dlka textu
         mov       si,offset HlavaNam
Start6:  mov       dl,ds:[si]
         inc       si
         cmp       dl,13
         je        Start62
         mov       ah,2
         int       21h
         loop      Start6
Start62:

; ------ parametry zavad؟e

         push      ds
         pop       es
         mov       si,offset HlavaDel
         mov       di,offset LoadHlav
         mov       cx,13
         cld
         rep       movsb                    ; penos parametr souboru

         mov       si,offset HlavaNam
         mov       di,offset LoadNam
         mov       cx,17
         rep       movsb                    ; penos jmna

         mov       si,offset HlavaCom+7
         mov       di,offset LoadComm+7
         mov       cx,offset(HlavaEnd-HlavaCom)-7
         rep       movsb                    ; penos poznmky

; ------ vysln zhlav zavad؟e

         mov       di,4000                  ; dlka tnu
         mov       bh,40
         mov       bl,40
         mov       cx,offset (LoadPrg-Loader) ; dlka zhlav zavad؟e
         mov       si,offset Loader         ; zhlav zavad؟e
         call      SaveHD                   ; vysln zhlav zavad؟e
         jc        StartX                   ; peruen programu

; ------ vysln programu zavad؟e

         shr       bh,1                     ; synchronizace 1
         shr       bl,1                     ; synchronizace 2
         mov       cx,offset(LoadEnd-LoadPrg) ; dlka programu zavad؟e
         mov       si,offset LoadPrg        ; program zavad؟e
         call      SaveHD                   ; vysln zavad؟e
         jc        StartX                   ; peruen programu

; ------ vysln dat souboru

         mov       byte ptr ds:[Konst08],55
         mov       byte ptr ds:[DelImp],2
         mov       di,10000
         mov       cx,ds:[HlavaDel]         ; dlka souboru
         xor       si,si                    ; poten adresa
         mov       es,ds:[DatSegm]          ; datov segment
         call      SaveHD                   ; vysln dat souboru

; ------ konec programu

StartX:
         mov       dx,offset CRTxt
         mov       ah,9
         int       21h

         int       20h

; -----------------------------------------------------------------------------
;        vysln hlaviky nebo dat
; -----------------------------------------------------------------------------
; VSTUP: BH=dlka synchronizace 1
;        BL=dlka synchronizace 2
;        CX=dlka bloku dat
;        DI=dlka vodnho tnu
;        ES:SI=poten adresa dat
;        DS=datov segment
; VSTUP:CY=peruen programu
; -----------------------------------------------------------------------------

SaveHD   PROC      NEAR

; ------ schova registr

         push      ax
         push      dx
         call      InitPort                 ; instalace a inicializace port

; ------ kontroln souet dat (do registru DX)

         call      KontrS                   ; kontroln souet hlaviky souboru

; ------ vysln vodu ped zpisem

         call      SaveUvod                 ; vysln vodnho tnu a synchron.
         jc        SaveHD9                  ; peruen programu

; ------ vysln obsahu dat

         call      SavBlok                  ; vysln obsahu dat
         jc        SaveHD9                  ; peruen programu

; ------ vysln kontrolnho soutu

         mov       al,dh                    ; vy bajt kontrolnho soutu
         call      SavByte                  ; vysln vyho bajtu kontr.soutu
         mov       al,dl                    ; ni bajt kontrolnho soutu
         call      SavByte                  ; vysln niho bajtu kontr.soutu

; ------ vysln STOP bitu "1"

         stc                                ; pznak bitu "1"
         call      SavBit                   ; vysln stop bitu "1"

; ------ nvrat registr

SaveHD9: pushf
         call      DeInit                   ; nvrat port a peruen
         popf
         pop       dx
         pop       ax
         ret

SaveHD   ENDP

; -----------------------------------------------------------------------------
;        kontroln souet bloku dat
; -----------------------------------------------------------------------------
; VSTUP: ES:SI=adresa bloku dat
;        CX=poet bajt dat
;        DS=datov segment
; VSTUP:DX=kontroln souet dat
; -----------------------------------------------------------------------------

KontrS   PROC      NEAR

; ------ schova registr

         push      ax
         push      cx
         push      si

; ------ kontrola, zda jsou njak data

         xor       dx,dx                    ; stada kontrolnho soutu
         jcxz      KontrS9                  ; nejsou dn data

; ------ piten jednoho bajtu dat

KontrS1: mov       al,es:[si]               ; datov bajt k piten
         inc       si                       ; zven ukazatele dat
         mov       ah,8                     ; poet bit bajtu
KontrS2: shl       al,1                     ; jeden bit k piten
         adc       dx,0                     ; piten bitu ke stadai
         dec       ah                       ; ta bit
         jnz       KontrS2                  ; piten dalho bitu bajtu
         loop      KontrS1                  ; piten dalho bajtu

; ------ nvrat registr

KontrS9: pop       si
         pop       cx
         pop       ax
         ret

KontrS   ENDP

; -----------------------------------------------------------------------------
;        vysln vodu dat
; -----------------------------------------------------------------------------
; VSTUP: DI=poet bit vodnho tnu ("0") (22000=hlavika, 11000=data)
;        BH=poet bit synchronizace 1 ("1") (40=hlavika, 20=data)
;        BL=poet bit synchronizace 2 ("0") (40=hlavika, 20=data)
;        DS=datov segment
; VSTUP:CY=peruen programu
; -----------------------------------------------------------------------------

SaveUvod PROC      NEAR

; ------ schova registr

         push      ax
         push      bx
         push      di

; ------ vysln vodnho tnu

         or        di,di
         jz        SaveUv11                 ; nen dn bit
SaveUv1: clc                                ; vyle se bit 0
         call      SavBit                   ; vysln bitu 0
         jc        SaveUv9                  ; peruen programu
         dec       di                       ; ta bit vodnho tnu
         jnz       SaveUv1                  ; vysln dalho bitu

; ------ vysln synchronizace 1

SaveUv11:cmp       bh,0                     ; jsou njak bity ?
         je        SaveUv22                 ; nejsou dn bity
SaveUv2: stc                                ; vyle se bit 1
         call      SavBit                   ; vysln bitu 1
         dec       bh                       ; poet bit synchronizace
         jnz       SaveUv2                  ; vysln synchronizace 1

; ------ vysln synchronizace 2

SaveUv22:cmp       bl,0                     ; jsou njak bity ?
         je        SaveUv4                  ; nejsou dn bity
SaveUv3: clc                                ; vyle se bit 0
         call      SavBit                   ; vysln bitu 0
         dec       bl                       ; poet bit synchronizace
         jnz       SaveUv3                  ; vysln synchronizace 2

; ------ vysln startovacho bitu

SaveUv4: stc                                ; vyle se bit 1
         call      SavBit                   ; vysln bitu 1

; ------ nvrat registr

SaveUv9: pop       di
         pop       bx
         pop       ax
         ret

SaveUvod ENDP

; -----------------------------------------------------------------------------
;        vysln bloku dat
; -----------------------------------------------------------------------------
; VSTUP: ES:SI=adresa bloku dat
;        CX=poet bajt k vysln
;        DS=datov segment
; VSTUP:CY=peruen programu
; -----------------------------------------------------------------------------

SavBlok  PROC      NEAR

; ------ schova registr

         push      ax
         push      cx
         push      si

; ------ kontrola, zda jsou njak data k vysln

         clc
         jcxz      SavBlok9                 ; nejsou dn data

; ------ vysln bloku dat

         cld
SavBlok1:mov       al,es:[si]               ; bajt k vysln
         inc       si                       ; zven ukazatele
         call      SavByte                  ; vysln jednoho bajtu dat
         jc        SavBlok9                 ; peruen programu
         loop      SavBlok1                 ; vysln dalho bajtu

; ------ nvrat registr

SavBlok9:pop       si
         pop       cx
         pop       ax
         ret

SavBlok  ENDP

; -----------------------------------------------------------------------------
;        vysln jednoho bajtu
; -----------------------------------------------------------------------------
; VSTUP: AL=bajt k vysln
;        DS=datov segment
; VSTUP:CY=peruen
; ni registr AX !
; -----------------------------------------------------------------------------

SavByte  PROC      NEAR

         stc                                ; start bit "1"
         call      SavBit                   ; vysln startovacho bitu "1"
         shl       al,1
         call      SavBit                   ; bit 7
         shl       al,1
         call      SavBit                   ; bit 6
         shl       al,1
         call      SavBit                   ; bit 5
         shl       al,1
         call      SavBit                   ; bit 4
         shl       al,1
         call      SavBit                   ; bit 3
         shl       al,1
         call      SavBit                   ; bit 2
         shl       al,1
         call      SavBit                   ; bit 1
         shl       al,1
         call      SavBit                   ; bit 0
         ret

SavByte  ENDP

; -----------------------------------------------------------------------------
;        vysln jednoho bitu
; -----------------------------------------------------------------------------
; VSTUP: CY=bit je 1 (NC=bit je 0)
;        DS=datov segment
; VSTUP:CY=peruen
; ni registr AH !
; -----------------------------------------------------------------------------

SavBit   PROC      NEAR

; ------ stanoven dlky impulsu

         mov       ah,ds:[DelImp]           ; dlka impulsu "0"
         jnc       SavBit1                  ; je bit 0
         shl       ah,1                     ; jinak impuls "1"

; ------ vysln dvou impuls

SavBit1: call      SavImp                   ; vysln impulsu "+"
         call      SavImp                   ; vysln impulsu "-"
         ret

SavBit   ENDP

; -----------------------------------------------------------------------------
;        zahjen vysln jednoho impulsu
; -----------------------------------------------------------------------------
; VSTUP: AH=dlka impulsu
;        DS=datov segment
; VSTUP:CY=peruen programu
; -----------------------------------------------------------------------------

SavImp   PROC      NEAR

; ------ test, zda je peruen porgramu

         sti                                ; peruen mus bt povoleno
SavImp1: test      byte ptr ds:[Param],bit0 ; je peruen programu ?
         stc                                ; pznak peruen
         jnz       SavImp9                  ; je peruen programu

; ------ ekn na ukonen jednoho impulsu

         cmp       byte ptr ds:[CitImp],0   ; je ji cel impuls ?
         jne       SavImp1                  ; ekn na ukonen impulsu

; ------ zatek novho impulsu

         mov       ds:[CitImp],ah           ; dlka novho impulsu
                                            ; zde je pznak NC
SavImp9: ret

SavImp   ENDP

; -----------------------------------------------------------------------------
;        obsluha INT 08h
; -----------------------------------------------------------------------------

INT08    PROC      FAR

; ------ schova registr

         push      ax

; ------ tn zmny vstupu

         mov       al,cs:[CitImp]           ; ta impuls
         cmp       al,0                     ; t se impuls ?
         je        Int089                   ; net se impuls
         dec       al                       ; snen tae
         mov       cs:[CitImp],al           ; nov stav tae
         jnz       Int089                   ; nen dosaeno 0

; ------ zmna portu LPT

         push      dx
         mov       al,cs:[OutPort]          ; vstup na port LPT
         xor       al,bit0                  ; zmna signlu STROBE
         mov       cs:[OutPort],al          ; schova nov hodnoty
PortLPT: mov       dx,37ah                  ; vstupn port
         out       dx,al                    ; vstup bajtu LPT
         pop       dx

; ------ uvolnn adie peruen

Int089:  mov       al,20h
         out       [20h],al                 ; uvolnn adie peruen

; ------ nvrat registr

         pop       ax
         iret

INT08    ENDP

; -----------------------------------------------------------------------------
;        obsluha INT 09h
; -----------------------------------------------------------------------------

INT09    PROC      FAR

; ------ schova registr

         push      ax
         push      cx

; ------ uvolnn adie peruen

         mov       al,20h
         out       [20h],al                 ; uvolnn adie peruen
         sti

; ------ uzamen klvesnice

         call      Cekej09                  ; ekn na pipravenost klvesnice
         mov       al,0adh
         out       [64h],al                 ; vysln povelu-uzamen klvesnice

; ------ ten kdu klvesy

         call      Cekej09                  ; ekn na pipravenost klvesnice
         in        al,[60h]

; ------ peruen programu

         cmp       al,1
         jne       Int095
         or        byte ptr cs:[Param],bit0 ; pznak peruen programu ESC

; ------ uvolnn klvesnice

Int095:  call      Cekej09                  ; ekn na pipravenost klvesnice
         mov       al,0aeh
         out       [64h],al                 ; vysln povelu-uvolnn klvesnice

; ------ nvrat registr

         pop       cx
         pop       ax
         iret

Int09    ENDP

; -----------------------------------------------------------------------------
;        ekn na pipravenost klvesnice (ni registry CX a AL !)
; -----------------------------------------------------------------------------

Cekej09  PROC      NEAR

         cli
         mov       cx,5000
Cekej091:in        al,[64h]
         test      al,bit1
         loopnz    Cekej091
         ret

Cekej09  ENDP

; *****************************************************************************
;                             InitPort
;                        inicializace port
; -----------------------------------------------------------------------------
; VSTUP: DS=datov segment
; *****************************************************************************

InitPort PROC      NEAR

; ------ schova registr

         push      ax
         push      bx
         push      cx
         push      dx
         push      es

; ------ vypnut motor disketovch mechanik

         mov       dx,3f2h
         mov       al,0ch
         out       dx,al                    ; vypnut motor mechanik
         push      ds
         xor       ax,ax
         mov       ds,ax
         and       byte ptr ds:[43fh],not bit0+bit1+bit2+bit3+bit4+bit5 ; motory

; ------ inicializace adres port

         mov       ax,ds:[408h]
         or        ax,ax
         jnz       InitPrt1
         mov       ax,ds:[40ah]
         or        ax,ax
         jnz       InitPrt1
         mov       ax,ds:[40ch]
         or        ax,ax
         jnz       InitPrt1
         mov       ax,ds:[40eh]
         or        ax,ax
         jnz       InitPrt1
         mov       ax,378h
InitPrt1:inc       ax
         inc       ax
         mov       word ptr cs:[PortLPT+1],ax    ; adresa portu LPT
         pop       ds
         mov       byte ptr ds:[OutPort],00000101b ; vstupn slovo na port

; ------ schova vektoru INT 08h

         mov       ax,3508h
         int       21h                      ; poskytnut adresy INT 08h
         mov       word ptr ds:[Old08],bx
         mov       word ptr ds:[Old08+2],es ; schova adresy INT 08h

; ------ schova vektoru INT 09h

         mov       ax,3509h
         int       21h                      ; poskytnut adresy INT 09h
         mov       word ptr ds:[Old09],bx
         mov       word ptr ds:[Old09+2],es ; schova adresy INT 09h

; ------ schova registru masky peruen

         in        al,[21h]                 ; registr masky peruen
         mov       ds:[OldMask],al          ; schova masky peruen

; ------ pechodn zkaz vech peruen

         cli                                ; zkaz peruen
         mov       al,0ffh
         out       [21h],al                 ; zkaz vech peruen

; ------ instalace INT 08h

         mov       dx,offset Int08          ; obsluha INT 08h
         mov       ax,2508h
         int       21h                      ; instalace obsluhy INT 08h

; ------ instalace INT 09h

         mov       dx,offset Int09          ; obsluha INT 09h
         mov       ax,2509h
         int       21h                      ; instalace obsluhy INT 09h

; ------ inicializace hodin INT 08h

         mov       al,34h
         out       [43h],al                 ; povel pro nastaven hodin
         mov       al,ds:[Konst08]          ; dlic konstanta pro hodiny
         out       [40h],al                 ; dlic konstanta pro hodiny
         mov       al,0
         out       [40h],al                 ; vy bajt dlic konstanty

; ------ uvolnn peruen od klvesnice a hodin

         mov       al,0fch
         out       [21h],al                 ; uvolnn peruen
         sti                                ; povolen peruen

; ------ nvrat registr

         pop       es
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

InitPort ENDP

; *****************************************************************************
;                                DeInit
;                         odinstalovn port
; -----------------------------------------------------------------------------
; VSTUP: DS=datov segment
; *****************************************************************************

DeInit   PROC      NEAR

; ------ schova registr

         push      ax
         push      bx
         push      cx
         push      dx
         push      es

; ------ pechodn zkaz vech peruen

         mov       al,0ffh
         out       [21h],al                 ; zkaz vech peruen
         cli                                ; zkaz peruen

; ------ vypnut vstupu na reproduktor

         in        al,[61h]
         and       al,not 3
         out       [61h],al                 ; vypnut vstupu na reproduktor

; ------ standardn nastaven hodin

         mov       al,34h
         out       [43h],al                 ; povel
         mov       al,0
         out       [40h],al                 ; konstanta - n bajt
         out       [40h],al                 ; konstanta - vy bajt

; ------ standardn nastaven tae reproduktoru

         mov       al,0b6h                  ; povel
         out       [43h],al
         mov       al,5                     ; konstanta asi tak 920 Hz
         out       [42h],al                 ; konstanta - n bajt
         out       [42h],al                 ; konstanta - vy bajt

; ------ nvrat adresy INT 08h

         push      ds
         lds       dx,ds:[Old08]            ; pvodn adresa INT 08h
         mov       ax,2508h
         int       21h                      ; nvrat adresy INT 08h
         pop       ds

; ------ nvrat adresy INT 09h

         push      ds
         lds       dx,ds:[Old09]            ; pvodn adresa INT 09h
         mov       ax,2509h
         int       21h                      ; nvrat adresy INT 09h
         pop       ds

; ------ nvrat registru peruen

         mov       al,ds:[OldMask]          ; pvodn registr masky peruen
         out       [21h],al                 ; nvrat registru masky peruen
         sti                                ; povolen peruen

; ------ ten hodin systmovho asu (AT)

         mov       dl,8ah                   ; pednastaven na nesmysl
         mov       ah,2
         int       1ah                      ; ten hodin
         cmp       dl,1                     ; peteen pes plnoc ?
         ja        Deinit3                  ; hodiny neplat

; ------ nvrat pznaku peteen pes plnoc

         jne       DeInit2                  ; nen peteen pes plnoc
         xor       ax,ax
         mov       es,ax
         mov       byte ptr es:[470h],dl    ; nvrat pznaku peteen plnoci

; ------ konverze z BCD formtu na binrn

DeInit2: mov       al,ch                    ; hodina
         call      BcdBin                   ; konverze na binrn tvar
         mov       ch,al
         mov       al,cl                    ; minuta
         call      BcdBin                   ; konverze na binrn tvar
         mov       cl,al
         mov       al,dh                    ; sekunda
         call      BcdBin                   ; konverze na binrn tvar
         mov       dh,al

; ------ nastaven asu

         xor       dl,dl                    ; setina sekundy
         mov       ah,2dh
         int       21h                      ; nastaven systmovho asu

DeInit3:

; ------ nvrat registr

         pop       es
         pop       dx
         pop       cx
         pop       bx
         pop       ax
         ret

DeInit   ENDP

; -----------------------------------------------------------------------------
;        Konverze sla z BCD tvaru na binrn
; -----------------------------------------------------------------------------

BcdBin   PROC      NEAR

         push      cx
         mov       ch,al
         and       ch,0fh
         and       al,0f0h
         mov       cl,4
         shr       al,cl
         mov       cl,10
         mul       cl
         add       al,ch
         pop       cx
         ret

BcdBin   ENDP

; -----------------------------------------------------------------------------
;        data
; -----------------------------------------------------------------------------

MemTxt   db        'Nedostatek pameti !',13,10,'$'
hlptxt   db        'Zadejte platne jmeno souboru k vyslani !'
CRTxt    db        13,10,'$'

; -----------------------------------------------------------------------------
;        zavad؟ TURBO
; -----------------------------------------------------------------------------

Loader   label     byte
         db        1                        ; typ
LoadNam  db        17 dup(13)               ; jmno souboru
LoadDel  dw        5ah                      ; dlka zavad؟e
LoadZac  dw        0d400h                   ; zatek zavad؟e
LoadStrt dw        0d400h                   ; start zavad؟e
LoadComm db        5bh,96h,0a5h,9dh,9ah,0b7h,5dh ; text "[turbo]"
         db        95 dup(0), 0cdh,0        ; zbytek komente
LoadPrg  label     byte                     ; program
         db        3eh,8                    ; D400: LD A,8
         db        0d3h,0ceh                ; D402: OUT (0ceh),A
         db        0e5h                     ; D404: PUSH HL
         db        21h,0,0                  ; D405: LD HL,0
         db        0d3h,0e4h                ; D408: OUT (0e4h),A
         db        7eh                      ; D40A: LD A,(HL)
         db        0d3h,0e0h                ; D40B: OUT (0e0h),A
         db        77h                      ; D40D: LD (HL),A
         db        23h                      ; D40E: INC HL
         db        7ch
         db        0feh,10h
         db        20h,0f4h
         db        3ah,4bh,0d4h
         db        32h,4bh,0ah
         db        3ah,4ch,0d4h
         db        32h,12h,5
         db        21h,4dh,0d4h
         db        11h,2,11h
         db        1,0dh,0
         db        0edh,0b0h
         db        0e1h
         db        7ch
         db        0feh,0d4h
         db        28h,12h
         db        2ah,4,11h
         db        0d9h
         db        21h,0,12h
         db        22h,4,11h
         db        0cdh,2ah,0
         db        0d3h,0e4h
         db        0c3h,9ah,0e9h
         db        0cdh,2ah,0
         db        0d3h,0e4h
         db        0c3h,24h,1
LoadKon1 db        15h
LoadKon2 db        1
LoadHlav db        13 dup(0)                ; hlavika od offsetu potu bajt
LoadEnd  label     byte

TextVys  db        'Vysilam $'

Param    db        0                        ; parametry
                                            ;  bit 0: 1=peruen ESC

CitImp   db        0                        ; ta impuls ke zmn signlu

SegmVRAM dw        0b000h                   ; segment videopamti

Konst08  db        75                       ; dlic konstanta hodin
                                            ;     (16.57 kHz / 60.34 us)
                                            ;      hodiny 1 193 182 Hz
DelImp   db        4                        ; zkladn dlka impulsu

; ------ zhlav souboru (80h bajt)

Hlava    label     byte
HlavaTyp db        1                        ; typ souboru (1=stroj., 5=BASIC)
HlavaNam db        17 dup(" ")              ; jmno souboru
HlavaDel dw        0                        ; dlka souboru
HlavaBeg dw        1200h                    ; poten adresa souboru
HlavaExe dw        1200h                    ; startovac adresa programu
HlavaCom db        80h - 24 dup(0)          ; poznmka souboru
HlavaEnd label     byte

Old08    dd        ?                        ; pvodn adresa INT 08h
Old09    dd        ?                        ; pvodn adresa INT 09h
OldMask  db        ?                        ; pvodn maska peruen

DatSegm  dw        ?                        ; adresa datovho bloku
DatNum   dw        ?                        ; poet bajt v bufferu

OutPort  db        ?                        ; vstup na port LPT

         dw        200h dup(?)              ; zsobnk
zasob    label     byte                     ; konec zsobnku

konec    label     byte                     ; konec programu

code     ends
         end       start
